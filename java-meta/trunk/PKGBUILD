# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=('java-runtime-headless-meta' 'java-runtime-meta' 'java-environment-meta')
pkgbase=java-meta
pkgver=2
pkgrel=4
arch=('any')
url='www.archlinux.org'
license=('GPL')
source=(profile_jre.csh
        profile_jre.sh
        bin_archlinux-java
        bin_java-wrapper)
sha256sums=('9f0032e47705ce55a615ab3b942720b8447f490d1ff430b214038b81c3019ac2'
            '8dbafa311f34d23c5bbef71fa942b97b224ebeb330b36195f22a1836285bbb83'
            '97556f1163beef8bfaff6d545c1e2c23bc95d194db5f21d78b6e8666702de17f'
            '622927af9ec374140a878993936f7fdd4206fe069dddfcefc6d4e15fa6d67f39')

package_java-runtime-headless-meta() {
  pkgdesc='Meta files for headless Java Runtime Environments'
  backup=(etc/profile.d/jre.sh
          etc/profile.d/jre.csh)
  install=install_java-runtime-headless-meta.sh

  cd "${srcdir}"

  install -d -m 755 "${pkgdir}/etc/profile.d"
  install -m 644 profile_jre.sh  "${pkgdir}/etc/profile.d/jre.sh"
  install -m 644 profile_jre.csh "${pkgdir}/etc/profile.d/jre.csh"

  install -D -m 755 bin_java-wrapper "${pkgdir}/usr/bin/java"
  # Official list of supplementary binaries that Java headless runtime packages MUST provide
  # This list is based on Arch Linux' default Java implementation: "OpenJDK 7 Icedtea"
  for bin in keytool orbd pack200 rmid rmiregistry servertool tnameserv unpack200; do
    ln -s java "${pkgdir}/usr/bin/${bin}"
  done

  install -D bin_archlinux-java "${pkgdir}/usr/bin/archlinux-java"
}

package_java-runtime-meta() {
  pkgdesc='Meta files for full Java Runtime Environments'

  cd "${srcdir}"

  install -d m 755 "${pkgdir}/usr/bin/"
  # Official list of supplementary binaries that Java runtime packages MUST provide
  # This list is based on Arch Linux' default Java implementation: "OpenJDK 7 Icedtea"
  ln -s java "${pkgdir}/usr/bin/policytool"
}

package_java-environment-meta() {
  pkgdesc='Meta files for Java Development Kits'

  cd "${srcdir}"

  install -d m 755 "${pkgdir}/usr/bin/"
  # Official list of supplementary binaries that Java environment packages MUST provide
  # This list is based on Arch Linux' default Java implementation: "OpenJDK 7 Icedtea"
  for bin in \
    appletviewer extcheck idlj jar jarsigner javac javadoc javah javap jcmd \
    jconsole jdb jdeps jhat jinfo jmap jps jrunscript jsadebugd jstack jstat \
    jstatd native2ascii rmic schemagen serialver wsgen wsimport xjc;
  do
    ln -s java "${pkgdir}/usr/bin/${bin}"
  done
}
