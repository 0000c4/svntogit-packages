# $Id$
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
pkgname=rssh
pkgver=2.3.3
pkgrel=2
pkgdesc='A restricted shell for use with OpenSSH, allowing only scp and/or sftp'
arch=('i686' 'x86_64')
url='http://www.pizzashack.org/rssh/'
depends=('openssh' 'glibc')
backup=('etc/rssh.conf')
license=('custom:rssh')
source=("http://downloads.sourceforge.net/sourceforge/rssh/rssh-$pkgver.tar.gz"
        'destdir.patch'
        'rsync.patch')
sha1sums=('0a6dd80b5e6059e0db12c9f1276121dd966b610a'
          '85bd1694decae5872cbeeafd578b147eb13313c6'
          '41f32f8a77b3a2b924ede6044ab67846e06b5d20')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	patch -p1 < ../destdir.patch
	patch -p1 < ../rsync.patch # FS#21783, debian patch

	./configure --prefix=/usr --libexecdir=/usr/lib/rssh \
		--mandir=/usr/share/man --sysconfdir=/etc
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/rssh/LICENSE
}
