# $Id$
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
pkgname=rssh
pkgver=2.3.3
pkgrel=1
pkgdesc='A restricted shell for use with OpenSSH, allowing only scp and/or sftp'
arch=('i686' 'x86_64')
url='http://www.pizzashack.org/rssh/index.shtml'
depends=('openssh' 'glibc')
backup=('etc/rssh.conf')
license=('custom:rssh')
source=("http://downloads.sourceforge.net/sourceforge/rssh/rssh-$pkgver.tar.gz"
        'rssh.patch')
sha1sums=('0a6dd80b5e6059e0db12c9f1276121dd966b610a'
          '07deb7c88a52dccb45aa45a59f9e98e5c511ff3a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 < ../rssh.patch
  ./configure --prefix=/usr --libexecdir=/usr/lib/rssh \
    --mandir=/usr/share/man --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/rssh/LICENSE
}
