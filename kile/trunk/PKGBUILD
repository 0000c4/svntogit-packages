# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : Damir Perisa <damir.perisa@bluewin.ch>

pkgname=kile
pkgver=2.1
pkgrel=1
pkgdesc="A user friendly TeX/LaTeX frontend for KDE."
arch=("i686" "x86_64")
license=('GPL2')
url="http://kile.sourceforge.net/"
depends=('kdebase-runtime' 'texlive-core')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl')
install=kile.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha1sums=('860ac96b68b6026f48b8a9c401c6024ba78e3cdf')

build() {
  cd ${srcdir}
  mkdir build 
  cd build    
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=$pkgdir install
}
