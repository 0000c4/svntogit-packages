# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=md4c
pkgver=0.3.4
pkgrel=1
pkgdesc="C Markdown parser"
arch=(x86_64)
url="https://github.com/mity/md4c"
license=(MIT)
depends=(glibc)
makedepends=(cmake chrpath)
source=($pkgname-$pkgver.tar.gz::"https://github.com/mity/md4c/archive/release-$pkgver.tar.gz")
sha256sums=('cf3f6b75d4b1304551a68272d3b403e4cd0952d976a280e8b7db68090354486f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-release-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-release-$pkgver/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/

  chrpath -d "$pkgdir"/usr/bin/md2html # Fix RPATH
}
