# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-openconnect
pkgver=0.8.999
pkgrel=1
pkgdesc="NetworkManager VPN integration for openconnect"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver}" 'openconnect' 'dbus-glib' 'gconf' 'libgnome-keyring')
makedepends=('intltool' 'namcap')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-openconnect/0.8/NetworkManager-openconnect-${pkgver}.tar.bz2)
sha256sums=('e191c71ff23268eac427609aeb0fb3df7bb1b6da2645bac84c62872cdfc218f5')

build() {
  cd "${srcdir}/NetworkManager-openconnect-${pkgver}"
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
	--disable-static
  make
}

package() {
  cd "${srcdir}/NetworkManager-openconnect-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
