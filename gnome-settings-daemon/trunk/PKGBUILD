# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-settings-daemon
pkgver=2.91.91
pkgrel=2
pkgdesc="The GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd' 'gnome-desktop' 'libnotify' 'hicolor-icon-theme' 'libpulse' 'libcanberra' 'gsettings-desktop-schemas' 'nss' 'gconf' 'dconf')
makedepends=('intltool' 'gtk-doc')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
replaces=(gnome-settings-daemon-pulse)
conflicts=(gnome-settings-daemon-pulse)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('ae649cc1c723ebae99491ca2bf68d8e6c08ac0b3a6c81e94aedbb5b7a8f91c86')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/bin --disable-static --enable-pulse 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
