# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica-qt5
pkgver=5.32.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Qt5 library that implements the Open Collaboration Services API'
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/attica-${pkgver}.tar.xz"{,.sig})
sha256sums=('e5428cda35021d00baecb16cadf12a08c84ef15e61e72790ae6e33ccef1b749b'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../attica-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
