# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
_pkgbasename=gtkmm
pkgname=('gtkmm3' 'gtkmm3-docs')
pkgver=3.1.18
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('gtk3' 'pangomm' 'atkmm' 'glibmm-docs' 'mm-common')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/3.1/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('71ea98e5d8dceb1347424120e4e4b08aaabf77ddff4de6ab07977bd9fa5e9251')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_gtkmm3() {
  pkgdesc="C++ bindings for gtk3"
  depends=('gtk3' 'pangomm' 'atkmm')
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  make DESTDIR="${pkgdir}" install
}

package_gtkmm3-docs() {
  pkgdesc="Developer documentation for gtkmm v3"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -C docs DESTDIR="${pkgdir}" install
}
