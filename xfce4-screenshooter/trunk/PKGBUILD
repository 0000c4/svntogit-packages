# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter
pkgver=1.8.0
pkgrel=1
pkgdesc="Plugin that makes screenshots for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-screenshooter"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool')
install=$pkgname.install
options=('!libtool')
source=(http://archive.xfce.org/src/apps/$pkgname/1.8/$pkgname-$pkgver.tar.bz2
        xfce4-screenshooter-1.7.9-dsofix.patch
        fix_segfault.diff)
conflicts=('xfce4-screenshooter-plugin')
replaces=('xfce4-screenshooter-plugin')
sha1sums=('cc105a5d3b17248542705cf01368827ef860bec9'
          'd3e803d5dd544f8b6368d7efe22f32a6f0aa0f21'
          '5493cccc4f906d8e4acb44f18e94c91e745b51a3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/xfce4-screenshooter-1.7.9-dsofix.patch"
  #patch -Np1 -i ${srcdir}/fix_segfault.diff

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
