# $Id$
# Maintainer: tobias <tobias funnychar archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter
pkgver=1.8.0
pkgrel=1
pkgdesc="plugin that makes screenshots for the Xfce4 panel"
arch=(i686 x86_64)
license=('GPL2')
url="http://goodies.xfce.org/projects/applications/xfce4-screenshooter"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool')
install=${pkgname}.install
options=('!libtool')
source=(http://archive.xfce.org/src/apps/${pkgname}/1.8/${pkgname}-${pkgver}.tar.bz2
	xfce4-screenshooter-1.7.9-dsofix.patch
	fix_segfault.diff)
conflicts=('xfce4-screenshooter-plugin')
replaces=('xfce4-screenshooter-plugin')
md5sums=('b51ab5725418e7258273c4a6402adb02'
         '355a7d005bf9200f5a5577f0bc901945'
         '794d7e0ff18cad369bd50a78e883f986')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/xfce4-screenshooter-1.7.9-dsofix.patch
  #patch -Np1 -i ${srcdir}/fix_segfault.diff
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
