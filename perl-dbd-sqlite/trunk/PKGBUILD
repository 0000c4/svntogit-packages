# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-dbd-sqlite
pkgver=1.58
pkgrel=2
pkgdesc="Self-contained RDBMS in a DBI driver"
arch=('x86_64')
url="http://search.cpan.org/dist/DBD-SQLite"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'sqlite')
source=(http://www.cpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-${pkgver}.tar.gz)
sha512sums=('080d1e304fb0d43926ce0345df3367132c317ba113bdc11952c0a159bc68d988a024828a28e66be267f82e07731dee225723854fb80914ba7c476b27dbdeb212')

build() {
  cd DBD-SQLite-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd DBD-SQLite-${pkgver}
  make test
}

package() {
  cd DBD-SQLite-${pkgver}
  make install DESTDIR="${pkgdir}"
}
