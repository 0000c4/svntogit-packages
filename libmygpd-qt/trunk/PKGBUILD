# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmygpo-qt
pkgver=1.0.2
pkgrel=2
pkgdesc="A C++/Qt client library for gpodder.net"
arch=('i686' 'x86_64')
url="http://wiki.gpodder.org/wiki/Libmygpo-qt"
license=('LGPL2.1')
depends=('qjson')
makedepends=('cmake')
source=("http://stefan.derkits.at/files/${pkgname}/${pkgname}.${pkgver}.tar.gz")
md5sums=('6f88444c82e9840638ec16e366457dd7')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}.${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
