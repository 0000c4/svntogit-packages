# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=procinfo-ng
pkgver=2.0.304
pkgrel=2
pkgdesc="Displays useful information from /proc"
arch=('i686' 'x86_64')
url="http://procinfo-ng.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
depends=('ncurses' 'perl')
provides=('procinfo')
conflicts=('procinfo')
replaces=('procinfo')
source=(http://downloads.sourceforge.net/project/procinfo-ng/procinfo-ng/2.0.304/${pkgname}-${pkgver}.tar.bz2
        ftp://ftp.archlinux.org/other/procinfo-ng/procinfo-18.tar.gz)
md5sums=('ed018fe37becc36ceb1ebe3ed909c4d2' '27658d0a69040aca05a65b9888599d50')
sha1sums=('da2176a505bcdf65c4eadcf5a2e056fb7a3f84af' '8bc75669cbd27775c3a837209425c5ce76238ebc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install tools from old procinfo package
  cd "${srcdir}/procinfo-18"
  install -m755 lsdev.pl "${pkgdir}/usr/bin/lsdev"
  install -m755 socklist.pl "${pkgdir}/usr/bin/socklist"
  install -m644 lsdev.8 "${pkgdir}/usr/share/man/man8/lsdev.8"
  install -m644 socklist.8 "${pkgdir}/usr/share/man/man8/socklist.8"
}
