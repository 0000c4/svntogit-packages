# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=noto-fonts
pkgname=(noto-fonts ttf-croscore)
_pkgver=2016-06-13
_commit=19ce9d5
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto TTF fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(Apache)
depends=(fontconfig)
provides=(ttf-font)
install=noto-fonts.install
#source=("https://github.com/googlei18n/noto-fonts/archive/v$_pkgver-license-adobe.tar.gz")
source=("https://github.com/googlei18n/noto-fonts/archive/$_commit.zip")
sha256sums=('41b6f69f73752546c25832edbb487752b87e5f91ca2f54a439255f34a59cf14f')

package_noto-fonts() {
  optdepends=('noto-fonts-cjk: CJK characters' 'noto-fonts-emoji: Emoji characters')

  mkdir -p "$pkgdir"/usr/share/fonts/noto
  install -m644 $pkgbase-*/hinted/Noto*.ttf "$pkgdir"/usr/share/fonts/noto
}

package_ttf-croscore() {
  pkgdesc='Chrome OS core fonts'

  cd $pkgbase-*/hinted
  mkdir -p "$pkgdir"/usr/share/fonts/TTF
  install -m644 Arimo*.ttf Cousine*.ttf Tinos*.ttf "$pkgdir"/usr/share/fonts/TTF
}


