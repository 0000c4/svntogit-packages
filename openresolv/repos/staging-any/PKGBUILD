# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.5.4
pkgrel=3
pkgdesc="resolv.conf management framework (resolvconf)"
arch=(any)
url="http://roy.marples.name/projects/openresolv"
license=('custom:BSD')
backup=(etc/resolvconf.conf)
provides=(resolvconf)
depends=(bash)
makedepends=(systemd)
source=("http://roy.marples.name/downloads/openresolv/${pkgname}-${pkgver}.tar.bz2"
        fs33801.patch 0001-Use-SBINDIR-instead-of-PREFIX-sbin.patch)
md5sums=('617a52fbe6f253f231d9f78f1ea74281'
         '440790d3403b2ae2c4bb1d657c7e36a4'
         '6686a4e74b00c72277a17bb4b4503465')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fs33801.patch
  patch -Np1 -i ../0001-Use-SBINDIR-instead-of-PREFIX-sbin.patch
  sed -n '2,25{s:^# \?::;p}' resolvconf.in >LICENSE 
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/resolvconf \
    --sbindir=/usr/bin
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
