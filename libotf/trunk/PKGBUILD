# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Ashish Shukla <wahjava.ml@gmail.com>

pkgname=libotf
pkgver=0.9.16
pkgrel=2
pkgdesc='OpenType Font library'
url='https://www.nongnu.org/m17n/'
license=('LGPL')
arch=('x86_64')
depends=('libxaw' 'freetype2')
source=("https://download.savannah.gnu.org/releases/m17n/${pkgname}-${pkgver}.tar.gz"
        'replace-freetype-config.patch')
sha256sums=('68db0ca3cda2d46a663a92ec26e6eb5adc392ea5191bcda74268f0aefa78066b'
            '619d1af60f5189543c490976e390e405cd320eb7e52aae670521b9238bff2926')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ${srcdir}/replace-freetype-config.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
