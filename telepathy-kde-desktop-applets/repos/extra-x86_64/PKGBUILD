# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-desktop-applets
_pkgname=ktp-desktop-applets
pkgver=17.08.0
pkgrel=1
pkgdesc='The KDE-Telepathy Plasma desktop applets'
arch=('i686' 'x86_64')
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-kde-common-internals' 'telepathy-kde-accounts-kcm' 'plasma-workspace')
makedepends=('extra-cmake-modules' 'python')
source=("https://download.kde.org/stable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('77f5c699dd70a98a1fdbf464fa2cdd370dfd0c2315ef9620b27f2c478b2a0ed8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
