# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=2.30.2
pkgrel=1
pkgdesc="A window manager for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('startup-notification>=0.10' 'gconf>=2.28.1' 'zenity>=2.30.0' 'libcanberra>=0.23' 'libgtop>=2.28.1')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.0')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        metacity-restartstyle.patch)
sha256sums=('d4c1cbf74cce4c099e95cc440c834833e3efc287a3cf952ba0b4cfc84351ffe3'
            '3a7e484ec2eb97e7b7f1baaa33e5d925e67a35c84e49c5abe6c32cf2c25f16eb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/metacity-restartstyle.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/metacity \
      --localstatedir=/var --disable-static
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain metacity ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
