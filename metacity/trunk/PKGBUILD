# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=2.30.0
pkgrel=2
pkgdesc="A window manager for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('startup-notification>=0.10' 'gconf>=2.28.1' 'zenity>=2.30.0' 'libcanberra>=0.23' 'libgtop>=2.28.1')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.0')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        metacity-restartstyle.patch
        fix-build-gcc-4.5.patch)
sha256sums=('5c4e2af2216de022181f692f15427361a1dae3d30756ef89b97a11ff5bd50b22'
            '3a7e484ec2eb97e7b7f1baaa33e5d925e67a35c84e49c5abe6c32cf2c25f16eb'
	    '100e6dd9f79e9b634a81a34c42fcdf8c19c91bc41f5d69809586021652a32c2b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/metacity-restartstyle.patch" || return 1
  patch -Np1 -R -i "${srcdir}/fix-build-gcc-4.5.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/metacity \
      --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain metacity ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
