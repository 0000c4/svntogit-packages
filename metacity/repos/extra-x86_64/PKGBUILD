# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=2.34.8
pkgrel=1
pkgdesc="A window manager for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('dconf' 'gtk2' 'gsettings-desktop-schemas' 'libcanberra'  'libgtop' 'libsm' 'startup-notification' 'zenity')
makedepends=('intltool' 'yelp-tools')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        fix_compositing_startup.patch)
sha256sums=('ab83038a9dc0b53a63b66c3f9323a10abb0b9a9ae2b5c5d3cc6bbabe2b607806'
            '5094a0ffe3eb8289ed752829877c2e1b743eddf938ad3fc92fb4574b42765ae2')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "${srcdir}/fix_compositing_startup.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/metacity \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
