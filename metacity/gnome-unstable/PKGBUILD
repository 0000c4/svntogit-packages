# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=2.27.1
pkgrel=1
pkgdesc="A window manager for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('startup-notification>=0.10' 'gconf>=2.27.0' 'zenity>=2.27.90' 'libcanberra>=0.14')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.17.5')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('d2384af84f0be4b9c638f4f8c8e486ab73bc3fe6c53f163f328fbd3112078742')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/metacity \
      --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain metacity ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
