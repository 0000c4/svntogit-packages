# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sqlite3
pkgver=3.6.14
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.sqlite.org/"
depends=('readline')
makedepends=('tcl')
conflicts=('sqlite')
replaces=('sqlite')
source=(http://www.sqlite.org/sqlite-$pkgver.tar.gz
	license.txt)
options=(!libtool)
md5sums=('4883696b283bae0838a2d468f2232437'
         'c1cdbc5544034d9012e421e75a5e4890')

build() {
  cd ${srcdir}/sqlite-${pkgver}
  autoreconf # broken configure script in 3.6.14
  export LTLINK_EXTRAS="-ldl"
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1"
  ./configure --prefix=/usr \
	--enable-threadsafe \
	--enable-threads-override-locks \
	--enable-cross-thread-connections \
	--disable-static \
	--enable-load-extension || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -Dm0644 sqlite3.1 ${pkgdir}/usr/share/man/man1/sqlite3.1

  # license
  install -Dm644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
