# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
pkgname=sqlite3
pkgver=3.6.1
pkgrel=2
pkgdesc="A C library that implements an SQL database engine"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.sqlite.org/"
depends=('readline')
makedepends=('tcl')
conflicts=('sqlite')
replaces=('sqlite')
source=(http://www.sqlite.org/sqlite-$pkgver.tar.gz
	sqlite-3.5.8-pkgconfig-version.patch
	license.txt)
options=(!libtool)
md5sums=('eeef61635d89710bb8d976c2a0b99841'
         '025965a176173eaf21877f6daed088d4'
         'c1cdbc5544034d9012e421e75a5e4890')

build() {
  cd ${srcdir}/sqlite-${pkgver}
  patch -Np1 -i ${srcdir}/sqlite-3.5.8-pkgconfig-version.patch || return 1
  ./configure --prefix=/usr --enable-threadsafe  --disable-static || return 1
  make || return 1
  make DESTDIR=${pkgdir} install
  install -Dm0644 sqlite3.1 ${pkgdir}/usr/share/man/man1/sqlite3.1

  # license
  install -Dm644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
