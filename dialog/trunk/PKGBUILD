# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.1_20110302
pkgrel=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="http://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('ncurses')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver/_/-}.tgz)
md5sums=('2eaa82055b86006e205d2c3f74d9b800')
sha1sums=('ba4e79abaf579e0d23f247ae65196437f8d8e031')

build() {
  cd "${srcdir}/$pkgname-${pkgver/_/-}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --with-ncursesw --enable-nls
  make
}

package() {
  cd "${srcdir}/$pkgname-${pkgver/_/-}"
  make DESTDIR="${pkgdir}" install install-lib
}
