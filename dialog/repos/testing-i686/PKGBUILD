# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.2_20130523
pkgrel=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="http://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('ncurses')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver/_/-}.tgz)
sha1sums=('54768b8443af3de2e6cd16073ec666f19d4ded7a')

build() {
  cd "${srcdir}/$pkgname-${pkgver/_/-}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --with-ncursesw --enable-nls
  make
}

package() {
  cd "${srcdir}/$pkgname-${pkgver/_/-}"
  make DESTDIR="${pkgdir}" install install-lib

  rm "${pkgdir}"/usr/lib/libdialog.a
}
