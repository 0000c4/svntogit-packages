# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.3_20160126
pkgrel=1
epoch=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="http://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('sh' 'ncurses')
provides=('libdialog.so')
source=(ftp://invisible-island.net/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('308ab0bc63eb525c852c85ad4aa8645c84277ea2f7fca19ef4cafafc08415ae3')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
    --prefix=/usr \
    --enable-nls \
    --with-libtool \
    --with-ncursesw
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}

# vim:set ts=2 sw=2 et:
