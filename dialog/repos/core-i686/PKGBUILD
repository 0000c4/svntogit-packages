# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.1_20110707
pkgrel=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="http://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('ncurses')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver/_/-}.tgz)
md5sums=('34d01aaacbb2932b77774e6c1eec8d2a')
sha1sums=('31e0e3c40cd51629cc802d7ee0b1a4e1ada90f00')

build() {
  cd "${srcdir}/$pkgname-${pkgver/_/-}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --with-ncursesw --enable-nls
  make
}

package() {
  cd "${srcdir}/$pkgname-${pkgver/_/-}"
  make DESTDIR="${pkgdir}" install install-lib
}
