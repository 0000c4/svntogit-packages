# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-ksaneplugin
pkgver=17.07.80
pkgrel=1
pkgdesc="A scan plugin that implements the scanning"
url="https://www.kde.org/applications/graphics/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libksane4')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/ksaneplugin-${pkgver}.tar.xz"{,.sig})
sha256sums=('b22497bc02cae6453ae096013a10405f345e571ee386d9d919fab992bd536c07'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ksaneplugin-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
