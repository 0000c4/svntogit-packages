# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-ksaneplugin
pkgver=17.04.0
pkgrel=1
pkgdesc="A scan plugin that implements the scanning"
url="https://www.kde.org/applications/graphics/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libksane4')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("https://download.kde.org/stable/applications/${pkgver}/src/ksaneplugin-${pkgver}.tar.xz"{,.sig})
sha256sums=('36bfe84ab4762c4756a37881f36a845f32d433346a5f93ceab081be3b8128983'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ksaneplugin-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
