# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktuberling
pkgver=17.03.80
pkgrel=1
pkgdesc="A simple constructor game suitable for children and adults alike"
url="https://kde.org/applications/games/ktuberling/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(hicolor-icon-theme libkdegames)
makedepends=(extra-cmake-modules kdoctools kdelibs4support python kdesignerplugin)
conflicts=(kdegames-ktuberling)
replaces=(kdegames-ktuberling)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('9514346ea90205ccb4f3b342354286544a54ae755d9418312a563bae664b40de'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
