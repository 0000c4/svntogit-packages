# $Id: PKGBUILD 165829 2012-08-31 06:08:20Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=webkitgtk3
pkgver=1.9.92
pkgrel=1
pkgdesc="GTK+ Web content engine library for GTK+ 3.0"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite' 'gstreamer0.10-base' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk2' 'gtk3')
makedepends=('gperf' 'gobject-introspection' 'python2' 'mesa' 'ruby' 'gtk-doc')
conflicts=('libwebkit3')
provides=("libwebkit3=${pkgver}")
replaces=('libwebkit3')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/webkit-$pkgver.tar.xz)
sha256sums=('27d28eb721ae4aae2d66e32eda4c11cc3ced53372e74da9ce0e1adc9857fbc22')

build() {
  cd "$srcdir/webkit-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --enable-introspection \
    --disable-silent-rules \
    --libexecdir=/usr/lib/webkitgtk3
  make all stamp-po
}

package() {
  cd "$srcdir/webkit-$pkgver"
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
