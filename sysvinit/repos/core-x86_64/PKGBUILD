# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=sysvinit
pkgver=2.88
pkgrel=6
pkgdesc="Linux System V Init"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/sysvinit"
license=('GPL')
groups=('base')
depends=('util-linux' 'coreutils' 'glibc' 'awk')
install=sysvinit.install
source=(http://download.savannah.gnu.org/releases/sysvinit/${pkgname}-${pkgver}dsf.tar.bz2
        "0001-simplify-writelog.patch"
        "0002-remove-ansi-escape-codes-from-log-file.patch")
sha1sums=('f2ca149df1314a91f3007cccd7a0aa47d990de26'
          '326112c8a9bd24cb45bd4bb2f958a25f0ac4773d'
          'bbecfa7dfa45ac7c37ed8ac59fb53f6a85064b32')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}dsf"

  # FS#30005
  patch -p1 -d "src" -i "${srcdir}/0001-simplify-writelog.patch"
  patch -p1 -d "src" -i "${srcdir}/0002-remove-ansi-escape-codes-from-log-file.patch"

  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}dsf"
  make ROOT="${pkgdir}" install

  # mountpoint is now provided by util-linux instead
  cd "${pkgdir}"
  rm bin/mountpoint
  rm usr/share/man/man1/mountpoint.1
}
