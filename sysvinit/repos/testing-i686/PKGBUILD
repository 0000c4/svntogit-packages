# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=sysvinit
pkgver=2.88
pkgrel=4
pkgdesc="Linux System V Init"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/sysvinit"
license=('GPL')
groups=('base')
depends=('util-linux' 'coreutils' 'glibc' 'awk')
install=sysvinit.install
source=(http://download.savannah.gnu.org/releases/sysvinit/${pkgname}-${pkgver}dsf.tar.bz2
        "Remove-handling-of-special-chars-fix-per-line-buffer.patch")
sha1sums=('f2ca149df1314a91f3007cccd7a0aa47d990de26'
          'cb1180009cc2ff3ba1bc4fa5609f3d279fed9d0e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}dsf"

  # FS#30005
  patch -p1 -d "src" -i "${srcdir}/Remove-handling-of-special-chars-fix-per-line-buffer.patch"

  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}dsf"
  make ROOT="${pkgdir}" install

  # mountpoint is now provided by util-linux instead
  cd "${pkgdir}"
  rm bin/mountpoint
  rm usr/share/man/man1/mountpoint.1
}
