# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=sysvinit
pkgver=2.88
pkgrel=5
pkgdesc="Linux System V Init"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/sysvinit"
license=('GPL')
groups=('base')
depends=('util-linux' 'coreutils' 'glibc' 'awk')
install=sysvinit.install
source=(http://download.savannah.gnu.org/releases/sysvinit/${pkgname}-${pkgver}dsf.tar.bz2
        "simplify-writelog.patch")
sha1sums=('f2ca149df1314a91f3007cccd7a0aa47d990de26'
          'd970e44e8b5574b3b6be709c7682845a974ed1ed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}dsf"

  # FS#30005
  patch -p1 -d "src" -i "${srcdir}/simplify-writelog.patch"

  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}dsf"
  make ROOT="${pkgdir}" install

  # mountpoint is now provided by util-linux instead
  cd "${pkgdir}"
  rm bin/mountpoint
  rm usr/share/man/man1/mountpoint.1
}
