# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>

pkgname=snort
pkgver=2.8.2.1
pkgrel=7
pkgdesc="A lightweight network intrusion detection system"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libpcap>=0.9.8' 'pcre')
backup=(etc/conf.d/snort
	etc/snort/{snort,threshold}.conf
	etc/snort/{confreference,classification}.config)
install=snort.install
source=("http://www.snort.org/dl/current/$pkgname-$pkgver.tar.gz"
        'snort' 
	'snort.conf.d' 
	'http://www.snort.org/pub-bin/downloads.cgi/Download/comm_rules/Community-Rules-CURRENT.tar.gz' 
	'snort.conf.patch')
url="http://www.snort.org"
options=('!makeflags' '!libtool')
md5sums=('b39e784dd8a5cf180aae20e94a7b52dd'
         '361b8b9e40b9af0164f6b3e3da2e8277'
         'b4fb8a68490589cd34df93de7609bfac'
         'f236b8a4ac12e99d3e7bd81bf3b5a482'
         '5a0e91513e05942612d70d36c2983968')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np0 < ${srcdir}/snort.conf.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/etc/snort --with-libpcap-includes=/usr/include/pcap \
  --without-mysql --without-postgresql --without-oracle --without-odbc
  make || return 1
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/{etc/rc.d,etc/snort/rules}

  install -d -m744 -o snort -g snort ${pkgdir}/var/log/snort
  install -D -m644 etc/{*.conf*,*.map} ${pkgdir}/etc/snort
  install -D -m644 ${srcdir}/snort.conf.d ${pkgdir}/etc/conf.d/snort
  install -D -m644 ${srcdir}/rules/*.rules ${pkgdir}/etc/snort/rules
  install -D -m755 ${srcdir}/snort ${pkgdir}/etc/rc.d/snort
}
