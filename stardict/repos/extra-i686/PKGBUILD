# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Neil Lin <neil@vip.url.com.tw>

pkgname=stardict
pkgver=3.0.2
pkgrel=1
pkgdesc='A powerful international dictionary written in Gtk2.'
arch=('i686' 'x86_64')
url='http://stardict.sourceforge.net/'
license=('GPL')
depends=('enchant' 'gtk2' 'libsigc++' 'libsm')
makedepends=('intltool' 'popt' 'gnome-doc-utils')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/stardict/$pkgname-$pkgver.tar.bz2"
        'help-in-destdir.patch')
sha1sums=('301875657cd39b5f25e5933ece22936a28db1230'
          '2016db86bb10782701c94777ebf67455282ca163')

build()
{
	cd "$srcdir/$pkgname-$pkgver"

	export LDFLAGS="${LDFLAGS// -Wl,--as-needed/}"
	patch -p1 -i ../help-in-destdir.patch

	./configure PKG_CONFIG=/usr/bin/pkg-config --prefix=/usr \
		--sysconfdir=/etc --mandir=/usr/share/man \
		--disable-gnome-support --disable-schemas-install \
		--disable-espeak --disable-gucharmap \
		--disable-festival --disable-updateinfo
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}
