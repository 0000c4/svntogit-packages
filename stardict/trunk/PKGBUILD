# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Neil Lin <neil@vip.url.com.tw>

pkgname=stardict
pkgver=3.0.1
pkgrel=2
pkgdesc="A powerful international dictionary written in Gtk2."
arch=(i686 x86_64)
url="http://stardict.sourceforge.net/"
license=('GPL')
depends=('enchant' 'gtk2' 'libsigc++2.0')
makedepends=('perlxml' 'popt')
options=('!libtool' '!emptydirs')
install=stardict.install
source=(http://downloads.sourceforge.net/sourceforge/stardict/$pkgname-$pkgver.tar.bz2 \
	stardict_gcc43.patch sigc++.patch)
md5sums=('93bb2d7825bc9d52a4f9ba4c1c506d1d'
         '9ef740f89d1a798e40855689a88d1c89'
         '5bf0232225dab4e62a8bd08eb2ec581c')

build()
{
   cd $srcdir/$pkgname-$pkgver

   patch -Np1 -i $srcdir/sigc++.patch || return 1
   patch -Np1 -i $srcdir/stardict_gcc43.patch || return 1

   ./configure 	PKG_CONFIG=/usr/bin/pkg-config --prefix=/usr --sysconfdir=/etc \
        --mandir=/usr/share/man --disable-gnome-support --disable-schemas-install \
        --disable-espeak --disable-gucharmap --disable-festival --disable-advertisement \
	--disable-updateinfo
   make || return 1
   make DESTDIR=$pkgdir install || return 1
}
