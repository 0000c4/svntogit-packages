# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Neil Lin <neil@vip.url.com.tw>

pkgname=stardict
pkgver=3.0.3
pkgrel=1
pkgdesc='International dictionary software'
arch=('i686' 'x86_64')
url='http://www.stardict.org/'
license=('GPL')
depends=('enchant' 'gtk2' 'libsigc++' 'libsm')
makedepends=('intltool' 'popt' 'gnome-doc-utils' 'libmysqlclient')
options=('!libtool' '!emptydirs')
source=("http://stardict-3.googlecode.com/files/stardict-${pkgver}.tar.bz2"
        'NetDictRequests.patch'
        'gcc46.patch')
sha1sums=('5043c0918963cd4d40e6d6289353f295766ec74f'
          '97af8fdb73c8f04674234b629c6867254e9bd043'
          '6605a9cdfc4abc2a0ab4462d1228233d34806f0a')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# ugly hack to avoid crashing with empty dictionaries
	patch -p1 -i ../NetDictRequests.patch
	patch -p1 -i ../gcc46.patch
	automake

	./configure \
		PKG_CONFIG=/usr/bin/pkg-config \
		--prefix=/usr \
		--sysconfdir=/etc \
		--mandir=/usr/share/man \
		--disable-gnome-support \
		--disable-schemas-install \
		--disable-espeak \
		--disable-gucharmap \
		--disable-festival \
		--disable-updateinfo \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
