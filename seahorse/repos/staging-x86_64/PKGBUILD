# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=2.32.0
pkgrel=2
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=('GPL')
url="http://projects.gnome.org/seahorse/"
depends=('libgnome-keyring>=2.32.0' 'avahi>=0.6.27' 'libnotify>=0.7.1' 'libsoup>=2.32.0' 'gpgme>=1.2.0' 'desktop-file-utils' 'hicolor-icon-theme' 'gconf>=2.32.0' 'gnome-keyring')
makedepends=('gettext' 'libldap' 'intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.1' 'openssh' 'libsm')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/seahorse/2.32/seahorse-${pkgver}.tar.bz2
        seahorse-2.32.0-libnotify-0.7.patch)
sha256sums=('f55468237246a485317d59e2fbc1b1ef5f5bd4c5a7b9ff6f40c3d921af0ed52d'
            '8f7b2fc0ad1cb572de3c31ed694e5131e8ec6373e9f3e202d98858a12902c467')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/seahorse-2.32.0-libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-ldap --enable-hkp \
      --enable-libnotify \
      --disable-scrollkeeper --disable-update-mime-database
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain seahorse ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
