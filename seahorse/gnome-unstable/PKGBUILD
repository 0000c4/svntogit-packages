# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=2.27.90
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=('GPL')
url="http://seahorse.sourceforge.net/"
depends=('gnome-keyring>=2.27.90' 'avahi>=0.6.25' 'libnotify>=0.4.5' 'libsoup>=2.27.91' 'gpgme>=1.2.0' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('gettext' 'libldap' 'intltool' 'pkgconfig' 'gnome-doc-utils>=0.17.4' 'openssh' 'libsm')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
replaces=('gnome-keyring-manager')
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/seahorse/2.27/seahorse-${pkgver}.tar.bz2
        seahorse-2.26.2-gpgme-lfs.patch)
sha256sums=('db2e479f50b5b6477bd0fb432f4c0a601dd951227ce1c37c9cd246e067e01d7d'
            'a0cd874dc4e2a79559eb4a4a65349c88e4c9925871493041d662e33b6a53a57d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/seahorse-2.26.2-gpgme-lfs.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-ldap --enable-hkp \
      --enable-libnotify \
      --disable-scrollkeeper --disable-update-mime-database || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain seahorse ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
