# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=3.32.2
pkgrel=1
epoch=1
pkgdesc="GNOME application for managing PGP keys."
url="https://wiki.gnome.org/Apps/Seahorse"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gcr libsecret libsoup gpgme gnome-keyring libpwquality)
makedepends=(libldap yelp-tools gobject-introspection vala git meson)
provides=(x11-ssh-askpass)
_commit=1dd5b4d6070e4c10c4c8863235945a028a162503  # gnome-3-32
source=("git+https://gitlab.gnome.org/GNOME/seahorse.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f 3.32.2 1dd5b4d6070e4c10c4c8863235945a028a162503 # Fixup missing tag
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
