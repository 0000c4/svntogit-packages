# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=2.24.0
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=('GPL')
url="http://seahorse.sourceforge.net/"
depends=('gnome-keyring>=2.24.0' 'libglade>=2.6.3' 'avahi>=0.6.23' 'libnotify>=0.4.4' 'libsoup>=2.24.0' 'gpgme' 'desktop-file-utils' 'zip')
makedepends=('gettext' 'libldap' 'perlxml' 'pkgconfig' 'gnome-doc-utils>=0.14.0' 'nautilus>=2.24.0' 'epiphany>=2.24.0' 'gnome-panel>=2.24.0' 'gedit>=2.24.0' 'xulrunner>=1.9.0.3')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
replaces=('gnome-keyring-manager')
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/seahorse/2.24/seahorse-${pkgver}.tar.bz2)
md5sums=('e8a7836c63b3a6f5b6c1311c86b7520b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-ldap --enable-hkp \
	      --enable-gedit --enable-nautilus \
	      --enable-agent --enable-libnotify \
	      --with-gecko=libxul-unstable \
	      --disable-scrollkeeper --disable-update-mime-database || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
