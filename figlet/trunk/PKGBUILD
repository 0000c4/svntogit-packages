# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=figlet
pkgver=2.2.3
pkgrel=1
pkgdesc="A program for making large letters out of ordinary text"
arch=('i686' 'x86_64')
url="http://www.figlet.org/"
license=('BSD')
depends=('glibc' 'sh')
source=(ftp://ftp.figlet.org/pub/figlet/program/unix/${pkgname}-${pkgver}.tar.gz)
md5sums=('c9e49dc83efc59070a00313b936002bf')
sha1sums=('e4c3c279d2747e0e2d80d11b9baf4e548f72e515')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DEFAULTFONTDIR=/usr/share/figlet/fonts all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" BINDIR=/usr/bin MANDIR=/usr/share/man \
    DEFAULTFONTDIR=/usr/share/figlet/fonts install 
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
