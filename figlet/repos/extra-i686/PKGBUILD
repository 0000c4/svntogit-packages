# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=figlet
pkgver=2.2.4
pkgrel=1
pkgdesc="A program for making large letters out of ordinary text"
arch=('i686' 'x86_64')
url="http://www.figlet.org/"
license=('BSD')
depends=('glibc' 'sh')
source=(ftp://ftp.figlet.org/pub/figlet/program/unix/${pkgname}-${pkgver}.tar.gz)
md5sums=('ea048d8d0b56f9c58e55514d4eb04203')
sha1sums=('365f1fce8525c0ff23631661f96534f06822e423')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DEFAULTFONTDIR=/usr/share/figlet/fonts all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" BINDIR=/usr/bin MANDIR=/usr/share/man \
    DEFAULTFONTDIR=/usr/share/figlet/fonts install 
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
