# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=nvidia-lts
pkgver=275.09.07
_kernver='2.6.32-lts'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-lts"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-lts>=2.6.32' 'kernel26-lts<2.6.33' "nvidia-utils=$pkgver")
makedepends=('kernel26-lts-headers>=2.6.32' 'kernel26-lts-headers<2.6.33')
provides=('nvidia')
license=('custom')
install=nvidia-lts.install
options=(!strip)
if [ "$CARCH" = "i686" ]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('d779057af3a2f96a05ddda201606618b')
elif [ "$CARCH" = "x86_64" ]; then
  _arch='x86_64'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('f75df248f7bf32199ed88db2f080cbfa')
fi

build() {
  cd "${srcdir}"
  sh ${_pkg}.run --extract-only
  cd ${_pkg}/kernel
  make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
    "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
  gzip "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
  install -d -m755 "${pkgdir}/etc/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-lts.conf"
  install -D -m644 "${srcdir}/${_pkg}/LICENSE" "${pkgdir}/usr/share/licenses/nvidia-lts/LICENSE"
}
