# $Id$
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=clamav
pkgver=0.97.4
pkgrel=2
pkgdesc='Anti-virus toolkit for Unix'
url='http://www.clamav.net/'
license=('GPL')
options=('!libtool')
arch=('i686' 'x86_64')
depends=('bzip2' 'libltdl')
backup=('etc/clamav/clamd.conf' 'etc/clamav/freshclam.conf' 'etc/conf.d/clamav')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'rc.d'
        'conf.d'
        'logrotate'
        'tmpfiles.d'
        'config.patch')
sha1sums=('56f90cf8a73acba8f97beca86b42c65c3923935d'
          'c9d508c1e5972f0f849d8694c1872455fa9e74de'
          'cb116cdab49a810381a515cbcfb6a6c148547f07'
          '7cace58743a36dae3e63e5e0c6cc73ea5ef9a6ee'
          'a224ea9b4d0f4f196827347d54bed51e11c197ea'
          '1c8ef193919b041135115170acd6313f008de808')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../config.patch

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Make sure conf files get installed, because make install
	# doesn't do that if clamav is already installed upon building.
	install -D -m644 etc/clamd.conf "${pkgdir}/etc/clamav/clamd.conf"
	install -D -m644 etc/freshclam.conf "${pkgdir}/etc/clamav/freshclam.conf"

	install -D -m644 ../tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/clamav.conf"
	install -D -m644 ../logrotate "${pkgdir}/etc/logrotate.d/clamav"
	install -D -m644 ../conf.d "${pkgdir}/etc/conf.d/clamav"
	install -D -m755 ../rc.d "${pkgdir}/etc/rc.d/clamav"

	# Un-distribute databases to require freshclam.
	rm "${pkgdir}"/var/lib/clamav/*.cvd
}
