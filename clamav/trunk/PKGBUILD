# $Id$
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=clamav
pkgver=0.99.2
pkgrel=3
pkgdesc='Anti-virus toolkit for Unix'
url='http://www.clamav.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('libmilter' 'llvm')
depends=('bzip2' 'libltdl' 'llvm-libs' 'libxml2' 'curl')
validpgpkeys=('B964E6D7BC7D7C82CCB8D45840B8EA2364221D53'
              'F79FB2D08751574C5D3FDFFBB3D5342C260429A0')
source=("http://www.clamav.net/downloads/production/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'logrotate'
        'tmpfiles.d'
        'clamd.conf'
        'freshclam.conf'
        'clamd.service'
        'freshclamd.service'
        'Add-support-for-LLVM-3.7.patch'
        'Add-support-for-LLVM-3.8.patch'
        'Add-support-for-LLVM-3.9.patch')
sha256sums=('167bd6a13e05ece326b968fdb539b05c2ffcfef6018a274a10aeda85c2c0027a'
            'SKIP'
            'ce4b9b8c300614641af600c9a73b52a00ee8e47ccc9f91b2428a113b0ecff21b'
            '0a61abee3b9bba94126afe3344e7d8e82da5120ca6dbd2b413b10f75da5b0b0d'
            'afdb95f93f7e11e163d368caccd5f6814206c6f0d74816b4f712c0267b50572a'
            '127b39e13525ffb4242198cfb76f99d4d517e5f2fd9fa8dcad3f31fc9f82f952'
            'e376ab0cefeefa5ac5f1cd611718452ea8646198e854aca3cc0026f5ffe58fb4'
            'dd5ff6c79ee360da5f2221c4d9110a2a8886d86293f6c93c16bf74fdb126593c'
            '2862fe2b5579c5bd59041c22326155c135946613ece1c3f2a6bd6f25b7af99a4'
            'c8d710f1d7f71bd754a7c4f58a1e1b6a23295b5eb18f7df3a098697e52e38b96'
            'e71225e55b42a06741feaaa303133e947a98898751994645f5b34dd6503b61df')

backup=('etc/clamav/clamd.conf'
        'etc/clamav/freshclam.conf'
        'etc/logrotate.d/clamav')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i ../Add-support-for-LLVM-3.7.patch
	patch -Np1 -i ../Add-support-for-LLVM-3.8.patch
	patch -Np1 -i ../Add-support-for-LLVM-3.9.patch
	autoreconf -vi libclamav/c++
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# --disable-zlib-vcheck because the configure script thinks that
	# zlib 1.2.11 is older than 1.2.2
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav \
		--disable-zlib-vcheck \
		--enable-milter \
		--with-system-llvm \
		--with-llvm-linking=dynamic
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../clamd.conf "${pkgdir}"/etc/clamav/clamd.conf
	install -Dm644 ../freshclam.conf "${pkgdir}"/etc/clamav/freshclam.conf
	install -Dm644 ../freshclamd.service "${pkgdir}"/usr/lib/systemd/system/freshclamd.service
	install -Dm644 ../clamd.service "${pkgdir}"/usr/lib/systemd/system/clamd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/clamav.conf
	install -Dm644 ../logrotate "${pkgdir}"/etc/logrotate.d/clamav

	install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
}
