# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Adri√† Arrufat (archdria) <adria dot arrufat at protonmail ldot ch>

pkgname=libhttpseverywhere
pkgver=0.6.2
pkgrel=1
pkgdesc="Leverage the power of HTTPS Everywhere"
url="https://git.gnome.org/browse/libhttpseverywhere"
arch=(i686 x86_64)
license=(LGPL3)
depends=(libgee libxml2 libarchive libsoup glib2 json-glib)
makedepends=(meson vala gobject-introspection git)
_commit=5245345227fb62d2aad00bbb624273977e12f325  # tags/0.6.2^0
source=("git+https://git.gnome.org/browse/libhttpseverywhere#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname -Denable_valadoc=true
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
