# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Adri√† Arrufat (archdria) <adria dot arrufat at protonmail ldot ch>

pkgname=libhttpseverywhere
pkgver=0.4.3
pkgrel=1
pkgdesc="library to use HTTPSEverywhere in desktop applications"
url="https://git.gnome.org/browse/libhttpseverywhere"
arch=(i686 x86_64)
license=(LGPL3)
depends=(libgee libxml2 libarchive libsoup glib2 json-glib)
makedepends=(meson valadoc gobject-introspection git)
_commit=83d4f4c75d97f3d03eeb43837b0ee7ff0adddc2b  # tags/0.4.3^0
source=("git+https://git.gnome.org/browse/libhttpseverywhere#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

check() {
  cd build
  ninja test
}

build() {
  cd build
  export LANG=en_US.UTF-8
  meson --prefix=/usr --buildtype=release ../$pkgname \
    -Denable_valadoc=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
