# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=rpcbind
pkgver=0.2.0
pkgrel=7
pkgdesc="portmap replacement which supports RPC over various protocols"
arch=(i686 x86_64)
depends=('bash' 'glibc' 'libtirpc')
url="http://rpcbind.sourceforge.net"
license=('custom')
replaces=('portmap')
source=(#http://downloads.sourceforge.net/sourceforge/rpcbind/rpcbind-0.2.0.tar.bz2
        ftp://ftp.archlinux.org/other/rpcbind/rpcbind-0.2.0.tar.gz
        0001-rpcbind-add-support-for-systemd-socket-activation.patch
        rpcbind-sunrpc.patch
        rpcbind)
md5sums=('1cb086aa6503b97b55382c224c7c77d4'
         'df50ef1e5520ab13d53143899eef82f5'
         'c02ac36a98baac70b8a26190524b7b73'
         '78a963654f57cbb209e228884767836e')

build() {
  cd $srcdir/$pkgname-$pkgver
  # patch for iana services file
  patch -Np1 -i ../rpcbind-sunrpc.patch
  # add systemd support
  patch -Np1 -i ../0001-rpcbind-add-support-for-systemd-socket-activation.patch
  ./configure --prefix=/usr --enable-warmstarts --with-statedir=/run
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  # install missing man page - https://bugs.archlinux.org/task/21271
  install -m644 man/rpcinfo.8 $pkgdir/usr/share/man/man8/
  # install daemon 
  install -D -m755 $srcdir/rpcbind $pkgdir/etc/rc.d/rpcbind
  # install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/rpcbind/COPYING
}
