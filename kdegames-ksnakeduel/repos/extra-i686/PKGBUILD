# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksnakeduel
pkgver=17.04.3
pkgrel=1
pkgdesc="A simple snake duel game"
url="https://kde.org/applications/games/ksnakeduel/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/${pkgver}/src/ksnakeduel-${pkgver}.tar.xz"{,.sig})
sha256sums=('980bf0ccff4d77ebf7a1b4547cea6981fa58a988d2f363082ba5df6451f66223'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../ksnakeduel-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
