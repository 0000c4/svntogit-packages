# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksnakeduel
pkgver=16.12.2
pkgrel=1
pkgdesc="A simple snake duel game"
url="http://kde.org/applications/games/ksnakeduel/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksnakeduel-${pkgver}.tar.xz"{,.sig})
sha1sums=('f7612d087277ee21de1070fe2e90404fd692e565'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../ksnakeduel-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
