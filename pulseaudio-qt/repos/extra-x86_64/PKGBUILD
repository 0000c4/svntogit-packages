# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=pulseaudio-qt
pkgver=1.0.0
pkgrel=1
pkgdesc='Qt bindings for libpulse'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base libpulse)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        pulseaudio-qt-deps.patch::"https://cgit.kde.org/pulseaudio-qt.git/patch/?id=58beeb48")
sha256sums=('a0a4f2793e642e77a5c4698421becc8c046c426811e9d270ff2a31b49bae10df'
            'SKIP'
            '6477de3934da6ab8086cb16fbe2df0ce6799c10cc02f6971999de2c6fa46aef4')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E')  # Jonathan Riddell <jr@jriddell.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../pulseaudio-qt-deps.patch # Remove unused dependencies
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_QCH=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
