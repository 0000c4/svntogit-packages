# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany-extensions
pkgver=2.24.0
pkgrel=1
url="http://www.gnome.org/projects/epiphany/"
pkgdesc="Various extentions for the Epiphany web browser"
arch=(i686 x86_64)
license=('GPL')
depends=('epiphany>=2.24.0.1' 'xulrunner>=1.9.0.3' 'python>=2.5.2' 'opensp')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.14.0')
options=('!libtool' '!emptydirs')
install=epiphany-extensions.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b8bbe16e0bc5b2549c9bea5edf88762a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
	      --with-gecko=libxul-embedding \
	      --with-extensions=really-all --disable-scrollkeeper || return 1
  make || return 1

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
