# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgnomekbd
pkgver=3.1.90
pkgrel=1
pkgdesc="Gnome keyboard library"
url="http://gswitchit.sourceforge.net"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libxklavier>=5.1' 'gtk3' 'dconf')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool' '!emptydirs')
install=libgnomekbd.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('bbe4ee869c6d3feabe9610255918e632dcfc873a76c1a462b92be8154456afc6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
