# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pep517
pkgname=('python-pep517' 'python2-pep517')
pkgver=0.7.0
pkgrel=1
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
makedepends=('python-setuptools' 'python2-setuptools' 'python-toml' 'python2-toml'
             'python-importlib-metadata' 'python2-importlib-metadata' 'python-zipp' 'python2-zipp')
checkdepends=('python-pytest' 'python-pytest-flake8' 'python-testpath' 'python-pip')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('6c59dd41ec05e5f3b5032ed2d262f4fc4ba51d31cd7e8115f8f128a3e56b9e8ba1ec6140f37be6370561618ab8ad9845422b144db09e6102f8178a2c52756b2a')

prepare() {
  cp -a pep517-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pep517-$pkgver
  python setup.py build

  cd "$srcdir"/pep517-$pkgver-py2
  python2 setup.py build
}

check() {
  cd pep517-$pkgver
  # https://github.com/pypa/pep517/issues/35
  pytest --deselect setup.py
}

package_python-pep517() {
  depends=('python-toml' 'python-importlib-metadata' 'python-zipp')

  cd pep517-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pep517() {
  depends=('python2-toml' 'python2-importlib-metadata' 'python2-zipp')

  cd pep517-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
