# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pep517
pkgname=('python-pep517' 'python2-pep517')
pkgver=0.3
pkgrel=1
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytoml' 'python2-pytoml')
checkdepends=('python-pytest' 'python2-pytest' 'python-testpath')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('137fd97d13bd535e2a2340c7762bd8121dfff92db165b2f63330a624d0dcc75b7a2af309a808800f72ecce13e2cc6ad35e879b261a8d45c79966b6b42dbc256a')

prepare() {
  cp -a pep517-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pep517-$pkgver
  python setup.py build
 
  cd "$srcdir"/pep517-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pep517-$pkgver
  pytest

  cd "$srcdir"/pep517-$pkgver-py2
  # No python2-testpath
  # pytest2
}
 
package_python-pep517() {
  depends=('python-pytoml')
 
  cd pep517-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
 
package_python2-pep517() {
  depends=('python2-pytoml')
 
  cd pep517-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
