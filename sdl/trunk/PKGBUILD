# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sdl
pkgver=1.2.14
pkgrel=2
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('LGPL')
depends=('glibc' 'libxext' 'libxrender' 'libx11')
makedepends=('alsa-lib' 'esound' 'mesa')
options=('!libtool')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
        sdl-1.2.14-fix-mouse-clicking.patch)
md5sums=('e52086d1b508fa0b76c52ee30b55bec4'
         '04d8c179f125e04bcd4c9d60e013c2d7')

build() {
  cd ${srcdir}/SDL-${pkgver}
  patch -Np1 -i ${srcdir}/sdl-1.2.14-fix-mouse-clicking.patch
  ./configure --prefix=/usr --disable-nasm --enable-alsa --enable-esd \
              --with-x --disable-rpath
  make || return 1
}

package() {
  cd ${srcdir}/SDL-${pkgver}
  make DESTDIR=${pkgdir} install
}
