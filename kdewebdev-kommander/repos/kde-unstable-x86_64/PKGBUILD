# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdewebdev-kommander
pkgver=16.11.80
pkgrel=1
arch=(i686 x86_64)
pkgdesc="Executor for Kommander dialogs"
url='http://www.kde.org'
license=(GPL LGPL FDL)
groups=(kde-applications kdewebdev)
depends=(kdebase-runtime)
makedepends=(cmake automoc4)
source=("http://download.kde.org/stable/applications/${pkgver}/src/kommander-$pkgver.tar.xz"{,.sig})
sha1sums=('d3676a5d6da8b2a654650551364b87aed13e75ac'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kommander-${pkgver} \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DKDE4_BUILD_TESTS=OFF \
	-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
