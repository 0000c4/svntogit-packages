# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.25.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('solid' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet' 'desktop-file-utils' 'kinit')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        kdebug-366795.patch::"https://quickgit.kde.org/?p=kio.git&a=commitdiff&h=fcbfb61a&o=plain")
md5sums=('0077d90029c34f734fb661912f2213d1'
         'f8fcb4c3de5d3f1f2a9a2fc30bed6965')

prepare() {
  mkdir -p build

# Fix pasting files in SMB/MTP paths http://bugs.kde.org/show_bug.cgi?id=366795
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-366795.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
