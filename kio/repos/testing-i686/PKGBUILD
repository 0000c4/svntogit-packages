# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.20.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('solid' 'karchive' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet'
         'desktop-file-utils')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
groups=('kf5')
install=kio.install
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
	kdebug-360488.patch::"https://quickgit.kde.org/?p=kio.git&a=commitdiff&h=bf37e9aa&o=plain")
md5sums=('f700e4089783d2420406b5024fef9f65'
         '6e6f61fab93c7460673c6280bc215c0a')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix crash in rename dialog http://bugs.kde.org/show_bug.cgi?id=360488
  patch -p1 -i ../kdebug-360488.patch 
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
