# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.74.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries' 'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
         kio-fix-kdeconnect-1.patch::"https://invent.kde.org/frameworks/kio/-/commit/43f76441.patch"
         kio-fix-kdeconnect-2.patch::"https://invent.kde.org/frameworks/kio/-/commit/dbc0d532.patch")
sha256sums=('e567872d42c92dde79189ad0ca32913dbc0ae09331715c5d33052223e1d2fdbf'
            'SKIP'
            '07f2482a4153f769e86f3f528de43107298276c12ef24a874a76540dc21ba5e5'
            '8752f065ff7d807ad5567067e9d729590d615a50d4b6ab6898ff9e06c3e2b308')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
# Fix navigating to kdeconnect://
  patch -d $pkgname-$pkgver -p1 -i ../kio-fix-kdeconnect-1.patch
  patch -d $pkgname-$pkgver -p1 -i ../kio-fix-kdeconnect-2.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
