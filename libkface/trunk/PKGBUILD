# $Id: PKGBUILD 226446 2014-11-19 17:09:45Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkface
pkgver=15.12.2
pkgrel=2
pkgdesc="A Qt/C++ wrapper around LibFace library to perform face recognition and detection over pictures"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkface"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(qt5-base opencv)
makedepends=(extra-cmake-modules)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	libkface-opencv3.1.patch::"https://git.reviewboard.kde.org/r/126833/diff/raw/")
sha1sums=('374d960f3c4072c2268547c6fe11d111d8e4bce1'
          '35f1b3aa533b384a7a8da460209d843b7c49fa30')

prepare() {
  mkdir -p build

# Fix build with opencv 3.1 https://bugs.kde.org/show_bug.cgi?id=358910
  cd $pkgname-$pkgver
  patch -p1 -i ../libkface-opencv3.1.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DENABLE_OPENCV3=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
