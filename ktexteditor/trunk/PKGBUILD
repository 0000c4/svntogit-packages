# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.39.0
pkgrel=2
pkgdesc='Advanced embeddable text editor'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kparts' 'syntax-highlighting' 'libgit2' 'editorconfig-core-c')
makedepends=('extra-cmake-modules' 'python' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
        ktexteditor-fix-indentation.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=aeebeadb")
sha256sums=('c5d9c67532ab6f3abee8ae2623ff21e6ca5280d5ea9ca5d23bdb55f937e565ae'
            'SKIP'
            'a87ef171d67e2dca50a1ec979d316c58e540a46cb135ffd9480ea9237b419b69')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../ktexteditor-fix-indentation.patch # Fix broken indentation in some languages
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
