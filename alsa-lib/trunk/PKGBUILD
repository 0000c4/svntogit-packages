# $Id$
# Maintainer: judd <jvinet@zeroflux.org>

pkgname=alsa-lib
pkgver=1.0.27
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
depends=('glibc')
optdepends=('python2: for python smixer plugin')
makedepends=('python2')
license=('GPL')
options=(!libtool)
source=(ftp://ftp.alsa-project.org/pub/lib/$pkgname-$pkgver.tar.bz2
        alsa-lib-1.0.27-segfault.patch
        alsa-lib-1.0.27-kernel.patch
        alsa-lib-1.0.27-inline.patch
        alsa-lib-1.0.27-inline2.patch)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # add upstream patches
  patch -Np1 -i "$srcdir/alsa-lib-1.0.27-segfault.patch"
  patch -Np1 -i "$srcdir/alsa-lib-1.0.27-kernel.patch"
  patch -Np1 -i "$srcdir/alsa-lib-1.0.27-inline.patch"
  patch -Np1 -i "$srcdir/alsa-lib-1.0.27-inline2.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-pythonlibs="-lpthread -lm -ldl -lpython2.7" --with-pythonincludes=-I/usr/include/python2.7
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
md5sums=('f090c7dbd0fe57d786d82e411eac2d08'
         '755cfcd116c28feac437eb6b5bdc51bb'
         '277e2bc587de25e3294a0f597ae9246e'
         'db14deccb57d8dbcfc6f6d22bed45c1e'
         '92ca1fec0cb99bfc302896cf9393a936')
