# $Id$
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-lib
pkgver=1.1.4
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
depends=('glibc')
optdepends=('python2: for python smixer plugin')
makedepends=('python2')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/lib/$pkgname-$pkgver.tar.bz2
        fix_binary_incompat.patch::"http://git.alsa-project.org/?p=alsa-lib.git;a=patch;h=eae572ef5e3acd16a75815cf5e1799882bf9c943")


prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../fix_binary_incompat.patch  # FS#54214
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-pythonlibs="-lpthread -lm -ldl -lpython2.7" --with-pythonincludes=-I/usr/include/python2.7
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
md5sums=('824082c6b2e341500902973dcc47d2e9'
         '6b9eb50091366bcd8fa9068db88edbf8')
