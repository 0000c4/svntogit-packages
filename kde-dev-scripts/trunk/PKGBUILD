# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-dev-scripts
pkgver=16.04.3
pkgrel=1
pkgdesc='Scripts and setting files useful during development of KDE software'
url='https://www.kde.org/applications/development/'
arch=('any')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs4support')
replaces=('kdesdk-dev-scripts')
conflicts=('kdesdk-dev-scripts')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('520db4c22f9930624647f7c5b6423273b44a78ff')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "$pkgdir"/usr/bin/{zonetab2pot.py,kde-systemsettings-tree.py,reviewboard-am}
  sed -i 's|#! /usr/bin/env python|#!/usr/bin/env python2|' \
    "$pkgdir"/usr/bin/{kdelnk2desktop.py,kde_generate_export_header}

# Install additional files
  cd "$srcdir"/$pkgname-$pkgver
  mkdir -p "$pkgdir"/usr/share/kde-dev-scripts
  cp -r kde-devel-* gdb "$pkgdir"/usr/share/kde-dev-scripts
}
