# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=logrotate
pkgver=3.11.0
pkgrel=1
pkgdesc="Rotates system logs automatically"
arch=('i686' 'x86_64')
url="https://github.com/logrotate/logrotate"
license=('GPL')
groups=('base')
depends=('popt' 'gzip' 'acl')
backup=('etc/logrotate.conf')
source=("https://github.com/logrotate/logrotate/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        'paths.patch'
        'logrotate.conf'
        logrotate.{timer,service})
md5sums=('3a9280e4caeb837427a2d54518fbcdac'
         'SKIP'
         'e76526bcd6fc33c9d921e1cb1eff1ffb'
         '94dae4d3eded2fab9ae879533d3680db'
         '287c2ad9b074cb5478db7692f385827c'
         '85560be5272ed68a88bb77a0a2293369')
validpgpkeys=('992A96E075056E79CD8214F9873DB37572A37B36')

build() {
	cd "$srcdir/${pkgname}-${pkgver}"

	patch -p0 -i "$srcdir/paths.patch"

	./autogen.sh
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man \
		--with-acl
	make
}

check() {
	cd "$srcdir/${pkgname}-${pkgver}"

	make test
}

package() {
	cd "$srcdir/${pkgname}-${pkgver}"

	make DESTDIR="$pkgdir" install

	install -dm755 "$pkgdir/etc/logrotate.d"
	install -Dm644 "$srcdir/logrotate.conf" "$pkgdir/etc/logrotate.conf"

	install -D -m644 ${srcdir}/logrotate.timer ${pkgdir}/usr/lib/systemd/system/logrotate.timer
	install -D -m644 ${srcdir}/logrotate.service ${pkgdir}/usr/lib/systemd/system/logrotate.service
	install -d -m755 ${pkgdir}/usr/lib/systemd/system/multi-user.target.wants
	ln -s ../logrotate.timer ${pkgdir}//usr/lib/systemd/system/multi-user.target.wants/logrotate.timer
}
