# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-ati
pkgver=6.9.0
pkgrel=1
_mesaver="7.1"
pkgdesc="X.org ati video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libdrm>=2.3.1' 'libgl>=7.1-2')
makedepends=('pkgconfig' 'xorg-server>=1.4.99.906' 'imake' 'mesa>=7.1'
	     'glproto>=1.4.9' 'xf86driproto' 'diffutils')
conflicts=('xorg-server<1.4.99.906')
groups=('xorg-video-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
       http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${_mesaver}.tar.bz2
       mesa-7.1-link-shared.patch)
md5sums=('afa4873f58ae6fa31fedeb9ed31531db'
         '6bff7f532d16f90f944a400c8bd7074d'
         'f0baa948d9810f268413111ee439d24b')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --enable-dri || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  cd ${srcdir}/Mesa-${_mesaver}
  patch -Np1 -i ${srcdir}/mesa-7.1-link-shared.patch || return 1
  export CFLAGS="${CFLAGS} -ffast-math"
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=radeon,r200,r300 \
    --enable-glx-tls \
    --disable-ttm-api \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR=${pkgdir} install || return 1
  rm -f ${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so
  rm -rf ${pkgdir}/usr/include
  rm -rf ${pkgdir}/usr/lib/pkgconfig
}
