# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2010m
pkgrel=1
_tzcode=2010m
_tzdata=2010m
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
depends=()
makedepends=()
optdepends=('sh: required by tzselect')
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch)
md5sums=('dee44c5b5ff70c424f62333b4c6d09a2'
         '9e33cb126c8cb6673f39390d347fb0e2'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz
  tar -xf ${pkgname}${_tzdata}.tar.gz

  patch -Np1 -i "${srcdir}/Makefile.patch"

  make
}

package() {
  cd ${srcdir}
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
