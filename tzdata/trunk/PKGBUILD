# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2012b
pkgrel=1
_tzcode=2012b
_tzdata=2012b
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="hhttp://www.iana.org/time-zones"
license=('GPL')
depends=()
makedepends=()
optdepends=('bash: required by tzselect')
options=('!emptydirs')
source=(#http://www.iana.org/time-zones/repository/releases/tzcode${_tzcode}.tar.gz
		ftp://munnari.oz.au/pub/tzcode${_tzcode}.tar.gz
        #http://www.iana.org/time-zones/repository/releases/${pkgname}${_tzdata}.tar.gz
        ftp://munnari.oz.au/pub/${pkgname}${_tzdata}.tar.gz
        Makefile.patch)
md5sums=('6137322ffd36e1fd5128885be1c57008'
         '0615fd29def380a917e528433c820368'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz
  tar -xf ${pkgname}${_tzdata}.tar.gz

  patch -Np1 -i "${srcdir}/Makefile.patch"

  make
}

check() {
  cd ${srcdir}
  make -k check # || /bin/true
}

package() {
  cd ${srcdir}
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
