# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2010n
pkgrel=1
_tzcode=2010n
_tzdata=2010n
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
depends=()
makedepends=()
optdepends=('sh: required by tzselect')
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch)
md5sums=('a7a776a5713831993e814fe0c05f9fd5'
         'b5eefb4986866895ab84f52220f1dfa6'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz
  tar -xf ${pkgname}${_tzdata}.tar.gz

  patch -Np1 -i "${srcdir}/Makefile.patch"

  make
}

package() {
  cd ${srcdir}
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
