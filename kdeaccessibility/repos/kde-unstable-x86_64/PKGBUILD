# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeaccessibility
pkgname=('kdeaccessibility-colorschemes'
         'kdeaccessibility-iconthemes'
         'kdeaccessibility-kmag'
         'kdeaccessibility-kmousetool'
         'kdeaccessibility-kmouth'
         'kdeaccessibility-kttsd')
pkgver=4.2.91svn979380
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-runtime' 'kdelibs' 'festival')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('fe3573fc81225b408381a1b94f5fe06d')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeaccessibility-colorschemes() {
	pkgdesc=''
	cd $srcdir/build/ColorSchemes
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-iconthemes() {
	pkgdesc=''
	cd $srcdir/build/IconThemes
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmag() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeaccessibility.install'
	cd $srcdir/build/kmag
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmag
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmousetool() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeaccessibility.install'
	cd $srcdir/build/kmousetool
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmousetool
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmouth() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeaccessibility.install'
	cd $srcdir/build/kmouth
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmouth
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kttsd() {
	pkgdesc='Text To Speech Daemon'
	depends=('kdelibs' 'kdebase-runtime' 'festival')
	install='kdeaccessibility.install'
	cd $srcdir/build/kttsd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kttsd
	make DESTDIR=$pkgdir install
}
