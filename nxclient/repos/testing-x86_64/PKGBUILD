# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org> 
pkgname=nxclient
pkgver=3.2.0.13
pkgrel=2
pkgdesc="Nomachine's nxclient for nx servers"
arch=(i686 x86_64)
url="http://nomachine.com"
makedepends=('rpmextract')
license=('custom:nomachine')
depends=('cups' 'libcups' 'nx-common' 'libxext' 'libxft' 'xorg-xauth')
[ "$CARCH" = "x86_64" ] && source=(http://64.34.161.181/download/3.2.0/Linux/$pkgname-3.2.0-13.x86_64.rpm nomachine.key)
[ "$CARCH" = "x86_64" ] && md5sums=('c5dab959aba1858459c4ce511cf4427b'
                                    'cb5df74aff5160eab9cd4313019ae228')
[ "$CARCH" = "i686" ] && source=(http://64.34.161.181/download/3.2.0/Linux/$pkgname-3.2.0-14.i386.rpm nomachine.key)
[ "$CARCH" = "i686" ] &&  md5sums=('e6fe204ec88d5801e21084ac2e6b1211'
                                   'cb5df74aff5160eab9cd4313019ae228')
install=nxclient.install

build() {
  cd $startdir/src
  rpmextract.sh *.rpm
  rm usr/NX/bin/nxssh usr/NX/bin/nxesd
  rm -r usr/NX/lib
  rm usr/NX/share/keys/server.id_dsa.key
  sed -i -e 's,/usr/NX,/opt/NX,g' etc/profile.d/nx.sh etc/profile.d/nx.csh
  sed -i -e 's,/usr/NX/,/opt/NX/,g' $startdir/src/usr/NX/share/applnk/network/*.desktop
  #sed -i -e 's,/bin/nxclient,/bin/nxclient-nomachine,g' $startdir/src/usr/NX/share/applnk/network/*.desktop

  install -D -m 755 $startdir/src/etc/profile.d/nx.sh $startdir/pkg/etc/profile.d/nx.sh 
  install -D -m 755 $startdir/src/etc/profile.d/nx.csh $startdir/pkg/etc/profile.d/nx.csh 
  install -D -m 644 $startdir/src/usr/NX/share/applnk/network/nxclient-wizard.desktop $startdir/pkg/usr/share/applications/nxclient-wizard.desktop
  install -D -m 644 $startdir/src/usr/NX/share/applnk/network/nxclient.desktop $startdir/pkg/usr/share/applications/nxclient.desktop
  install -D -m 644 $startdir/src/usr/NX/share/applnk/network/nxclient-admin.desktop $startdir/pkg/usr/share/applications/nxclient-admin.desktop
  rm -r $startdir/src/usr/NX/share/applnk
  cd usr
  mkdir -p $startdir/pkg/opt
  mv NX $startdir/pkg/opt
  # add default key file
  install -D -m 644 $startdir/src/nomachine.key $startdir/pkg/opt/NX/share/keys/server.id_dsa.key
  #mv $startdir/pkg/opt/NX/bin/nxclient $startdir/pkg/opt/NX/bin/nxclient-nomachine
  # add license file
  mkdir -p $startdir/pkg/usr/share/licenses/nxclient
  mv $startdir/pkg/opt/NX/share/documents/client/license-info $startdir/pkg/usr/share/licenses/nxclient/LICENSE
}
