# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=vpnc
_vpncver=0.5.3
pkgver=$_vpncver.svn514
pkgrel=1
pkgdesc="VPN client for cisco3000 VPN Concentrators"
url="http://www.unix-ag.uni-kl.de/~massar/vpnc/"
license=('GPL')
depends=('libgcrypt' 'openssl' 'iproute2')
optdepends=('openresolv: Let vpnc manage resolv.conf')
arch=('i686' 'x86_64')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.xz"
        'vpnc.conf')
backup=('etc/vpnc/default.conf')
md5sums=('f5b9cab940732d21fcb853a25de020c8'
         'a3f4e0cc682f437e310a1c86ae198e45')

mktarball() {
  [[ -d tarball ]] || mkdir tarball

  pushd tarball

  svn co http://svn.unix-ag.uni-kl.de/vpnc/trunk
  _svnrev=$(cd trunk >/dev/null; svn info | awk '/^Revision:/ { print $2 }')

  git clone git://git.infradead.org/users/dwmw2/vpnc-scripts.git
  cp vpnc-scripts/vpnc-script trunk/

  find trunk/ -type d -name .svn -exec rm -rf {} +
  mv trunk "$pkgname-$_vpncver.svn$_svnrev"
  tar cJf "../$pkgname-$_vpncver.svn$_svnrev.tar.xz" "$pkgname-$_vpncver.svn$_svnrev/"

  popd
  rm -rf tarball
}

build() {
  cd "$pkgname-$pkgver"

  # Build hybrid support
  sed -i 's|^#OPENSSL|OPENSSL|g' Makefile

  # fix resolvconf location for community/openresolv
  sed -i 's|/sbin/resolvconf|/usr&|g' vpnc-script

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  rm -f "$pkgdir"/etc/vpnc/vpnc.conf
  install -D -m644 "$srcdir"/vpnc.conf "$pkgdir"/etc/vpnc/default.conf
}
