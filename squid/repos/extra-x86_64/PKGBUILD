# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=squid
pkgver=2.7.STABLE7
pkgrel=1
pkgdesc="A full-featured Web proxy cache server."
arch=(i686 x86_64)
url="http://www.squid-cache.org"
depends=('openssl' 'pam' 'cron' 'perl')
makedepends=('libcap>=2.16')
license=('GPL')
backup=(etc/squid/squid.conf etc/squid/mime.conf etc/conf.d/squid)
install=$pkgname.install
source=(http://www.squid-cache.org/Versions/v2/2.7/${pkgname}-${pkgver}.tar.bz2
        squid-makefiles.patch squid squid.conf.d squid.pam
        squid.cron fix-for-new-cap-headers.patch)
md5sums=('c18b0371fca813d5e7c7e0baf87baa22'
         '082e53bb6e2c79bc44ac95c3d69145da'
         'd213b0cc1db72b749bb8c88716fdab39'
         '81f9a446d143f42c6a3f30304e91c39f'
         '270977cdd9b47ef44c0c427ab9034777'
         '5e17df989e2a74e869790c066f61225b'
         '315eea1079de827dd7eb94be2470132f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/squid-makefiles.patch
  patch -Np0 -i ${srcdir}/fix-for-new-cap-headers.patch

  ./configure --prefix=/usr --datadir=/usr/share/squid \
      --sysconfdir=/etc/squid --libexecdir=/usr/lib/squid \
      --localstatedir=/var --enable-auth="basic,digest,ntlm" \
      --enable-removal-policies="lru,heap" \
      --enable-digest-auth-helpers="password" \
      --enable-storeio="aufs,ufs,diskd,coss,null" \
      --enable-basic-auth-helpers="getpwnam,YP,NCSA,SMB,MSNT,PAM,multi-domain-NTLM" \
      --enable-external-acl-helpers="ip_user,unix_group,wbinfo_group" \
      --enable-ntlm-auth-helpers="SMB,fakeauth,no_check" \
      --enable-delay-pools --enable-arp-acl --enable-ssl \
      --enable-linux-netfilter --enable-ident-lookups \
      --enable-useragent-log --enable-cache-digests --enable-referer-log \
      --enable-async-io --enable-truncate --enable-arp-acl \
      --enable-htcp --enable-carp --enable-epoll --with-maxfd=4096
#some versions have build problems with both async and truncate enabled:
#      --enable-async-io --enable-arp-acl \

  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  rm -rf ${pkgdir}/var
  mv ${pkgdir}/usr/bin/Run* ${startdir}/pkg/usr/lib/squid
  mkdir -p ${pkgdir}/var/cache/squid
  mkdir -p ${pkgdir}/var/log/squid
  install -D -m755 ${srcdir}/squid ${pkgdir}/etc/rc.d/squid
  install -D -m755 ${srcdir}/squid.cron ${pkgdir}/etc/cron.weekly/squid
  install -D -m644 ${srcdir}/squid.conf.d ${pkgdir}/etc/conf.d/squid
  install -D -m644 ${srcdir}/squid.pam ${pkgdir}/etc/pam.d/squid
  install -D -m644 ${pkgdir}/etc/squid/squid.conf \
                   ${pkgdir}/etc/squid/squid.conf.default
}
# vim: ts=2 sw=2 et ft=sh
