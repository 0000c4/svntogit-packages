# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=('python-mako' 'python2-mako')
pkgver=0.7.2
pkgrel=1
pkgdesc="Hyperfast and lightweight templating for the Python2 platform."
arch=('any')
url="http://www.makotemplates.org/"
license=('MIT')
makedepends=('python-distribute' 'python2-distribute')
source=("http://www.makotemplates.org/downloads/Mako-$pkgver.tar.gz")
md5sums=('e3c0a677aa4216da9e89ef8fa76cbafb')

build() {
    cp -r Mako-$pkgver python2-Mako-$pkgver

    cd "$srcdir/Mako-$pkgver"
    python setup.py build

    cd "$srcdir/python2-Mako-$pkgver"
    python2 setup.py build
}

package_python-mako() {
    depends=('python-markupsafe' 'python-beaker')

    cd "$srcdir/Mako-$pkgver"
    python setup.py install --root="$pkgdir" --optimize=1
    mv "$pkgdir"/usr/bin/mako-render "$pkgdir"/usr/bin/python3-mako-render
    install -D LICENSE "$pkgdir/usr/share/licenses/python-mako/COPYING"
}

package_python2-mako() {
    depends=('python2-markupsafe' 'python2-beaker')

    cd "$srcdir/python2-Mako-$pkgver"
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -D LICENSE "$pkgdir/usr/share/licenses/python2-mako/COPYING"
}
