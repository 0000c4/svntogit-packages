# $Id$
# Maintainer: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.3.101
pkgrel=2
pkgdesc="Extensions to Xfce by os-cillation"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL')
url="http://www.os-cillation.com/article.php?sid=40"
groups=('xfce4')
depends=("libxfce4util>=4.6.1" "hal>=0.5.12git20090406" 'hicolor-icon-theme')
makedepends=("pygtk>=2.14.1" 'pkgconfig' 'libnotify' 'perl-uri' 'intltool')
optdepends=('libnotify		notification support for exo-mount-notify' \
            'perl-uri		for mail-compose helper script')
options=('!libtool')
install=${pkgname}.install
source=(http://www.xfce.org/archive/xfce-4.6.1/src/${pkgname}-${pkgver}.tar.bz2)
md5sums=('1c0eebb360f53eab70454b70e1bde36e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var --disable-static --enable-notifications \
    --enable-python || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
