# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.0.11
pkgrel=3
pkgdesc="Cantarell font family"
arch=('any')
url="http://abattis.org/cantarell/"
license=('GPL2')
depends=('fontconfig')
install=cantarell-fonts.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver::3}/$pkgname-${pkgver}.tar.xz
        21-cantarell-hinting.conf)
sha256sums=('89b68fc3e9557309bbca1a174210dad3525a7908d4c11dff20f7e9a01f940b63'
            '7da2d00e68d9eeab071cb861fe7da35098d5164008f638a5a9c289f717924bd0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-configdir=/etc/fonts/conf.avail
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../21-cantarell-hinting.conf \
    "$pkgdir/etc/fonts/conf.avail/21-cantarell-hinting.conf"
  install -d "$pkgdir/etc/fonts/conf.d"
  ln -s ../conf.avail/21-cantarell-hinting.conf "$pkgdir/etc/fonts/conf.d"
  ln -s ../conf.avail/31-cantarell.conf "$pkgdir/etc/fonts/conf.d"
}

# vim:set ts=2 sw=2 et:
