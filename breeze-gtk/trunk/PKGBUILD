# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=breeze-gtk
pkgver=5.6.4
pkgrel=3
pkgdesc="Breeze widget theme for GTK 2 and 3"
arch=(i686 x86_64)
url='https://quickgit.kde.org/?p=breeze-gtk.git'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules git)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz" breeze-gtk-3.20-{1,2,3}.patch)
md5sums=('39d7e635079545938fa225fef0e29d05'
         '15817d45cf3adf8b8b8aada41676a520'
         'a9dda4b30690cfedf65188dd797c8b48'
         '5bc3bbd044806189704a493032e96576')

prepare(){
  mkdir -p build

# Port to GTK 3.20
  cd $pkgname-$pkgver
  for _n in 1 2 3; do
   git apply -p1 < ../breeze-gtk-3.20-$_n.patch
  done
}

build(){
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_GTK3_VERSION=3.20
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
