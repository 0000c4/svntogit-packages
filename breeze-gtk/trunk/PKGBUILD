# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=breeze-gtk
pkgver=5.11.4
pkgrel=1
pkgdesc="Breeze widget theme for GTK 2 and 3"
arch=(x86_64)
url='https://quickgit.kde.org/?p=breeze-gtk.git'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ea81653067069ae2089d27627f1b3393ec144fbe8805534e300f0473e5b02e59'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare(){
  mkdir -p build
}

build(){
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
