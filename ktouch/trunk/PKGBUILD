# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktouch
pkgver=19.04.2
pkgrel=2
pkgdesc="Touch Typing Tutor"
url="https://kde.org/applications/education/ktouch/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(kqtquickcharts libxkbfile kcmutils hicolor-icon-theme qt5-quickcontrols qt5-graphicaleffects qt5-xmlpatterns)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdeedu-ktouch)
replaces=(kdeedu-ktouch)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-408742.patch::"https://cgit.kde.org/ktouch.git/patch/?id=0ad22345")
sha256sums=('280bd4e0b5ec992d7f87446f2d154a673ee8a127abd0be297bc9c671aac8d16b'
            'SKIP'
            '11e53538c062a7f4ac5f1a6f7a15d3cbbc6ceab85a2ff322bcc697c23aab8cfd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-408742.patch # Fix course editor crash
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCOMPILE_QML=OFF # Don't compile QML, it creates a hard dependency on a specific Qt version
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
