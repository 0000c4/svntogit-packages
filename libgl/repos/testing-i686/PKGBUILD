# $Id$
# Maintainer: Alexander Baldeck <kth5@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=libgl
pkgver=7.1rc4
pkgrel=1
pkgdesc="Mesa DRI OpenGL library and drivers"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.mesa3d.org"
depends=('libdrm>=2.4.0' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1')
makedepends=('dri2proto>=1.1' 'glproto>=1.4.9' 'pkgconfig')
provides=('libgl-dri')
replaces=('libgl-dri' 'libgl-mesa')
source=(http://www.mesa3d.org/beta/MesaLib-7.1-rc4.tar.gz)
md5sums=('5844564b90fdf0a462c8a4e601fa3f78')

build() {
  cd ${startdir}/src/Mesa-7.1-rc4
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=swrast \
    --enable-glx-tls \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  rm -rf ${pkgdir}/usr/include
  rm -rf ${pkgdir}/usr/lib/pkgconfig
  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/extensions
  ln -sf libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so || return 1
}
