# $Id$
# Maintainer: Alexander Baldeck <kth5@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=libgl
pkgver=7.5
pkgrel=1
pkgdesc="Mesa DRI OpenGL library and drivers"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.mesa3d.org"
depends=('libdrm>=2.4.12' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1')
makedepends=('dri2proto=2.1' 'glproto>=1.4.9' 'pkgconfig')
provides=('libgl-dri')
replaces=('libgl-dri' 'libgl-mesa')
options=(!makeflags)
source=(http://downloads.sourceforge.net/mesa3d/MesaLib-${pkgver}.tar.bz2
      	mesa-7.1-link-shared.patch)
md5sums=('459f332551f6ebb86f384d21dd15e1f0'
         '9eddc02e23ec08295822911e726ae89a')

build() {
  cd "${startdir}/src/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=swrast \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw \
    --disable-gallium-intel || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -f ${pkgdir}/usr/lib/libEGL.so*
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -sf libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so || return 1
}
