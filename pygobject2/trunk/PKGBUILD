# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pygobject
pkgname=(pygobject py3gobject pygobject-devel)
pkgver=2.28.6
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2' 'gobject-introspection')
makedepends=(python python2 python-cairo python2-cairo)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz
        python3-fix-build.patch
        python3-fix-maketrans.patch)
options=('!libtool')
url="http://www.pygtk.org/"
sha256sums=('fb8a1d4f665130a125011659bd347c7339c944232163dbb9a34fd0686577adb8'
            'feafd4664f8455edf0bf8407ac45e219bb550df806ce0d601baae951e8c188ca'
            '4bff9adcea13a824c45d14ec501c927df47d23c22507a2456d8b5ec885924c0a')

build() {
  cd "${srcdir}"
  cp -a "${pkgbase}-${pkgver}" python2-build
  mkdir devel

  (
    cd python2-build
    export PYTHON=/usr/bin/python2
    ./configure --prefix=/usr
    make
  )

  (
    cd "${pkgbase}-${pkgver}"
    #patches available in 2.28 branch but unreleased.
    patch -Np1 -i "${srcdir}/python3-fix-build.patch"
    patch -Np1 -i "${srcdir}/python3-fix-maketrans.patch"

    ./configure --prefix=/usr
    make
  )
}

package_pygobject() {
  pkgdesc="Python 2 bindings for GObject"
  depends+=('python2' 'python2-cairo' 'pygobject-devel')

  cd "${srcdir}/python2-build"
  make DESTDIR="${pkgdir}" install

  find "$pkgdir"/usr/share/pygobject -name '*.py' | \
    xargs sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

  # Split devel stuff
  mv "$pkgdir"/usr/{bin,include,lib/pkgconfig,share/{gtk-doc,pygobject/{2.0/codegen,xsl}}} \
    "$srcdir/devel/"
}

package_py3gobject() {
  pkgdesc="Python 3 bindings for GObject"
  depends+=('python' 'python-cairo' 'pygobject-devel')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Delete devel stuff
  rm -r "$pkgdir"/usr/{bin,include,lib/pkgconfig,share/{gtk-doc,pygobject/{2.0/codegen,xsl}}}
}

package_pygobject-devel() {
  pkgdesc="Development files for the pygobject bindings"
  depends=(python2)

  cd "${srcdir}/devel"
  mkdir -p "$pkgdir"/usr/{include,lib,share/pygobject/2.0}

  mv bin       "$pkgdir/usr/"
  mv include   "$pkgdir/usr/"
  mv pkgconfig "$pkgdir/usr/lib/"
  mv gtk-doc   "$pkgdir/usr/share/"
  mv codegen   "$pkgdir/usr/share/pygobject/2.0/"
  mv xsl       "$pkgdir/usr/share/pygobject/"
}
