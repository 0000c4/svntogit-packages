# $Id$
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-battery-plugin
pkgver=0.5.1
pkgrel=1
pkgdesc="A battery monitor plugin for the Xfce panel"
arch=(i686 x86_64)
license=('GPL2')
url="http://xfce-goodies.berlios.de/"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('pkgconfig' 'perlxml')
options=('!libtool')
install=${pkgname}.install
source=(http://goodies.xfce.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	compile_x86_64.diff)
md5sums=('d8a666d85bb3c1dd007b547de4dd7037' 'd9a927bca032e9b10a04046c154243a1')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  # fix http://bugzilla.xfce.org/show_bug.cgi?id=4359
  patch -Np1 -i ${srcdir}/compile_x86_64.diff || return 1
 # the purpose of this code is to check if there is an acpi system installed at all
 # but since the code also exits when no /proc/acpi/battery directory is there we are safe
  ex panel-plugin/libacpi.c << EOC > /dev/null 2>&1
/proc.acpi.info
d10
w
q
EOC
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static
  make || return 1
  make DESTDIR=${startdir}/pkg install
}
