# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-miners
pkgver=3.20.1
pkgrel=1
pkgdesc="Crawls through your online content"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/GnomeOnlineMiners"
license=(GPL)
depends=(libgdata gnome-online-accounts grilo libzapojit tracker gfbgraph dleyna-server)
makedepends=(python)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('fd67b9abad6233689a921d84dbe583a8b89d5ade7f1b5d2f348493888baa3429')
 
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
