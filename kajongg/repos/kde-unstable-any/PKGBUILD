# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kajongg
pkgver=20.03.80
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="https://kde.org/applications/games/kajongg/"
arch=(any)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(python-twisted python-pyqt5 libkmahjongg hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('vorbis-tools: for playing sounds')
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kajongg-destdir.patch)
sha256sums=('00d70a5cc67207012c34360ab7f7124da597243c22416faf20829995d0934076'
            'SKIP'
            '456282348c31c71e8e49dea26df9f47de992cdaf3306e60005c433bf4326d565')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kajongg-destdir.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
