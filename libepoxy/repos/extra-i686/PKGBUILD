# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=libepoxy
pkgver=1.4.0
pkgrel=1
pkgdesc="Library handling OpenGL function pointer management"
url="https://github.com/anholt/libepoxy"
arch=('i686' 'x86_64')
license=('MIT')
depends=('glibc')
makedepends=('xorg-util-macros' 'python' 'mesa-libgl')
source=(${url}/releases/download/v${pkgver%.0}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('25a906b14a921bc2b488cfeaa21a00486fe92630e4a9dd346e4ecabeae52ab41')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # Most tests get skipped because we have no X server (Xvfb doesn't have GLX)
  make -k check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

