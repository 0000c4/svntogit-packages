# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=2.5.2
pkgrel=4
_pybasever=2.5
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org"
depends=('db>=4.7' 'bzip2' 'gdbm' 'openssl>=0.9.8d' 'zlib')
makedepends=('tk>=8.5.0' 'sqlite3')
provides=('python-elementree' 'python-ctypes')
replaces=('python-elementree' 'python-ctypes')
install=python.install
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
	modulator-launcher
	pynche-launcher
	Python-2.4.4-gdbm-1.patch
	db4.7-setup.py-detection.patch
	python-2.5.CVE-2007-4965-int-overflow.patch
	python2.6-set_wakeup_fd4.patch
	Python-2.5.2-idlelib.patch)
md5sums=('afb5451049eda91fbde10bd5a4b7fadc'
         'faa0d4b947bfe67fc8684b5c464f46f2'
         '60a44af1d8af276274e20c564c0f3d8b'
         '2b61e06365e9fb5f3af22d946efa8be6'
         '176398feef253b7c01de2b4691a2719b'
         '491416c1b337fc61b6cdf5981d895a7d'
         'ec56710959deb01b5fa35829cafec6a3'
         'e6441e0b013ac64795221154e0495e10')

build() {
  cd ${startdir}/src/Python-${pkgver}
  patch -Np1 -i ${srcdir}/Python-2.4.4-gdbm-1.patch || return 1
  patch -Np0 -i ${srcdir}/db4.7-setup.py-detection.patch || return 1
  # fix http://cve.mitre.org/cgi-bin/cvename.cgi?name=CVE-2007-4965
  patch -Np1 -i ${srcdir}/python-2.5.CVE-2007-4965-int-overflow.patch || return 1
  
  patch -Np2 -i ${srcdir}/python2.6-set_wakeup_fd4.patch || return 1
  patch -Np0 -i ${srcdir}/Python-2.5.2-idlelib.patch

  ./configure --prefix=/usr --enable-shared --with-threads --enable-unicode
  make || return 1
  make DESTDIR=${pkgdir} install

  rm ${pkgdir}/usr/bin/python
  ln -sf python${_pybasever} ${pkgdir}/usr/bin/python

  # Install the tools
  #modulator
  install -m755 ${srcdir}/modulator-launcher ${pkgdir}/usr/bin/modulator
  cp -r Tools/modulator ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #pynche
  install -m755 ${srcdir}/pynche-launcher ${pkgdir}/usr/bin/pynche
  rm -f Tools/pynche/*.pyw
  cp -r Tools/pynche ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #gettext
  install -m755 Tools/i18n/pygettext.py ${pkgdir}/usr/bin/
  install -m755 Tools/i18n/msgfmt.py ${pkgdir}/usr/bin/

  mkdir -p ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts
  install Tools/scripts/README ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/
  install Tools/scripts/*py ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/

  # license
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
