# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=3.26.0
pkgrel=2
epoch=1
pkgdesc='A GIT repository viewer based on GTK+'
arch=('x86_64')
url='https://git.gnome.org/browse/gitg/'
license=('GPL')
depends=('gtksourceview3' 'git' 'desktop-file-utils' 'libgit2-glib' 'libgee' 'webkit2gtk'
         'libpeas' 'gtkspell3' 'python-gobject')
makedepends=('intltool' 'gobject-introspection' 'gnome-common')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('26730d437d6a30d6e341b9e8da99d2134dce4b96022c195609f45062f82b54d5')

# Don't convert back to Git sources until compilation with current Vala is fixed (FS#58215)

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mv "$pkgdir"/usr/share/{appdata,metainfo}
}
