# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=3.26+23+g9c785938
pkgrel=1
pkgdesc='A GIT repository viewer based on GTK+'
arch=('x86_64')
url='https://git.gnome.org/browse/gitg/'
license=('GPL')
depends=('gtksourceview3' 'git' 'desktop-file-utils' 'libgit2-glib' 'libgee' 'webkit2gtk'
         'libpeas' 'gtkspell3' 'python-gobject')
makedepends=('intltool' 'vala' 'gobject-introspection' 'glade' 'git' 'meson' 'gtk-doc')
groups=('gnome-extra')
_commit=9c7859381b8eb5f0e8c00bca120802148ea3bc8e  # master
source=("git+https://git.gnome.org/browse/gitg#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D docs=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # Fix up icon names
  local f d
  for f in "$pkgdir"/usr/share/icons/hicolor/*/apps/*; do
    d="${f%/*}"
    mv "$f" "$d/${f##*_}"
  done

  python -m compileall "$pkgdir/usr/lib/python3.6"
  python -O -m compileall "$pkgdir/usr/lib/python3.6"
}
