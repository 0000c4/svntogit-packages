# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=3.30.1
pkgrel=1
epoch=1
pkgdesc="GNOME GUI client to view git repositories"
url="https://wiki.gnome.org/Apps/Gitg"
arch=(x86_64)
license=(GPL)
depends=(gtksourceview3 git libgit2-glib libgee webkit2gtk libpeas gtkspell3 python-gobject)
makedepends=(intltool gobject-introspection git meson vala)
_commit=a10e9899a4ec5f6ce382d6a0d42fcc0d66b114ef  # tags/v3.30.1^0
source=("git+https://gitlab.gnome.org/GNOME/gitg.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
