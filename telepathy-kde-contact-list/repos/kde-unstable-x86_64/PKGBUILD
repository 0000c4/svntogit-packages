# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-contact-list
_pkgname=ktp-contact-list
pkgver=20.03.80
pkgrel=1
pkgdesc='KDE Telepathy contact list application'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kdenetwork telepathy-kde)
depends=(telepathy-kde-common-internals)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig}
         ktp-contact-list-qt5.15.patch)
sha256sums=('5786979e91823bf1fad3f89487400f5fd2979fe9164a8cf8deb1c70bbfe11518'
            'SKIP'
            'a0f01b5dc1b023296784877fc1b4a9474a1fca1d8a6ca78c9164441be251f58a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $_pkgname-$pkgver
  patch -p1 -i ../ktp-contact-list-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
