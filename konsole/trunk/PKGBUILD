# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsole
pkgver=16.08.0
pkgrel=2
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/konsole/'
pkgdesc="Terminal"
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdebase')
depends=('knotifyconfig' 'kpty' 'kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('kdebase-keditbookmarks: to manage bookmarks')
replaces=('kdebase-konsole')
conflicts=('kdebase-konsole<4.14.3-2')
source=("http://download.kde.org/stable/applications/${pkgver}/src/konsole-${pkgver}.tar.xz"
        kdebug-366368.patch::"https://quickgit.kde.org/?p=konsole.git&a=commitdiff&h=fca305eb&o=plain")
sha1sums=('d1b0187afbd879f2211c2b229a2a64cbd0be952b'
          'ab722ec281677d7ecdba2f0a269dba25bf824474')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix background transparency http://bugs.kde.org/show_bug.cgi?id=366368
  patch -p1 -i ../kdebug-366368.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
