# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.17+17.04.20161109
pkgrel=1
pkgdesc='UI component responsible for handling the user interactions which can happen during the login process of an online account'
arch=(i686 x86_64)
url="https://launchpad.net/online-accounts-signon-ui"
license=(GPL)
depends=(libaccounts-qt qt5-webkit signond libnotify)
source=("http://archive.ubuntu.com/ubuntu/pool/main/s/$pkgname/${pkgname}_$pkgver.orig.tar.gz")
md5sums=('4d065eab7e5a3aad48ccf38383f58fe8')

prepare() {
  # Do not install tests
  echo 'INSTALLS =' >>tests/unit/tst_inactivity_timer.pro
  echo 'INSTALLS =' >>tests/unit/tst_signon_ui.pro
}

build() {
  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  make INSTALL_ROOT="$pkgdir" install
}

