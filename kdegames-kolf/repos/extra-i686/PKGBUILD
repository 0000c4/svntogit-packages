# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kolf
pkgver=17.04.2
pkgrel=1
pkgdesc="A miniature golf game with 2d top-down view"
url="https://kde.org/applications/games/kolf/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kolf-${pkgver}.tar.xz"{,.sig})
sha256sums=('4f2112e46daf5cf41266b256b99287a82d10776275e4ce2e4963ecf7e7bdef94'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../kolf-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
