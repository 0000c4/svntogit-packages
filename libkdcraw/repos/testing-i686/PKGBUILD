# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdcraw
pkgver=4.7.0
pkgrel=1
pkgdesc="A C++ interface used to decode RAW picture"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'lcms')
makedepends=('cmake' 'automoc4')
conflicts=('kdegraphics-libs')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('7c8d7ed8eb18c3f2951d451a2cf46dde9a336216')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
