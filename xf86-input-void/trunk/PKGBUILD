# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-void
_gitdate=20110318
pkgver=1.3.1.99_git${_gitdate} # see configure.ac
pkgrel=1
pkgdesc="X.org void input driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server-devel')
conflicts=('xorg-server<1.10.0')
groups=('xorg-drivers' 'xorg')
options=('!libtool')
source=(#${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	ftp://ftp.archlinux.org/other/$pkgname/${pkgname}-${_gitdate}.tar.bz2)
md5sums=('0ec2820fc178138d393fd248f56b8754')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/${pkgname}-${_gitdate}
	pushd /tmp/${pkgname}-${_gitdate}
	git clone -v --depth 1 git://anongit.freedesktop.org/xorg/driver/xf86-input-void
	cd xf86-input-void
	git archive --prefix=xf86-video-void-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/${pkgname}-${_gitdate}/${pkgname}-${_gitdate}.tar.bz2
	popd
}

build() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#  ./configure --prefix=/usr
  cd ${srcdir}/xf86-video-void-${_gitdate}
  ./autogen.sh --prefix=/usr
  make
}

package() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
  cd ${srcdir}/xf86-video-void-${_gitdate}
  
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
