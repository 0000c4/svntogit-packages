# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-pkgconfig
_realname=ExtUtils-PkgConfig
pkgver=1.13
pkgrel=1
pkgdesc="The Perl Pkgconfig module"
arch=(any)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
depends=('perl>=5.10.0')
replaces=('extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/${_realname}-${pkgver}.tar.gz)
options=('!emptydirs')
md5sums=('cc41813a491e15e0d46eef3233786369')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
