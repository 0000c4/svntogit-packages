# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=module-init-tools
pkgver=3.8
pkgrel=1
pkgdesc="utilities needed by Linux systems for managing loadable kernel modules"
arch=('i686' 'x86_64')
url="http://kerneltools.org"
license=('GPL')
groups=('base')
depends=('glibc')
backup=('etc/modprobe.d/modprobe.conf')
source=(http://www.kernel.org/pub/linux/utils/kernel/module-init-tools/module-init-tools-$pkgver.tar.bz2
        modprobe.conf)
md5sums=('470d7830fd263d29a92149da6ae9f122'
         '316f1bda4c21af02b30252eb014a0a55')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --exec-prefix=/
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # Remove old-school conversion util... we haven't shipped modules.conf in years
  rm $pkgdir/sbin/generate-modprobe.conf
  
  # Install our custom (read: empty) modprobe.conf
  install -Dm644 $srcdir/modprobe.conf $pkgdir/etc/modprobe.d/modprobe.conf
}
