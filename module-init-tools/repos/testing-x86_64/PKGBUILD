# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=module-init-tools
pkgver=3.16
pkgrel=2
pkgdesc="Utilities needed by Linux systems for managing loadable kernel modules"
arch=('i686' 'x86_64')
url="https://modules.wiki.kernel.org"
license=('GPL')
depends=('glibc')
makedepends=('docbook2x')
backup=('etc/modprobe.d/modprobe.conf')
source=("http://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.bz2"
        modprobe.conf
        docfix.patch)
md5sums=('bc44832c6e41707b8447e2847d2019f5'
         '325657db471b3190f685e5a89c9b90e3'
         '4320d19902ded43bed55c804b065cdc7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix headers in SGML manpage sources and an XML typo
  patch -Np0 < "$srcdir/docfix.patch"

  ./configure --prefix=/usr --exec-prefix=/ --enable-zlib
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=$pkgdir install

  # extra modprobe.d conf dir
  install -dm755 "$pkgdir/lib/modprobe.d"

  # Install our custom (read: empty) modprobe.conf
  install -Dm644 "$srcdir/modprobe.conf" "$pkgdir/etc/modprobe.d/modprobe.conf"
}
