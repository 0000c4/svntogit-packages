# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=module-init-tools
pkgver=3.4
pkgrel=1
pkgdesc="Utilities for inserting and removing modules from the Linux kernel"
arch=(i686 x86_64)
url="http://www.kernel.org"
license=('GPL')
groups=('base')
depends=('glibc')
backup=('etc/modprobe.conf')
source=(http://www.kernel.org/pub/linux/utils/kernel/module-init-tools/module-init-tools-$pkgver.tar.bz2
        ftp://sunsite.informatik.rwth-aachen.de/pub/linux/gentoo/distfiles/module-init-tools-$pkgver-manpages.tar.bz2
        makefile.patch
        modprobe.conf
        fix-modprobe-ignore-path.patch)
md5sums=('db6ac059e80e8dd4389dbe81ee61f3c6'
         '006678277788b5a0402b171f4a235cd6'
         '47e14fda7a46668290d11d0444d81826'
         '6db59d41e04941a790f80c1a4432faef'
         '335c3f8317f257ddd70b09271b4360bd')

build() {
  cd $startdir/src/$pkgname-$pkgver
  patch -Np1 -i ../makefile.patch
  patch -Np0 -i ../fix-modprobe-ignore-path.patch || return 1
  sed -i 's|/usr/bin/install|/bin/install|g' install-with-care
  ./configure --prefix=/usr --exec-prefix=/
  make || return 1
  INSTALL=/bin/install make DESTDIR=$startdir/pkg install || return 1
  install -D -m644 ../modprobe.conf $startdir/pkg/etc/modprobe.conf
}
