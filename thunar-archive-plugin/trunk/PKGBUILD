# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-archive-plugin
pkgver=0.2.4
pkgrel=8
pkgdesc="create and deflate archives in thunar"
arch=(i686 x86_64)
license=('GPL2')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-archive-plugin"
groups=('xfce4-goodies')
depends=('thunar>=1.2.0' 'thunar-vfs')
makedepends=('intltool')
options=('!libtool')
install=${pkgname}.install
source=(http://download.berlios.de/xfce-goodies/${pkgname}-${pkgver}.tar.bz2
	thunar-vfs2gio.patch)
md5sums=('4c389e6328af9322937af76382f0baec'
         'f1453d7c9cd9dde04f23d78f271298cd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # thunar 1.2 fix
  patch -Np1 -i ${srcdir}/thunar-vfs2gio.patch
  sed -i -e 's/thunarx-1/thunarx-2/g' `grep -l 'thunarx-1' * -R`
 
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var \
	--disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
