# $Id$
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-archive-plugin
pkgver=0.2.4
pkgrel=7
pkgdesc="create and deflate archives in thunar"
arch=(i686 x86_64)
license=('GPL2')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-archive-plugin"
groups=('xfce4-goodies')
depends=('thunar>=1.2.0')
makedepends=('intltool')
options=('!libtool')
install=${pkgname}.install
source=(http://download.berlios.de/xfce-goodies/${pkgname}-${pkgver}.tar.bz2)
md5sums=('4c389e6328af9322937af76382f0baec')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # thunar 1.2 hack
  sed -i "s/thunarx\-1/thunarx\-2/g" configure
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var \
	--disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
