# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.26.0
pkgrel=1
pkgdesc="Configuration database system"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(LGPL2.1)
depends=(glib2)
makedepends=(vala dbus git gtk-doc python)
_commit=102bb4a209e4195cb8fe7bb67406458b6841a8c2
source=("git://git.gnome.org/dconf#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/dconf --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install \
    completiondir=/usr/share/bash-completion/completions
}

# vim:set ts=2 sw=2 et:
