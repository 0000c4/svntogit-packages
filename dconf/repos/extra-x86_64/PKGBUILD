# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.14.0
pkgrel=1
pkgdesc="A low-level configuration system."
arch=(i686 x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('glib2' 'gtk-update-icon-cache')
makedepends=('vala' 'gtk3' 'intltool' 'docbook-xsl')
optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('9ae848bf25c3de0fb8c0157679630ddf39be1c10ed2f3beb2b8f0165d768da33')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd "$pkgname-$pkgver"
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
