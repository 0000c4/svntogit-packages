# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=quota-tools
pkgver=4.00_pre1 
pkgrel=1
pkgdesc="Tools to manage kernel-level quotas in Linux"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/linuxquota/"
license=('GPL' 'BSD')
depends=('e2fsprogs')
backup=('etc/warnquota.conf' 'etc/quotatab' 'etc/quotagrpadmins')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/linuxquota/quota-${pkgver/_/-}.tar.gz LICENSE \
        quotaon.diff system_inodes.diff repquota.diff nulls.diff)
md5sums=('ef9d66e8a968ecffd2d9df648fa8ada2'
         'dd792440e684043e4e4ef80963d0237b'
         '9ac7ca4746de1ad057baee21474b7906'
         '92d9f2a4b3e5e3adf2977051391785a7'
         'd1d70d4167e53d1414079b4391f1cfb8'
         '094bce5226c4fd1c383bd0b75405ee2c')
sha1sums=('adf29b49dab449078eb6ffdfe8af51fe85419e28'
          '57297bdc9e638c500506169bbbe12eb89bcf7d07'
          '2304f03cddd06d8791167f621683f7ef54610673'
          'e2a33f1f95a3ff8c741a2067058e898f6054af09'
          '8b00e7c4f7af2188ad49a50a616c1d71eee20459'
          '0527f761aa869f5d9e463ceab4a3bf82881d05c2')

build() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i "${srcdir}/quotaon.diff"
  patch -p1 -i "${srcdir}/system_inodes.diff"
  patch -p1 -i "${srcdir}/repquota.diff"
  patch -p1 -i "${srcdir}/nulls.diff"
  ./configure --prefix=/usr --sysconfdir=/etc
  sed -i -e 's/#define HOSTS_ACCESS 1//' -e 's/HOSTS_ACCESS//' config.h
  sed -i 's/-lwrap//' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make ROOTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

# remove conflicts with glibc and man-pages
  rm "${pkgdir}"/usr/include/rpcsvc/rquota.{h,x}
  rm "${pkgdir}/usr/share/man/man2/quotactl.2"
}
