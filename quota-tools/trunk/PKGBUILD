# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=quota-tools
pkgver=4.00
pkgrel=1
epoch=1
pkgdesc="Tools to manage kernel-level quotas in Linux"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/linuxquota/"
license=('GPL' 'BSD')
depends=('e2fsprogs')
backup=('etc/warnquota.conf' 'etc/quotatab' 'etc/quotagrpadmins')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/linuxquota/quota-${pkgver}.tar.gz \
        LICENSE)
sha1sums=('9ef79933bebfd80f007600fd422616ad161c5fd0'
          '57297bdc9e638c500506169bbbe12eb89bcf7d07')

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make RPCGEN="rpcgen -Y /usr/bin"
}

package() {
  cd "${srcdir}/${pkgname}"
  make ROOTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

# remove conflicts with glibc
  rm "${pkgdir}"/usr/include/rpcsvc/rquota.{h,x}
}
