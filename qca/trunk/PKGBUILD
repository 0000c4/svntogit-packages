# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=qca
pkgname=(qca-qt4 qca-qt5)
pkgver=2.1.3
pkgrel=10
pkgdesc="Qt Cryptographic Architecture"
arch=(x86_64)
url="http://delta.affinix.com/qca/"
license=(LGPL)
makedepends=(qt4 qt5-base cmake doxygen nss pkcs11-helper botan)
source=("http://download.kde.org/stable/$pkgbase/$pkgver/src/$pkgbase-$pkgver.tar.xz" qca-openssl-1.1.patch qca-botan2.patch)
sha256sums=('003fd86a32421057a03b18a8168db52e2940978f9db5ebbb6a08882f8ab1e353'
            'b1505bc313fd2f4e350cd4c94af69256c901afa419ae6700b208cb6e40e6926d'
            '4fc5c0179367837ed73bd09aeb5b0aee74385a7aa6ebd99008de916ba02ef0ed')

prepare() {
  mkdir -p build{4,5}

  cd $pkgbase-$pkgver
  patch -p1 -i ../qca-openssl-1.1.patch # Fix build with OpenSSL 1.1 https://bugs.kde.org/show_bug.cgi?id=379810
  patch -p1 -i ../qca-botan2.patch      # Fix build with botan 2
}

build() {
  cd build4
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DQCA_LIBRARY_INSTALL_DIR=/usr/lib \
    -DQCA_FEATURE_INSTALL_DIR=/usr/share/qt4/mkspecs/features/ \
    -DQT4_BUILD=ON
  make

  cd ../build5
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DQCA_INSTALL_IN_QT_PREFIX=ON \
    -DQCA_MAN_INSTALL_DIR=/usr/share/man
  make
}

package_qca-qt4() {
  depends=(qt4 nss)
  optdepends=('pkcs11-helper: PKCS-11 plugin' 'botan: botan plugin')
  conflicts=(qca qca-gnupg qca-ossl)
  provides=(qca qca-gnupg qca-ossl)
  replaces=(qca qca-gnupg qca-ossl)

  cd build4
  make DESTDIR="$pkgdir" install
}

package_qca-qt5() {
  depends=(qt5-base nss ca-certificates)
  optdepends=('pkcs11-helper: PKCS-11 plugin' 'botan: botan plugin')

  cd build5
  make DESTDIR="$pkgdir" install
}


