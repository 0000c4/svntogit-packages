# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qca
pkgver=2.2.0
pkgrel=1
pkgdesc="Qt Cryptographic Architecture"
arch=(x86_64)
url="https://userbase.kde.org/QCA"
license=(LGPL)
depends=(qt5-base nss ca-certificates)
optdepends=('pkcs11-helper: PKCS-11 plugin' 'botan: botan plugin')
makedepends=(cmake doxygen pkcs11-helper botan)
conflicts=(qca-qt5)
provides=(qca-qt5)
replaces=(qca-qt5)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        qca-fix-build-qt-prefix.patch)
sha256sums=('3b479826858099c9ac1bd7992a0e2f1cd68d684d9a32910dbc4372bef592318a'
            'SKIP'
            'e480b850ab78df4fe887c1262053cc4ab8b9e458299a4944a04121b06cbdf646')
validpgpkeys=(CB9387521E1EE0127DA804843FDBB55084CC5D84) # Harald Sitter <sitter@kde.org>

prepare() {
  mkdir -p build

  cd qca-$pkgver
  patch -p1 -i ../qca-fix-build-qt-prefix.patch # Fix build when installing under Qt prefix
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DQCA_INSTALL_IN_QT_PREFIX=ON \
    -DQCA_MAN_INSTALL_DIR=/usr/share/man
  make
}

package() {
  depends=(qt5-base nss ca-certificates)
  optdepends=('pkcs11-helper: PKCS-11 plugin' 'botan: botan plugin')

  cd build
  make DESTDIR="$pkgdir" install
}

