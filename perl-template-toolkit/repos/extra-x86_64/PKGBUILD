# $Id$
# Maintainer: Firmicus <francois.archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: FJ <joostef@gmail.com>

pkgname=perl-template-toolkit
_realname=Template-Toolkit
pkgver=2.24
pkgrel=1
pkgdesc="Perl template processing system"
arch=(i686 x86_64)
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-appconfig' 'perl>=5.10.0')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/A/AB/ABW/${_realname}-$pkgver.tar.gz)
md5sums=('c25fdab1beebf8818c2e624bc9f9d212')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}
