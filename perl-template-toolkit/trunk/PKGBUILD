# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: FJ <joostef@gmail.com>

pkgname=perl-template-toolkit
pkgver=3.003
pkgrel=1
pkgdesc="Perl template processing system"
arch=('x86_64')
url="https://search.cpan.org/dist/Template-Toolkit/"
license=('PerlArtistic')
depends=('perl-appconfig' 'perl')
checkdepends=('perl-cgi' 'perl-image-info' 'perl-test-leaktrace')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/A/AT/ATOOMIC/Template-Toolkit-$pkgver.tar.gz)
sha512sums=('a8b7b2baf203d44165c305cabcc2452d95a85f0eec45c0948a0b83a8f671c805a276237bb74d5d8f47b2b59b1490d7ae389734404e7b7f6827f9f34a37d17d78')

build() {
  cd Template-Toolkit-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor TT_ACCEPT=y
  make
}

check() {
  cd Template-Toolkit-$pkgver
  make test
}

package() {
  cd Template-Toolkit-$pkgver
  make install DESTDIR="${pkgdir}"
}
