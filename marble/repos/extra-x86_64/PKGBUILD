# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=marble
pkgname=(marble-data marble-common marble-qt marble)
pkgver=16.12.2
pkgrel=3
pkgdesc="Desktop Globe"
arch=(i686 x86_64)
url="https://www.kde.org/applications/system/marble"
license=(GPL)
makedepends=(extra-cmake-modules gpsd kdoctools knewstuff kparts krunner libwlocate phonon-qt5
             plasma-workspace qt5-serialport qt5-tools qt5-webkit shapelib)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz"{,.sig})
md5sums=('83bc30b139eca4f4e511e3ea2587dd0d'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DQT_PLUGINS_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  make
}

package_marble-data() {
  pkgdesc='Data for Marble'
  depends=(hicolor-icon-theme)
  conflicts=(kdeedu-marble)

  cd build
  make -C doc DESTDIR="$pkgdir" install
  make -C data DESTDIR="$pkgdir" install
}

package_marble-common() {
  pkgdesc='Common libraries and plugins for Marble'
  depends=(marble-data qt5-svg qt5-webkit phonon-qt5)
  optdepends=('gpsd: GPS based geolocation'
              'libwlocate: WLAN based geolocation'
              'qt5-serialport: APRS plugin'
              'shapelib: SHP plugin')
  conflicts=(kdeedu-marble libastro libmarble4)
  replaces=(libastro)

  cd build
  make -C src/lib/marble DESTDIR="$pkgdir" install
  make -C src/lib/astro DESTDIR="$pkgdir" install
  make -C src/lib/marble/declarative DESTDIR="$pkgdir" install
  make -C src/plugins DESTDIR="$pkgdir" install
}

package_marble-qt() {
  pkgdesc+=' (Qt version)'
  depends=(marble-common)

  cd build
  make -C src/apps/marble-qt DESTDIR="$pkgdir" install
}

package_marble() {
  depends=(marble-common knewstuff kparts)
  optdepends=('krunner: Krunner plugin')
  conflicts=(kdeedu-marble)
  replaces=(kdeedu-marble)
  groups=(kde-applications kdeedu)

  cd build
  make -C src/apps/marble-kde DESTDIR="$pkgdir" install
  make -C src/plasma DESTDIR="$pkgdir" install
  make -C src/plasmarunner DESTDIR="$pkgdir" install
  make -C src/thumbnailer DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/share/icons
}
