# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=spice
pkgver=0.12.8
pkgrel=2
pkgdesc="SPICE client and server"
arch=('i686' 'x86_64')
url="https://www.spice-space.org"
license=('LGPL2.1')
depends=(alsa-lib celt0.5.1 libcacard libjpeg-turbo libsasl libxinerama libxfixes libxrandr pixman)
makedepends=(python2-pyparsing python2-six qemu spice-protocol)
source=(https://www.spice-space.org/download/releases/$pkgname-$pkgver.tar.bz2{,.sign}
        git-fixes.patch)
validpgpkeys=('94A9F75661F77A6168649B23A9D8C21429AC6C82')
sha256sums=('f901a5c5873d61acac84642f9eea5c4d6386fc3e525c2b68792322794e1c407d'
            'SKIP'
            '70a6d71e24095bc0864f659b0f3b196ffea5c618e8799281607112726dc2484d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../git-fixes.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr --disable-static --enable-smartcard --enable-client
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
