# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=klibc-kbd
pkgver=1.15.1
pkgrel=2
pkgdesc="Keytable files and keyboard utilities"
arch=('i686' 'x86_64')
url="ftp://ftp.altlinux.org/pub/people/legion/kbd/"
license=('GPL')
groups=('base')
depends=('klibc' $(basename /lib/klibc-*.so .so) 'kbd')
source=(ftp://ftp.altlinux.org/pub/people/legion/kbd/kbd-${pkgver}.tar.gz
        no-isatty.patch
        no-exit.patch
        keymap_install
        keymap_hook)
md5sums=('f997c490fe5ede839aacf31da6c4eb06'
         '53e9612ac5fc1b23601f793410742ed3'
         '8c1bec330e8c98355502ac861561667d'
         '735d7268f567deee4db3bed951a8303a'
         '85457e44dfd7046224e87f0add8da4b2')
options=('force')

build() {
  cd ${srcdir}/kbd-${pkgver}
  patch -Np1 -i ../no-isatty.patch || return 1
  patch -Np1 -i ../no-exit.patch || return 1

  aclocal
  autoconf
  automake --add-missing
  sed -i 's/ferror (f)/0/' configure
  ./configure --enable-klibc --disable-klibc-layout --datadir=/share/kbd

  cd src/
  make kbd_mode loadkeys setfont || return 1

  install -D loadkeys ${pkgdir}/lib/initcpio/kbd/loadkeys
  install -D kbd_mode ${pkgdir}/lib/initcpio/kbd/kbd_mode
  install -D setfont ${pkgdir}/lib/initcpio/kbd/setfont
  # install hook
  install -Dm644 ${srcdir}/keymap_hook ${pkgdir}/lib/initcpio/hooks/keymap
  install -Dm644 ${srcdir}/keymap_install ${pkgdir}/lib/initcpio/install/keymap
}
