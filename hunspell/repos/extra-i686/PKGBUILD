# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=hunspell
pkgver=1.2.14
pkgrel=2
pkgdesc="Spell checker and morphological analyzer library and program"
arch=('i686' 'x86_64')
url="http://hunspell.sourceforge.net/"
license=('GPL' 'LGPL' 'MPL')
depends=('gcc-libs' 'readline')
optdepends=('perl: for ispellaff2myspell')
options=('!libtool')
source=(http://downloads.sourceforge.net/hunspell/hunspell-$pkgver.tar.gz
	hunspell-phone.patch)
md5sums=('c2f289af57a677e6b258f2d18ecb178e'
         '8be4d84cc361a5deb0c1660a92991854')

build() {
  cd "$srcdir/hunspell-$pkgver"
  # http://sourceforge.net/tracker/?func=detail&aid=3158994&group_id=143754&atid=756395
  patch -Np0 -i ${srcdir}/hunspell-phone.patch
  
  ./configure --prefix=/usr --disable-static \
	--with-ui --with-readline --with-experimental
  make
}

package() {
  cd "$srcdir/hunspell-$pkgver"
  make DESTDIR="$pkgdir" install
}
