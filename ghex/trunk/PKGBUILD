# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=ghex
pkgver=3.4.1
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Ghex"
depends=('gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
options=('!libtool' '!emptydirs')
install=ghex.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('8696babf56a24be01cbf856c6fbad6f1d0e10c44736957625f9535430dfee692')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
