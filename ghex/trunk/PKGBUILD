# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=ghex
pkgver=3.8.1
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Ghex"
depends=('gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'itstool' 'docbook-xsl')
options=('!libtool' '!emptydirs')
install=ghex.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('84ce220eed0dc9f640a87db68533e65a4dc06a1a6136b244270d8b2a1ae93534')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
