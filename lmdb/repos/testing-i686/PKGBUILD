# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matt Monaco <cx monaco matt>

pkgname=lmdb
pkgver=0.9.19
pkgrel=1
pkgdesc="Symas Lightning Memory-Mapped Database"
arch=('i686' 'x86_64')
url="http://symas.com/mdb"
license=('custom:OpenLDAP')
depends=('glibc')
makedepends=('git')
options=('!emptydirs')
source=("git+https://github.com/LMDB/lmdb.git#tag=LMDB_$pkgver" lmdb.pc)
sha256sums=('SKIP'
            '6eed8c6fde6f5e2523099462779656f7cb92b3fc7384023d96508a6e73a730da')

prepare() {
  sed -e "s|@PKGVER@|$pkgver|" -i lmdb.pc
}

build() {
  cd "$srcdir/lmdb/libraries/liblmdb"
  make prefix=/usr
}

check() {
  cd "$srcdir/lmdb/libraries/liblmdb"
  make test
}

package() {
  cd "$srcdir/lmdb/libraries/liblmdb"
  install -dm755 "$pkgdir"/usr/{bin,lib,include,man/man1,share}
  make DESTDIR="$pkgdir" prefix=/usr install

  mv "$pkgdir/usr/man" "$pkgdir/usr/share/man"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir"/lmdb.pc "$pkgdir"/usr/lib/pkgconfig/lmdb.pc
}
