# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=deluge
pkgver=1.3.1
pkgrel=2
pkgdesc="A bittorrent client written with python and pygtk"
arch=('any')
url="http://deluge-torrent.org/"
license=('GPL3')
depends=('pyxdg' 'python2-distribute' 'libtorrent-rasterbar>=0.15.0' 'twisted' 'pyopenssl' 'xdg-utils' 'python2-chardet')
makedepends=('intltool' 'pygtk' 'librsvg' 'python-mako')
optdepends=('python-notify: libnotify notifications'
	    'pygtk: needed for gtk ui'
	    'librsvg: needed for gtk ui'
	    'python-mako: needed for web ui')
source=(http://download.deluge-torrent.org/source/${pkgname}-${pkgver}.tar.bz2 deluged deluged.conf.d deluge-web)
backup=(etc/conf.d/deluged)
install=deluge.install
md5sums=('496a9cfaf01e6cf7ab141498acf2d1f1'
         '7d5790eeb14cd0b47d1bb3e0a19c9935'
         '332d23fe0d21ce92b494efd73322856c'
         '6c913ec7322eaa60494ac9d53103005f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -d -m755 "${pkgdir}"/srv/deluge
  install -D -m644 deluge/data/pixmaps/deluge.svg "${pkgdir}"/usr/share/pixmaps/deluge.svg
  install -D -m755 "${srcdir}"/deluged "${pkgdir}"/etc/rc.d/deluged
  install -D -m755 "${srcdir}"/deluge-web "${pkgdir}"/etc/rc.d/deluge-web
  install -D -m644 "${srcdir}"/deluged.conf.d "${pkgdir}"/etc/conf.d/deluged
}
