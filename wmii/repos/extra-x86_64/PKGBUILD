# $Id$
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Contributor: Jeffrey 'jf' Lim <jfs.world@gmail.com>

pkgname=wmii
pkgver=3.6
pkgrel=2
pkgdesc="Next generation WMI (Window Manager Improved 2)"
arch=('i686' 'x86_64') 
license=('MIT')
url="http://wmii.suckless.org/"
depends=('glibc' 'libx11' 'libixp>=0.4' 'dmenu' 'libxt')
options=('!makeflags')
source=(http://dl.suckless.org/wmii/$pkgname-$pkgver.tar.gz
        libixp-api.patch
        wmii.desktop)
md5sums=('9d17a09871fada998b4d989d9318bbf5'
         '3ec94cc2dd2cf38f3e33bc70d2c0193e'
         '997d01cd35931b548fa7e6d1011ff6d4')
sha1sums=('48e24f84f60b9c6ce37f715a46bc32e35d2086f3'
          'ebf3d771b9211a5f3cd3fc9ea90c52892478602e'
          'd6e1bc22cd326be62084b7380a2fdee9fe6c422a')

build()
{
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i $srcdir/libixp-api.patch

  sed -i \
        -e "/^PREFIX/s|=.*|= ${pkgdir}/usr|" \
		-e "/^ETC/s|=.*|= ${pkgdir}/etc|" \
		-e "/^LIBDIR/s|=.*|= /usr/lib|" \
		config.mk || return 1

  make -j1 || return 1
  make -j1 DESTDIR=$pkgdir install

  mkdir -p $pkgdir/etc/X11/sessions
  install -m644 $srcdir/wmii.desktop $pkgdir/etc/X11/sessions/

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/wmii/LICENSE

  # Rid paths of temporary install directory.
  sed -i -e "s|${pkgdir}||g" "${pkgdir}/usr/bin/wmiistartrc"
}
