# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdgantt2
pkgver=16.07.90
pkgrel=1
pkgdesc="Library providing gantt support"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules ki18n python)
conflicts=('libkdepim<16')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('ae9957fc06bf76829e831e4baffff8a8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
