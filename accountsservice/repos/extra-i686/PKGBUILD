# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.34
pkgrel=2
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'systemd')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!libtool')
source=($url/$pkgname-$pkgver.tar.xz more-exclusions.patch git-fixes.patch)
md5sums=('dbf4323b688a314b4bf76890ad6d5279'
         '45ef529fb38d32ec7a1d03005dd20ced'
         '5082c3ba917c520b70f3c5089cb02c9c')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../git-fixes.patch
  patch -Np1 -i ../more-exclusions.patch
}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var --disable-static --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
