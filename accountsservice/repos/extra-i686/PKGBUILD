# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.40
pkgrel=2
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/accountsservice/"
license=(GPL3)
depends=(glib2 polkit systemd)
makedepends=(intltool gobject-introspection python2 gtk-doc)
source=($url/$pkgname-$pkgver.tar.xz)
md5sums=('8b21fe2fbb2b0cf1ba8329596898b203')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/libsystemd-login/libsystemd/g' configure.ac
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var --disable-static --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
