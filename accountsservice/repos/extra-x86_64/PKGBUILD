# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.25
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'libsystemd')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!libtool')
source=(http://cgit.freedesktop.org/accountsservice/snapshot/${pkgname}-${pkgver}.tar.xz
        more-exclusions.patch)
md5sums=('8438df64afffa3bd065c9c3134adb1e7'
         '1d5cf127e5aac407fe7c37b2f4fd503c')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../more-exclusions.patch
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var --disable-static --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
