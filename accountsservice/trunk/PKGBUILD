# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.34
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'systemd')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!libtool')
source=($url/$pkgname-$pkgver.tar.xz more-exclusions.patch)
md5sums=('dbf4323b688a314b4bf76890ad6d5279'
         '45ef529fb38d32ec7a1d03005dd20ced')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../more-exclusions.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var --disable-static --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
