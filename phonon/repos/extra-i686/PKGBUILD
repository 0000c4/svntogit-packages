# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon
pkgver=4.6.0
pkgrel=2
epoch=1
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
license=('LGPL')
pkgdesc="The multimedia framework for KDE"
depends=('qt' 'phonon-backend' 'libpulse' 'libqzeitgeist')
optdepends=('pulseaudio: PulseAudio support')
makedepends=('cmake' 'automoc4' 'pulseaudio' 'qtwebkit')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'fix-plugindir-for-qt-app.patch')
md5sums=('bbe0c1c62ed14c31479c4c1a6cf1e173'
         'e37d67038c2501f25ac7ee7c2de54a9a')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/fix-plugindir-for-qt-app.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHONON_QT_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DPHONON_QT_PLUGIN_INSTALL_DIR=/usr/lib/qt/plugins/designer
  make
}

package(){
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
