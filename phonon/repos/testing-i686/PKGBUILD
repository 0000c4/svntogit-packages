# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon
pkgname=('phonon' 'phonon-xine' 'phonon-gstreamer')
pkgver=4.4.3
pkgrel=1
arch=('i686' 'x86_64')
url="http://phonon.kde.org"
license=('LGPL')
makedepends=('cmake' 'automoc4' 'qt' 'xine-lib' 'gstreamer0.10-base-plugins'
        'mesa' 'pulseaudio')
source=("http://download.kde.org/stable/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('50262d590beb648be9dcad6b913b920db19a84f8')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package_phonon(){
  pkgdesc="The multimedia API for KDE4"
  depends=('qt' 'phonon-backend' 'libpulse')
  optdepends=('pulseaudio: PulseAudio support')
  cd ${srcdir}/build/phonon
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/build/includes
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/build
  install -Dm644 phonon.pc ${pkgdir}/usr/lib/pkgconfig/phonon.pc
}

package_phonon-gstreamer(){
  pkgdesc="Phonon Gstreamer backend"
  depends=('qt' 'gstreamer0.10-base-plugins')
  provides=('phonon-backend')
  cd ${srcdir}/build/gstreamer
  make DESTDIR=${pkgdir} install
}

package_phonon-xine(){
  pkgdesc="Phonon Xine backend"
  depends=('qt' 'xine-lib')
  provides=('phonon-backend')
  cd ${srcdir}/build/xine
  make DESTDIR=${pkgdir} install
}
