# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>
pkgname=dosfstools
pkgver=3.0.18
pkgrel=2
pkgdesc="DOS filesystem utilities"
arch=(i686 x86_64)
depends=('glibc')
source=(http://www.daniel-baumann.ch/files/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        3.0.18-use-memcpy-instead-of-strcpy.patch)
url="http://www.daniel-baumann.ch/software/dosfstools/"
license=('GPL2')

prepare() {
   cd $srcdir/$pkgname-$pkgver
   patch -Np0 -i ../3.0.18-use-memcpy-instead-of-strcpy.patch
}
build() {
   cd $srcdir/$pkgname-$pkgver
   make
}

package () {
   cd $srcdir/$pkgname-$pkgver
   make DESTDIR=$pkgdir PREFIX=/usr SBINDIR=/usr/bin MANDIR=/usr/share/man DOCDIR=/usr/share/doc install
}
md5sums=('a80aab6aac9e56e937b0392a85bfadb9'
         'SKIP'
         '771cf771aadce6ab0b1c9944bf535011')
