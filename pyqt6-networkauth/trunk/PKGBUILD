# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-networkauth
pkgname=(python-pyqt6-networkauth)
pkgver=6.0.0
pkgrel=1
pkgdesc="Python bindings for QtNetworkAuth"
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtnetworkauth/intro"
license=(GPL3)
groups=(pyqt6)
depends=(qt6-networkauth python-pyqt6)
makedepends=(sip pyqt-builder)
source=("https://pypi.python.org/packages/source/P/PyQt6-NetworkAuth/PyQt6_NetworkAuth-$pkgver.tar.gz")
sha256sums=('2128e0b9036ac0d6cdd2fb96b2bb777c7fac92c11428ae7dffe81778b989ce72')

build() {
  cd PyQt6_NetworkAuth-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake-qt6
  cd build
  make
}

package_python-pyqt6-networkauth(){
  cd PyQt6_NetworkAuth-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
