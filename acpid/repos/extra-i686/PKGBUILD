# $Id$
# Maintainer:
# Contributor: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us

pkgname=acpid
pkgver=2.0.12
pkgrel=1
pkgdesc="A daemon for delivering ACPI power management events with netlink support"
arch=('i686' 'x86_64')
url="http://tedfelix.com/linux/acpid-netlink.html"
license=('GPL')
depends=('bash')
replaces=('acpid2')
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything' 'etc/conf.d/acpid')
source=("http://www.tedfelix.com/linux/$pkgname-$pkgver.tar.gz"
        'acpid'
        'anything'
        'handler.sh'
        'acpid.conf.d')
md5sums=('c5104722f72451f64f893b5541bd9ab4'
         '955490c4db5233ec44461db694b873a4'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '24bd717aeb6b628a144b6956ff661ab0'
         '929c6d2e91295c22ed9ec6212d7eabef')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  install -Dm755 "$srcdir/acpid" "$pkgdir/etc/rc.d/acpid"
  install -Dm644 "$srcdir/anything" "$pkgdir/etc/acpi/events/anything"
  install -Dm755 "$srcdir/handler.sh" "$pkgdir/etc/acpi/handler.sh"
  install -Dm644 "$srcdir/acpid.conf.d" "$pkgdir/etc/conf.d/acpid"

  chmod 755 "${pkgdir}"/usr/sbin/acpid
}
