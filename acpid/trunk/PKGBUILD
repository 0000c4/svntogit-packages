# $Id$
# Maintainer:
# Contributor: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us

pkgname=acpid
pkgver=2.0.9
pkgrel=1
pkgdesc="A daemon for delivering ACPI power management events with netlink support"
arch=('i686' 'x86_64')
url="http://tedfelix.com/linux/acpid-netlink.html"
license=('GPL')
depends=('bash')
source=("http://www.tedfelix.com/linux/$pkgname-$pkgver.tar.gz"
        'acpid'
        'anything'
        'handler.sh')
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  install -Dm755 "$srcdir/acpid" "$pkgdir/etc/rc.d/acpid"
  install -Dm644 "$srcdir/anything" "$pkgdir/etc/acpi/events/anything"
  install -Dm755 "$srcdir/handler.sh" "$pkgdir/etc/acpi/handler.sh"

  chmod 755 "${pkgdir}"/usr/sbin/acpid
}
md5sums=('8b30aa1b31f607161745a4ac7a19699b'
         '277d6f5199650ac83fd3ad3612f79d2f'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '0e8dd13793b1baa79a745f4034888367')
