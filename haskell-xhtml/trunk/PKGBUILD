# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
pkgname=haskell-xhtml
pkgver=3000.2.0.1
pkgrel=4
pkgdesc="Combinators for producing XHTML 1.0, including the Strict, Transitional and Frameset variants."
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/xhtml"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/xhtml/$pkgver/xhtml-$pkgver.tar.gz)
install=${pkgname}.install

build() {
    cd ${srcdir}/xhtml-$pkgver
    runhaskell Setup configure -p --enable-split-objs --enable-shared --prefix=/usr
    runhaskell Setup build
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
    cd ${srcdir}/xhtml-$pkgver
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('9e3e7d9c66618c5e29716f90e54363c9')
