# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=modemmanager
_realname=ModemManager
pkgver=0.7.990
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=(i686 x86_64)
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=(GPL2)
depends=(udev ppp libqmi polkit hicolor-icon-theme)
makedepends=(intltool)
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
install=modemmanager.install
source=(http://download.gnome.org/sources/$_realname/${pkgver::3}/$_realname-$pkgver.tar.xz
        ModemManager.service)
sha256sums=('36547db99fccd7eac1f1dd75178232a61306c6b11e8926b83bcfc3a12c4222ca'
            '4c622bea22582751c1a0715f69cf7f393ac9d1318e09b02c2ea8d251261983ee')

build() {
  cd "$_realname-$pkgver"
   ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-udev-base-dir=/usr/lib/udev \
        --disable-static \
        --with-polkit
  make
}

package() {
  cd "$_realname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/ModemManager.service" \
    "$pkgdir/usr/lib/systemd/system/ModemManager.service"
  echo "SystemdService=ModemManager.service" \
    >> "$pkgdir/usr/share/dbus-1/system-services/org.freedesktop.ModemManager1.service"
}
