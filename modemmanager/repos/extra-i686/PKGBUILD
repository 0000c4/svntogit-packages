# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=ModemManager
pkgname=(modemmanager libmm-glib)
pkgver=1.2.0
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2)
depends=(systemd polkit ppp libqmi libmbim)
makedepends=(intltool gtk-doc gobject-introspection vala)
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
source=(http://www.freedesktop.org/software/$pkgbase/$pkgbase-$pkgver.tar.xz)
sha256sums=('8bd9d8c3b506f34a9f927e95784026d1fe132c9d60fc426a892b690f7d6408bc')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --with-udev-base-dir=/usr/lib/udev \
        --with-polkit=permissive \
        --enable-gtk-doc \
        --disable-static
  make
}

package_modemmanager() {
  depends+=(libmm-glib)

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libmm-glib uninstall
  make DESTDIR="$pkgdir" -C vapi uninstall
}

package_libmm-glib() {
  pkgdesc="ModemManager library"
  depends=(glib2)

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" -C libmm-glib install
  make DESTDIR="$pkgdir" -C vapi install
}
