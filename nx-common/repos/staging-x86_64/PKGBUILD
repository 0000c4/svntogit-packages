# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org>

pkgname=nx-common
pkgver=3.5.0
pkgrel=1
pkgdesc="NoMachine NX common package for client and server"
arch=('i686' 'x86_64')
license=('GPL')
url="http://nomachine.com/"
depends=('libjpeg-turbo>=1.1.1' 'libpng>=1.4.5' 'openssl>=1.0.0' 'gcc-libs' 'bash')
makedepends=('xorg-server-devel')
source=( #Compression libs and proxy sources
	http://64.34.161.181/download/$pkgver/sources/nxcomp-$pkgver-1.tar.gz
	http://64.34.161.181/download/$pkgver/sources/nxcompsh-$pkgver-1.tar.gz
	http://64.34.161.181/download/$pkgver/sources/nxssh-$pkgver-1.tar.gz
	nxcompsh-gcc43.patch)
options=(!libtool)
md5sums=('30a182146479004ec63b8a1b9adccfcf'
         '84ade443b79ea079380b754aba9d392e'
         '9f13262bc0bf5187fcc7e22924955bc7'
         'b6c279654dac421fc3dd1a27d66ff53c')

build() {
  # nxcomp
  cd ${srcdir}/nxcomp
  ./configure --prefix=/opt/NX
  make
  # nxcompsh
  cd ${srcdir}/nxcompsh
  patch -Np1 -i ${srcdir}/nxcompsh-gcc43.patch
  ./configure --prefix=/opt/NX
  make
  # nxssh
  cd ${srcdir}/nxssh
  ./configure --prefix=/opt/NX
  make
}

package() {
  mkdir -p ${pkgdir}/opt/NX/bin
  mkdir -p ${pkgdir}/opt/NX/lib 

  # nxcomp
  cd ${srcdir}/nxcomp
  cp -a libXcomp.so* ${pkgdir}/opt/NX/lib
  # nxcompsh
  cd ${srcdir}/nxcompsh
  cp -a libXcompsh.so* ${pkgdir}/opt/NX/lib
  # nxssh
  cd ${srcdir}/nxssh
  install -D -m755 nxssh ${pkgdir}/opt/NX/bin/nxssh
}
