# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kholidays
pkgver=17.03.90
pkgrel=1
pkgdesc="KDE library for regional holiday information"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('78bc0ebeceab5d10c5fe4f377123a9d3242fb63633707e7da655e231fc0aa595'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
