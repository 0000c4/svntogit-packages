# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kholidays
pkgver=15.12.3
pkgrel=1
pkgdesc="KDE library for regional holiday information"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
        kholidays-isnan.patch::"https://quickgit.kde.org/?p=kholidays.git&a=commitdiff&h=488aa158&o=plain")
md5sums=('a29a9116c63f73535f2d0d9887f6981d'
         'c2977e7694aee2646f60a8fc4ca58e7c')

prepare() {
  mkdir -p build

# Fix build with glibc 2.23
  cd $pkgname-$pkgver
  patch -p1 -i ../kholidays-isnan.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
