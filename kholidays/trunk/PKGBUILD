# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kholidays
pkgver=16.04.3
pkgrel=1
pkgdesc="KDE library for regional holiday information"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('9330a4b4a316e8ec32e72ddf1233f401')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
