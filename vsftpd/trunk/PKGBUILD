# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=2.3.4
pkgrel=1
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
url="http://vsftpd.beasts.org/"
license=('GPL2')
depends=('tcp_wrappers' 'openssl')
backup=('etc/vsftpd.conf' 'etc/xinetd.d/vsftpd')
install=vsftpd.install
source=(ftp://vsftpd.beasts.org/users/cevans/vsftpd-$pkgver.tar.gz \
        vsftpd.xinetd vsftpd.d)
md5sums=('2ea5d19978710527bb7444d93b67767a' 'b07fd4609c70063c1d6b20142910c1a6'\
         '60060a6c7db3b3b7baa1561a302e6923')
sha1sums=('b774cc6b4c50e20f4fe9ca7f6aa74169ce7fe5ea' 'c87b4ce56dac15223694a6e86c01ea813b877596'\
         '24f268956c24e78be0c234c6d31f41487922eafe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_TCPWRAPPERS$|#define VSF_BUILD_TCPWRAPPERS|' \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h
  make CFLAGS="${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -D -m755 vsftpd "${pkgdir}/usr/sbin/vsftpd"
  install -D -m644 vsftpd.conf "${pkgdir}/etc/vsftpd.conf"
  install -D -m644 vsftpd.8 "${pkgdir}/usr/share/man/man8/vsftpd.8"
  install -D -m644 vsftpd.conf.5 "${pkgdir}/usr/share/man/man5/vsftpd.conf.5"
  install -D -m644 "${srcdir}/vsftpd.xinetd" "${pkgdir}/etc/xinetd.d/vsftpd"
  install -D -m755 "${srcdir}/vsftpd.d" "${pkgdir}/etc/rc.d/vsftpd"

  install -d -m755 "${pkgdir}/usr/share/empty"
}
