# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=2.3.2
pkgrel=1
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
license=('GPL')
backup=(etc/vsftpd.conf etc/xinetd.d/vsftpd)
url="http://vsftpd.beasts.org/"
depends=('tcp_wrappers' 'openssl')
install=vsftpd.install
source=(ftp://vsftpd.beasts.org/users/cevans/vsftpd-$pkgver.tar.gz \
        vsftpd.xinetd vsftpd.d)
md5sums=('bad7b117d737a738738836041edc00db'
         'b07fd4609c70063c1d6b20142910c1a6'
         '60060a6c7db3b3b7baa1561a302e6923')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_TCPWRAPPERS$|#define VSF_BUILD_TCPWRAPPERS|' \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h
  make CFLAGS="${CFLAGS}"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -D -m755 vsftpd ${pkgdir}/usr/sbin/vsftpd
  install -D -m644 vsftpd.conf ${pkgdir}/etc/vsftpd.conf
  install -D -m644 vsftpd.8 ${pkgdir}/usr/share/man/man8/vsftpd.8
  install -D -m644 vsftpd.conf.5 ${pkgdir}/usr/share/man/man5/vsftpd.conf.5
  install -D -m644 ${srcdir}/vsftpd.xinetd ${pkgdir}/etc/xinetd.d/vsftpd
  install -D -m755 ${srcdir}/vsftpd.d ${pkgdir}/etc/rc.d/vsftpd

  install -d -m755 ${pkgdir}/usr/share/empty
}
