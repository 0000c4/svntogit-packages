# $Id: PKGBUILD 2392 2008-06-01 19:38:25Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=mousetweaks
pkgver=2.27.91
pkgrel=1
pkgdesc="Mouse accessibility enhancements for the GNOME desktop"
arch=(i686 x86_64)
license=('GPL3' 'FDL')
depends=('gnome-panel>=2.27.91' 'at-spi>=1.27.91')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.17.4' 'intltool')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=mousetweaks.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('7a03e1fbd5f4dfff12f8f5e585eb668bb85e917808a1ff725861ab2eb3cc62bd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain mousetweaks ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
