# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnutls
pkgver=3.0.0
pkgrel=2
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
install=gnutls.install
options=('!libtool' '!zipman')
depends=('gcc-libs' 'libtasn1' 'readline' 'zlib' 'nettle>=2.2')
source=(ftp://ftp.gnu.org/gnu/gnutls/${pkgname}-${pkgver}.tar.xz
	align.patch)
md5sums=('0677a66667f48810ff8df8335a9a9f9b'
         'bce979498addb45d0fa8d6951f93fab3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix AES-NI for i686
  patch -Np1 -i ${srcdir}/align.patch
  ./configure --prefix=/usr \
	--with-zlib \
	--disable-static \
	--disable-guile \
	--without-p11-kit
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check # passes all  || /bin/true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
