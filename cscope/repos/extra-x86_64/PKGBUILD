# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=cscope
pkgver=15.8.a
_realver=15.8a
pkgrel=4
pkgdesc="A developers tool for browsing program code"
arch=('x86_64')
url="http://cscope.sourceforge.net/"
license=('BSD')
depends=('ncurses' 'sh')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_realver}.tar.gz)
sha512sums=('bedba69820eca86420f6c975d1f267de67a457b182a3fe49720a2ae5ea48759c5b5b8db06ed5ffd85173ddf4a0f12ca722e87200dea4a2da7d43c8c711b00318')

build() {
	cd "${srcdir}/${pkgname}-${_realver}"
	sed -i 's|/usr/local/lib/cs|/usr/lib/cs|' contrib/ocs

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${_realver}"
	make DESTDIR="${pkgdir}" install
  
	install -d ${pkgdir}/usr/lib/cs
	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

	cd contrib/xcscope
	install -Dm644 xcscope.el "${pkgdir}"/usr/share/emacs/site-lisp/xcscope.el
	install -m755 cscope-indexer "${pkgdir}"/usr/bin
}
