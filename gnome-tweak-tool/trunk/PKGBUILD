# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.26.1
pkgrel=1
pkgdesc="Customize advanced GNOME 3 options"
arch=(any)
url="https://wiki.gnome.org/Apps/GnomeTweakTool"
license=(GPL)
depends=(gnome-settings-daemon python-gobject)
makedepends=(git meson)
groups=(gnome-extra)
_commit=bc0a6fb59dcbdeb70d2c6124b5286186b4e2e8ef  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/gnome-tweak-tool#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname \
    --libexecdir=/usr/lib/$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
