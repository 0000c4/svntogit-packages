# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.24.0+3+g01bffca
pkgrel=1
pkgdesc="Customize advanced GNOME 3 options"
arch=(any)
url="https://wiki.gnome.org/Apps/GnomeTweakTool"
license=(GPL)
depends=(gnome-settings-daemon python2-gobject)
makedepends=(intltool gnome-common git)
groups=(gnome-extra)
_commit=01bffca3a608572a6f253f6cff0dd57dca3f9b9d  # gnome-3-24
source=("git+https://git.gnome.org/browse/gnome-tweak-tool#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i '1s|python$|&2|' gnome-tweak-tool
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
