# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.2.2
pkgrel=2
pkgdesc="A tool to customize advanced GNOME 3 options."
arch=(any)
url="https://live.gnome.org/GnomeTweakTool"
license=('GPL')
depends=('gnome-settings-daemon' 'python2-gobject' 'gnome-shell')
makedepends=('intltool')
optdepends=('gnome-shell-extension-user-theme: Shell theme selector')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('57ad08b813ca2f5ea2e131b22cb365d33788d051b80f3f6decc04e3a5404f8f5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix user-theme extension name
  sed -i '/THEME_EXT_NAME =/s/gnome.org/gcampax.github.com/' gtweak/tweaks/tweak_shell.py

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gnome-tweak-tool
}

# vim:set ts=2 sw=2 et:
