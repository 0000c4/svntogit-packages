# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.26.3
pkgrel=1
pkgdesc="Customize advanced GNOME 3 options"
url="https://wiki.gnome.org/Apps/GnomeTweakTool"
arch=(any)
license=(GPL)
depends=(gnome-settings-daemon python-gobject nautilus)
makedepends=(git meson)
groups=(gnome-extra)
_commit=3d53a5ad00cedcc7df30bc709c923cbdf97f82cf  # tags/3.26.3^0
source=("git+https://git.gnome.org/browse/gnome-tweak-tool#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
