# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.3.4
pkgrel=2
pkgdesc="A tool to customize advanced GNOME 3 options."
arch=(any)
url="https://live.gnome.org/GnomeTweakTool"
license=('GPL')
depends=('gnome-settings-daemon' 'python2-gobject')
makedepends=('intltool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        schema-crash.patch)
sha256sums=('de18bf67e44af1b6757cf8e3a78c480b84286658be3d5d9ba7c13f5c49763dc6'
            '32add78601b30a93a4bd753f6a3bb99927f5ae8a68c94c0f75a0f3497e7032df')

build() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../schema-crash.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gnome-tweak-tool
}

# vim:set ts=2 sw=2 et:
