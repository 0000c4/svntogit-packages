# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gnome-tweak-tool
pkgver=3.1.90
pkgrel=1
pkgdesc="A tool to customize advanced GNOME 3 options."
arch=(any)
url="https://live.gnome.org/GnomeTweakTool"
license=('GPL')
depends=('gconf' 'gsettings-desktop-schemas' 'python2-gobject')
makedepends=('intltool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('d5f2d4a2508132aadefecc8bd572e42fc66ee8954afed19f45372887dba6e56c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gnome-tweak-tool
}

# vim:set ts=2 sw=2 et:
