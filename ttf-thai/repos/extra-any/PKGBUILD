# $Id$
# Maintainer: Firmicus <francois.archlinux.org>
# Contributor: Camille Moncelier <pix@devlife.org>

pkgname=ttf-thai
pkgver=0.4.17
pkgrel=1
pkgdesc="A collection of basic Thai fonts"
arch=('any')
url="http://linux.thai.net/taxonomy/term/3"
license=('GPL' 'custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
provides=('thai-ttf')
install=ttf.install
source=("ftp://linux.thai.net/pub/ThaiLinux/software/thai-ttf/thai-ttf-${pkgver}.tar.gz"
        'LICENSE')
sha1sums=('a11b2a74d3fb5cb4f7ffb42d9a94b2e2d88b4169'
          '39a8c3eb83856f9dff63a3493f891a702d3fa802')

package() {
  cd "${srcdir}/thai-ttf-${pkgver}"

  # install fonts to right place
  install -d "${pkgdir}"/usr/share/fonts/TTF
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/TTF/

  # install specification for fonts to right place
  install -d "${pkgdir}"/etc/fonts/conf.avail/
  install -m644 etc/fonts/conf.avail/64-ttf-thai-tlwg.conf "${pkgdir}"/etc/fonts/conf.avail/
  install -m644 etc/fonts/conf.avail/89-ttf-thai-tlwg-synthetic.conf "${pkgdir}"/etc/fonts/conf.avail/

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
