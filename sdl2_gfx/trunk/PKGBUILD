# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=sdl2_gfx
epoch=1
pkgver=1.0.1
pkgrel=1
pkgdesc="SDL Graphic Primitives (Version 2)"
arch=('i686' 'x86_64')
url="http://www.ferzkopp.net/joomla/software-mainmenu-14/4-ferzkopps-linux-software/19-sdlgfx"
license=('zlib')
depends=('sdl2')
makedepends=('svn')
source=("https://www.ferzkopp.net/Software/SDL2_gfx/SDL2_gfx-${pkgver}.tar.gz")
sha512sums=('413f0ac8a60b6c4c91f154dd72a512f410e134642fefb80cd3482646c9aab7dd56d2cc9015e78ee59b5bc89adc7397a248fe2132077c004114ad6505ca4194de')

build() {
  cd "${srcdir}/SDL2_gfx-${pkgver}"

  ./autogen.sh
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/SDL2_gfx-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
