# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kactivities-frameworks
pkgname=('kactivities-frameworks' 'libkactivities-frameworks')
pkgver=4.100.0
pkgrel=1
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
makedepends=('qt5-declarative' 'kservice' 'extra-cmake-modules' 'boost')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/kactivities-${pkgver}.tar.xz")
md5sums=('45e20c18096f272ce665a120aa32ac83')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package_kactivities-frameworks() {
  pkgdesc="The KActivity Manager Daemon"
  depends=('kwindowsystem' 'libkactivities-frameworks')
  provides=('kactivities')
  conflicts=('kactivities')
  groups=('kf5')

  cd build
  install -Dm755 src/service/kactivitymanagerd \
    "${pkgdir}"/usr/bin/kactivitymanagerd
}

package_libkactivities-frameworks() {
  pkgdesc="Core components for the KDE's Activities"
  depends=('kconfig' 'qt5-declarative' 'ki18n' 'kcoreaddons' 'kdbusaddons')

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm "${pkgdir}"/usr/bin/kactivitymanagerd
  rmdir "${pkgdir}"/usr/bin
}
