# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon-vlc
pkgver=0.6.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
pkgdesc="Phonon VLC backend"
license=('LGPL')
depends=('vlc')
makedepends=('cmake' 'automoc4' 'phonon')
provides=('phonon-backend')
source=("http://download.kde.org/stable/phonon/phonon-backend-vlc/${pkgver}/src/phonon-backend-vlc-${pkgver}.tar.xz")
md5sums=('d227b92619124a2b85e2d2e5f0fff90e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../phonon-backend-vlc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
