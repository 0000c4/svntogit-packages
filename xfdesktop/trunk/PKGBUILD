# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.8.3
pkgrel=2
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'thunar>=1.2.0' 'garcon' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool' 'xfce4-panel')
optdepends=('xfce4-panel: panel menu plugin')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
options=('!libtool')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/$pkgname/4.8/$pkgname-$pkgver.tar.bz2
        implement-paste-on-desktop.patch)
sha256sums=('e58460f52ae96c389402f6db62c46db61cd51cdb550a42bd97c0ab2a5b62f424'
            'f26a75fe27c198cd40fad139310e984f4eaac0dfa2cae0d7f32498a8f58322b2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix FS#27866: Paste menu entry in context menu is deactivated
  # Upstream report/patch: https://bugzilla.xfce.org/show_bug.cgi?id=3804
  patch -Np1 -i "$srcdir/implement-paste-on-desktop.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-exo \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
