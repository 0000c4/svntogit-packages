# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.13.3
pkgrel=2
pkgdesc="A desktop manager for Xfce"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck3' 'exo' 'thunar' 'garcon' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        bug15198-move-icons-to-other-displays.patch)
sha256sums=('80f7f28ef17638fe1cf4481db694731c208785a56b6c840005202f9c2db238f5'
            'a469774b478b0c666a6fcc15dd5165b1a5a15ed263fc85f7bfd91f7b3ca1168b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=15198
  patch -Np1 -i ../bug15198-move-icons-to-other-displays.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
