# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.12.3
pkgrel=3
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'thunar' 'garcon' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        0001-Settings-Allow-directories-to-be-selected.patch)
sha256sums=('a8a8d93744d842ca6ac1f9bd2c8789ee178937bca7e170e5239cbdbef30520ac'
            '6dbd98fc621b5da741d4528c884e0eb9e1c045407879b3d046a1a7faae00bac6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../0001-Settings-Allow-directories-to-be-selected.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
