# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kldap
pkgver=17.11.80
pkgrel=1
pkgdesc="LDAP access API for KDE"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcompletion)
optdepends=('kio: LDAP kioslave')
makedepends=(extra-cmake-modules python kio kdoctools)
conflicts=(kio-pim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1e662838b883ac5fd772c196b12ef12b1fe8c839fd7a1599dcd305f35f79dc55'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
