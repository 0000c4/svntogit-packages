# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=imagemagick-doc
pkgver=6.6.6.10
pkgrel=1
pkgdesc="The ImageMagick documentation (utilities manuals and libraries API)"
arch=('any')
url="http://www.imagemagick.org/"
license=('custom')
depends=('imagemagick')
options=('!makeflags')
source=(ftp://ftp.imagemagick.org/pub/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz)
md5sums=('59e2da8333ef645ade5f490c270651a1')
sha1sums=('d83fd4cef64a7fd7d3cb96a2d424fdda07caa404')

build() {
	cd "${srcdir}"/ImageMagick-${pkgver%.*}-${pkgver##*.}
	./configure --prefix=/usr
}

package() {
	cd "${srcdir}"/ImageMagick-${pkgver%.*}-${pkgver##*.}
	make DESTDIR="${pkgdir}" install-data-html
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 NOTICE "${pkgdir}/usr/share/licenses/${pkgname}/NOTICE"
}
