# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=3.28.1
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
url="https://git.gnome.org/browse/sushi"
arch=(x86_64)
license=(GPL2)
groups=(gnome)
depends=(clutter-gtk clutter-gst gjs libmusicbrainz5 webkit2gtk gtksourceview3 evince)
optdepends=('libreoffice: OpenDocument formats')
makedepends=(intltool gobject-introspection git)
_commit=d398178aecd7f87cddc547edfb5ba100b63366f2  # tags/3.28.1^0
source=("git+https://git.gnome.org/browse/sushi#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  mkdir m4
  intltoolize
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
