# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=sushi
pkgver=0.1.92
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=(gnome-extra)
depends=('clutter-gtk' 'clutter-gst' 'gjs' 'libmusicbrainz3' 'libwebkit3' 'gtksourceview3' 'evince')
makedepends=('intltool' 'gobject-introspection')
options=(!libtool)
source=(http://download.gnome.org/sources/$pkgname/0.1/$pkgname-$pkgver.tar.xz)
sha256sums=('93e5982739a79ba4d305785c64b0f1de79aa5da57b2287bcaaa4a071e66d8688')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --libexecdir=/usr/lib/sushi
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
