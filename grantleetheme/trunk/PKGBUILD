# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=grantleetheme
pkgver=16.08.2
pkgrel=1
pkgdesc="Library for Grantlee theming support"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(grantlee knewstuff)
makedepends=(extra-cmake-modules python)
conflicts=('libkdepim<16')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('d497f473de12086db129e364bfefe7c1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
