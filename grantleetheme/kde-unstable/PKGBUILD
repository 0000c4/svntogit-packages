# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=grantleetheme
pkgver=19.11.90
pkgrel=1
pkgdesc="Library for Grantlee theming support"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(grantlee knewstuff)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c99ca25c5d9fb5239a83a675b8c81228a7224b67a1ad9ebeec57bdd0121ea36f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
