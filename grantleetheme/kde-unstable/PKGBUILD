# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=grantleetheme
pkgver=16.03.90
pkgrel=1
pkgdesc="Library for Grantlee theming support"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(grantlee-qt5 knewstuff)
makedepends=(extra-cmake-modules python)
conflicts=('libkdepim<16')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('e4b94df4d2594dbe28760ef5f00a9d3d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
