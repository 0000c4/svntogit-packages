# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=solid
pkgver=5.45.0
pkgrel=2
pkgdesc='Hardware integration and detection'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base media-player-info upower udisks2)
makedepends=(extra-cmake-modules qt5-tools doxygen qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-391706.patch::"https://cgit.kde.org/solid.git/patch/?id=122a6cd8"
        kdebug-370975.patch::"https://cgit.kde.org/solid.git/patch/?id=d735708f")
sha256sums=('ade2068f5d0affeff3599ef12b67b016a4518e3085863739f9b6e90685d4b09c'
            'SKIP'
            '72219ab1071975698b31f6992adedc3f0e5f8228c0a38a33d50d73df167a4463'
            '4533aeb76a6aaac4820d3248657f7dce053592e5688b676627a6c380c35eb5b2')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-391706.patch # Fix automonting of removable devices
  patch -p1 -i ../kdebug-370975.patch # Fix bogus error message when mounting external drives
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
