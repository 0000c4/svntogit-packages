# $Id: PKGBUILD 5595 2008-07-18 21:58:26Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.26.1
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
makedepends=('pango>=1.28.0' 'glibmm>=2.24.0' 'cairomm>=1.8.2' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
conflicts=('gtkmm<2.13.0')
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
sha256sums=('95a0974d93dddffe65bea8adfc252907383f60fa9998de2a9a5a8c01f9cbada2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
}
package_pangomm() {
  pkgdesc="C++ bindings for pango"
  depends=('pango>=1.28.0' 'glibmm>=2.24.0' 'cairomm>=1.8.2')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install || return 1
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install || return 1
}
