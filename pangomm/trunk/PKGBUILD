# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.40.1
pkgrel=1
pkgdesc="C++ bindings for Pango"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('pango' 'glibmm' 'cairomm')
makedepends=('glibmm-docs' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::4}/${pkgname}-${pkgver}.tar.xz)
url="http://gtkmm.sourceforge.net/"
sha256sums=('9762ee2a2d5781be6797448d4dd2383ce14907159b30bc12bf6b08e7227be3af')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}
package_pangomm() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_pangomm-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
