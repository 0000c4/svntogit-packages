# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=(pangomm pangomm-docs)
pkgver=2.42.1
pkgrel=1
pkgdesc="C++ bindings for Pango"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(pango glibmm cairomm)
makedepends=(git mm-common glibmm-docs cairomm-docs meson)
_commit=7dfc6c3372faaa4a7c492d08f09881b02095145b  # tags/2.42.1^0
source=("git+https://gitlab.gnome.org/GNOME/pangomm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

build() {
  mkdir -p build
  cd build
  arch-meson ../$pkgbase
  ninja
}

package_pangomm() {
  cd build
  DESTDIR="$pkgdir" ninja install
  mv "$pkgdir"/usr/share "$srcdir"
}

package_pangomm-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "$pkgdir"/usr
  mv share "$pkgdir"/usr
}
