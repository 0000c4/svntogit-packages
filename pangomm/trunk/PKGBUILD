# $Id: PKGBUILD 5595 2008-07-18 21:58:26Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.26.3
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
makedepends=('pango>=1.28.0' 'glibmm>=2.24.2' 'glibmm-docs>=2.24.2' 'cairomm>=1.8.2' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
conflicts=('gtkmm<2.13.0')
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
md5sums=('1f2712d53918cfb4745c291a67b86156')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}
package_pangomm() {
  pkgdesc="C++ bindings for pango"
  depends=('pango>=1.28.0' 'glibmm>=2.24.2' 'cairomm>=1.8.2')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
