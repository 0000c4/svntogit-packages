# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-weather-plugin
pkgver=0.7.3.99.git20110129
pkgrel=1
pkgdesc="A weather plugin for the Xfce4 panel"
arch=(i686 x86_64)
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-weather-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel>=4.8.0' 'libxfcegui4' 'libxml2' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
options=('!libtool')
install=${pkgname}.install
source=(#http://archive.xfce.org/src/panel-plugins/${pkgname}/0.7/${pkgname}-${pkgver}.tar.bz2
	#http://git.xfce.org/panel-plugins/xfce4-weather-plugin/snapshot/xfce4-weather-plugin-7c5e6380ad8b47165baf57a977f9912ebb753a71.tar.bz2
	ftp://ftp.archlinux.org/other/$pkgname/xfce4-weather-plugin-7c5e6380ad8b47165baf57a977f9912ebb753a71.tar.bz2
	0001-Make-the-plugin-compatible-with-Xfce-4.8-bug-6965.patch)
md5sums=('880217e17e6d8f5a0769703490acf28f'
         '12bd5b6949804b28f2d704790a28c6b8')

build() {
#  cd ${srcdir}/${pkgname}-${pkgver}
  cd ${srcdir}/${pkgname}*
  # panel 4.8 patch from http://bugzilla.xfce.org/show_bug.cgi?id=6965
  patch -Np1 -i ${srcdir}/0001-Make-the-plugin-compatible-with-Xfce-4.8-bug-6965.patch
  xdt-autogen
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
#  cd ${srcdir}/${pkgname}-${pkgver}
  cd ${srcdir}/${pkgname}*
  make DESTDIR=${pkgdir} install
}
