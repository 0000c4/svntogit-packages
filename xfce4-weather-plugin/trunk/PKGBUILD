# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-weather-plugin
pkgver=0.8.11
pkgrel=1
pkgdesc="A weather plugin for the Xfce4 panel"
arch=('x86_64')
url="https://goodies.xfce.org/projects/panel-plugins/xfce4-weather-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxml2' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('0382ce99c0ad4424afe7096a354f301a9396d12807a0f2ab041912d6ab58145d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --disable-upower
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
