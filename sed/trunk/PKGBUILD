# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=sed
pkgver=4.3
pkgrel=1
pkgdesc='GNU stream editor'
arch=('i686' 'x86_64')
url='https://www.gnu.org/software/sed/'
license=('GPL3')
groups=('base' 'base-devel')
depends=('glibc' 'acl' 'attr')
makedepends=('gettext')
source=("https://ftp.gnu.org/pub/gnu/sed/$pkgname-$pkgver.tar.xz"{,.sig})
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') #Jim Meyering <jim@meyering.net>
md5sums=('1957fe58dffa4a4106c1f1d7cc8dee18'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
