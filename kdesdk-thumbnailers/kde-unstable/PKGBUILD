# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-thumbnailers
pkgver=4.10.95
pkgrel=1
pkgdesc='Plugins for the thumbnailing system'
url='http://kde.org/applications/development/cervisia/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/kdesdk-thumbnailers-${pkgver}.tar.xz")
sha1sums=('b1530866d6b866b65972a9e4521b302b702a303f')

build() {
  mkdir build
  cd build
  cmake ../kdesdk-thumbnailers-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
