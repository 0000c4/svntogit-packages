# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=libwebkit
pkgname=(libwebkit libwebkit3)
pkgver=1.7.91
pkgrel=1
pkgdesc="An opensource web content engine"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'icu' 'gstreamer0.10-base' 'libsoup' 'enchant' 'libgl')
makedepends=('gperf' 'gobject-introspection' 'python2' 'gtk2' 'gtk3' 'mesa' 'gtk-doc')
options=('!libtool' '!emptydirs')
install=libwebkit.install
source=(http://webkitgtk.org/webkit-$pkgver.tar.xz
        webkit-gtk-1.7.90-parallel-make-hack.patch
        python.patch)
sha256sums=('9d9de6968c466b973988ce97e4f2f53535dd4f72356bd7b29b03ba9dab937545'
            'e20d7d45e6230308f7d4a7aac1095cb1fe8d9c7eb0f3e65b061acf8bc4bbaf73'
            '29b82e47277d982bc57a7ba967d28ae76e07556986627c2e456694b699ae0e09')

build() {
  cd "$srcdir/webkit-$pkgver"
  patch -Np1 -i $srcdir/webkit-gtk-1.7.90-parallel-make-hack.patch
  patch -Np1 -i $srcdir/python.patch
  autoreconf -I Source/autotools -fi
  mkdir build-gtk{2,3}

  ( cd build-gtk2 && _build --with-gtk=2.0 )
  ( cd build-gtk3 && _build --with-gtk=3.0 )
}

_build() {
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-introspection \
    --with-font-backend=freetype \
    --with-unicode-backend=icu \
    --enable-geolocation=no \
    --enable-spellcheck "$@"
#  make all stamp-po
#  until parallel building is fixed
  make all-built-sources-local
  make all-ltlibraries-local
  make all-programs-local
  make all-data-local
  make stamp-po
}

package_libwebkit() {
  pkgdesc+=" (for GTK2)"
  depends+=(gtk2)

  cd "$srcdir/webkit-$pkgver/build-gtk2"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/libwebkit/LICENSE"
}

package_libwebkit3() {
  pkgdesc+=" (for GTK3)"
  depends+=(gtk3)

  cd "$srcdir/webkit-$pkgver/build-gtk3"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/libwebkit3/LICENSE"
}



