# $Id: PKGBUILD,v 1.4 2008/08/08 17:43:21 BaSh Exp $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.0 # see configure.ac
_svnver=41121 # date: 20090221
pkgrel=0.${_svnver}
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkit.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.0' 'gstreamer0.10-base' 'libsoup')
makedepends=('gperf')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webgitgtk-svn')
source=(http://builds.nightly.webkit.org/files/trunk/src/WebKit-r${_svnver}.tar.bz2)
options=('!libtool')
md5sums=('091b632ae95765b3db8d2ea7a84dfb3b')

build() {
  cd ${srcdir}/WebKit-r${_svnver}
  ./autogen.sh --prefix=/usr \
	--enable-video \
	--with-font-backend=freetype \
	--with-http-backend=soup \
	--enable-jit # not yet supported on x86_64
  make || return 1
  make DESTDIR="$pkgdir" install

  # install license
  install -Dm644 ${srcdir}/WebKit-r${_svnver}/WebKit/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

