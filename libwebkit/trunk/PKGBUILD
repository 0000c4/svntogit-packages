# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.2.5
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.4' 'gstreamer0.10-base' 'libsoup' 'enchant')
makedepends=('gperf' 'gtk-doc' 'gobject-introspection')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
options=('!libtool') # '!makeflags')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz
        introspection.patch)
md5sums=('09f04985665b9abf6f0d9956f86a6a31'
         'e7e83312618cb30cc9a1567a21cd0e06')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -Np0 -i "${srcdir}/introspection.patch"
  ./configure --prefix=/usr \
      --enable-video --enable-introspection \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu
  make
}

package() {
  cd "${srcdir}/webkit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
