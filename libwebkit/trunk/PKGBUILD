# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.2.4
pkgrel=2
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.4' 'gstreamer0.10-base' 'libsoup' 'enchant')
makedepends=('gperf' 'gtk-doc' 'gobject-introspection')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
options=('!libtool')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz
	webkitgtk-1.2.5.patch
        introspection.patch)
md5sums=('dc3a92dd0e8c2e70263fbfdf809b51a5'
         '41542fac596ef2359592a83451dec37d'
         'e7e83312618cb30cc9a1567a21cd0e06')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -Np1 -i "${srcdir}/webkitgtk-1.2.5.patch"
  patch -Np0 -i "${srcdir}/introspection.patch"
  ./configure --prefix=/usr \
      --enable-video --enable-introspection \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu
  make || return 1
}

package() {
  cd "${srcdir}/webkit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
