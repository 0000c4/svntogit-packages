# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=libwebkit
pkgname=(libwebkit libwebkit3)
pkgver=1.3.11
pkgrel=1
pkgdesc="An opensource web content engine"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'icu>=4.6' 'gstreamer0.10-base' 'libsoup' 'enchant')
makedepends=('gperf' 'gtk-doc' 'gobject-introspection' 'python2' 'gtk2' 'gtk3')
options=('!libtool')
install=libwebkit.install
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz)
md5sums=('6d4506086fb0d274d1a2c871fcaf851e')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  mkdir build-gtk2
  pushd build-gtk2
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-video --enable-introspection \
    --with-font-backend=freetype --enable-gtk-doc \
    --enable-jit --with-unicode-backend=icu \
    --enable-3D-transforms --enable-mathml \
    --with-gtk=2.0
  make
  popd

  mkdir build-gtk3
  pushd build-gtk3
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-video --enable-introspection \
    --with-font-backend=freetype --enable-gtk-doc \
    --enable-jit --with-unicode-backend=icu \
    --enable-3D-transforms --enable-mathml \
    --with-gtk=3.0
  make
  popd
}

package_libwebkit() {
  pkgdesc+=" (for GTK2)"
  depends+=(gtk2)
  provides=('webkitgtk-svn')
  conflicts=('webkitgtk-svn')
  replaces=('webkitgtk-svn')

  cd "$srcdir/webkit-$pkgver/build-gtk2"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../Source/WebKit/LICENSE "${pkgdir}/usr/share/licenses/libwebkit/LICENSE"
}

package_libwebkit3() {
  pkgdesc+=" (for GTK3)"
  depends+=(gtk3)

  cd "${srcdir}/webkit-${pkgver}/build-gtk3"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../Source/WebKit/LICENSE "${pkgdir}/usr/share/licenses/libwebkit3/LICENSE"
}
