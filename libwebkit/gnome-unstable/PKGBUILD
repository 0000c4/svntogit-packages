# $Id: PKGBUILD,v 1.4 2008/08/08 17:43:21 BaSh Exp $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.15
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2>=2.17.11' 'icu>=4.2' 'gstreamer0.10-base>=0.10.24' 'libsoup>=2.28.0' 'enchant>=1.4.2')
makedepends=('gperf' 'gtk-doc>=1.11')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
options=('!libtool')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz
        bug29599.patch)
md5sums=('5478d48e6b134b3e09a3d9b17f3907e8'
         'fdab6578224f1848f7579b0c4068a9e3')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -Np1 -i "${srcdir}/bug29599.patch" || return 1
  ./configure --prefix=/usr \
      --enable-video \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
