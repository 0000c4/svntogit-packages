# $Id: PKGBUILD,v 1.4 2008/08/08 17:43:21 BaSh Exp $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.12
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2>=2.17.8' 'icu>=4.2' 'gstreamer0.10-base' 'libsoup>=2.27.90' 'enchant>=1.4.2')
makedepends=('gperf' 'gtk-doc>=1.11')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('1a10c117a0dd3717c2bcaa7a115cf218')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  ./configure --prefix=/usr \
      --enable-video \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
