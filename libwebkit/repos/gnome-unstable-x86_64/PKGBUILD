# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.21
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3>=3.6.22' 'gtk2>=2.19.5' 'icu>=4.2' 'gstreamer0.10-base>=0.10.26' 'libsoup>=2.29.90' 'enchant>=1.5.0')
makedepends=('gperf' 'gtk-doc>=1.11')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
options=('!libtool')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz)
md5sums=('8957bcffd0a0394a75f8f7936ab270bc')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  ./configure --prefix=/usr \
      --enable-video \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
