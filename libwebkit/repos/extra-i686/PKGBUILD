# $Id: PKGBUILD,v 1.4 2008/08/08 17:43:21 BaSh Exp $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.0.3 # see configure.ac
_svnver=39737 # date: 20090109
pkgrel=0.${_svnver}
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkit.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.0' 'gstreamer0.10-base' 'libsoup')
makedepends=('gperf')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webgitgtk-svn')
source=(http://builds.nightly.webkit.org/files/trunk/src/WebKit-r${_svnver}.tar.bz2
	buildfix_webkit_bison2.4.diff)
options=('!libtool')
md5sums=('2abdb91cf96b247bdabde58f4a4cf6dc'
         '98c8c22b290160f4de7dfcf62eef67bc')

build() {
  cd ${srcdir}/WebKit-r${_svnver}
  patch -Np1 -i ${srcdir}/buildfix_webkit_bison2.4.diff || return 1 

  ./autogen.sh --prefix=/usr \
	--enable-video \
	--with-font-backend=freetype \
	--with-http-backend=soup \
	--enable-jit # not yet supported on x86_64
  make || return 1
  make DESTDIR="$pkgdir" install

  # install license
  install -Dm644 ${srcdir}/WebKit-r${_svnver}/WebKit/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

