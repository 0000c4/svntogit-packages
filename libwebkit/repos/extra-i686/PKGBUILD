# $Id: PKGBUILD,v 1.4 2008/08/08 17:43:21 BaSh Exp $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.8
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2>=2.16.1' 'icu>=4.0' 'gstreamer0.10-base' 'libsoup>=2.26.1' 'enchant>=1.4.2')
makedepends=('gperf' 'gtk-doc>=1.11')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webgitgtk-svn')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('0991b4f3c2ef0d09e8c5ac0737dfeba1')

build() {
  cd ${srcdir}/webkit-${pkgver}
  ./configure --prefix=/usr \
	--enable-video \
	--with-font-backend=freetype --enable-gtk-doc \
	--enable-jit --with-unicode-backend=icu
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  install -Dm644 ${srcdir}/webkit-${pkgver}/WebKit/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

