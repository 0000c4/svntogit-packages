# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.15.3
pkgrel=2
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3>=3.6.18' 'gtk2>=2.18.0' 'icu>=4.2' 'gstreamer0.10-base>=0.10.24' 'libsoup>=2.28.0' 'enchant>=1.4.2')
makedepends=('gperf' 'gtk-doc>=1.11')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
options=('!libtool')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz
        webkitdownload-use-webkitnetworkresponse.patch)
md5sums=('18736af3c751eb078fa3f4a7393cc864'
         'a041ddf393002ebd4ea4eaec972c47c2')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -Np1 -i "${srcdir}/webkitdownload-use-webkitnetworkresponse.patch" || return 1
  ./configure --prefix=/usr \
      --enable-video \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
