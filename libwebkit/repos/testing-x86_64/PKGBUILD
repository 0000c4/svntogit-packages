# $Id: PKGBUILD,v 1.4 2008/08/08 17:43:21 BaSh Exp $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.1 # see configure.ac
_svnver=41443 # date: 20090305
pkgrel=0.${_svnver}
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkit.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.0' 'gstreamer0.10-base' 'libsoup>=2.25.91')
makedepends=('gperf')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webgitgtk-svn')
source=(http://builds.nightly.webkit.org/files/trunk/src/WebKit-r${_svnver}.tar.bz2)
options=('!libtool')
md5sums=('98aab33845d1adaaf9851d1ba0698025')

build() {
  cd ${srcdir}/WebKit-r${_svnver}
  ./autogen.sh --prefix=/usr \
	--enable-video \
	--with-font-backend=freetype \
	--with-http-backend=soup \
	--enable-jit # not yet supported on x86_64
  make || return 1
  make DESTDIR="$pkgdir" install

  # install license
  install -Dm644 ${srcdir}/WebKit-r${_svnver}/WebKit/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

