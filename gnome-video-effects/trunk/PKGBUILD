# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-video-effects
pkgver=0.4.1+56+g393c78f
pkgrel=1
pkgdesc="A collection of GStreamer effects"
arch=(any)
url="https://git.gnome.org/browse/gnome-video-effects"
license=(GPL2)
makedepends=(intltool gnome-common git)
_commit=393c78fc9a7dd0a78383b50f0ea08139160377df
source=("git://git.gnome.org/gnome-video-effects#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
