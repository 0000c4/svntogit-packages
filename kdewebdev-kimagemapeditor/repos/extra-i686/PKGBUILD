# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdewebdev-kimagemapeditor
pkgver=17.04.3
pkgrel=1
arch=(i686 x86_64)
pkgdesc="HTML Image Map Editor"
url='https://www.kde.org'
license=(GPL LGPL FDL)
groups=(kde-applications kdewebdev)
depends=(kdebase-runtime)
makedepends=(cmake automoc4)
source=("https://download.kde.org/stable/applications/${pkgver}/src/kimagemapeditor-$pkgver.tar.xz"{,.sig})
sha256sums=('caaa8c7d48653f76428cbb333fd8183f689f8b5dbdf9427a240a10a6d6d82fb0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kimagemapeditor-${pkgver} \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DKDE4_BUILD_TESTS=OFF \
	-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
