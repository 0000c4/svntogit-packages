# $Id$
# Contributor : phrakture < aaron m griffin :: gmail-com >
# Maintainer: simo <simo@archlinux.org>
pkgname=denyhosts
pkgver=2.6
pkgrel=6
pkgdesc="DenyHosts is a script to help thwart ssh server attacks"
arch=('any')
url="http://denyhosts.sourceforge.net/"
license=('GPL')
depends=('python2')
backup=('etc/denyhosts/denyhosts.cfg')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/DenyHosts-$pkgver.tar.gz \
        denyhosts.cfg \
        denyhosts \
        archify.diff
        regex-fixes.diff)
md5sums=('fc2365305a9402886a2b0173d1beb7df'
         '5352744bf34b29bfc95c54bfb14eb3e3'
         '0f186257e2473b8710c9b9e9f601ff19'
         'ab8b175618f76b53c9cc81355cad0f86'
         '3171c2fd783321af724a8539151bbd8d')

build()
{
  cd $srcdir/DenyHosts-$pkgver
  patch -p1 -i $srcdir/archify.diff
  patch -p1 -i $srcdir/regex-fixes.diff
  sed -i -e 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' denyhosts.py
  sed -i -e 's|#!/usr/bin/python$|#!/usr/bin/python2|' plugins/test_deny.py
  sed -i -e 's|#!/bin/env python$|#!/usr/bin/env python2|' scripts/*.py
}

package() {
  cd $srcdir/DenyHosts-$pkgver
  python2 setup.py install --root=$pkgdir
  
  install -Dm644 $srcdir/denyhosts.cfg $pkgdir/etc/denyhosts/denyhosts.cfg
  install -Dm755 $srcdir/denyhosts $pkgdir/etc/rc.d/denyhosts
  install -dm755 $pkgdir/var/lib/denyhosts
}
