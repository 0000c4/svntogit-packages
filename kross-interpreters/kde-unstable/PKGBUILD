# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kross-interpreters
pkgver=16.03.80
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=(i686 x86_64)
url='http://projects.kde.org/kross-interpreters'
license=(GPL)
depends=(kross kdelibs4support)
makedepends=(extra-cmake-modules kdoctools python2)
optdepends=('python2: kross-python plugin')
groups=(kde-applications kdebindings)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('61ed7fdf619feedd23ea7e4b6209b7242f04199d443263bb0685a0551e00d8f9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

