# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kross-interpreters
pkgver=17.04.1
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=(i686 x86_64)
url='https://www.kde.org/applications/development/'
license=(GPL)
depends=(kross)
makedepends=(extra-cmake-modules kdoctools python2 ruby)
optdepends=('python2: kross-python plugin' 'ruby: kross ruby plugin')
groups=(kde-applications kdebindings)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('436135ed853d8f44608d275b1b2f55512f132213b9d76cf42d9b1a2f3390ce56'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

