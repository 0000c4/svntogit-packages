# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: TingPing <tingping@tingping.se>

pkgname=libgrss
pkgver=0.7.0+9+gf0637f8
pkgrel=1
pkgdesc='A Glib-based library to manage RSS and Atom feeds'
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/Libgrss"
license=('LGPL3')
depends=('glib2' 'libsoup' 'libxml2')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'git')
_commit=f0637f8d8945a7559d06079917f4c0d55bcd4b80
source=("git://git.gnome.org/libgrss#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
