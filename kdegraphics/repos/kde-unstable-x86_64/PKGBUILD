# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdegraphics
pkgname=('kdegraphics-okular')
pkgver=4.6.80
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
makedepends=('kdelibs' 'pkgconfig' 'cmake' 'automoc4' 'qimageblitz' 'ebook-tools'
             'poppler-qt' 'libspectre' 'chmlib' 'djvulibre')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('69f191663db147cf481edd9f528fcae50c1dc2c3')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdegraphics-okular() {
	pkgdesc='Document Viewer'
	depends=('kdebase-runtime' 'qimageblitz' 'poppler-qt' 'chmlib' 'djvulibre'
             'ebook-tools' 'libspectre')
	url="http://kde.org/applications/graphics/okular/"
	install='kdegraphics-okular.install'
	cd $srcdir/build/okular
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/okular
	make DESTDIR=$pkgdir install
}
