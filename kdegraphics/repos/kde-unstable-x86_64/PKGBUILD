# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdegraphics
pkgname=('kdegraphics-gwenview'
         'kdegraphics-kamera'
         'kdegraphics-kcolorchooser'
         'kdegraphics-kgamma'
         'kdegraphics-kolourpaint'
         'kdegraphics-kruler'
         'kdegraphics-ksaneplugin'
         'kdegraphics-ksnapshot'
         'kdegraphics-libs'
         'kdegraphics-okular'
         'kdegraphics-strigi-analyzer'
         'kdegraphics-svgpart'
         'kdegraphics-thumbnailers')
pkgver=4.2.98
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdelibs' 'kdebase-runtime' 'poppler-qt' 'libspectre' 'sane' 'chmlib' 'libdjvu' 'lcms' 'qca' 'qimageblitz' 'ebook-tools')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('c7675a09e66c396a263ca78a88de54f1')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdegraphics-gwenview() {
	pkgdesc='A simple image viewer'
	depends=('kdebase-runtime' 'kdegraphics-libs')
	replaces=('gwenview' 'gwenview-i18n')
	install='kdegraphics.install'
	cd $srcdir/build/gwenview
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/gwenview
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kamera() {
	pkgdesc='Configure Kamera'
	depends=('kdelibs' 'libgphoto2')
	cd $srcdir/build/kamera
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kcolorchooser() {
	pkgdesc='Color Chooser'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdegraphics.install'
	cd $srcdir/build/kcolorchooser
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kgamma() {
	pkgdesc='A monitor calibration tool'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdegraphics.install'
	cd $srcdir/build/kgamma
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kgamma
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kolourpaint() {
	pkgdesc='Paint Program'
	depends=('kdelibs' 'kdebase-runtime' 'qimageblitz')
	install='kdegraphics.install'
	cd $srcdir/build/kolourpaint
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kolourpaint
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kruler() {
	pkgdesc='Screen Ruler'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdegraphics.install'
	cd $srcdir/build/kruler
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kruler
	make DESTDIR=$pkgdir install
}

package_kdegraphics-ksaneplugin() {
	pkgdesc='KDE Scan Service'
	depends=('kdegraphics-libs')
	cd $srcdir/build/ksaneplugin
	make DESTDIR=$pkgdir install
}

package_kdegraphics-ksnapshot() {
	pkgdesc='Screen Capture Program'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdegraphics.install'
	cd $srcdir/build/ksnapshot
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ksnapshot
	make DESTDIR=$pkgdir install
}

package_kdegraphics-libs() {
	pkgdesc='Support library for KDE Graphics Software'
	groups=()
	depends=('kdelibs' 'sane' 'lcms')
	replaces=('libkexiv2' 'libkdcraw')
	install='kdegraphics.install'
	cd $srcdir/build/libs
	make DESTDIR=$pkgdir install
}

package_kdegraphics-okular() {
	pkgdesc='Document Viewer'
	depends=('kdelibs' 'kdebase-runtime' 'qimageblitz' 'qca' 'poppler-qt' 'chmlib' 'libdjvu' 'libspectre' 'ebook-tools')
	install='kdegraphics.install'
	cd $srcdir/build/okular
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/okular
	make DESTDIR=$pkgdir install
}

package_kdegraphics-strigi-analyzer() {
	pkgdesc='Strigi-Analyzer for KDE Graphics Software'
	depends=('strigi')
	groups=(${groups[@]} 'strigi-analyzer')
	cd $srcdir/build/strigi-analyzer
	make DESTDIR=$pkgdir install
}

package_kdegraphics-svgpart() {
	pkgdesc='Svg Part'
	depends=('kdelibs')
	cd $srcdir/build/svgpart
	make DESTDIR=$pkgdir install
}

package_kdegraphics-thumbnailers() {
	pkgdesc='Thumb creator for KDE Graphics Software'
	depends=('kdebase-runtime' 'kdegraphics-libs')
	cd $srcdir/build/thumbnailers
	make DESTDIR=$pkgdir install
}
