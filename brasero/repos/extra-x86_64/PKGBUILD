# $Id: PKGBUILD,v 1.31 2009/03/14 02:03:57 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=2.32.0
pkgrel=3
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!libtool' '!emptydirs')
depends=('gstreamer0.10-base-plugins>=0.10.30' 'totem-plparser>=2.31.92' 'libbeagle>=0.3.9' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'libunique>=1.1.6' 'libcanberra>=0.25' 'dvd+rw-tools' 'dconf')
makedepends=('gnome-doc-utils>=0.20.1' 'intltool' 'libburn' 'libisofs' 'nautilus' 'gobject-introspection')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'gstreamer0.10-bad-plugins: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
install=brasero.install
source=(http://ftp.gnome.org/pub/gnome/sources/brasero/2.32/brasero-${pkgver}.tar.bz2
        fix_crasher.patch)
sha256sums=('72fbc36ace7a38f29499aff80ca426c5e30a04377417101ca047ca174af9ff97'
            'eb186bb03320716e838a06aeb3e681c81646785e5671efce2a21ef3fae1ead7d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix_crasher.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-caches \
      --disable-schemas-compile
  make
  make DESTDIR=${pkgdir} install
}
