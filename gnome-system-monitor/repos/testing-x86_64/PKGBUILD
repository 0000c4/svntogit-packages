# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=3.22.0+3+g8e6b08a
pkgrel=1
pkgdesc="View current processes and monitor system state"
url="https://wiki.gnome.org/Apps/SystemMonitor"
arch=(i686 x86_64)
license=(GPL)
depends=(libgtop gtkmm3 librsvg systemd libwnck3)
makedepends=(itstool docbook-xsl intltool desktop-file-utils appdata-tools gnome-common git)
options=(!emptydirs)
groups=(gnome)
_commit=8e6b08a68383bfc6cc274be31735ba9eb275015a  # gnome-3-22
source=("git://git.gnome.org/gnome-system-monitor#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-systemd --enable-wnck
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
