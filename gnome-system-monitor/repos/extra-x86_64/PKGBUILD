# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=2.28.0
pkgrel=1
pkgdesc="A system monitor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgksu>=2.0.5' 'libwnck>=2.27.92' 'gconf>=2.27.0' 'libgtop>=2.28.0' 'gtkmm>=2.17.11' 'librsvg>=2.26.0' 'gnome-icon-theme>=2.27.90')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.17.4' 'intltool')
options=(!emptydirs)
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-system-monitor.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('638875c6041cd2b0ad7219fecd8fe86eb9d4c8b0662166a84d7659bd6a9db1d3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --enable-pcrecpp || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
