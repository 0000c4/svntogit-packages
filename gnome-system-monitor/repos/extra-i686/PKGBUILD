# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=3.8.2.1
pkgrel=1
pkgdesc="A system monitor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libwnck3' 'libgtop' 'gtkmm3' 'librsvg' 'gnome-icon-theme' 'systemd')
makedepends=('itstool' 'docbook-xsl' 'intltool')
options=(!emptydirs)
url="http://www.gnome.org"
groups=('gnome')
install=gnome-system-monitor.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz)
sha256sums=('df4d2c4dbb4f3408b621140732efb3e38853658addd6aae73613027856f6bb44')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
