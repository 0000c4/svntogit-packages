# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=2.99.3
pkgrel=1
pkgdesc="A system monitor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libwnck3' 'libgtop' 'gtkmm3' 'librsvg' 'gnome-icon-theme')
makedepends=('pkgconfig' 'gnome-doc-utils' 'intltool')
options=(!emptydirs)
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-system-monitor.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.99/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('d9b8333f0d0996c6e34fc8b25d7cd66522270264be552572f86c41c6db79be0b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
