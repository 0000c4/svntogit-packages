# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=3.8.2
pkgrel=1
pkgdesc="A system monitor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libwnck3' 'libgtop' 'gtkmm3' 'librsvg' 'gnome-icon-theme' 'libsystemd')
makedepends=('itstool' 'docbook-xsl' 'intltool')
options=(!emptydirs)
url="http://www.gnome.org"
groups=('gnome')
install=gnome-system-monitor.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('8349ec4d8e6cd041c581354fc4e844c7cda12d7f1153fc6999ceea0f8aceb58c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/gnome-system-monitor
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
