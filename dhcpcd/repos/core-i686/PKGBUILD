# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd
pkgver=5.0.2
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/dhcpcd/"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
depends=('glibc' 'bash')
backup=('etc/conf.d/dhcpcd' 'etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.bz2" \
        'dhcpcd.conf.d' 'glibc-segfault.patch')
md5sums=('0a51f2e4bb6f4a1b188bda8390ff504a'
         '372d33485556982b64a97f301e17c5dd'
         'f3526cdcd7abfe18435963950161b5cd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Fix glibc incompatibility
  patch -Np0 -i ${srcdir}/glibc-segfault.patch || return 1
  
  # Fix Installation Locations
  export PREFIX=/usr
  sed -i 's/${PREFIX}\/etc/\/etc/' Makefile || return 1
  sed -i 's/\/db/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' dhcpcd-hooks/Makefile || return 1
  sed -i 's/${PREFIX}\/sbin/\/sbin/' Makefile || return 1

  # Build
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # Create Binary Symlink
  install -d ${pkgdir}/usr/sbin || return 1
  ln -sf /sbin/dhcpcd ${pkgdir}/usr/sbin/dhcpcd || return 1

  # Install Configuration File used in /etc/rc.d/network
  install -D -m644 ../dhcpcd.conf.d $pkgdir/etc/conf.d/$pkgname || return 1

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname || return 1
  awk '{if(FNR<27)print $0}' ${srcdir}/${pkgname}-${pkgver}/config.h \
	>> ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  # Make Man Pages FHS Compliant
  mv -f ${pkgdir}/usr/man ${pkgdir}/usr/share/man || return 1

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf || return 1 # Disable ip4vall
}
