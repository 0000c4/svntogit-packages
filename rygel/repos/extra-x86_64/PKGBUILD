# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.36.2
pkgrel=1
pkgdesc="UPnP AV MediaServer and MediaRenderer that allows you to easily share audio, video and pictures, and control of media player on your home network"
url="https://wiki.gnome.org/Projects/Rygel"
arch=(x86_64)
license=(LGPL)
depends=(gupnp-av gupnp-dlna libgee gtk3 libunistring tracker libmediaart)
makedepends=(vala intltool python gobject-introspection git valadoc)
optdepends=('gst-plugins-base: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'tracker-miners: Share indexed media files')
backup=(etc/rygel.conf)
groups=(gnome)
_commit=7d0e90cd4cc4d59e36d7e8681ff41c3a66d3bc09  # tags/rygel-0.36.2^0
source=("git+https://gitlab.gnome.org/GNOME/rygel.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^RYGEL_//;s/^rygel-//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --enable-mediathek-plugin --enable-gst-launch-plugin \
              --enable-apidocs
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
