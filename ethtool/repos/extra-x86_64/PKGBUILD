# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Martin Kemp <mdkemp@elys.com>

pkgname=ethtool
pkgver=4.8
pkgrel=1
epoch=1
pkgdesc="Utility for controlling network drivers and hardware"
arch=('i686' 'x86_64')
url="https://www.kernel.org/pub/software/network/ethtool/"
license=('GPL')
depends=('glibc')
source=(https://www.kernel.org/pub/software/network/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha1sums=('db82313f212aa1e1b8f5f892f25a9883bea26b9f'
          'SKIP')
validpgpkeys=(CE4A4D080F0D304F23B9EBDD972D5BF4DC613806) # John W. Linville <linville@kernel.org>

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin
    make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
