# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sweeper
pkgver=18.11.90
pkgrel=1
pkgdesc='System Cleaner'
url='https://kde.org/applications/utilities/sweeper'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kactivities-stats kio)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kdeutils)
conflicts=(kdeutils-sweeper)
replaces=(kdeutils-sweeper)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5c7b9ba4a75963d14cd7cd7b88469a9e5e2a3eb99ffe831faa7ae181dbd22cf2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../sweeper-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
