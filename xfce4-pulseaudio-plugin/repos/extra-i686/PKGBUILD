# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: <alcasa dot mz at gmail dot com>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=xfce4-pulseaudio-plugin
pkgver=0.2.4
pkgrel=5
pkgdesc="Pulseaudio plugin for Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="https://goodies.xfce.org/projects/panel-plugins/xfce4-pulseaudio-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libpulse' 'libkeybinder3' 'gtk-update-icon-cache' 'libnotify')
makedepends=('intltool' 'dbus-glib')
optdepends=('pavucontrol: default pulseaudio mixer')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('8d9330ddf1d44a864a36d566cce4b76d4f859c5984bba7653d7dc39aa24d5c3e')
sha512sums=('b53ab48e282a506fc7fe91fa70520f0db32f2f73653a0210681bf7389f13441d5af8f855604b59817b1fe7a3f41e2d4fe52b052ac0c3479f3f4e37b2afedb193')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --enable-keybinder \
    --enable-libnotify

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
