# $Id$
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.9.2
pkgrel=1
pkgdesc="User interface builder for GTK+ and GNOME."
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gtk3' 'libxml2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'python2' 'gtk-doc')
optdepends=('python2: Python widgets support')
options=('!libtool')
url="http://glade.gnome.org/"
install=glade.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('cb26c1532e72c4763bdd47af03d3f1ebbe389f350d03cf89a49d55ec08c33b26')

build() {
  cd "${srcdir}/$pkgname-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/$pkgname-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
