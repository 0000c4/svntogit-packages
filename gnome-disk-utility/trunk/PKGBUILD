# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.8.2
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify hicolor-icon-theme desktop-file-utils)
makedepends=(intltool docbook-xsl gnome-settings-daemon)
options=('!libtool' '!emptydirs')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        typefixes.patch)
sha256sums=('6a64de6f40ddfebfe7566f3ef7856c1cb33fab7d21d9be43db74f014309298c3'
            '0a1f2f80f1c741119639b3b2b8cf32d6261b126baa85189309d6473e171894d2')

prepare() {
  cd $pkgname-$pkgver

  # Gets rid of warnings and compilation errors on i686
  patch -Np1 -i ../typefixes.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
