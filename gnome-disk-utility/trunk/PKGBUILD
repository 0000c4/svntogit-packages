# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-disk-utility
pkgver=2.91.6
pkgrel=1
pkgdesc="GNOME libraries and applications for dealing with storage devices"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('dbus-glib' 'libgnome-keyring' 'libunique3' 'udisks' 'libnotify' 'hicolor-icon-theme' 'avahi')
makedepends=('gtk-doc' 'intltool')
replaces=('gnome-mount')
conflicts=('gnome-mount')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        fix-freeze.patch)
sha256sums=('9ea50bea3b2dd5609658fd4e7cce17dbd96a47940275a484e7be743339eadea1'
            '818b70360978968457d2c50738347c6726ed8d0c007eb57074881a7e33652526')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-freeze.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-disk-utility \
      --disable-scrollkeeper --disable-nautilus --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
