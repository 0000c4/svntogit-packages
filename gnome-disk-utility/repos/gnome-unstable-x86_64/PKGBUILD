# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.28.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
url="https://git.gnome.org/browse/gnome-disk-utility"
arch=(x86_64)
license=(GPL)
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify parted)
makedepends=(docbook-xsl appstream-glib git meson)
_commit=da956bbd3e4301a7090b50e2f0f9ddb246bc6d9e  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/gnome-disk-utility#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
