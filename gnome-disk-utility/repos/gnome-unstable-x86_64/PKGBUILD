# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.6.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
groups=('gnome-extra')
depends=('gtk3' 'udisks2' 'hicolor-icon-theme' 'libsecret' 'libpwquality' 'desktop-file-utils')
makedepends=('intltool' 'docbook-xsl')
options=('!libtool' '!emptydirs')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('0e3ccd8ae0f79a8ccc41bfaaf0f34c14fec7b193249e650862c589d82053e57d')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
