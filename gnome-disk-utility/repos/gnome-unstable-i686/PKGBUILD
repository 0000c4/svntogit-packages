# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.24.0+2+g9e4c9f5c
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
url="https://git.gnome.org/browse/gnome-disk-utility"
arch=(i686 x86_64)
license=(GPL)
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify parted)
makedepends=(intltool docbook-xsl autoconf-archive appstream-glib git)
_commit=9e4c9f5c915ea482c7870b4c89c22f4352f66412  # gnome-3-24
source=("git+https://git.gnome.org/browse/gnome-disk-utility#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --disable-Werror --libexecdir=/usr/lib/gnome-disk-utility
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}
