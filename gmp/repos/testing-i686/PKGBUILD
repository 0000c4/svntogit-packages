# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gmp
pkgver=5.1.0
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3')
options=(!libtool)
install=gmp.install
source=(ftp://ftp.gmplib.org/pub/gmp-${pkgver}/gmp-${pkgver}.tar.xz{,.sig})
md5sums=('69fd511e435f81a0334f14f00c7d77b3'
         '7a02b99dc7edd9822c210f78d88140e5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  [[ ${CARCH} = "i686" ]] && export ABI="32"
  ./configure --build=${CHOST} \
    --prefix=/usr --enable-cxx
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
