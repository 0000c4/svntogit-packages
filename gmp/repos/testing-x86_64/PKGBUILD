# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gmp
pkgver=5.0.3
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3')
options=(!libtool)
install=gmp.install
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.xz{,.sig})
md5sums=('1e8edebe33bc3ac6de17df3201732825'
         '9efbe14533ded9cd3ae4e998ee0efc98')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  [ "${CARCH}" = "i686" ] && export ABI="32"
  ./configure --build=${CHOST} \
    --prefix=/usr --infodir=/usr/share/info \
    --enable-cxx
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
