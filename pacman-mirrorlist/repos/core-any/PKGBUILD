# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman-mirrorlist
pkgver=20121128
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="http://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=(mirrorlist)

# NOTE on building this package:
# * Go to the trunk/ directory
# * Run bash -c ". PKGBUILD; updatelist"
# * Update the checksums, update pkgver
# * Build the package

updatelist() {
  rm -f mirrorlist
  wget -O mirrorlist https://www.archlinux.org/mirrorlist/all/smart/
}

build() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/mirrorlist $pkgdir/etc/pacman.d/
}

md5sums=('e80390b1b57e0e26e8a5124cc3939ec2')
sha256sums=('3480c0671d206e58d9eaa6cc66b7326333cbd1155b968b694e541846821513d2')
