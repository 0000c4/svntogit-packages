# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman-mirrorlist
pkgver=20170729
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=(mirrorlist)

# NOTE on building this package:
# * Go to the trunk/ directory
# * Run bash -c ". PKGBUILD; updatelist"
# * Update the checksums, update pkgver
# * Build the package

updatelist() {
  rm -f mirrorlist
  curl -o mirrorlist https://www.archlinux.org/mirrorlist/all/
}

package() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/mirrorlist $pkgdir/etc/pacman.d/
}

md5sums=('7e264251bb3891ab1c0fe9bbbfdb3f7d')
sha256sums=('b9e2fc0be980023c150c6f5823b8fe3a22048a1fc0f0b0c13009ee087446dca2')
