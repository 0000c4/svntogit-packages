# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=htop
pkgver=0.9
pkgrel=3
pkgdesc="Interactive process viewer"
arch=('i686' 'x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
optdepends=('lsof' 'strace')
options=('!emptydirs')
changelog=ChangeLog
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        'fix-sort-key-bug.patch'
        'fix-segfault-small-terminal.patch')
md5sums=('7c5507f35f363f3f40183a2ba3c561f8'
         '750e8ad8476c5d9738da2a2bc7a7c2b6'
         'd93e39c9662522367dc3a73cc3057039')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
	sed -i 's|python|python2|' scripts/MakeHeader.py

    patch -Np0 -i "${srcdir}/fix-sort-key-bug.patch"
    patch -p1 -i "${srcdir}/fix-segfault-small-terminal.patch"

	./configure --prefix=/usr \
      --enable-unicode \
      --enable-openvz \
	  --enable-vserver \
      --enable-cgroup
	make 
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install 
}
