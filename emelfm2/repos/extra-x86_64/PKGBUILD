# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: contrasutra

pkgname=emelfm2
pkgver=0.8.1
pkgrel=1
pkgdesc="A file manager that implements the popular two-pane design"
arch=('i686' 'x86_64')
url="http://emelfm2.net/"
depends=('gtk2')
license=('GPL3')
source=("http://emelfm2.net/rel/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('5528bdd7001451dbdba95bf26ee588be9a0b86da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" install install_i18n

  install -m 755 "${pkgdir}/usr/share/pixmaps/emelfm2/emelfm2_48.png" \
      "${pkgdir}/usr/share/pixmaps/emelfm2.png"
  sed -e '/Icon=/ s^emelfm2/emelfm2_48.png^emelfm2.png^' \
      -e '/Categories=/ s^;$^;Application;Utility;^' \
      -i "${pkgdir}/usr/share/applications/emelfm2.desktop"
}
