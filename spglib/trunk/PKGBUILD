# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=1.10.1
pkgrel=2
pkgdesc="C library for finding and handling crystal symmetries"
arch=(x86_64)
url="https://atztogo.github.io/spglib/"
license=(custom)
depends=(glibc)
makedepends=()
source=($pkgname-$pkgver.tar.gz::"https://github.com/atztogo/spglib/archive/v$pkgver.tar.gz"
        spglib-undefined-symbols.patch::"https://github.com/atztogo/spglib/commit/54a30606.patch")
sha256sums=('8ed979cda82f6d440567197ec191bffcb82ee83c5bfe8a484c5a008dd00273f0'
            '1644feee810193ec9699bf28990315278af7e6b242a73fd7cb38facc3e7f27b5')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../spglib-undefined-symbols.patch # Fix undefined symbols

  touch INSTALL NEWS README AUTHORS
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
