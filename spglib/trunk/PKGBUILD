# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=1.10.1
pkgrel=1
pkgdesc="C library for finding and handling crystal symmetries"
arch=(i686 x86_64)
url="http://spglib.sf.net/"
license=(custom)
depends=(glibc)
makedepends=()
source=($pkgname-$pkgver.tar.gz::"https://github.com/atztogo/spglib/archive/v$pkgver.tar.gz")
sha256sums=('8ed979cda82f6d440567197ec191bffcb82ee83c5bfe8a484c5a008dd00273f0')

build() {
  cd $pkgname-$pkgver
  touch INSTALL NEWS README AUTHORS
  autoreconf -vi
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
