# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kontact
pkgver=17.08.3
pkgrel=1
pkgdesc="KDE Personal Information Manager"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(hicolor-icon-theme kontactinterface kmail-account-wizard kdepim-apps-libs)
makedepends=(extra-cmake-modules kdoctools boost python)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('31a8dc56b96cb9054bc2245cc312230ad85089a88cccc906717f8d63f7db46c2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
