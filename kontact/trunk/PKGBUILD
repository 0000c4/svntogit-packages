# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kontact
pkgver=18.04.1
pkgrel=2
pkgdesc="KDE Personal Information Manager"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kontactinterface kmail-account-wizard kdepim-apps-libs qt5-webengine)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kontact-qt5.11.patch::"https://cgit.kde.org/kontact.git/patch/?id=b78bb730")
sha256sums=('a2805f419446040e9dacaa590a8550c01dca924d570adc6d4f036e67dd8faa03'
            'SKIP'
            'fb9faf5122691d07cbfc6608cce3bdda92a656d8984dc175309cd50433583780')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kontact-qt5.11.patch # Fix error message with Qt 5.11
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
