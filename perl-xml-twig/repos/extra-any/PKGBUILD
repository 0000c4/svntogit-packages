# $Id$
# Maintainer: damir <damir@archlinux.org>
# Maintainer/Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-xml-twig
_realname=XML-Twig
pkgver=3.41
pkgrel=1
pkgdesc="A module for easy processing of XML"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
license=('GPL' 'PerlArtistic')
depends=('perl-xml-parser' 'perl-text-iconv' 'perl')
checkdepends=('perl-libwww' 'perl-test-pod' 'perl-xml-simple' 'perl-html-tree')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/M/MI/MIROD/${_realname}-${pkgver}.tar.gz")
sha1sums=('e03e69f2ba03287db15bdf7003f184e4ce0d5442')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL -y INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
