# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.0.2
pkgrel=2
pkgdesc="Is a software library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('gnutls'  'glib2' 'libplist>=1.3' 'usbmuxd>=1.0.4')
makedepends=('swig' 'python2')
options=('!libtool')
source=(http://libimobiledevice.org/downloads/${pkgname}-${pkgver}.tar.bz2
        swig-version-check.patch)
md5sums=('b83dd6f501e2ef199927c20866e9e113'
        '253398a6ccf7b21b3e80991b67422f4d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # SWIG version check is wrong (still not fixed upstream)
  patch -p0 -i "$srcdir"/swig-version-check.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
