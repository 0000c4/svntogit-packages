# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.1.2
pkgrel=1
pkgdesc="Is a software library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('usbmuxd' 'libplist')
makedepends=('python2' 'cython2')
options=('!libtool')
source=(http://libimobiledevice.org/downloads/$pkgname-$pkgver.tar.bz2)
md5sums=('82851e4ca9794ee660ffca9f78c8e068')

build() {
  cd "$pkgname-$pkgver"

  mkdir "$srcdir/cython2-path"
  ln -s /usr/bin/cython2 "$srcdir/cython2-path/cython"
  export PATH="$srcdir/cython2-path:$PATH"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
