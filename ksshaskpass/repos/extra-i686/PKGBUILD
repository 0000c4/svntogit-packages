# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=ksshaskpass
pkgver=5.11.2
pkgrel=1
pkgdesc='ssh-add helper that uses kwallet and kpassworddialog'
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL)
arch=(i686 x86_64)
depends=(kwallet)
makedepends=(extra-cmake-modules kdoctools python)
provides=(x11-ssh-askpass)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
install=$pkgname.install
sha256sums=('19d058562b1454ac80fe1fc748a4c3794db9e01d0e5fb842713745cf27aa7bfc'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
