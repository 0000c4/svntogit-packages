# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=3.0.0.1
pkgrel=3
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="The GNOME Panel"
url="http://www.gnome.org"
depends=('gnome-menus' 'gnome-desktop' 'evolution-data-server' 'librsvg' 'libwnck3' 'libsm' 'dconf' 'telepathy-glib')
makedepends=('gnome-doc-utils' 'intltool' 'gobject-introspection' 'networkmanager' 'libcanberra')
install=gnome-panel.install
groups=('gnome')
replaces=('gnome-panel-bonobo')
provides=("gnome-panel-bonobo=${pkgver}")
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*.*}/${pkgname}-${pkgver}.tar.bz2
        fix-crasher-launchers.patch)
sha256sums=('2014225215afc8a8a4363481c26f9a9443d3dfc944aa7bcb0c5224f723c0b98b'
            '6724690bb2943f50ab296af6f1e1a7c10a91415ba150586b5623f20cc214ace0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-crasher-launchers.patch"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-panel \
      --disable-scrollkeeper \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-panel-3.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
