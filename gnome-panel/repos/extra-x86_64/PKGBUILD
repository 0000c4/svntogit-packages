# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-panel
pkgname=('gnome-panel' 'gnome-panel-bonobo')
pkgver=2.32.1
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
makedepends=('libwnck' 'gnome-menus' 'evolution-data-server' 'gnome-desktop' 'libgweather' 'librsvg' 'hicolor-icon-theme' 'libcanberra' 'libbonoboui' 'gnome-doc-utils' 'intltool' 'networkmanager' 'gobject-introspection')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.32/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('8f7828eb7a21c817a73b697844a4e20256059b32386414a10f97d51e7d9fca5f')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-panel \
      --disable-scrollkeeper
  make
  sed -e 's/$(am__append_1)//' -i Makefile
}

package_gnome-panel() {
  pkgdesc="The GNOME Panel"
  depends=('gnome-menus>=2.30.5' 'gnome-desktop>=2.32.1' 'evolution-data-server>=2.32.1' 'librsvg>=2.32.1' 'libcanberra>=0.25' 'libwnck>=2.30.6' 'hicolor-icon-theme')
  optdepends=('gnome-panel-bonobo: libbonoboui-based applet support')
  groups=('gnome')
  install=gnome-panel.install
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-panel-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

package_gnome-panel-bonobo() {
  pkgdesc="Modules for libbonoboui-based applet support for the GNOME Panel"
  depends=('libbonoboui>=2.24.3' "gnome-panel=${pkgver}")
  conflicts=('gnome-panel<2.31.92')
  install=gnome-panel-bonobo.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C bonobo "${pkgdir}" DESTDIR="${pkgdir}" install
}
