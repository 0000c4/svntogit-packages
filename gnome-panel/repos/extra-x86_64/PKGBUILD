# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=2.28.0
pkgrel=2
pkgdesc="The GNOME Panel"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org"
depends=('libwnck>=2.28.0' 'gnome-menus>=2.28.0.1' 'evolution-data-server>=2.28.1' 'gnome-desktop>=2.28.1' 'libgweather>=2.28.0' 'librsvg>=2.26.0' 'libgnomeui>=2.24.2' 'hicolor-icon-theme')
makedepends=('gnome-doc-utils>=0.18.0' 'pkgconfig' 'intltool' 'networkmanager')
options=('!libtool' '!emptydirs')
groups=('gnome')
install=gnome-panel.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2
        bug597101.patch)
sha256sums=('c1168c0c8f84692002557d60870c782f4831635080d2c52cd2a3665fb7718465'
            '8fb24923d7a6eacc6ff27178bde2a893f05cc5174a51332830ad376750646aa7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/bug597101.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-panel \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-panel-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
