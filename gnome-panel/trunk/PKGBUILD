# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-panel
pkgname=('gnome-panel' 'gnome-panel-bonobo')
pkgver=2.31.92
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
makedepends=('libwnck' 'gnome-menus' 'evolution-data-server' 'gnome-desktop' 'libgweather' 'librsvg' 'hicolor-icon-theme' 'libcanberra' 'libbonoboui' 'gnome-doc-utils' 'pkg-config' 'intltool' 'networkmanager')
options=('!libtool' '!emptydirs')
install=gnome-panel.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.31/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('e8b75954e0437f7cefd6cef070ed646bc595a617c39db0b99e3939ddda5bcca3')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-panel \
      --disable-scrollkeeper --disable-introspection
  make
  sed -e 's/$(am__append_1)//' -i Makefile
}

package_gnome-panel() {
  pkgdesc="The GNOME Panel"
  depends=('gnome-menus' 'gnome-desktop' 'evolution-data-server' 'librsvg' 'libcanberra' 'libwnck' 'hicolor-icon-theme')
  optdepends=('gnome-panel-bonobo: libbonoboui-based applet support')
  groups=('gnome')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-panel-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

package_gnome-panel-bonobo() {
  pkgdesc="Modules for libbonoboui-based applet support for the GNOME Panel"
  depends=('libbonoboui' 'gnome-panel')
  conflicts=('gnome-panel<2.31.92')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C bonobo "${pkgdir}" DESTDIR="${pkgdir}" install
}
