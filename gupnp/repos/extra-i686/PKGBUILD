# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gupnp
pkgver=1.0.1
pkgrel=1
pkgdesc="An object-oriented UPNP framework"
arch=(i686 x86_64)
url="http://gupnp.org/"
license=(LGPL)
depends=(gssdp util-linux)
makedepends=(gobject-introspection gtk-doc vala autoconf-archive git) 
optdepends=('python2: gupnp-binding-tool')
_commit=21e86d01f074fad9b05e474466f8c5772514af11  # tags/gupnp-1.0.1^0
source=("git://git.gnome.org/gupnp#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gupnp-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i '1s|^#!.*python$|&2|' tools/gupnp-binding-tool
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-context-manager=linux --enable-gtk-doc
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
