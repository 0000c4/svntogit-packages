# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiconthemes
pkgver=5.24.0
pkgrel=2
pkgdesc='Support for icon themes'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-svg' 'kconfigwidgets' 'kitemviews' 'karchive')
makedepends=('extra-cmake-modules' 'python' 'mesa')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        kdebug-365130.patch)
md5sums=('74b986efc24bec1e78b9e72b4c0defcd'
         '680419b1157cdd7396cd2170b0b89bc2')

prepare() {
  mkdir -p build

# Fix missing icons http://bugs.kde.org/show_bug.cgi?id=365130
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-365130.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
