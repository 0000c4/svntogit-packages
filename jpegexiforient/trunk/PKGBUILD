# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=jpegexiforient
pkgver=20170825.f8111e7
pkgrel=1
pkgdesc='Get and set the Exif Orientation Tag'
url='https://anonscm.debian.org/cngit/collab-maint/libjpeg-turbo.git'
license=('custom')
arch=('x86_64')
makedepends=('git')
depends=('libjpeg-turbo')
source=("git://anonscm.debian.org/collab-maint/libjpeg-turbo.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/libjpeg-turbo"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

build() {
	cd "${srcdir}/libjpeg-turbo/debian/extra"
	make
}

package() {
	cd "${srcdir}/libjpeg-turbo/debian/extra"
	make DESTDIR="${pkgdir}" install
	install -Dm644 /usr/share/licenses/libjpeg-turbo/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
