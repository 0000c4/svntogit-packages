# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-palapeli
pkgver=17.08.3
pkgrel=1
pkgdesc="A single-player jigsaw puzzle game"
url="https://kde.org/applications/games/palapeli/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'qhull')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/$pkgver/src/palapeli-$pkgver.tar.xz"{,.sig})
sha256sums=('df49d749467b2a52974b0674767afb515ddf3e6e8226eff840f152ccfc4bc8c4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../palapeli-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
