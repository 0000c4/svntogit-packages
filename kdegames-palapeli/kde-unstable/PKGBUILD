# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-palapeli
pkgver=16.11.90
pkgrel=1
pkgdesc="A single-player jigsaw puzzle game"
url="http://kde.org/applications/games/palapeli/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'qhull')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/palapeli-${pkgver}.tar.xz"{,.sig})
sha1sums=('ef1c5a9546019b73f191d02397d57e7d47f21450'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../palapeli-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
