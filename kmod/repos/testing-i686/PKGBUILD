# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=11
pkgrel=2
pkgdesc="Linux kernel module handling"
arch=('i686' 'x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('gtk-doc')
options=('!libtool')
provides=('module-init-tools=3.16')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz"
        "0001-depmod-fix-hash-lookup-by-relpath-instead-of-uncrelp.patch"
        "0002-depmod-fix-asserting-mod-kmod-NULL.patch"
        "depmod-search.conf")
md5sums=('607e33b0144625c2e5221e5a7df49c7a'
         '239549791aeca08a56428c27c558af7f'
         'f39fc93d4be1e109ea73c6509ab171eb'
         'dd62cbf62bd8f212f51ef8c43bec9a77')

build() {
  cd "$pkgname-$pkgver"

  # upstream commit 06294621a944e4611e15ce8201df80870e052e7d
  patch -Np1 <"$srcdir/0001-depmod-fix-hash-lookup-by-relpath-instead-of-uncrelp.patch"

  # upstream commit 02c64df3c2b33880b18d3f4aba9fa8e48e5ca904
  patch -Np1 <"$srcdir/0002-depmod-fix-asserting-mod-kmod-NULL.patch"

  ./configure \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --with-zlib

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d "$pkgdir/sbin"

  # add symlinks to kmod
  ln -s ../usr/bin/kmod "$pkgdir/sbin/modprobe"
  ln -s ../usr/bin/kmod "$pkgdir/sbin/depmod"

  for tool in {ins,ls,rm}mod modinfo; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
