# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=5
pkgrel=2
pkgdesc="Linux kernel module handling"
arch=('i686' 'x86_64')
url="http://git.profusion.mobi/cgit.cgi/kmod.git"
license=('GPL2')
depends=('glibc' 'zlib')
options=('!libtool')
provides=('module-init-tools=3.16')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("http://packages.profusion.mobi/$pkgname/$pkgname-$pkgver.tar.xz"
        '0001-libkmod-module-probe-Fix-ignore-loaded-flag-not-bein.patch'
        "depmod-search.conf")
md5sums=('b271c2ec54aba1c67bda63c8579d8c15'
         '81545a1509b43008f85c03fb980f0e86'
         '4b8cbcbc54b9029c99fd730e257d4436')

build() {
  cd "$pkgname-$pkgver"

  # fix modprobe --show-depends failures on loaded modules
  patch -Np1 <"$srcdir/0001-libkmod-module-probe-Fix-ignore-loaded-flag-not-bein.patch"

  ./configure \
    --sysconfdir=/etc \
    --with-rootprefix= \
    --with-zlib

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,lib}/{depmod,modprobe}.d "$pkgdir/sbin"

  # add symlinks to kmod
  ln -s ../usr/bin/kmod "$pkgdir/sbin/modprobe"
  for tool in {dep,ins,ls,rm}mod modinfo; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
