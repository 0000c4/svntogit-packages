# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=10
pkgrel=2
pkgdesc="Linux kernel module handling"
arch=('i686' 'x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('gtk-doc')
options=('!libtool')
provides=('module-init-tools=3.16')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz"
        '0001-depmod-fix-parsing-of-modules.order-with-compressed-.patch'
        '0001-libkmod-Add-support-for-.-in-module-parameter-on-kcm.patch'
        "depmod-search.conf")
md5sums=('e2a883c4df15a50f78a7a61d5b64089f'
         '47005a6e70496d429d40e3fc4fd89755'
         'ecf5bfd4fd9ed14cf0dc1ce4025d256b'
         'dd62cbf62bd8f212f51ef8c43bec9a77')

build() {
  cd "$pkgname-$pkgver"

  # upstream commit 88c247f7f18ac25181ddcaff97fbbecbd3a29f57
  patch -Np1 < "$srcdir/0001-depmod-fix-parsing-of-modules.order-with-compressed-.patch"

  # upstream commit 66f3228d17d66d7e2dd484427259290fbc82b2f0
  patch -Np1 < "$srcdir/0001-libkmod-Add-support-for-.-in-module-parameter-on-kcm.patch"

  ./configure \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --with-zlib

  make
}

check() {
  # testsuite is broken on 32-bit because of an unhandled EEXIST on mkdir_p
  make -C "$pkgname-$pkgver" check || :
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d "$pkgdir/sbin"

  # add symlinks to kmod
  ln -s ../usr/bin/kmod "$pkgdir/sbin/modprobe"
  ln -s ../usr/bin/kmod "$pkgdir/sbin/depmod"

  for tool in {ins,ls,rm}mod modinfo; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
