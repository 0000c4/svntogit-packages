# $Id$
# Maintainer: dale <dale@archlinux.org>

pkgname=epiphany
pkgver=2.27.91
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the WebKit rendering engine."
arch=(i686 x86_64)
license=('GPL')
depends=('libwebkit>=1.1.13' 'startup-notification>=0.10' 'iso-codes>=3.10.1' 'desktop-file-utils' 'libxslt>=1.1.24' 'libnotify>=0.4.5' 'avahi>=0.6.25' 'libsoup-gnome>=2.27.91')
makedepends=('gnome-doc-utils>=0.17.3' 'pkgconfig' 'networkmanager' 'intltool')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('037f1f5df3a203e39dd2f8184b8bf99434782f63ae0b0c722f0d58bb86457e22')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-network-manager || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain epiphany ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
