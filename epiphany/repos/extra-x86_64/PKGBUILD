# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=2.28.2
pkgrel=2
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the WebKit rendering engine."
arch=(i686 x86_64)
license=('GPL')
depends=('libwebkit>=1.1.15.4-2' 'startup-notification>=0.10' 'iso-codes>=3.10.1' 'desktop-file-utils' 'libxslt>=1.1.24' 'libnotify>=0.4.5' 'avahi>=0.6.25' 'libsoup-gnome>=2.28.2')
makedepends=('gnome-doc-utils>=0.18.0' 'pkgconfig' 'networkmanager' 'intltool')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2
        ephy-download-mime.patch
        bug605860.patch)
sha256sums=('98b7a60bb42fa5c8ebdec1bf623319ae730e5674ddbcfaf51649093adb079490'
            '3e8a13d14afd5fb16486fffbafdd8bc47b117d948e13870712bd067586133728'
            '52bb77728780eaa8998b769d84beb89f97cb4476db265b7c38a1c2e6c548a4a3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/ephy-download-mime.patch" || return 1
  patch -Np1 -i "${srcdir}/bug605860.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-network-manager || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain epiphany ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
