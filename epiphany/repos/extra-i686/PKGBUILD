# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.8.2
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup' 'libnotify' 'gsettings-desktop-schemas' 'webkitgtk3' 'nss' 'iso-codes' 'dconf' 'desktop-file-utils'
         'hicolor-icon-theme' 'gcr' 'gnome-desktop' 'libwnck3')
makedepends=('intltool' 'gnome-doc-utils' 'startup-notification' 'gobject-introspection')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('ae89d6902ed243304d2cbf2fa361f6dd0cb3401fdaf726e6fdd4acc09197d50a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
