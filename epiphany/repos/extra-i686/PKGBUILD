# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.2.0
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup-gnome' 'libnotify' 'gsettings-desktop-schemas' 'libwebkit3' 'nss' 'iso-codes' 'dconf' 'gobject-introspection' 'desktop-file-utils'
         'hicolor-icon-theme' 'seed')
makedepends=('intltool' 'gnome-doc-utils' 'startup-notification')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
       fix_crash::http://git.gnome.org/browse/epiphany/patch/?id=1cb7fb1109aecedd224d03fda202165adf3423a7)
sha256sums=('e5402cb8b52d3de5c09feb01bbf28a1bd3c20f260a2e0956c9d132f41040e5d2'
            'ef1423bfaea72c44bd1bc1cc49d06e98dab89235fab2ad5dcd89d87b4d62cb4f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix_crash"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-seed
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
