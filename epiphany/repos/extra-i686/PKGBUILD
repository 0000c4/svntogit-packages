# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.4.2
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup-gnome' 'libnotify' 'gsettings-desktop-schemas' 'libwebkit3' 'nss' 'iso-codes' 'dconf' 'gobject-introspection' 'desktop-file-utils'
         'hicolor-icon-theme' 'seed')
makedepends=('intltool' 'gnome-doc-utils' 'startup-notification')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('fb5b0d48c4d8725031d023fd6a054c10cb9f166cd430db7fc2383d8d2af5d615')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-seed
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
