# $Id$
# Maintainer: dale <dale@archlinux.org>

pkgname=epiphany
pkgver=2.24.3
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the mozilla rendering engine."
arch=(i686 x86_64)
license=('GPL')
depends=('xulrunner>=1.9.0.5-2' 'gnome-desktop>=2.24.3' 'iso-codes>=2.0' 'desktop-file-utils' 'libxslt>=1.1.24' 'enchant>=1.4.2' 'libnotify>=0.4.5' 'libcanberra')
makedepends=('gnome-doc-utils>=0.14.0' 'gnome-python>=2.22.3' 'pkgconfig' 'libnetworkmanager>=0.6.6' 'intltool')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('af51614c7b78f8b8c68e9ece3d8f4d8f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-python \
  	--enable-certificate-manager \
	--enable-dbus \
        --with-gecko=libxul-embedding \
	--disable-scrollkeeper \
	--enable-network-manager \
	--enable-spell-checker || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
