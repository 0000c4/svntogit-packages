# $Id$
# Maintainer: dale <dale@archlinux.org>

pkgname=epiphany
pkgver=2.22.2
pkgrel=2
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the mozilla rendering engine."
arch=(i686 x86_64)
license=('GPL')
depends=('xulrunner>=1.9' 'gnome-desktop>=2.22.2' 'iso-codes>=2.0' 'desktop-file-utils' 'libxslt>=1.1.24' 'enchant>=1.4.2' 'libnotify>=0.4.4')
makedepends=('perlxml' 'gnome-doc-utils>=0.12.2' 'gnome-python>=2.22.' 'pkgconfig' 'libnetworkmanager>=0.6.6')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.22/${pkgname}-${pkgver}.tar.bz2
	98a_xul_b5_rc1_api_transition.patch)
md5sums=('f90dc0a01dae58dfa393b17bce717d3c'
	 '29153a94a51024c8766df9f1f6b15213')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i ${startdir}/src/98a_xul_b5_rc1_api_transition.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-python \
  	--enable-certificate-manager \
	--enable-dbus \
        --with-gecko=libxul-embedding \
	--disable-scrollkeeper \
	--enable-network-manager \
	--enable-spell-checker || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
