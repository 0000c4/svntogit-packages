# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-accounts-kcm
_pkgname=ktp-accounts-kcm
pkgver=19.11.80
pkgrel=1
pkgdesc='KCM Module for configuring Telepathy Instant Messaging Accounts'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kdenetwork telepathy-kde)
depends=(telepathy-qt kaccounts-providers)
makedepends=(extra-cmake-modules intltool)
optdepends=('telepathy-gabble: XMPP/Jabber accounts support' 'telepathy-haze: account types supported by Pidgin/libpurple'
            'telepathy-morse: Telegram accounts support' 'telepathy-salut: link-local XMPP account support')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig}
         telepathy-qt-0.9.8.patch)
sha256sums=('751825b44c5ef1d39541f52ea8bbb527c2db8ddbbc1d43866d2a720918e1ffb6'
            'SKIP'
            'f34e8e9ae6ca9877208f9e8d5df4927542618561e3a81c6a2e7e972306ea80fb')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $_pkgname-$pkgver
  patch -p1 -i ../telepathy-qt-0.9.8.patch # Fix build with telepathy-qt 0.9.8
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
