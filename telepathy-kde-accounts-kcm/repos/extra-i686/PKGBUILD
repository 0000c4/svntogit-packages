# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-accounts-kcm
pkgver=0.2.0
pkgrel=2
pkgdesc="KCM Module for configuring Telepathy Instant Messaging Accounts"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdelibs' 'telepathy-qt4' 'telepathy-mission-control' 'telepathy-kde-contact-list'
         'telepathy-kde-auth-handler' 'telepathy-kde-integration-module' 'telepathy-kde-approver'
         'telepathy-kde-filetransfer-handler')
makedepends=('cmake' 'automoc4')
optdepends=('telepathy-gabble: XMPP/Jabber/GTalk support'
            'telepathy-haze: all protocols supported by libpurple')
groups=('telepathy-kde')
conflicts=('telepathy-kde-accounts-kcm-plugins')
replaces=('telepathy-kde-accounts-kcm-plugins')
source=("http://download.kde.org/unstable/telepathy-kde/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('2ebc4ac117a3e28e2812a2a9c97f7d0ee97dac67')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
