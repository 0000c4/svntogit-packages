# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-accounts-kcm
pkgver=0.1.0
pkgrel=1
pkgdesc="KCM Module for configuring Telepathy Instant Messaging Accounts"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdelibs' 'telepathy-qt4' 'telepathy-mission-control')
makedepends=('cmake' 'automoc4')
groups=('telepathy-kde')
source=("http://download.kde.org/unstable/telepathy-kde/${pkgver}/src/${pkgname}_${pkgver}.tar.bz2")
sha1sums=('23c1d3289ecaf4b4361d0ed324a7044cccfae801')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}_${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
