# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Thomas Baechler <thomas@archlinux.org>
pkgname=kernel26-firmware
pkgver=2.6.29
pkgrel=1
pkgdesc="The included firmware files of the Linux Kernel"
arch=(i686 x86_64)
license=('GPL2')
groups=('base')
url="http://www.kernel.org"
depends=()
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$pkgver.tar.bz2
        config
        config.x86_64)
options=(!makeflags)
md5sums=('64921b5ff5cdadbccfcd3820f03be7d8'
         '8c308a5016ca528aa8360a180eaa5570'
         '0af971f70ad9eb88b41e678952cdf146')

build() {
  cd ${srcdir}/linux-$pkgver
  if [ "$CARCH" = "x86_64" ]; then
    cat ../config.x86_64 >./.config
  else
    cat ../config >./.config
  fi
  make firmware || return 1
  make INSTALL_MOD_PATH=${pkgdir} firmware_install || return 1
}
