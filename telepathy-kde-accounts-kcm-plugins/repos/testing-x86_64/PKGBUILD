# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-accounts-kcm-plugins
pkgver=0.1.0
pkgrel=1
pkgdesc="Plugins for protocol-specific UI's in the Telepathy Accounts KCM"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('telepathy-kde-accounts-kcm')
makedepends=('cmake' 'automoc4')
optdepends=('telepathy-gabble: XMPP/Jabber/GTalk support'
            'telepathy-butterfly: MSN/Windows Live support'
            'telepathy-haze: all protocols supported by libpurple')
groups=('telepathy-kde')
source=("http://download.kde.org/unstable/telepathy-kde/${pkgver}/src/${pkgname}_${pkgver}.tar.bz2")
sha1sums=('340202386d0e0e8e0f18fb3cb856a9055fc200a5')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}_${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
