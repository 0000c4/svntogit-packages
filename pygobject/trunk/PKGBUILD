# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pygobject
pkgver=2.27.0
pkgrel=1
pkgdesc="Python bindings for GObject"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('python2' 'glib2' 'pycairo' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool')
url="http://www.pygtk.org/"
sha256sums=('7594bd86c5b92a4ba1f26927800f1b29e1ec5c246cad67aa085c279d6425b8a2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  find "$pkgdir"/usr/share/pygobject -name '*.py' | \
    xargs sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"
}
