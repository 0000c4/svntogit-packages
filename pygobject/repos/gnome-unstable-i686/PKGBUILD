# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(python-gobject python2-gobject)
pkgver=2.90.3
pkgrel=3
arch=('i686' 'x86_64')
url="https://live.gnome.org/PyGObject"
license=('LGPL')
depends=('glib2' 'gobject-introspection')
makedepends=(python python2 python-cairo python2-cairo)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz
        documentation-location.patch)
options=('!libtool')
sha256sums=('8e14f8db0780ad26a8ce6583da30313463ca40519b35f5cda8780f02bd6ddca4'
            '6174cdeea99e134168ef8235251d9c3802200ee75dc63f86144de75f864e1344')

build() {
  cd "${srcdir}"
  cp -a "${pkgbase}-${pkgver}" python2-build

  (
    cd python2-build
    patch -Np1 -i ${srcdir}/documentation-location.patch
    autoreconf -fi
    export PYTHON=/usr/bin/python2
    ./configure --prefix=/usr
    make
  )

  (
    cd "${pkgbase}-${pkgver}"
    patch -Np1 -i ${srcdir}/documentation-location.patch
    autoreconf -fi
    ./configure --prefix=/usr
    make
  )
}

package_python-gobject() {
  pkgdesc="Python 3 bindings for GObject"
  depends+=('python' 'python-cairo')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/python3.2/site-packages/gtk-2.0/"
}

package_python2-gobject() {
  pkgdesc="Python 2 bindings for GObject"
  depends+=('python2' 'python2-cairo')
  conflicts=('python-gobject')

  cd "${srcdir}/python2-build"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/python2.7/site-packages/gtk-2.0/"
}
