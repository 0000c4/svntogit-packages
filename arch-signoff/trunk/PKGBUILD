# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=arch-signoff
pkgver=0.2.1
pkgrel=1
pkgdesc='Sign off Arch Linux testing packages'
arch=(any)
url="https://github.com/archlinux/arch-signoff"
license=("custom:ISC")
depends=('python' 'pyalpm' 'python-click' 'python-dateutil'
         'python-requests' 'python-setuptools')
makedepends=('python-setuptools-scm' 'asciidoc')
source=($pkgname-$pkgver.tar.gz::https://github.com/archlinux/arch-signoff/archive/${pkgver}.tar.gz)
sha512sums=('ec4a45be02fe05af20eba26209400394576da9d836431f1665a406e3d949eb508b1435e8c23abc1e52ffc88f5ffae717738d22cb9ea846cdbd5fa9f26f23f57c')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  make -C man
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "man/signoff.1" "$pkgdir/usr/share/man/man1/signoff.1"
}
