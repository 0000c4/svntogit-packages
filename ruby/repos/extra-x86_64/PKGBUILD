# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>
# Contributors: John Proctor <jproctor@prium.net>
#               Jeramy Rutley <jrutley@gmail.com>
pkgname=ruby
pkgver=1.8.7_p72
_pkgver=1.8.7-p72
pkgrel=3
pkgdesc="An object-oriented language for quick and easy programming"
arch=(i686 x86_64)
license=('custom')
url="http://www.ruby-lang.org/en/"
depends=('gdbm>=1.8.3' 'db>=4.7.25' 'openssl>=0.9.8j' 'zlib>=1.2.3.3' 'readline>=5.2.013')
makedepends=('tk>=8.5')
source=(ftp://ftp.ruby-lang.org/pub/ruby/stable/${pkgname}-${_pkgver}.tar.bz2) 
options=('!emptydirs' 'force')
md5sums=('0b215c46b89b28d7ab8d56d96e72d5b9')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr --enable-shared --enable-pthread || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  #generate the ri docs
  make -j1 DESTDIR="${pkgdir}" install-doc || return 1
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
