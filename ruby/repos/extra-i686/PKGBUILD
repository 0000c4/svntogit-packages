# $Id$
# Maintainer:
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgbase=ruby
pkgname=('ruby' 'ruby-docs')
pkgver=1.9.2_p136
_pkgver=1.9.2-p136
pkgrel=2
pkgdesc="An object-oriented language for quick and easy programming"
arch=('i686' 'x86_64')
url="http://www.ruby-lang.org/en/"
license=('custom')
makedepends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'tk' 'libffi' 'doxygen'
        'graphviz')
options=('!emptydirs')
source=("ftp://ftp.ruby-lang.org/pub/ruby/1.9/${pkgbase}-${_pkgver}.tar.bz2")
sha256sums=('33092509aad118f07f0483a3db1d4c5adaccf4bb0324cd43f44e3bd3dd1858cb')

build() {
  cd "${srcdir}/${pkgbase}-${_pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --enable-shared \
      --enable-pthread \
      --disable-rpath
  make
}

package_ruby() {
  depends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'libffi')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
  optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgbase}-${_pkgver}"
  make DESTDIR="${pkgdir}" install-nodoc
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ruby-docs() {
  pkgdesc="Documentation files for ruby"
  depends=('ruby')

  cd "${srcdir}/${pkgbase}-${_pkgver}"
  make -j1 DESTDIR="${pkgdir}" install-doc install-capi
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
}
