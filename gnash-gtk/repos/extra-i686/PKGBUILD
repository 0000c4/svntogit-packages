# $Id$

pkgname=gnash-gtk
pkgver=0.8.4
pkgrel=1
pkgdesc='A GNU Flash movie player (gtk)'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/gnash/'
license=("GPL3")
depends=("gnash-common=$pkgver" 'gtk2' 'gtkglext')
makedepends=('pkgconfig')
source=("http://ftp.gnu.org/gnu/gnash/${pkgver}/gnash-${pkgver}.tar.bz2")
options=('!libtool' '!emptydirs' '!docs')
md5sums=('b47a7ed4b7c66b8c2ebacc2286ef0d4b')

build() {
	cd $srcdir/gnash-$pkgver

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-gui=gtk \
		--with-plugins-install=system \
		--with-npapi-plugindir=/usr/lib/mozilla/plugins \
		--enable-renderer=ogl \
		--enable-z \
		--enable-jpeg \
		--enable-libpng \
		--enable-gif \
		--enable-fontconfig \
		--enable-Xft \
		--enable-expat \
		--enable-gstreamer \
		--enable-media=gst || return 1

	make || return 1
	make DESTDIR=$pkgdir install install-plugin || return 1
	rm -rf $pkgdir/usr/{lib/gnash,share}
	rm -f $pkgdir/usr/bin/{gnash,gprocessor,dumpshm,soldumper,flvdumper}
	rm -rf $pkgdir/etc
}
