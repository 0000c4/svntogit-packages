# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=2.91.6
pkgrel=2
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2' 'python2')
makedepends=('pkg-config' 'intltool' 'gobject-introspection')
options=('!libtool' '!makeflags')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2
        menus.patch fix-menu-editor.patch)
sha256sums=('60de5b2b964e82f6bf7e1271f228bf3d7c72684e05c1fca32ab2a80d0075feb5'
            'df2779777906b038fa911dc745534564ede9524a64ab368e2f6f8e38d54e7acc'
            'f75eb5862818ffb7772c06c7e6e341edb819ec9352b6d4d2d7681461cd72df27')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/menus.patch"
  patch -Np1 -i "${srcdir}/fix-menu-editor.patch"

  PYTHON=/usr/bin/python2 ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" \
    $pkgdir/usr/lib/python2.7/site-packages/GMenuSimpleEditor/config.py
}
