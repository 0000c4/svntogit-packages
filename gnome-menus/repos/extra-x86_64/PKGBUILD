# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.32.0
pkgrel=1
pkgdesc="GNOME menu specifications"
arch=('x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection' 'python2' git gnome-common)
license=('GPL' 'LGPL')
url="https://www.gnome.org"
groups=('gnome')
_commit=4a3944773c0d7201148055a5f7fd103c99263183  # tags/3.32.0^0
source=("git+https://git.gnome.org/browse/gnome-menus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

check() {
  cd $pkgname
  make check
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}
