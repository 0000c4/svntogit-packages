# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=3.22.0+11+g3977943
pkgrel=1
pkgdesc="A VNC server for the GNOME desktop"
url="https://wiki.gnome.org/Projects/Vino"
arch=(x86_64)
license=(GPL)
depends=(libnotify libxtst libsm telepathy-glib gtk3 libsecret avahi gnutls)
makedepends=(intltool networkmanager gnome-common git)
groups=(gnome)
_commit=39779437dba89027cfa38b3f0b2c2559d128e53b  # master
source=("git+https://git.gnome.org/browse/vino#commit=$_commit"
        dont-crash-on-wayland.patch)
sha256sums=('SKIP'
            'd49376d49e56c545d47367c3bdd4aa9df717d60d9bcb24d0991d5ba8d29fd64a')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # Fixup missing tag for pkgver()
  git tag -f 3.22.0 2448f101df88d8f50c82b2969cd3cb4e9c755fc6
  patch -Np1 -i ../dont-crash-on-wayland.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --disable-http-server --with-secret

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
