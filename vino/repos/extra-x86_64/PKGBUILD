# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=2.26.0
pkgrel=1
pkgdesc="a VNC server for the GNOME desktop"
arch=(i686 x86_64)
license=('GPL')
depends=('libgnomeui>=2.24.1' 'libnotify>=0.4.5' 'libxtst' 'gnutls>=2.6.4' 'libsoup>=2.26.0' 'libunique>=1.0.8')
makedepends=('pkgconfig' 'intltool')
groups=('gnome-extra')
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7a10464824d8446411b90c162d34401e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/vino \
              --localstatedir=/var || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain vino ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
