# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-vpnc
pkgver=0.9.8.0
pkgrel=2
pkgdesc="NetworkManager VPN plugin for vpnc"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver}" 'vpnc' 'hicolor-icon-theme' 'gtk3' 'libgnome-keyring')
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
options=('!libtool')
install=networkmanager-vpnc.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-vpnc/0.9/NetworkManager-vpnc-${pkgver}.tar.xz
        "vpnc-paths.patch")
sha256sums=('e4504c569315c20f2468995f073123a787cdb66695d321f20f994abd1d7eb1c1'
            '0b07abc21823b316bfd9a9faf7bf34ded35d66e904d081c5e285f8dc8ce30a54')

prepare() {
  cd NetworkManager-vpnc-${pkgver}
  patch -Np1 <"$srcdir/vpnc-paths.patch"
}

build() {
  cd NetworkManager-vpnc-${pkgver}
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
	--disable-static
  make
}

package() {
  cd NetworkManager-vpnc-${pkgver}
  make DESTDIR="${pkgdir}" install
}
