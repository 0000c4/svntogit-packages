# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-vpnc
pkgver=1.1.93
pkgrel=1
pkgdesc="NetworkManager VPN plugin for VPNC"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/projects/NetworkManager/"
depends=(libnm-gtk libsecret vpnc)
makedepends=(intltool python)
install=networkmanager-vpnc.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-vpnc/${pkgver::3}/NetworkManager-vpnc-${pkgver}.tar.xz)
sha256sums=('5c0c7aa2addfa1175366010c065b49f0963d3bf3a360db020b0b52432db54b79')

build() {
  cd NetworkManager-vpnc-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --enable-more-warnings=yes \
    --disable-static
  make
}

package() {
  cd NetworkManager-vpnc-$pkgver
  make DESTDIR="$pkgdir" install
}
