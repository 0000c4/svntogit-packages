# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=libxcb
pkgver=1.1.92
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.2' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python')
options=('!libtool')
license=('custom')
source=(${url}/dist/${pkgname}-${pkgver}.tar.bz2
	libxcb-1.1-no-pthread-stubs.patch)
md5sums=('8855de3467ad11b5fc78296624770dc4'
         'a53f09ab3ec5cbfc1b0848bd137c535a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/libxcb-1.1-no-pthread-stubs.patch || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr --enable-xinput || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1
}
