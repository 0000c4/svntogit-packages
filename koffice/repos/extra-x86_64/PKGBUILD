# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgbase=koffice
pkgname=(
	'koffice-filters'
	'koffice-interfaces'
	'koffice-libs'
	'koffice-plugins'
	'koffice-pics'
	'koffice-servicetypes'
	'koffice-templates'
	'koffice-tools'
	'koffice-karbon'
	'koffice-karbon-doc'
#	'koffice-kchart'
#	'koffice-kchart-doc'
	'koffice-kexi'
	'koffice-kexi-doc'
	'koffice-kformula'
	'koffice-kformula-doc'
	'koffice-kounavail'
	'koffice-kplato'
#	'koffice-kplato-doc'
	'koffice-kpresenter'
	'koffice-kpresenter-doc'
	'koffice-krita'
#	'koffice-krita-doc' # no krita-doc anymore upstream
	'koffice-kspread'
	'koffice-kspread-doc'
	'koffice-kword'
#	'koffice-kword-doc' # see http://userbase.kde.org/KWord/Manual for documentation (not supplied by upstream anymore in the sources)
	'koffice-handbook'
	'koffice-thesaurus-doc'
#	'koffice-kdgantt' # still an empty package
)
pkgver=2.3.0
pkgrel=1
arch=('i686' 'x86_64')
url='http://koffice.kde.org'
license=('GPL' 'LGPL' 'FDL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'eigen' \
  'kdelibs' 'gsl' 'qca' 'lcms' 'glew' 'qimageblitz' \
  'kdepimlibs' 'pstoedit' 'poppler-qt' 'libwpd' 'libwpg' 'openexr' 'opengtl' 'kdegraphics-libs' 'sqlite3' 'docbook-xml' 'docbook-xsl')
groups=('koffice')
source=("http://download.kde.org/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.bz2"
	"wordperfect.patch")
#	"01_kpresenter.patch"
#	"02_kspreadpatch.patch"
#	"03_iconv.patch"
#	"04_kwordascii.patch"
#	"05_filter_excel.patch"
#	"06_headers.patch"
#	"10_nokcharpart.patch"
#	"16_kformula_kdeinit4.patch")
sha256sums=('a8e9a3ed882536cc4ca59388cc82268478339d328b62daaaec1a7bc970b73e1c'
            '2116ee234a11d23e47a09d2863795488895c1b265421ecfec3c6f903adb38c5f'
            'ebe834d39e6d7701d8c59694328736c4b6223ae5629a81a9e07c516388203749'
            'bba3e8be1cfe18559dd0210887bbfb008d97d0266b160499f43d2f7860feaa42'
            '976bc94157571357636b2029ebe5c08cd8ee7a34aea1defc046e108e255bac84'
            'f36194350b228f5303ee4e136f1476ec6bf217d4c76e9a04732b0c2dd4c913d8'
            '246f1b032646b17c48e1c35da4ace513b8a9a3646b68e973da5921386fd8b7c7'
            '9a23a933caf76094d1602f7408c27f24d51515af31238c6eb8798385cd3a1656'
            'a6f7d57c8d9740eba3267ed816271394f95091d960e69d407bc84396df5f9675')
	
build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	patch -Np1 -i ${srcdir}/wordperfect.patch
#	patch -Np1 -i ${srcdir}/01_kpresenter.patch
#	patch -Np1 -i ${srcdir}/02_kspreadpatch.patch
#	patch -Np1 -i ${srcdir}/03_iconv.patch
#	patch -Np1 -i ${srcdir}/04_kwordascii.patch
#	patch -Np1 -i ${srcdir}/05_filter_excel.patch
#	patch -Np1 -i ${srcdir}/06_headers.patch
#	patch -Np1 -i ${srcdir}/10_nokcharpart.patch
#	patch -Np1 -i ${srcdir}/16_kformula_kdeinit4.patch

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make 
}

##### 1st package common files, depends, optdepends of the koffice applications #####

package_koffice-filters(){
	pkgdesc="Filters for de KDE office suite"
        optdepends=('libwpd: WordPerfect document importer' 'libwpg: Corel WordPerfect Graphics image importer' 
	'pstoedit: Karbon EPS import filter' 'kdegraphics-libs: support for raw images')
	install=filters.install
        cd ${srcdir}/build/filters
        make DESTDIR=${pkgdir} install
}

package_koffice-interfaces(){
	pkgdesc="Interfaces for the KDE office suite"
	depends=('kdelibs' 'koffice-libs')
	cd ${srcdir}/build/interfaces
	make DESTDIR=${pkgdir} install
}

package_koffice-libs(){
	pkgdesc="Libs for the KDE office suite"
	depends=('kdepimlibs' 'qca' 'lcms' 'koffice-servicetypes')
        cd ${srcdir}/build/libs
        make DESTDIR=${pkgdir} install
}

package_koffice-plugins(){
	pkgdesc="Plugins for the KDE office suite"
	depends=('koffice-libs')
	replaces=('koffice-kchart')
	conflicts=('koffice-kchart')
        cd ${srcdir}/build/plugins
        make DESTDIR=${pkgdir} install
}

package_koffice-pics(){
	pkgdesc="Icons for the KDE office suite"
	install=koffice.install
        cd ${srcdir}/build/pics
        make DESTDIR=${pkgdir} install
}

package_koffice-servicetypes(){
	pkgdesc="Servicetypes for the KDE office suite"
        cd ${srcdir}/build/servicetypes
        make DESTDIR=${pkgdir} install
}

package_koffice-templates(){
	pkgdesc="Templates for the KDE office suite"
        cd ${srcdir}/build/templates
        make DESTDIR=${pkgdir} install
}

package_koffice-tools(){
	pkgdesc="Tools for the KDE office suite"
	depends=('koffice-libs')
        cd ${srcdir}/build/tools
        make DESTDIR=${pkgdir} install
}

#### package common files done #####

#### package apps and their docs #####

package_koffice-karbon(){
	pkgdesc="Vector drawing application for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'koffice-templates')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/karbon
        make DESTDIR=${pkgdir} install
}

package_koffice-karbon-doc(){
	pkgdesc="Documentation for karbon"
        cd ${srcdir}/build/doc/karbon
        make DESTDIR=${pkgdir} install
}

package_koffice-kexi(){
        pkgdesc="Integrated data management application for the KDE office suite"
        depends=('kdelibs' 'koffice-libs' 'koffice-kspread' 'sqlite3')
        cd ${srcdir}/build/kexi
        make DESTDIR=${pkgdir} install
}

package_koffice-kexi-doc(){
        pkgdesc="Documentation for kexi"
        cd ${srcdir}/build/doc/kexi
        make DESTDIR=${pkgdir} install
}

package_koffice-kformula(){
        pkgdesc="Formula editor for the KDE office suite"
		depends=('kdelibs' 'koffice-libs')
        cd ${srcdir}/build/kformula
        make DESTDIR=${pkgdir} install
}

package_koffice-kformula-doc() {
	pkgdesc="Documentation for koffice-kformula"
	cd ${srcdir}/build/doc/kformula
        make DESTDIR=${pkgdir} install
}


package_koffice-kounavail(){
	pkgdesc="Kounavail for the KDE office suite"
	depends=('kdelibs' 'koffice-libs')
        cd ${srcdir}/build/kounavail
        make DESTDIR=${pkgdir} install
}

package_koffice-kplato(){
	pkgdesc="Project management application for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'kdepimlibs' 'koffice-templates' \
		'koffice-plugins' 'koffice-kword')
	install=koffice.install
        cd ${srcdir}/build/kplato
        make DESTDIR=${pkgdir} install
}

#package_koffice-kplato-doc(){
#	pkgdesc="Documentation for kplato"
#        cd ${srcdir}/build/doc/kplato
#        make DESTDIR=${pkgdir} install
#}

package_koffice-kpresenter(){
	pkgdesc="Presentation tool for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'koffice-templates')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/kpresenter
        make DESTDIR=${pkgdir} install
}

package_koffice-kpresenter-doc(){
	pkgdesc="Documentation for kpresenter"
        cd ${srcdir}/build/doc/kpresenter
        make DESTDIR=${pkgdir} install
}

package_koffice-krita(){
	pkgdesc="Graphics editor for KDE"
	depends=('hicolor-icon-theme' 'glew' 'qimageblitz' 'koffice-libs' \
		'koffice-templates' 'koffice-plugins' 'poppler-qt' 'shared-mime-info' \
		'openexr' 'opengtl')
        optdepends=('koffice-filters: import/export filters')
	install=krita.install
        cd ${srcdir}/build/krita
        make DESTDIR=${pkgdir} install
}

#package_koffice-krita-doc(){
#	pkgdesc="Documentation for krita"
#        cd ${srcdir}/build/doc/krita
#        make DESTDIR=${pkgdir} install
#}


package_koffice-kspread(){
	pkgdesc="Spreadsheet application for KDE"
	depends=('hicolor-icon-theme' 'gsl' 'koffice-libs' 'koffice-templates' 'koffice-plugins')
	optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/kspread
        make DESTDIR=${pkgdir} install
}

package_koffice-kspread-doc(){
	pkgdesc="Documentation for kspread"
	cd ${srcdir}/build/doc/kspread
        make DESTDIR=${pkgdir} install
}

package_koffice-kword(){
	pkgdesc="Wordprocessor for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'koffice-templates' 'koffice-pics' \
		'koffice-kounavail' 'koffice-plugins')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/kword
        make DESTDIR=${pkgdir} install

        # some sed lines to have python2 in extension scripts
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" ${pkgdir}/usr/share/apps/kword/scripts/extensions/oouno.py
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" ${pkgdir}/usr/share/apps/kword/scripts/extensions/importdoxyxml.py
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" ${pkgdir}/usr/share/apps/kword/scripts/extensions/onlinehelp.py
}

#package_koffice-kword-doc(){
#	pkgdesc="Documentation for kword"
#        cd ${srcdir}/build/doc/kword
#        make DESTDIR=${pkgdir} install
#}

##### package apps done #####

#package_koffice-kchart(){
#	pkgdesc="Charting tool for KDE"
#	depends=('hicolor-icon-theme' 'koffice-interfaces' 'koffice-templates')
#        optdepends=('koffice-filters: import/export filters')
#	install=koffice.install
#        cd ${srcdir}/build/kchart
#        make DESTDIR=${pkgdir} install
#}

#package_koffice-kchart-doc(){
#	pkgdesc="Documentation for kchart"
#        cd ${srcdir}/build/doc/kchart
#        make DESTDIR=${pkgdir} install
#}

#package_koffice-kdgantt(){
#	pkgdesc="Gantt for KDE"
#        cd ${srcdir}/build/kdgantt
#        make DESTDIR=${pkgdir} install
#}

package_koffice-handbook() {
	pkgdesc="Documentation for koffice"
	cd ${srcdir}/build/doc/koffice
        make DESTDIR=${pkgdir} install
}

package_koffice-thesaurus-doc(){
	pkgdesc="Documentation for koffice-thesaurus"
	cd ${srcdir}/build/doc/thesaurus
        make DESTDIR=${pkgdir} install
}

sha256sums=('a8e9a3ed882536cc4ca59388cc82268478339d328b62daaaec1a7bc970b73e1c'
            '06a140b14a40d717c9128758139ff9e6b994b00fc96f183d3a60323c180dd898')
