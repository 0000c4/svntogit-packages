# $Id$
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgbase=xpdf-languages
pkgver=20110815
pkgrel=1
pkgdesc='Encoding information to use specific character sets in Xpdf; does not include fonts'
url='http://foolabs.com/xpdf/'
license=('GPL2')
arch=('any')
depends=('xpdf')

install=install

pkgname=('xpdf-arabic'
         'xpdf-chinese-simplified'
         'xpdf-chinese-traditional'
         'xpdf-cyrillic'
         'xpdf-greek'
         'xpdf-hebrew'
         'xpdf-japanese'
         'xpdf-korean'
         'xpdf-latin2'
         'xpdf-thai'
         'xpdf-turkish')

_tars=(${pkgname[@]/%/.tar.gz})
source=(${_tars[@]/#/ftp://ftp.foolabs.com/pub/xpdf/}
        'xpdfrc-chinese-simplified.add'
        'xpdfrc-chinese-traditional.add'
        'xpdfrc-japanese.add'
        'xpdfrc-korean.add'
        'LICENSE.adobe')

sha1sums=('b3d1f6fa2299de0b6ee25ea73967502ea3a98228'
          'aa9745f435841fc0f86f744257446bae82975f18'
          'a97f21bc551da092abe557ac70ab1fb57703cf57'
          'c5c55919064e452e712a83c69063479a2de08e80'
          '0e968c80d21352a36fcded0916b8a623af02ec40'
          '2820c90b1f303849ed24aabd9afd8581daba095b'
          '19f263a00dd2b4bbec93c0787d96cd96a05dfd0b'
          '7b3ce80d37f2abbcafdf20c1524944732c5b4836'
          '508f237d795bb26abf89fb5999998a87555af159'
          'd8bf58d960121508fb823c7bc7475bc0008b670e'
          'aabf97033e074593b3efccfdd92068e41e48c356'
          '6bb0b98d0bae449388395e8d211a5998de7de8d6'
          '87717bed8e5db3b0c6f1b23e9094aebfdffcec75'
          'd0a88b14d6cdf2ee9cfe12d12fedc22e23c1f435'
          '5f12a6fb5d45c9052c86b43e0422335be98c492f'
          '1a6619a36e605909de20ca4cca4dd3ade1494667')

for i in ${pkgname[@]}; do j=${i/xpdf-/}; eval 'package_'${i}'() {
	cd "${srcdir}"
	
	sed \
		-e "s:/usr/local/share/xpdf:/usr/share/xpdf:" \
		-e "/displayCIDFontX.*/d" \
		-i '${i}'/add-to-xpdfrc \

	[[ -e xpdfrc-'${j}'.add ]] && {
		cat xpdfrc-'${j}'.add >> '${i}'/add-to-xpdfrc
		# exactly those packages for which `grep -q Adobe *` matches...
		install -D LICENSE.adobe "${pkgdir}"/usr/share/licenses/'${i}'/LICENSE
		license=(GPL2 custom)
	}

	install -d "${pkgdir}"/usr/share/xpdf
	cp -r '${i}' "${pkgdir}"/usr/share/xpdf/'${j}'
}'; done
