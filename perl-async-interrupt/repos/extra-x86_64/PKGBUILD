# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-async-interrupt
pkgver=1.23
pkgrel=1
pkgdesc='allow C/XS libraries to interrupt perl asynchronously'
arch=(x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-canary-stability' 'perl-common-sense')
url=https://metacpan.org/release/Async-Interrupt
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/Async-Interrupt-$pkgver.tar.gz")
sha512sums=('edea9bdfe403999bc6f58115e70f2abb33bf16f34417822115f61700dc8dfbe03bcf01b46ba7381d9a988d3c7d7f83e8201cb6f514420c0abf55eadd38986275')

build() {
  cd Async-Interrupt-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  make
}

check() {
  cd Async-Interrupt-$pkgver
  make test
}

package() {
  cd Async-Interrupt-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
