# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libva
pkgver=1.8.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=(i686 x86_64)
url='https://01.org/linuxmedia/vaapi'
license=(MIT)
depends=(libgl libdrm libxfixes)
makedepends=(mesa)
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
source=(https://github.com/01org/libva/releases/download/$pkgver/libva-${pkgver}.tar.bz2)
sha256sums=('eb92f3dcbe3981df3575348377263b31361113c77b2c3745f23174d1f562d658')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
