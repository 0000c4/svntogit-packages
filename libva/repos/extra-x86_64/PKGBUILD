# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libva
pkgver=1.8.1
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=(i686 x86_64)
url='https://01.org/linuxmedia/vaapi'
license=(MIT)
depends=(libgl libdrm libxfixes)
makedepends=(mesa)
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
source=(https://github.com/01org/libva/releases/download/$pkgver/libva-${pkgver}.tar.bz2)
sha256sums=('c1d5d85b6b40b76f37993b4da33388d3d73b64998dcbc160b7578e24ed775c73')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
