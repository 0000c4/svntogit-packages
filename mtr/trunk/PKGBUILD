# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=mtr
pkgname=(mtr mtr-gtk)
pkgver=0.82
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('ncurses' 'gtk2')
source=(ftp://ftp.bitwizard.nl/mtr/$pkgbase-$pkgver.tar.gz)
url="http://www.bitwizard.nl/mtr/"
md5sums=('10601ea543fda3e51545c4bce195b64c')

build() {
    install -d  "$srcdir/"{mtr-cli,mtr-gtk}
    cd "$srcdir/mtr-cli"
    ../$pkgbase-$pkgver/configure --prefix=/usr --disable-gtktest --without-gtk
    make

    cd "$srcdir/mtr-gtk"
    ../$pkgbase-$pkgver/configure --prefix=/usr
    make
}

package_mtr() {
    pkgdesc="Combines the functionality of traceroute and ping into one tool (CLI version)"
    depends=('ncurses')
    cd "$srcdir/mtr-cli"
    make DESTDIR="$pkgdir" install
}

package_mtr-gtk() {
    pkgdesc="Combines the functionality of traceroute and ping into one tool (GTK version)"
    depends=('ncurses' "gtk2")
    conflicts=("mtr")
    provides=("mtr=$pkgver")

    cd "$srcdir/mtr-gtk"
    make DESTDIR="$pkgdir" install
}
