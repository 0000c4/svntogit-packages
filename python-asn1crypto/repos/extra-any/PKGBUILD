# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Massimiliano Torromeo < massimiliano DOT torromeo AT gmail DOT com >

pkgbase=python-asn1crypto
pkgname=('python-asn1crypto' 'python2-asn1crypto')
pkgver=0.21.1
pkgrel=1
arch=('any')
license=('MIT')
pkgdesc="Python ASN.1 library with a focus on performance and a pythonic API"
url="https://github.com/wbond/asn1crypto"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/wbond/asn1crypto.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a asn1crypto{,-py2}
}

build() {
  cd "$srcdir"/asn1crypto
  python setup.py build

  cd "$srcdir"/asn1crypto-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/asn1crypto
  python run.py tests

  cd "$srcdir"/asn1crypto-py2
  python2 run.py tests
}

package_python-asn1crypto() {
  depends=('python')

  cd asn1crypto
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python-asn1crypto/LICENSE
}

package_python2-asn1crypto() {
  depends=('python2')

  cd asn1crypto-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python2-asn1crypto/LICENSE
}
