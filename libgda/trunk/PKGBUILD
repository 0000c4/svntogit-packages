# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=4.2.1
pkgrel=1
pkgdesc="data abstraction layer; with mysql, pgsql, xml, sqlite providers"
arch=(i686 x86_64)
license=('GPL')
depends=('glib2>=2.26.0' 'libxslt' 'db>=5.1.19' 'ncurses>=5.7' 'libsoup>=2.32.0' 'libmysqlclient>=5.1.51' 'postgresql-libs>=9.0.1')
makedepends=('intltool')
options=('!libtool')
url="http://www.gnome-db.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/4.2/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('d485d24fb19bd95faad196c8ed22b2cb8f9e9275d907f880a3f8f079f6bf40f7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/python/python2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
