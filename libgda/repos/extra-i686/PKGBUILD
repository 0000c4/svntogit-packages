# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=5.1.1
pkgrel=1
pkgdesc="Data abstraction layer with mysql, pgsql, xml, sqlite providers"
arch=(i686 x86_64)
license=('GPL')
depends=('gtksourceview3' 'libxslt' 'json-glib' 'db' 'ncurses' 'libsoup'
         'libmariadbclient' 'postgresql-libs' 'python2' 'libgnome-keyring'
         'hicolor-icon-theme' 'desktop-file-utils' 'graphviz' 'gdk-pixbuf2'
         'iso-codes' 'openssl' 'libgcrypt' 'libldap')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc' 'vala')
options=('!libtool')
url="http://www.gnome-db.org"
install=libgda.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('41a54e60d91758d1d239e5127c856ff0412a5709e92440e9107178af928ed5e1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '1s/python$/&2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static --enable-json
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
