# $Id$
# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=17.04.2
pkgrel=2
pkgdesc="Desktop Planetarium"
url="https://kde.org/applications/education/kstars/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('wcslib' 'libraw' 'kplotting' 'knewstuff' 'knotifyconfig' 'libindi' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'eigen' 'xplanet' 'libindi')
optdepends=('xplanet: XPlanet support')
replaces=('kdeedu-kstars')
conflicts=('kdeedu-kstars')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kstars-${pkgver}.tar.xz"{,.sig})
sha256sums=('92cd4c82f7619d8ba15a3c3725131e8858dc1a373b03fb6a560c2c17ffa087e4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
