# $Id$
# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=16.12.3
pkgrel=1
pkgdesc="Desktop Planetarium"
url="https://kde.org/applications/education/kstars/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('wcslib' 'libindi' 'kplotting' 'knewstuff' 'hicolor-icon-theme' 'libraw')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'eigen' 'xplanet')
optdepends=('xplanet: XPlanet support')
replaces=('kdeedu-kstars')
conflicts=('kdeedu-kstars')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kstars-${pkgver}.tar.xz"{,.sig})
sha256sums=('3eb054971d250be83edfc603c379854a5b5a1caf03bc66386005711bf2b19951'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' \
    "${pkgdir}"/usr/share/kstars/scripts/supernova_updates_parser.py
}
