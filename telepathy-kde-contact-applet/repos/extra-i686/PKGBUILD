# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-contact-applet
_pkgname=ktp-contact-applet
pkgver=0.5.1
pkgrel=1
pkgdesc="The KDE-Telepathy contact plasmoid"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals' 'kdebase-workspace')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
source=("http://download.kde.org/unstable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('2550fa3f9e8d777a5f7858adb0f7c57f25e47ea4')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
