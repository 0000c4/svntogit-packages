# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-contact-applet
pkgver=0.2.0
pkgrel=1
pkgdesc="The kde-telepathy contact plasmoid"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdelibs' 'telepathy-qt4')
makedepends=('cmake' 'automoc4')
groups=('telepathy-kde')
source=("http://download.kde.org/unstable/telepathy-kde/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('b1e2ad706deccd6ca4d745828a8b54fd87bc2066')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
