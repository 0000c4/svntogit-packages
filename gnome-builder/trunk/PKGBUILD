# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.34.1+11+gbf485934b
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils ctags
         libpeas vte3 vala python-jedi autoconf-archive sysprof flatpak-builder gspell libdazzle
         template-glib jsonrpc-glib python-sphinx webkit2gtk glade python-lxml meson)
makedepends=(llvm gobject-introspection gtk-doc yelp-tools appstream-glib git mm-common
             python-sphinx_rtd_theme)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-code-assistance: Legacy assistance services')
groups=(gnome-extra)
_commit=bf485934b3762c6915419458623b850b4a63c2a8  # gnome-builder-3-34
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit"
        libgit2.diff)
sha256sums=('SKIP'
            '968d30b68c67e7b89d39e5db870b62adbf73cfd98a8e2f44b4cd515035f73f50')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build with meson 0.52
  git cherry-pick -n c8b862b491cfbbb4f79b24d7cd90e4fb1f37cb9f
  git cherry-pick -n da26dfbf78468f5ed724e022b300a07862a95833

  # Fix build with libgit2 0.99
  git apply -3 ../libgit2.diff
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D help=true
  ninja -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
