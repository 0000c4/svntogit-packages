# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.32.4
pkgrel=2
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils ctags
         libpeas vte3 vala python-jedi autoconf-archive sysprof flatpak-builder gspell libdazzle
         template-glib jsonrpc-glib python-sphinx webkit2gtk glade python-lxml meson)
makedepends=(llvm gobject-introspection gtk-doc yelp-tools appstream-glib git mm-common
             python-sphinx_rtd_theme)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-code-assistance: Legacy assistance services')
groups=(gnome-extra)
_commit=6854e08c105feb655ca3ad3253dd3571b838cabe  # tags/3.32.4^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D help=true
  ninja -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
