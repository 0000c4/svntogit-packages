# $Id$
# Maintainer: Andreas Radke <andyrtr at archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=raptor
pkgver=2.0.3
pkgrel=1
pkgdesc="A C library that parses RDF/XML/N-Triples into RDF triples"
arch=('i686' 'x86_64')
url="http://librdf.org/raptor"
depends=('libxml2>=2.7.8' 'curl>=7.21.2' 'zlib>=1.2.5' 'libxslt>=1.1.26')
license=('LGPL')
options=('!libtool')
source=(http://librdf.org/dist/source/raptor2-$pkgver.tar.gz)
md5sums=('46eff4b20f8752d1146a3e0c8b2168dd')

build() {
  cd ${srcdir}/raptor2-${pkgver}

  # fix build with curl >= 7.21.2
  sed -i '/#include <curl\/types.h>/d' src/raptor_internal.h

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${srcdir}/raptor2-${pkgver}
  make prefix=${pkgdir}/usr install
}
