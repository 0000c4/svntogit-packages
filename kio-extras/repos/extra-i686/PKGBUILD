# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=15.12.1
pkgrel=2
pkgdesc="Additional components to increase the functionality of KIO"
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=(LGPL)
depends=(kdelibs4support khtml kdnssd libssh smbclient exiv2 openexr openslp libmtp kpty)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kdenetwork)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	bug359015.patch::"https://quickgit.kde.org/?p=kio-extras.git&a=commitdiff&h=af92d618&o=plain")
md5sums=('5a45e9df178f3bba38f257be6e026f22'
         'e47b7f6af98cce2c36929731e3bb2ea5')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix deprecated perl syntax http://bugs.kde.org/show_bug.cgi?id=359015
  patch -p1 -i ../bug359015.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

