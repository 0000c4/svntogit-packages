# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdav
pkgver=17.03.80
pkgrel=1
pkgdesc="A DAV protocol implemention with KJobs"
url='https://pim.kde.org/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kio)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('18a63dafa06cad692a0debe5c6c253073e9fb3831293f2aab49fbce58f9e38f7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
