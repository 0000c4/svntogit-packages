# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Max Fehrm max.fehrm@gmail.com

pkgname=libmusicbrainz4
pkgver=4.0.0
pkgrel=1
pkgdesc="Library That Provides Access to the MusicBrainz Server  "
url="http://musicbrainz.org/"
arch=('i686' 'x86_64')
license=('LGPL2.1')
depends=('neon')
makedepends=('cmake')
source=(ftp://ftp.musicbrainz.org/pub/musicbrainz/libmusicbrainz-$pkgver.tar.gz
        0001-Make-sure-gcc-4.7-can-find-usleep-declaration.patch)
md5sums=('21c33f5dd20b8bb945b800e2a964f27e'
         'df1dfa423d32a7a1d08b3fc0114bf087')

build() {
  cd libmusicbrainz-$pkgver
  patch -Np1 -i ../0001-Make-sure-gcc-4.7-can-find-usleep-declaration.patch
  cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr
  make
}

package() {
  cd libmusicbrainz-$pkgver
  make DESTDIR="$pkgdir" install
}
