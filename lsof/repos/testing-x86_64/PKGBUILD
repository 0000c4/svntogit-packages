# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=lsof
pkgver=4.90
pkgrel=2
pkgdesc="Lists open files for running Unix processes"
arch=('x86_64')
url="http://people.freebsd.org/~abe/"
license=('custom')
depends=('glibc')
source=(ftp://ftp.fu-berlin.de/pub/unix/tools/lsof/lsof_$pkgver.tar.bz2
        fix_excessive_reporting.patch::https://github.com/masatake/lsof-linux/commit/f5d024877006deb5d0a500235068f8c4f928939e.patch
        license.txt)
sha1sums=('9980f2ed68242c292f7aeb077d450c1e911ded88'
          '6d5b5ab446e85253af287d1adbb9feec95fbcde5'
          'db6b6d90ce895e4053f91ad25c7761fbf9a37dd6')
#validpgpkeys=(9AFD62A840BD3D55) It is PGP-2 key

prepare() {
	cd lsof_$pkgver
	tar xf lsof_${pkgver}_src.tar
	cd lsof_${pkgver}_src
	sed -i 's|/\* #define\tHASSECURITY\t1 \*/|#define\tHASSECURITY\t1|' dialects/linux/machine.h

	patch -p1 < $srcdir/fix_excessive_reporting.patch
}

build() {
	cd lsof_$pkgver/lsof_${pkgver}_src
	./Configure -n linux
	make 
}

check() {
	cd lsof_$pkgver/lsof_${pkgver}_src/tests
	sed 's/END=0/exit 0/' -i CkTestDB
	make all
}

package() {
	cd lsof_$pkgver/lsof_${pkgver}_src

	install -Dm0755 lsof "$pkgdir"/usr/bin/lsof 
	install -Dm0644 lsof.8 "$pkgdir"/usr/share/man/man8/lsof.8 
   
	install -D -m0644 "$srcdir"/license.txt \
	     "$pkgdir"/usr/share/licenses/lsof/LICENSE 
}
