# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname=bftpd
pkgver=3.1
pkgrel=1
pkgdesc="Small, easy-to-configure FTP server"
arch=(i686 x86_64)
url="http://bftpd.sourceforge.net/"
license=('GPL')
depends=('glibc' 'pam')
install=bftpd.install
backup=(etc/bftpd.conf etc/logrotate.d/bftpd etc/pam.d/bftpd)
source=(http://downloads.sourceforge.net/sourceforge/bftpd/bftpd-$pkgver.tar.gz
        bftpd.rc bftpd.logrotate bftpd.pam)
md5sums=('1aeda6f3da2b10dcf5dca1b1d8466c48'
         '56a1bf7738a6db2b2cff89e611f4f11a'
         '853680dad0df39d0b4d2fb43d4be430a'
         '96f82c38f3f540b53f3e5144900acf17')

build() {
  cd ${srcdir}/$pkgname
  ./configure --prefix=/usr --enable-pam --mandir=/usr/share/man
  make || return 1
}

package() {
  cd ${srcdir}/$pkgname
  install -d -m755 ${pkgdir}/usr/{sbin,share/man/man8}
  install -d -m755 ${pkgdir}/var/run/bftpd
  install -d -m755 ${pkgdir}/var/log
  install -D -m644 ${srcdir}/bftpd.logrotate ${pkgdir}/etc/logrotate.d/bftpd || return 1
  install -D -m755 ${srcdir}/bftpd.rc ${pkgdir}/etc/rc.d/bftpd || return 1
  make DESTDIR=${pkgdir} install || return 1
  rm -f ${pkgdir}/var/log/bftpd.log
  rmdir ${pkgdir}/var/log
  mkdir -p ${pkgdir}/etc/pam.d
  cp ${srcdir}/bftpd.pam ${pkgdir}/etc/pam.d/bftpd || return 1
}
