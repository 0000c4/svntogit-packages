# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=knotes
pkgver=17.11.90
pkgrel=2
pkgdesc="Popup notes"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kdepim-runtime kdnssd kontactinterface)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin boost python)
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        knotes-qt5.10.patch::"https://cgit.kde.org/knotes.git/patch/?id=db1a8748")
sha256sums=('d91cd0cf147cdd0b888feb5203b92aa2c5c906dd2fa99aab5b96b42c8c5f5b1d'
            'SKIP'
            'e4df8adc419e0d96e3de87c94958add3392a57738577aab5702d50543ed7fbfd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../knotes-qt5.10.patch # Qt 5.10 rc2 API changes
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
