# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.28.0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
url="https://wiki.gnome.org/Apps/Clocks"
arch=(x86_64)
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop geoclue2 geocode-glib gsound)
makedepends=(vala gobject-introspection itstool yelp-tools git meson)
groups=(gnome)
_commit=57a9ea019209695532f92ad633fb55b51990b0b0  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/gnome-clocks#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
