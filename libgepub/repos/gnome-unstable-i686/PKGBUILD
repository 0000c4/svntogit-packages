# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgepub
pkgver=0.4+10+g0da0ba3
pkgrel=1
pkgdesc="Simple library to read epub files using glib"
url="https://git.gnome.org/browse/libgepub"
arch=(i686 x86_64)
license=(LGPL)
depends=(webkit2gtk libarchive)
makedepends=(git gnome-common gobject-introspection)
_commit=0da0ba35e21cc6a09149d7bf827576fbfd2556e6  # master
source=("git+https://git.gnome.org/browse/libgepub#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
