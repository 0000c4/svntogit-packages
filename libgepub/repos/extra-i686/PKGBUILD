# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgepub
pkgver=0.5.1
pkgrel=2
pkgdesc="Simple library to read epub files using glib"
url="https://git.gnome.org/browse/libgepub"
arch=(i686 x86_64)
license=(LGPL)
depends=(webkit2gtk libarchive)
makedepends=(git gtk-doc gobject-introspection meson)
_commit=140312fc2fc01c5f116c85b7e87b8b5cb088dd2e  # tags/0.5.1^0
source=("git+https://git.gnome.org/browse/libgepub#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname

  # Fixup gir version
  sed -i "/nsversion:/s/gepub_version/'0.5'/" libgepub/meson.build
}

build() {
  cd build
  meson --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

check() {
  cd build
  mesontest
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
