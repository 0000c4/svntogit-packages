# $Id$
# Maintainer : Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Morgan LEFIEUX <comete@archlinuxfr.org>

pkgname=hplip
pkgver=2.8.12
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('i686' 'x86_64')
url="http://hplip.sourceforge.net"
license=('GPL')
makedepends=('pyqt3' 'pkgconfig' 'sane' 'net-snmp>=5.4' 'portmap')
depends=('cups' 'python' 'ghostscript' 'foomatic-db' 'foomatic-db-hpijs' 'foomatic-db-engine' 'libcups')
replaces=('hpijs')
options=('!libtool')
install=hplip.install
source=(http://downloads.sourceforge.net/sourceforge/hplip/$pkgname-$pkgver.tar.gz)
optdepends=('sane: for scanner support'
            'net-snmp and portmap: for network support'
            'pyqt3: for running hp-toolbox')
build() {
 cd $srcdir/$pkgname-$pkgver
 ./configure --prefix=/usr --enable-foomatic-rip-hplip-install --enable-foomatic-ppd-install
 make || return 1
 make DESTDIR=$pkgdir/ install

 rm -rf $pkgdir/etc/sane.d
 # remove autostart of hp-daemon
 rm -r $pkgdir/etc/xdg
}

md5sums=('f6d04d4ed5c41e3e1c84e6a7af6b8db7')
