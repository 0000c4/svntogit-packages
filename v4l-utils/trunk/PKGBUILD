# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=v4l-utils
pkgver=0.9.3
pkgrel=1
pkgdesc="Userspace tools and conversion library for Video 4 Linux"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/libv4l"
provides=("libv4l=$pkgver")
replaces=('libv4l')
conflicts=('libv4l')
backup=(etc/rc_maps.cfg)
license=('LGPL')
makedepends=('qt')
optdepends=('qt')
depends=('glibc' 'gcc-libs' 'sysfsutils' 'libjpeg-turbo')
source=(http://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.bz2
        http://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.bz2.asc)
sha256sums=('502db0d865a392d87bff22c4f3bcfb3d0755937b1c18496edd941fd2d1784a3d'
            '120388a92c3b720bd82e7617a908f3f7e1071d3b9075e9b537b7c60fc3575b08')
options=(!libtool)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  MAKEFLAGS="-j1" make install DESTDIR="${pkgdir}/"
  rm "${pkgdir}/usr/bin/ivtv-ctl"
}
