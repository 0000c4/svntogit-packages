# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmplot
pkgver=15.12.3
pkgrel=1
pkgdesc="Mathematical Function Plotter"
url="http://kde.org/applications/education/kmplot/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdelibs4support' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
replaces=('kdeedu-kmplot')
conflicts=('kdeedu-kmplot')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kmplot-${pkgver}.tar.xz" kmplot-isnan.patch)
sha1sums=('31144ee66225ddf0e4edfb5e123da8237412aed6'
          'f344ff55983ce1246b103437f616f4ce8df3d4ff')

prepare() {
  mkdir -p build

# Fix build with glibc 2.23
  cd $pkgname-$pkgver
  patch -p1 -i ../kmplot-isnan.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
