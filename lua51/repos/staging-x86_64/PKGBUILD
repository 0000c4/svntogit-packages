# $Id $
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lua51
pkgver=5.1.5
pkgrel=1
pkgdesc='A powerful light-weight programming language designed for extending applications'
arch=('i686' 'x86_64')
url='http://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=("http://www.lua.org/ftp/lua-$pkgver.tar.gz"
        'lua-arch.patch'
        'lua-5.1-cflags.diff')
md5sums=('2e115fe26e435e33b0d5c022e4490567'
         'fa25feb70ef9fec975b1c20da5098b3c'
         '249582bf1fd861ccf492d2c35a9fe732')

build() {
    cd lua-$pkgver
    patch -p1 -i "$srcdir/lua-arch.patch"
    patch -p1 -i "$srcdir/lua-5.1-cflags.diff"
    [[ $CARCH == x86_64 ]] && export CFLAGS="$CFLAGS -fPIC"
    sed -i -e 's:llua:llua5.1:' -e 's:/include:/include/lua5.1:' etc/lua.pc
    make MYCFLAGS="$CFLAGS" MYLDFLAGS="$LDFLAGS" linux
}

package() {
    cd lua-$pkgver
    make \
      TO_LIB="liblua.a liblua.so.5.1 liblua.so.$pkgver" \
      INSTALL_DATA='cp -d' \
      INSTALL_TOP="$pkgdir/usr" \
      INSTALL_INC="$pkgdir/usr/include/lua5.1" \
      INSTALL_MAN="$pkgdir/usr/share/man/man1" \
      install
    install -D -m644 etc/lua.pc "$pkgdir/usr/lib/pkgconfig/lua5.1.pc"
    # Install the documentation
    install -d "$pkgdir/usr/share/doc/$pkgname"
    install -m644 doc/*.{gif,png,css,html} "$pkgdir/usr/share/doc/$pkgname"
    # Install copyrigth file
    install -D -m644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
    # rename
    cd "$pkgdir/usr/bin"
    mv lua lua5.1
    mv luac luac5.1
    cd "$pkgdir/usr/lib"
    mv liblua.a liblua5.1.a
    cd "$pkgdir/usr/share/man/man1"
    mv lua.1 lua5.1.1
    mv luac.1 luac5.1.1
}

# vim:set ts=4 sw=4 et:
