# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=tomcat-native
pkgver=1.1.24
pkgrel=1
pkgdesc="The Apache Tomcat Native Library"
arch=('i686' 'x86_64')
url="http://tomcat.apache.org/"
license=("Apache")
depends=('apr' 'openssl')
makedepends=('java-environment')
source=(http://www.apache.org/dist/tomcat/tomcat-connectors/native/${pkgver}/source/${pkgname}-${pkgver}-src.tar.gz)
md5sums=('cfd4762f2da19ba79d1b6a2a948c9d21')

build() {
  cd "${srcdir}/tomcat-native-${pkgver}-src/jni/native"

  ./configure \
    --prefix=/usr \
    --with-java-home=${JAVA_HOME} \
    --with-apr=/usr \
    --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src/jni/native"

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include}
}
