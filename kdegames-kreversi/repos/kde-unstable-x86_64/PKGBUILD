# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kreversi
pkgver=15.12.0
pkgrel=1
pkgdesc="A simple one player strategy game played against the computer"
url="http://kde.org/applications/games/kreversi/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kreversi-${pkgver}.tar.xz")
sha1sums=('2f9eb507051582b961a0ba8f4ffef2982da461a8')

build() {
  mkdir -p build
  cd build
  cmake ../kreversi-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
