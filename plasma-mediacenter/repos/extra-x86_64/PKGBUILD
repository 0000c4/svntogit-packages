# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mediacenter
pkgver=5.7.1
pkgrel=1
pkgdesc="A mediacenter user interface based on KDE Plasma components"
arch=(i686 x86_64)
url="http://community.kde.org/Plasma/Plasma_Media_Center/"
license=(GPL3)
depends=(plasma-workspace baloo hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6dc76a32172ec7f334a9e59aa644e727472a2eba48acd289b2da267ddf3eac8c'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}    

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
