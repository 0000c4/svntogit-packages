# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.net>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=jack
_longname=jack-audio-connection-kit
pkgver=0.121.3
pkgrel=1
pkgdesc="A low-latency audio server"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('libsamplerate' 'readline')
makedepends=('doxygen' 'libffado' 'celt')
optdepends=('libffado: FireWire support'
            'celt: NetJACK driver')
url="http://jackaudio.org/"
options=('!libtool')
provides=("$_longname=$pkgver")
conflicts=("$_longname")
replaces=("$_longname")
changelog=$pkgname.changelog
source=("http://jackaudio.org/downloads/$_longname-$pkgver.tar.gz")
md5sums=('35f470f7422c37b33eb965033f7a42e8')

build() {
  cd "$srcdir/$_longname-$pkgver"

  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "$srcdir/$_longname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
