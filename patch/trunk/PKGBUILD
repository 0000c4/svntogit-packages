# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=patch
pkgver=2.7.6
pkgrel=5
pkgdesc='A utility to apply patch files to original sources'
arch=('x86_64')
url='https://www.gnu.org/software/patch/'
license=('GPL')
groups=('base-devel')
depends=('glibc' 'attr')
makedepends=('ed')
optdepends=('ed: for patch -e functionality')
validpgpkeys=('259B3792B3D6D319212CC4DCD5BF9FEB0313653A') # Andreas Gruenbacher
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        "https://github.com/mirror/patch/commit/f290f48a621867084884bfff87f8093c15195e6a.patch" # CVE-2018-6951
        # http://git.savannah.gnu.org/cgit/patch.git/patch/?id=123eaff0d5d1aebe128295959435b9ca5909c26d CVE-2018-1000156
        "CVE-2018-1000156.patch"
        # https://github.com/mirror/patch/commit/9c986353e420ead6e706262bf204d6e03322c300.patch CVE-2018-6952
        "CVE-2018-6952.patch")
md5sums=('78ad9937e4caadcba1526ef1853730d5'
         'SKIP'
         '7e34fc859ccc07b235a8b01b043ff456'
         '45f911c911468c40328a07f74b9266ac'
         'aa8ac1e3dccbd523143b01e9f60b06e8')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/f290f48a621867084884bfff87f8093c15195e6a.patch
  patch -Np1 -i ${srcdir}/CVE-2018-1000156.patch
  patch -Np1 -i ${srcdir}/CVE-2018-6952.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
