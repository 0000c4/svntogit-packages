# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gjs
pkgver=0.7.14
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gjs"
license=('GPL')
depends=('gobject-introspection' 'xulrunner')
options=('!libtool')
source=(http://download.gnome.org/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('3af6f449a505c419d4a0e5938737da8d1b9b09b33710cc1ac1edee2eaa86e348')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|python|python2|' scripts/make-tests
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
