# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jason A. Donenfeld <Jason@zx2c4.com>

pkgname=wireguard-dkms
pkgver=0.0.20200128
pkgrel=1
pkgdesc='next generation secure network tunnel - module sources'
arch=('x86_64')
url='http://www.wireguard.com/'
license=('GPL')
depends=('dkms')
provides=('WIREGUARD-MODULE')
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE') # Jason A. Donenfeld <Jason@zx2c4.com>
source=("https://git.zx2c4.com/wireguard-linux-compat/snapshot/wireguard-linux-compat-${pkgver}.tar"{.xz,.asc})
sha256sums=('8610c6d8712cfd885f50b1a8c572518edf318c094d68491ea218bb50566a9a8a'
            'SKIP')

package_wireguard-dkms() {
	cd wireguard-linux-compat-${pkgver}/

	make -C src/ \
		DESTDIR="${pkgdir}/" \
		DKMSDIR="/usr/src/wireguard-${pkgver}/" \
		dkms-install
}
