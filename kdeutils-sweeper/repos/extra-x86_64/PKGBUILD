# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-sweeper
pkgver=17.04.0
pkgrel=1
pkgdesc='System Cleaner'
url='https://kde.org/applications/utilities/sweeper'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
source=("https://download.kde.org/stable/applications/${pkgver}/src/sweeper-${pkgver}.tar.xz"{,.sig})
sha256sums=('05b0012c9cc7dacc8cf29533c27cb4c69054f7f534de995afd1b32daa6b84b22'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../sweeper-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
