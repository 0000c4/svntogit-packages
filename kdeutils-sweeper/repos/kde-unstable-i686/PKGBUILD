# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-sweeper
pkgver=16.07.90
pkgrel=1
pkgdesc='System Cleaner'
url='http://kde.org/applications/utilities/sweeper'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/sweeper-${pkgver}.tar.xz")
sha1sums=('06ae018846520ed6f6c08c240f2bff162159cb99')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../sweeper-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
