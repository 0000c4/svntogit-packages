# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=16.04.1
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'libotr' 'qimageblitz' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost' 'mediastreamer' 'libgadu')
optdepends=('mediastreamer: jingle support' 'libgadu: Gadu-Gadu protocol')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz" kopete-gcc6.patch)
sha1sums=('bbf5c00dbbdc14be87b88757d472f0651040fe7e'
          '98bf4a261f5a8097bf9ea6ab4b5ded3d895e2695')

prepare() {
  mkdir -p build

# Fix build with GCC 6 (Fedora)
  cd kopete-$pkgver
  patch -p1 -i ../kopete-gcc6.patch
}

build() {
  cd build
  cmake ../kopete-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
