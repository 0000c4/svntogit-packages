# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=17.07.80
pkgrel=1
pkgdesc='Instant Messenger'
url='https://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'libotr' 'qimageblitz' 'kdepimlibs4' 'qca-qt4')
makedepends=('cmake' 'automoc4' 'boost' 'mediastreamer' 'libgadu' 'jsoncpp')
optdepends=('mediastreamer: jingle support' 'libgadu: Gadu-Gadu protocol')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz"{,.sig} 
        kopete-mediastreamer2.14.patch kopete-pthread-link.patch kopete-openssl-1.1.patch)
sha256sums=('332605fa0422c36aea509123f3173260706f35815a111796a12b810ffd368aec'
            'SKIP'
            'c34a268aaa058e563174581dbe37c15fed91e82326ec704d2a872463139865e0'
            'a7ebdd8dd4336b7ec085ef254bf18040f473b73379a8cd84569b28441a6c71df'
            'c3edbf1fe6d6a35a0260878937e9dd378358dbca0222015a0659670eab1de628')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

# Fix build with mediastreamer 2.14
  cd kopete-$pkgver
  patch -p1 -i ../kopete-mediastreamer2.14.patch
# Fix linking to pthread in libjingle-call
  patch -p1 -i ../kopete-pthread-link.patch
# Fix build with OpenSSL 1.1 (based on Fedora patch for libjingle)
  cd protocols/jabber/libjingle
  patch -p1 -i "$srcdir"/kopete-openssl-1.1.patch
}

build() {
  cd build
  cmake ../kopete-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
