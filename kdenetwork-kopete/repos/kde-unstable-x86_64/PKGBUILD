# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=16.03.90
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'libotr' 'qimageblitz' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost' 'mediastreamer' 'libgadu')
optdepends=('mediastreamer: jingle support' 'libgadu: Gadu-Gadu protocol')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz")
sha1sums=('94703259df4b4554325727bf1cc503efc04c72a1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kopete-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
