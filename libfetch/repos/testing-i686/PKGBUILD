# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Xavier Chantry <shiningxc@gmail.com>

pkgname=libfetch
pkgver=2.20
pkgrel=1
pkgdesc="URL based download library"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
depends=('openssl')
url="http://cvsweb.netbsd.org/bsdweb.cgi/pkgsrc/net/fetch/"
source=(Makefile http://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('7b1b639fb829bd837cda935171ac264d'
         '085d4f5c9b83a3b5416e2c54e3c8f9cb')
         
# source PKGBUILD && mksource
mksource() {
	export CVSROOT=:pserver:anoncvs@anoncvs.NetBSD.org:/cvsroot
    D=pkgsrc/net/libfetch
	[ -d "$D" ] && cvs up "$D" || cvs co "$D"
	pushd "$D"
	dirname=$(cvs log 2>/dev/null | /bin/grep "^libfetch-.*:$" | head -n1 | sed 's/://')
	cp -r files $dirname
	tar -cv --exclude=CVS -f ../../../$dirname.tar.gz $dirname
	rm -r $dirname
    popd
}

build() {
  cd $srcdir/${pkgname}-${pkgver}
  cp ../Makefile .
  make || return 1
  make DESTDIR=$pkgdir install
}
