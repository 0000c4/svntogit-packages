# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.4.14
pkgrel=2
pkgdesc="A distributed memory object caching system"
arch=(i686 x86_64)
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
backup=('etc/conf.d/memcached')
install=memcached.install
source=(http://memcached.googlecode.com/files/$pkgname-$pkgver.tar.gz
        memcached.conf
		memcached.sh
		memcached.service)
sha256sums=('f2e2ad8ee8d256f2c3748d7f741dcddfd0ab6bdece6fa1c18484e83f311156ef'
            '17f66873b77e96c6706dcf13ab0aa8ddff94b2851366c787fc23e614dfc11f47'
            '72efa639c5a39c7c14f07cc51731ebbf82299870d9cf31cf3aaa981bc084b4eb'
            '453cad82bddb25fac43fd5cedc5702b9b6c6abaf7498e21445facf20f25f53a6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  install -D -m 644 ../memcached.conf "$pkgdir"/etc/conf.d/memcached
  install -D -m 755 ../memcached.sh "$pkgdir"/etc/rc.d/memcached
  install -D -m 644 ../memcached.service "$pkgdir"/usr/lib/systemd/system/memcached.service
}
