# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kalgebra
pkgver=18.03.90
pkgrel=1
pkgdesc="Graph Calculator"
url="https://kde.org/applications/education/kalgebra/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(analitza kio qt5-webengine)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('kirigami2: for KAlgebra Mobile')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('413a5892fe4216cea7ddef0a290871c0e4fce6c02a1b881eb4bff1c1f4fd8a42'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
