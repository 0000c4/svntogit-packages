# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.2.8
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
arch=('i686' 'x86_64')
url='https://github.com/hughsie/libgusb'
license=('LGPL2.1')
depends=('glib2' 'glibc' 'libusb')
makedepends=('gobject-introspection' 'python2')
source=("http://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a9c269ef21145b228e59fdcb533ef9c4ae214a6f4748d17ecd2e022f79389eed')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
