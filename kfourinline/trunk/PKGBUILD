# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfourinline
pkgver=16.12.3
pkgrel=1
pkgdesc="A four-in-a-row game"
url="https://kde.org/applications/games/kfourinline/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'kdesignerplugin')
conflicts=('kdegames-kfourinline')
replaces=('kdegames-kfourinline')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kfourinline-${pkgver}.tar.xz"{,.sig})
sha256sums=('4f931a875a0282e110b5a2bad49f8eab04ed29df5b6611d10bb1f4f73400ec20'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
