# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfourinline
pkgver=16.07.80
pkgrel=1
pkgdesc="A four-in-a-row game"
url="http://kde.org/applications/games/kfourinline/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'kdesignerplugin')
conflicts=('kdegames-kfourinline')
replaces=('kdegames-kfourinline')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kfourinline-${pkgver}.tar.xz")
sha1sums=('3bbb038604a2e6d6d6482645e8e10d25339b52ad')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
