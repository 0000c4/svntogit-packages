# $Id$ 
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=pangox-compat
pkgver=0.0.2+2+gedb9e09
pkgrel=1
pkgdesc="X Window System font support for Pango"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('pango')
makedepends=(git)
_commit=edb9e0904d04d1da02bba7b78601a2aba05aaa47  # master
source=("git+https://git.gnome.org/browse/pangox-compat#commit=$_commit")
url="http://www.pango.org/"
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make -j1 DESTDIR="${pkgdir}" install
}
