# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ev
pkgver=4.32
pkgrel=1
pkgdesc='perl interface to libev, a high performance full-featured event loop'
arch=('x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-common-sense')
makedepends=('perl-canary-stability')
url='https://metacpan.org/release/EV'
source=("https://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/EV-$pkgver.tar.gz")
sha512sums=('c6df1b79633e88c2cd006986f2f6a319920b185e50a9e1d35ef7a8abc8f763efb031a64adaf43cec12e3b468f0e2d1d5a7b7174b8b7c87f6e61aff7502886d06')

build() {
  cd EV-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd EV-$pkgver
  make test
}

package() {
  cd EV-$pkgver
  make install DESTDIR="$pkgdir"
}
