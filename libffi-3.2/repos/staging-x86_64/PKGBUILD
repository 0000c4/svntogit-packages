# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi-3.2
pkgver=3.2.1
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
options=('!emptydirs')
source=(ftp://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz)
sha256sums=('d06ebb8e1d9a22d19e38d63fdb83954253f39bedc5d46232a05645685722ca37')

build() {
  cd libffi-$pkgver
  ./configure --prefix=/usr --disable-static \
    --enable-pax_emutramp
  make
}

package() {
  cd libffi-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -type f ! -name 'libffi.so.6*' -delete
  rm "$pkgdir/usr/lib/libffi.so"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
