# $Id: PKGBUILD 41765 2011-03-08 20:18:32Z spupykin $
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-mail-dkim
pkgver=0.52
pkgrel=1
pkgdesc='Signs/verifies Internet mail with DKIM/DomainKey signatures'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-crypt-openssl-rsa>=0.24' perl-mailtools perl-net-dns
         perl-net-dns-resolver-mock perl-yaml-libyaml)
checkdepends=(perl-test-requiresinternet)
url=https://metacpan.org/release/Mail-DKIM
source=("http://search.cpan.org/CPAN/authors/id/M/MB/MBRADSHAW/Mail-DKIM-$pkgver.tar.gz")
md5sums=(f366863f09905bbe5faa1f986133759c)
sha512sums=(2283e7f2dadfa8fd371d20b56841a434f26bfa37066fd11b1c83d2b96b967be2d90f258913c1653f605677f7b2623adab1352f4e8c740c3bb8b2ad041bc0ceca)
_ddir="Mail-DKIM-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB

  # Test fails due to dns errors in chroot. Weird, but works on the host itself...
  rm t/policy.t

  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
