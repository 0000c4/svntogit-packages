# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=qscintilla
pkgname=('qscintilla-qt5' 'python-qscintilla-qt5')
pkgver=2.11.3
pkgrel=4
license=('GPL')
arch=('x86_64')
url="https://www.riverbankcomputing.com/software/qscintilla/intro"
makedepends=('python-pyqt5' 'qt5-tools' 'sip')
options=('!buildflags') # Fix Qt Designer plugin
source=("https://www.riverbankcomputing.com/static/Downloads/QScintilla/$pkgver/QScintilla-$pkgver.tar.gz")
sha256sums=('cfadbb7f32fb8a9404fbe5db0ad14ac02229ebdf30a519141236080236ae2dd6')

build() {
    cd "$srcdir"/QScintilla-${pkgver}
    export QMAKEFEATURES=$PWD/Qt4Qt5/features/

    cd Qt4Qt5
    qmake-qt5
    make

    cd ../designer-Qt4Qt5
    qmake-qt5 INCLUDEPATH+=../Qt4Qt5 QMAKE_LIBDIR+=../Qt4Qt5
    make

    cd ../Python
    python configure.py --pyqt=PyQt5 -n ../Qt4Qt5/ -o ../Qt4Qt5/ -c --qmake /usr/bin/qmake-qt5
    make
}

package_qscintilla-qt5() {
    pkgdesc="A port to Qt5 of Neil Hodgson's Scintilla C++ editor class"
    depends=(qt5-base)

    cd QScintilla-${pkgver}/Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    cd ../designer-Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

package_python-qscintilla-qt5() {
    pkgdesc="Python bindings for QScintilla2"
    depends=(qscintilla-qt5 python-pyqt5)
    replaces=(python-qscintilla-qt5-common)

    cd QScintilla-${pkgver}/Python
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}
