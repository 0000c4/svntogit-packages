# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmahjongg
pkgver=18.03.90
pkgrel=1
pkgdesc="A tile matching game for one or two players"
url="https://kde.org/applications/games/kmahjongg/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkmahjongg libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdegames-kmahjongg)
replaces=(kdegames-kmahjongg)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a45b67ff4b4fe36d03098faa9c961c32d46bc98e5a7226c2033879e78218ef47'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
