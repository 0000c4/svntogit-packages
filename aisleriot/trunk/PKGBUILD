# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=aisleriot
pkgver=3.2.3.2
pkgrel=1
pkgdesc="GNOME Solitaire card games"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
groups=('gnome-extra')
depends=(libsm gconf guile libcanberra librsvg hicolor-icon-theme dconf)
makedepends=(intltool itstool docbook-xsl)
options=('!emptydirs' '!libtool')
install=aisleriot.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz)
sha256sums=('06eeeef49e7a0f8a98142085a64bc7830d0dd02e480fbfa1309545fccc977a36')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain aisleriot ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
