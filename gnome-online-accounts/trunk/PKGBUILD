# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.22.1
pkgrel=1
pkgdesc="Single sign-on framework for GNOME"
url="https://wiki.gnome.org/Projects/GnomeOnlineAccounts"
arch=(i686 x86_64)
license=(LGPL)
depends=(webkit2gtk json-glib libnotify rest libsecret telepathy-glib krb5 gcr)
makedepends=(intltool gobject-introspection gtk-doc vala git gnome-common)
optdepends=('gvfs-goa: Virtual file systems (e.g. OwnCloud)'
            'gvfs-google: Google Drive')
_commit=aecc7a8ccc3d42b8788c597b0cd2ac25a54f254c  # tags/3.22.1^0
source=("git://git.gnome.org/gnome-online-accounts#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --enable-media-server \
    --enable-kerberos \
    --enable-lastfm \
    --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
