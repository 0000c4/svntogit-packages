# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.3.0
pkgrel=1
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
depends=('libwebkit3' 'json-glib' 'libnotify' 'rest' 'hicolor-icon-theme')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl')
options=(!libtool)
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('c5c0bc70b46f5ed5d6aa734834b2e0ef73fb8eddd9fd20606f0582043efb5d22')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --libexec=/usr/lib/gnome-online-accounts
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
