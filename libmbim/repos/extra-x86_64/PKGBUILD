# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libmbim
pkgver=1.2.0
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(i686 x86_64)
url="http://cgit.freedesktop.org/libmbim/libmbim"
license=(GPL2)
depends=(glib2 bash systemd)
makedepends=(gtk-doc python)
options=('!libtool')
source=(http://cgit.freedesktop.org/libmbim/libmbim/snapshot/$pkgname-$pkgver.tar.xz)
sha256sums=('0b00085486bb34b772e56643d6605eaffc3b2dd620485bc598cab8b0c6051825')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --with-tests
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
