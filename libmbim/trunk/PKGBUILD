# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libmbim
pkgver=1.0.0
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(i686 x86_64)
url="http://cgit.freedesktop.org/libmbim/libmbim"
license=(GPL2)
depends=(glib2 bash systemd)
makedepends=(gtk-doc python)
options=('!libtool')
source=($url/snapshot/$pkgname-$pkgver.tar.xz format.patch)
sha256sums=('c3d668a5dc51700749a62fb5d50f149900e8dd4265e4d132c1d9cc4cb77eef0c'
            'fbf54d12660c132f756c59cebd448a38750e3bbc9de8a7a97ceda466440dcb41')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../format.patch
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --with-tests
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
