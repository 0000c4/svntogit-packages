# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=lftp
pkgver=4.4.7
pkgrel=2
pkgdesc="Sophisticated command line based FTP client"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gcc-libs' 'readline>=6.2' "gnutls" "expat>=2.0.1-4" 'sh')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
url="http://lftp.yar.ru/"
backup=('etc/lftp.conf')
source=(http://ftp.yars.free.net/pub/source/lftp/${pkgname}-${pkgver}.tar.bz2 
        f8ee088.diff)
md5sums=('a4e7b88d6b7aca389010ce6d6dca8099'
         'a8f1c1bb3f5ab60b48507c9722c64d1c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix sftp transfer - FS#35456
  # upstream: https://github.com/lavv17/lftp/issues/39
  patch -Np0 -i ${srcdir}/f8ee088.diff

  ./configure --prefix=/usr \
    --with-gnutls \
    --without-openssl \
    --without-included-regex \
    --disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/lib
}
