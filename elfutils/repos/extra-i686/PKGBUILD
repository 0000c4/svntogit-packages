# $Id$
# Maintainer: 
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=elfutils
pkgver=0.154
pkgrel=1
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
provides=('libelf')
replaces=('libelf')
conflicts=('libelf')
options=('!makeflags')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig} \
        elfutils-0.154-binutils-pr-ld-13621.patch)
sha1sums=('3a92a6f636cc3d14d4b3b00ba5c1e88a2a8d510b'
          '956461d44b03823e0b448e78b1f4aa25ef269e1e'
          '09681b728edb86c77eed47286554bef048f3507f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=" -g"  # required for test-suite success
  patch -p1 -i "${srcdir}/elfutils-0.154-binutils-pr-ld-13621.patch"
  ./configure --prefix=/usr --program-prefix="eu-"
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
