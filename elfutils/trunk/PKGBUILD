# $Id$
# Maintainer: 
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=elfutils
pkgver=0.153
pkgrel=1
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/elfutils/"
license=('GPL2')
depends=('glibc')
provides=('libelf')
replaces=('libelf')
conflicts=('libelf')
options=('!makeflags')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
md5sums=('289a146182bc29f0236eaa15d8ebdf98'
         '1af9fe25e9316173b81fa4fad3a17ed5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-"
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s#./alldts#testrun ./alldts#" tests/run-alldts.sh
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
