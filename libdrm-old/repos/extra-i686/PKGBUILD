#Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libdrm-old
pkgname=(libdrm-old libdrm-nouveau1)
pkgver=2.4.33
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind')
options=('!libtool' '!emptydirs')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/libdrm/libdrm-$pkgver.tar.bz2
        no-pthread-stubs.patch
        COPYING
)
sha1sums=('4da2c635491724e44326871e6a49ccfec0b6b5a6'
          '825ff5e0c4238b31bdea52f104bfec8949270e25'
          'ba3dcd636997ee0d30df14b03dae05c24ae5d094')

build() {
  cd "libdrm-$pkgver"
  patch -Np1 -i "$srcdir/no-pthread-stubs.patch"

  #libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --disable-libkms \
      --disable-intel \
      --disable-radeon \
      --enable-nouveau-experimental-api
  make
}

package_libdrm-old() {
  pkgdesc="Userspace interface to kernel DRM services - used as makedepends for nouveau-dri"
  conflicts=('libdrm')
  provides=("libdrm=$pkgver")
  cd "libdrm-$pkgver"
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libdrm_nouveau.so.1*
}

package_libdrm-nouveau1() {
  pkgdesc="Userspace interface to kernel DRM services for nouveau - used as depends for nouveau-dri"
  depends=(libdrm)
  cd "libdrm-$pkgver"
  make DESTDIR="$pkgdir" install-libdrm_laLTLIBRARIES
  make -C nouveau DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" uninstall-libdrm_laLTLIBRARIES
  rm -rf "$pkgdir"/usr/include/  "$pkgdir"/usr/lib/pkgconfig/libdrm_nouveau.pc  "$pkgdir"/usr/lib/libdrm_nouveau.so
}
