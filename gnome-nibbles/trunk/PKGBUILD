# $Id: PKGBUILD 168943 2012-10-16 16:31:44Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-nibbles
pkgver=3.8.0
pkgrel=1
pkgdesc="Guide a worm around a maze"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'clutter-gtk' 'libcanberra' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs' '!libtool')
install=gnome-nibbles.install
url="https://live.gnome.org/Nibbles"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('bc1e4f687f1e794ef15a4162492154970300ac682378e59f9471327e5fd0d606')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile \
      --with-scores-user=root --with-scores-group=games
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Remove all scores, we generate them from postinstall
  rm -rf "$pkgdir/var"
}
