# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_qtver=5.14.0-beta1
pkgver=${_qtver/-/}
pkgrel=2
arch=('x86_64')
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx')
makedepends=('python2' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/development_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        qt5-webengine-linux-5.2.patch::"https://code.qt.io/cgit/qt/qtwebengine-chromium.git/patch?id=74e69da9"
        qt5-webengine-system-icu.patch)
sha256sums=('4255e0a4f1510b6cae552f053b774659d93116cf17080ac003884c82dc00961c'
            '8deb3df43a379976bbdb1fa814e68b2ce0433043695efc694c11e47f67c51b6a'
            '057fcd43300931e438c6714d2d42516d45420b6e8ae513c901a599d49215796a')

prepare() {
  mkdir -p build

  cd $_pkgfqn/src/3rdparty/chromium
  patch -p2 -i "$srcdir"/qt5-webengine-linux-5.2.patch # Fix build with recent kernel
  patch -p2 -i "$srcdir"/qt5-webengine-system-icu.patch # Fix build with system ICU
}

build() {
  cd build

  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
