# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=0.1.33
pkgrel=1
pkgdesc="System daemon for managing color devices"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/colord"
license=(GPL2)
depends=(lcms2 libgusb polkit shared-color-profiles sqlite udev dconf dbus)
makedepends=(intltool gobject-introspection vala docbook2x sane)
optdepends=('sane: UseSANE support [/etc/colord.conf]')
options=('!libtool')
install=colord.install
backup=(etc/colord.conf)
source=($url/releases/$pkgname-$pkgver.tar.xz)
sha1sums=('41888fc853e033302c59539777b09317a7e1ab3d')

build() {
  cd $pkgname-$pkgver
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-vala --enable-sane --disable-volume-search
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions
}

# vim:set ts=2 sw=2 et:
