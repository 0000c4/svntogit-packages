# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=0.1.15
pkgrel=3
pkgdesc="Color daemon"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lcms2' 'libgusb' 'polkit' 'sane' 'shared-color-profiles' 'sqlite3' 'udev')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook2x')
source=($url/releases/$pkgname-$pkgver.tar.xz
        scan_only_color_profile_dir.patch)
options=('!libtool')
sha1sums=('e83a68add3fac9c677829925794ee353743dc9c8'
          'b4b8ea1008ecfdc1084ec69d647127587a2a87f7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/scan_only_color_profile_dir.patch"
  ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
