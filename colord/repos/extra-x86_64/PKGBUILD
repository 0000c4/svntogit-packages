# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=colord
pkgver=0.1.12
pkgrel=2
pkgdesc="Color daemon"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/software/colord/"
license=('GPL2')
depends=('lcms2' 'sqlite3' 'polkit' 'udev' 'sane')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook2x')
source=($url/releases/$pkgname-$pkgver.tar.xz)
options=('!libtool')
sha1sums=(9bc0c4193c0f790702923e0a783962f0f26487e6)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static --enable-sane
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
