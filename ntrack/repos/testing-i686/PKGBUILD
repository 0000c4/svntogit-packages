# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=ntrack
pkgver=14
pkgrel=1
epoch=1
pkgdesc="A network connectivity tracking library"
arch=('i686' 'x86_64')
url="http://launchpad.net/ntrack/"
license=('LGPL')
depends=('qt' 'libnl' 'gcc-libs')
makedepends=('python2')
source=("http://launchpad.net/${pkgname}/main/0${pkgver}/+download/${pkgname}-0${pkgver}.tar.gz"
        'dead-loop-fix.patch' "ntrack_libnl_link.diff")
md5sums=('d8af7c0f77c030d089bfa5ce0dd1057b'
         '02bda6ae15ef04b6a7d9273c37752681'
         'f5a0fbdb1f1a5e6355119dfc0d9c555b')
options=('!libtool' '!emptydirs')

build() {
  cd "${srcdir}/${pkgname}-0${pkgver}"

  patch -p0 -i "${srcdir}"/dead-loop-fix.patch

  sed -i 's/libnl-3.0/libnl-3.1/g' configure

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-0${pkgver}"
  make DESTDIR="${pkgdir}" install
}
