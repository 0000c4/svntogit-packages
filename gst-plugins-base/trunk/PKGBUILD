# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-plugins-base
pkgname=('gst-plugins-base-libs' 'gst-plugins-base')
pkgver=1.0.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango' 'gobject-introspection')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgbase/$pkgbase-${pkgver}.tar.xz)
sha256sums=('066ba8a7fa709a25ad291f7b21329b73f513637aafceee00396e588e723d7f9e')

build() {
  cd $pkgbase-$pkgver
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Base Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgbase-$pkgver
  # Testsuite fails on one test. Some refcount leak
  #make check
}

package_gst-plugins-base-libs() {
  pkgdesc="GStreamer Multimedia Framework Base Plugin libraries"
  depends=('gstreamer' 'orc' 'libxv')

  cd $pkgbase-$pkgver
  make DESTDIR="${pkgdir}" install
}

package_gst-plugins-base() {
  depends=("gst-plugins-base-libs=$pkgver" 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango')

  cd $pkgbase-$pkgver
  make -C gst-libs DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
  make -C gst-libs DESTDIR="${pkgdir}" uninstall
}
