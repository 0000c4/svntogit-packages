# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=xvidcore
pkgver=1.3.0
pkgrel=1
pkgdesc="XviD is an open source MPEG-4 video codec"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xvid.org/"
depends=('glibc')
makedepends=('nasm')
source=(http://downloads.xvid.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('22621243c4a588dcee599e77736309c2')
sha1sums=('3220e3feee2c92ae73169009a23c9ef929867bc7')

build() {
  cd "${srcdir}/${pkgname}/build/generic"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}/build/generic"
  make DESTDIR="${pkgdir}" install

  #Fix dynamic libraries
  cd "${pkgdir}/usr/lib"
  _mylib=$(basename libxvidcore.so.*)
  ln -sf ${_mylib} libxvidcore.so.4
  ln -sf ${_mylib} libxvidcore.so
}
