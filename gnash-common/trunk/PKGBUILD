# $Id$

pkgname=gnash-common
pkgver=0.8.5
pkgrel=1
pkgdesc='A GNU Flash movie player'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/gnash/'
license=('GPL3')
depends=('curl' 'giflib' 'boost' 'libldap' 'ffmpeg' 'sdl' 'agg'
         'libjpeg' 'libpng' 'libtool')
makedepends=('pkgconfig')
provides=('gnash')
replaces=('gnash')
backup=('etc/gnashrc' 'etc/gnashpluginrc')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnu.org/gnu/gnash/${pkgver}/gnash-${pkgver}.tar.bz2")
md5sums=('366f703c2eb1747e4109602b75439ff4')

build() {
	cd $srcdir/gnash-$pkgver

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-plugins \
		--disable-menus \
		--enable-gui=sdl \
		--enable-renderer=agg \
		--enable-z \
		--enable-jpeg \
		--enable-libpng \
		--enable-gif \
		--enable-fontconfig \
		--enable-Xft \
		--enable-expat \
		--enable-ffmpeg \
		--enable-media=ffmpeg || return 1

	make || return 1
	make DESTDIR=$pkgdir install || return 1
}
