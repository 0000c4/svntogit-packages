# $Id$
# Maintainer:

pkgname=gnash-common
pkgver=0.8.6
pkgrel=3
pkgdesc="A GNU Flash movie player"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gnash/"
license=('GPL3')
depends=('curl' 'giflib' 'boost' 'libldap' 'gstreamer0.10-base' 'gstreamer0.10-ffmpeg' 
         'sdl' 'agg' 'libjpeg' 'libpng' 'libtool' 'speex' 'libxi' 'fontconfig')
makedepends=('pkgconfig')
provides=('gnash')
replaces=('gnash')
backup=('etc/gnashrc' 'etc/gnashpluginrc')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnu.org/gnu/gnash/${pkgver}/gnash-${pkgver}.tar.bz2
	'libpng-1.4.patch')
md5sums=('913bcbffd37c9603b84486227c65dce7'
         '24a030c4da4580bcb0f69545c0822912')

build() {
	cd "$srcdir/gnash-$pkgver"
	patch -Np1 -i ${srcdir}/libpng-1.4.patch

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-plugins \
		--disable-menus \
		--enable-gui=sdl \
		--enable-renderer=agg \
		--enable-z \
		--enable-jpeg \
		--enable-png \
		--enable-gif \
		--enable-speex \
		--enable-fontconfig \
		--enable-Xft \
		--enable-expat \
		--enable-gstreamer \
		--enable-media=gst || return 1

	make || return 1
	make DESTDIR="$pkgdir" install || return 1
}
