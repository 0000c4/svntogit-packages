# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Rustam Tsurik <rustam.tsurik@gmail.com>
# Contributor: Nico Suarez <nicodoggie@gmail.com>

pkgname=galera
pkgver=25.3.25
pkgrel=1
pkgdesc='write set replication (WSREP) provider for MariaDB cluster'
arch=('x86_64')
license=('GPL2' 'custom:OpenSSL Linking Exception')
depends=('mariadb' 'openssl')
makedepends=('git' 'check' 'scons' 'boost')
optdepends=('rsync: transfer database state to a newly joined node with rsync')
url='http://www.codership.com/'
options=('!libtool')
validpgpkeys=('44B7345738EBDE52594DAD80D669017EBC19DDBA') # Codership Oy <info@galeracluster.com>
source=("git+https://github.com/codership/galera.git#tag=release_${pkgver}?signed"
        'git+https://github.com/codership/wsrep-API.git')
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd "${pkgname}"

  git config --file=.gitmodules submodule.wsrep-API.url ../wsrep-API/
  git submodule init
  git submodule update
}

build() {
  cd "${pkgname}"

  scons system_asio=0
}

package() {
  cd "${pkgname}"

  install -D -m0755 libgalera_smm.so "${pkgdir}"/usr/lib/galera/libgalera_smm.so
  ln -s galera/libgalera_smm.so "${pkgdir}"/usr/lib/libgalera_smm.so

  install -D -m0755 garb/garbd "${pkgdir}"/usr/bin/garbd

  install -D -m0644 scripts/packages/README "${pkgdir}"/usr/share/doc/galera/README
  install -D -m0644 scripts/packages/README-MySQL "${pkgdir}"/usr/share/doc/galera/README-MySQL
  install -D -m0644 scripts/mysql/README "${pkgdir}"/usr/share/doc/galera/mysql/README
  install -D -m0644 COPYING "${pkgdir}"/usr/share/doc/galera/COPYING
}

