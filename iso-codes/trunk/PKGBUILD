# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=iso-codes
pkgver=3.79
pkgrel=1
pkgdesc="Lists of the country, language, and currency names"
arch=('any')
url="https://pkg-isocodes.alioth.debian.org/"
license=('LGPL')
makedepends=('python' git)
_commit=c1d5dd96b33cb815ebc21d2708bc045b1545249d  # tags/iso-codes-3.79^0
source=("git+https://salsa.debian.org/iso-codes-team/iso-codes/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^iso-codes-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" pkgconfigdir=/usr/lib/pkgconfig install
}

