# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>

pkgname=x2goclient
pkgver=4.0.0.2
pkgrel=1
pkgdesc="x2go qt4 client"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.x2go.org"
depends=('qt' 'libldap' 'libcups' 'nxproxy' 'libssh' 'libxpm' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('man2html')
install=x2goclient.install
source=(http://code.x2go.org/releases/source/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('7a8b6869d51e82989c24fb1191df09ad')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s:qmake-qt4:qmake:g" Makefile
  sed -i "s:-o root -g root ::" Makefile
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
