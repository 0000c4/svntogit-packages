# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=korganizer
pkgver=20.03.90
pkgrel=2
pkgdesc="Calendar and scheduling Program"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kontactinterface incidenceeditor kuserfeedback phonon-qt5)
makedepends=(extra-cmake-modules kdoctools qt5-tools boost)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         korganizer-revert-qt5.15-port.patch::"https://cgit.kde.org/korganizer.git/patch/?id=cc87c7fc")
sha256sums=('e1eefdd5b2e87d52be003df326b1e77d5d38345d5ea530b367ac4fa702a57d85'
            'SKIP'
            'aff481dfd3b35734a6b877955507fd6732e329b26f6cda60fbc9da6ff0711a8d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../korganizer-revert-qt5.15-port.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
