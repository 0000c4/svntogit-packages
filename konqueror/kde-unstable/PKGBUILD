# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=20.03.90
pkgrel=2
pkgdesc="KDE File Manager & Web Browser"
arch=(x86_64)
url="https://konqueror.org/"
license=(LGPL)
depends=(dolphin keditbookmarks qt5-webengine qt5-script kdelibs4support)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin kdesu)
optdepends=('kdesu: shell command plugin')
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         konqueror-kbookmarks-5.69.patch::"https://cgit.kde.org/konqueror.git/patch/?id=078f357b")
sha256sums=('5bfd0a9bedc64c6bab20d62a50a13c9dfb6071843e7480db957ace9d90edf56b'
            'SKIP'
            'ee1a034b10ce6f95530c667f5c9e3976ebbeffed86919bf8a86f10d6d80a0e76')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../konqueror-kbookmarks-5.69.patch # Fix crash with kbookmarks 5.69
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
