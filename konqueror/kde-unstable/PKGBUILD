# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=18.03.80
pkgrel=1
pkgdesc="KDE File Manager & Web Browser"
arch=(x86_64)
url="https://konqueror.org/"
license=(LGPL)
depends=(dolphin keditbookmarks qt5-webengine kdelibs4support khtml)
makedepends=(extra-cmake-modules kdoctools python tidy kdesignerplugin kdesu)
optdepends=('tidy: tidy HTML plugin' 'kdesu: shell command plugin')
conflicts=(kdebase-konqueror kdebase-konq-plugins)
replaces=(kdebase-konqueror kdebase-konq-plugins)
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e77165f07797e63fe9aa4a7ad53976a6c449033ea1b956773aae1d1e6beaee25'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
