# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=20.11.90
pkgrel=1
pkgdesc="KDE File Manager & Web Browser"
arch=(x86_64)
url="https://konqueror.org/"
license=(LGPL)
depends=(dolphin keditbookmarks qt5-webengine qt5-script)
makedepends=(extra-cmake-modules kdoctools kdesu)
optdepends=('kdesu: shell command plugin')
groups=(kde-applications kde-network)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a40087920e9e49e597aedc7f91883d4b7178aac33a2b60887648ebd8662274ff'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
