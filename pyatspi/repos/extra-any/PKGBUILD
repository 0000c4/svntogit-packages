# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi python2-atspi)
pkgver=2.8.0
pkgrel=1
arch=(any)
url="http://www.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=('GPL2')
makedepends=(python-gobject python2-gobject at-spi2-core)
source=(http://download.gnome.org/sources/${pkgbase}/${pkgver::3}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('f1695670f13c804898f959e3e7fc517db1c7f1079c051d10e22a35889524fd17')

build() {
  mkdir python2 python3

  cd $pkgbase-$pkgver
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf -fi

  cd ../python2
  ../$pkgbase-$pkgver/configure --prefix=/usr --with-python=/usr/bin/python2
  make

  cd ../python3
  ../$pkgbase-$pkgver/configure --prefix=/usr --with-python=/usr/bin/python
  make
}

package_python-atspi() {
  pkgdesc="Python 3 bindings for at-spi"
  depends=(python-gobject at-spi2-core)

  cd python3
  make DESTDIR="$pkgdir" install
}

package_python2-atspi() {
  pkgdesc="Python 2 bindings for at-spi"
  depends=(python2-gobject at-spi2-core)
  provides=("pyatspi=$pkgver")
  replaces=(pyatspi)
  conflicts=(pyatspi)

  cd python2
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
