# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgoogle
pkgver=0.3.2
pkgrel=1
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://projects.kde.org/projects/kdereview/libkgoogle'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdepimlibs' 'qjson')
makedepends=('cmake' 'automoc4' 'boost')
replaces=('akonadi-google')
conflicts=('akonadi-google')
source=("http://www.progdan.cz/wp-content/uploads/${pkgname}-${pkgver}.tar.gz")
md5sums=('faa3af3bd402827b229c697b95865d1a')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
