# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdeedu
pkgver=4.1.3
pkgrel=1
pkgdesc="KDE Educational programs"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdebase-workspace' 'openbabel' 'indilib' 'libnova' 'libqalculate' 'gsl' 'boost')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ocaml' 'facile' 'eigen' 'cfitsio' 'gmm++')
options=('docs')
install='kdeedu.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2" 'python-2.6.patch')
md5sums=('8b513d6dde79119842eef059550c0123' 'b02494e14cfb969faed5a279b0ee4aa2')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -p1 -i $srcdir/python-2.6.patch || return 1
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
	# provided by indilib
	rm -f $pkgdir/usr/bin/indiserver
}
