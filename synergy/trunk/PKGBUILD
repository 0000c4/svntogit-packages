# $Id$
# Maintainer: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=synergy
pkgver=1.3.4
pkgrel=1
pkgdesc="Share a single mouse and keyboard between multiple computers"
url="http://synergy2.sourceforge.net"
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libxtst' 'libxinerama')
license=('GPL2')
makedepends=('libxt')       # used by configure to test for libx11...
backup=('etc/synergy.conf')
source=(http://synergy-plus.googlecode.com/files/${pkgname}-plus-${pkgver}.tar.gz 'synergys.rc')
md5sums=('2c565afe5f920d363eef38dd97449b73' '056e59b1d04a66820ffa4b53e5ec20bc')

build() {
  cd ${srcdir}/${pkgname}-plus-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-plus-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  install -m755 -d ${pkgdir}/etc
  install -m644 examples/synergy.conf ${pkgdir}/etc/ || return 1
  install -Dm755 ${srcdir}/synergys.rc ${pkgdir}/etc/rc.d/synergy || return 1
}

# vim: set noexpandtab tabstop=8 shiftwidth=8 textwidth=132 autoindent
