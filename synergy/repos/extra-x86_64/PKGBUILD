# $Id$
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=synergy
pkgver=1.3.1
pkgrel=4
pkgdesc="Share a single mouse and keyboard between multiple pcs"
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libxtst' 'libxinerama')
license=('GPL2')
makedepends=('libxt')       # used by configure to test for libx11...
backup=('etc/synergy.conf')
url="http://synergy2.sourceforge.net/"
source=(http://downloads.sourceforge.net/${pkgname}2/$pkgname-$pkgver.tar.gz
	'gcc43.patch' 'synergy-stuttered-delay-fix.patch')
md5sums=('a6e09d6b71cb217f23069980060abf27'
         '255c3f641c5f077e33e284db771220b8'
         '1969a09008893971bbc4f227b2fd27fb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/gcc43.patch" || return 1
  patch -Np1 -i "${srcdir}/synergy-stuttered-delay-fix.patch" || return 1

  ./configure --prefix=/usr \
    --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/etc"
  install -m644 examples/synergy.conf "${pkgdir}/etc/" || return 1
}
