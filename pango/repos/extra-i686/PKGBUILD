# $Id$ 
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango
pkgver=1.40.3
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libthai' 'cairo' 'libxft' 'harfbuzz')
makedepends=('harfbuzz' 'fontconfig' 'cairo' 'libxft' 'libthai' 'gobject-introspection' 'help2man'
             'gtk-doc')
checkdepends=('ttf-dejavu')
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('abba8b5ce728520c3a0f1535eab19eac3c14aeef7faa5aded90017ceac2711d3')

prepare() {
  cd ${pkgname}-${pkgver}
  rm pango-view/pango-view.1.in
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check || :
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
