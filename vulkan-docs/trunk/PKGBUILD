# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-docs
pkgname=(vulkan-headers vulkan-man-pages vulkan-html-docs)
pkgver=1.0.29
pkgrel=1
epoch=1
_pkgname=Vulkan-Docs
_pkgver=1.0-core-20160930
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(asciidoc python3)
groups=(vulkan-devel)
source=("https://github.com/KhronosGroup/${_pkgname}/archive/v${_pkgver}.tar.gz")
md5sums=('3f9370c61eadfb28895f0884c94eada7')

build() {
  cd "${_pkgname}-${_pkgver}/doc/specs/vulkan"

  # ./makeKHR manpages
  # Broken, see https://github.com/KhronosGroup/Vulkan-Docs/issues/367
  # workaround :( (yes, twice the same command)
  LANG="en_US.UTF8" make manpages
  LANG="en_US.UTF8" make manpages
  ./makeKHR chunked
}

package_vulkan-headers() {
  pkgdesc="Vulkan header files"
  cd "${_pkgname}-${_pkgver}"

  install -dm755 "${pkgdir}/usr/include/vulkan"
  install -dm755 "${pkgdir}/usr/share/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  install -m644 src/vulkan/vk_platform.h ${pkgdir}/usr/include/vulkan/
  install -m644 src/vulkan/vulkan.h ${pkgdir}/usr/include/vulkan
  install -m644 src/spec/vk.xml ${pkgdir}/usr/share/vulkan

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}

package_vulkan-man-pages() {
  pkgdesc="Vulkan man pages"
  cd "${_pkgname}-${_pkgver}"

  install -dm755 "${pkgdir}/usr/share/man/man3"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  install -m644 out/1.0/man/3/*.3 "${pkgdir}/usr/share/man/man3/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation"
  cd "${_pkgname}-${_pkgver}"

  install -dm755 "${pkgdir}/usr/share/doc/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  mv -v out/1.0/chunked/* "${pkgdir}/usr/share/doc/vulkan/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}
