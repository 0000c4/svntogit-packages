# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=junit
pkgver=4.11
pkgrel=2
pkgdesc='Automated testing framework for Java'
arch=('any')
url='http://junit.sourceforge.net/'
license=('CPL')
depends=('java-runtime-headless' 'java-hamcrest')
makedepends=('git' 'apache-ant')

build() {
  cd ${srcdir}

  # No source tarball available: https://github.com/junit-team/junit/blob/r4.11/BUILDING
  # So building from specific github tag: https://github.com/junit-team/junit/tags
  git clone https://github.com/junit-team/${pkgname}.git
  cd junit
  git checkout r${pkgver}

  ant dist -Dversion-status=''
}

check() {
  cd ${srcdir}/${pkgname}

  ant test
}

package() {
  install -Dm644 ${srcdir}/${pkgname}/${pkgname}${pkgver}/${pkgname}-dep-${pkgver}.jar \
                 ${pkgdir}/usr/share/java/${pkgname}-${pkgver}.jar
  ln -s ${pkgname}-${pkgver}.jar \
                 ${pkgdir}/usr/share/java/${pkgname}.jar
}
