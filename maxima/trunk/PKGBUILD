# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=maxima
pkgver=5.17.0
pkgrel=1
pkgdesc="Maxima - a sophisticated computer algebra system"
arch=('i686' 'x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('clisp>=2.45' 'libsigsegv' 'readline' 'bash')
optdepends=('tk: graphical xmaxima interface')

# needs rebuild when {readline,bash} changes version

options=('!makeflags')
install=$pkgname.install
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz" "$pkgname.desktop")
md5sums=('98ec3ec237d55639a8de5b317990eb2d'
         'a44160f17c95d4ce1d6f3b4ca7cf6b9e')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
	--libexecdir=/usr/lib
  make || return 1
  make DESTDIR=$pkgdir install || return 1
  
  # install some freedesktop.org compatibility
  install -Dm644 $srcdir/$pkgname.desktop \
  	$pkgdir/usr/share/applications/$pkgname.desktop
  
  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d $pkgdir/usr/share/pixmaps/
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png \
	$pkgdir/usr/share/pixmaps/${pkgname}.png

# handle info files
  rm -f $pkgdir/usr/share/info/dir
  gzip $pkgdir/usr/share/info/*

}


