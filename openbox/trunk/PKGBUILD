# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=openbox
pkgver=3.4.10
pkgrel=3
pkgdesc="A window manager for the X11 windowing system"
arch=('i686' 'x86_64')
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
	'libxcursor' 'pango')
optdepends=('pyxdg: for the xdg-autostart script')
url="http://openbox.org"
groups=('lxde')
install=openbox.install
options=('!libtool' 'force')
backup=('etc/xdg/openbox/menu.xml' 'etc/xdg/openbox/rc.xml')
source=(http://www.icculus.org/openbox/releases/${pkgname}-${pkgver}.tar.gz
	'which-2.20.patch'
	'fix-bug-in-openbox-gnome-session.patch')
md5sums=('daccf153c7808950313eb00bc250f9bc'
         'b72cac492e0ed83fd5ced9c1263cfa24'
         '9ad61fcc37d3d2258c3fe549e7c54cdb')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # FS#11455
  patch -Np1 -i ${srcdir}/which-2.20.patch || return 1

  # Already fixed in git
  patch -Np1 -i ${srcdir}/fix-bug-in-openbox-gnome-session.patch || return 1

  ./configure --prefix=/usr \
    --with-x \
    --enable-startup-notification \
    --sysconfdir=/etc
  make || return 1
  make DESTDIR=${pkgdir} install

  sed -i "s:startkde:/usr/bin/\0:" ${pkgdir}/usr/share/xsessions/openbox-kde.desktop
  sed -i "s:=gnome-session:=/usr/bin/gnome-session:" ${pkgdir}/usr/share/xsessions/openbox-gnome.desktop
}
