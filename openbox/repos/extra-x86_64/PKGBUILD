# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=openbox
pkgver=3.4.11
pkgrel=1
pkgdesc="A window manager for the X11 windowing system"
arch=('i686' 'x86_64')
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
	'libxcursor' 'pango')
optdepends=('pyxdg: for the xdg-autostart script')
url="http://openbox.org"
groups=('lxde')
install=${pkgname}.install
options=('!libtool' 'force')
backup=('etc/xdg/openbox/menu.xml' 'etc/xdg/openbox/rc.xml')
source=(http://www.icculus.org/openbox/releases/${pkgname}-${pkgver}.tar.gz
	'which-2.20.patch')
md5sums=('bbbb93c1a4fc8048f2a8ceb4179882a5'
         '470ab8424315a96eb2b8955f1c482317')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # FS#11455
  patch -Np1 -i ${srcdir}/which-2.20.patch || return 1

  ./configure --prefix=/usr \
    --with-x \
    --enable-startup-notification \
    --sysconfdir=/etc
  make || return 1
  make DESTDIR=${pkgdir} install

  sed -i "s:startkde:/usr/bin/\0:" ${pkgdir}/usr/share/xsessions/openbox-kde.desktop
  sed -i "s:=gnome-session:=/usr/bin/gnome-session:" ${pkgdir}/usr/share/xsessions/openbox-gnome.desktop
}
