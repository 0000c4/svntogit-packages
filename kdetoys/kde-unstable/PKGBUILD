# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdetoys
pkgname=('kdetoys-amor'
         'kdetoys-kteatime'
         'kdetoys-ktux'
         'kdetoys-kweather')
pkgver=4.2.90
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdetoys')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-workspace')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('a0817b44d76752951a00f775e6cde5f4')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdetoys-amor() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/amor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/amor
	make DESTDIR=$pkgdir install
}

package_kdetoys-kteatime() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/kteatime
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kteatime
	make DESTDIR=$pkgdir install
}

package_kdetoys-ktux() {
	pkgdesc=''
	depends=('kdebase-workspace' )
	install='kdetoys.install'
	cd $srcdir/build/ktux
	make DESTDIR=$pkgdir install
}

package_kdetoys-kweather() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/kweather
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kweather
	make DESTDIR=$pkgdir install
}

