# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeadmin
pkgname=('kdeadmin-kcron'
         'kdeadmin-ksystemlog'
         'kdeadmin-kuser'
         'kdeadmin-system-config-printer-kde')
pkgver=4.5.95
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebindings-python'
	    'system-config-printer-common' 'pyqt' 'docbook-xsl')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
        'syslog-path.patch')
sha1sums=('d5c9bb6fd00f0b3799a5eed5c249a14edf4d2d31'
          '96988f1eb531efae56d32a2471bd8ef5329ca749')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	patch -p1 -i ${srcdir}/syslog-path.patch

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_kpackage=OFF \
		-DBUILD_knetworkconf=OFF \
		-DBUILD_lilo-config=OFF \
		-DBUILD_strigi-analyzer=OFF
	make
}

package_kdeadmin-kcron() {
	pkgdesc='Configure and schedule tasks'
	depends=('kdelibs')
	cd $srcdir/build/kcron
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcron
	make DESTDIR=$pkgdir install
}

package_kdeadmin-ksystemlog() {
	pkgdesc='System log viewer tool'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/system/ksystemlog/"
	cd $srcdir/build/ksystemlog
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ksystemlog
	make DESTDIR=$pkgdir install
}

package_kdeadmin-kuser() {
	pkgdesc='User Manager'
	depends=('kdepim-runtime')
	url="http://kde.org/applications/system/kuser/"
	install='kdeadmin.install'
	cd $srcdir/build/kuser
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kuser
	make DESTDIR=$pkgdir install
}

package_kdeadmin-system-config-printer-kde() {
	pkgdesc='Configure local and remote Printers'
	depends=('kdebindings-python' 'system-config-printer-common' 'pyqt')
	url="http://kde.org/applications/system/printerapplet/"
	cd $srcdir/build/system-config-printer-kde
	make DESTDIR=$pkgdir install

    # Use the python2 executable
    find ${pkgdir} -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
