# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdeadmin
pkgver=4.2.3
pkgrel=1
pkgdesc="KDE Administration Programs"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdepimlibs' 'kdebase-runtime' 'kdebindings' 'system-config-printer>=1.1.7')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
install='kdeadmin.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	'fix-syslog-path.patch')
md5sums=('dc352ccb03c285a87fcc68d7d18e9d43'
         '41b71823c7a33975ec08aaf75d67a374')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/fix-syslog-path.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_kpackage=OFF \
		-DBUILD_knetworkconf=OFF \
		-DBUILD_lilo-config=OFF
	make
	make DESTDIR=$pkgdir install
}
