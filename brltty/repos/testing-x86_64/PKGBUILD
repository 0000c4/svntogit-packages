# Maintainer:
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=5.6
pkgrel=6
pkgdesc="Braille display driver for Linux/Unix"
arch=(x86_64)
url="http://mielke.cc/brltty"
license=(GPL LGPL)
depends=(libxaw gpm icu tcl bluez-libs espeak)
makedepends=(at-spi2-core tcl speech-dispatcher cython)
optdepends=('at-spi2-core: X11/GNOME Apps accessibility'
            'atk: ATK bridge for X11/GNOME accessibility'
            'speech-dispatcher: speech-dispatcher driver')
backup=(etc/brltty.conf)
options=('!emptydirs')
install=brltty.install
source=(http://mielke.cc/brltty/archive/brltty-$pkgver.tar.xz
        brltty.service brltty.sysusers)
sha256sums=('6171258aca833bb2012afd9c63928573a48c3daab7b149a3e90001ba53beb80f'
            'a342a5e9d36f00a320e1d23d91c482bd5445af52588334307b7e9c09a8cc38ec'
            '8290ea5ac3f13a731d4dcc2b5e8355ff6a6c254e6d90d67dcc5dfb753780e060')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --mandir=/usr/share/man \
    --with-tables-directory=/usr/share/brltty \
    --with-screen-driver=lx \
    --enable-gpm \
    --disable-java-bindings \

  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 Documents/brltty.conf "$pkgdir/etc/brltty.conf"
  install -Dm644 ../brltty.service "$pkgdir/usr/lib/systemd/system/brltty.service"
  install -Dm644 ../brltty.sysusers "$pkgdir"/usr/lib/sysusers.d/brltty.conf
}
