# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=4.5
pkgrel=1
pkgdesc="Braille display driver for Linux/Unix"
arch=(i686 x86_64)
url="http://mielke.cc/brltty"
license=(GPL LGPL)
depends=(libxaw at-spi2-core gpm 'icu>=51.1' tcl atk cython)
makedepends=(bluez)
optdepends=('bluez: bluetooth support')
backup=(etc/brltty.conf)
options=('!emptydirs')
install=brltty.install
source=(http://mielke.cc/$pkgname/releases/$pkgname-$pkgver.tar.gz
        brltty.service)
md5sums=('7b52fa7746fed41ed344a1f75ce55951'
         '7acecd700b9f94d76fe4a6ad56cb0448')
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --mandir=/usr/share/man \
    --with-tables-directory=/usr/share/brltty \
    --with-screen-driver=a2 \
    --enable-gpm \
    --disable-java-bindings \
    --disable-static

  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 Documents/brltty.conf "$pkgdir/etc/brltty.conf"
  install -Dm644 ../brltty.service "$pkgdir/usr/lib/systemd/system/brltty.service"
}
