# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities4
pkgver=4.13.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='API for using and interacting with the Activity Manager'
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('GPL' 'FDL')
depends=('baloo')
makedepends=('cmake' 'automoc4' 'mesa')
replaces=('kactivities')
conflicts=('kactivities' 'kactivities-frameworks')
provides=('kactivities')
source=("http://download.kde.org/stable/${pkgver}/src/kactivities-${pkgver}.tar.xz")
sha1sums=('bcece0a4fa92d34cb2bb83d208c914b8830d203d')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
