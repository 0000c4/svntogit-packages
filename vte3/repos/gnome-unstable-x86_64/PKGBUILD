# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=vte3
pkgname=(vte3 vte-common)
pkgver=0.29.1
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
makedepends=('intltool' 'gobject-introspection' 'gtk3')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/vte/0.29/vte-${pkgver}.tar.xz)
sha256sums=('5f846c2763eaac95bb15b99dd212d76ec7d723c1c6f81a8b562143f6eaf2ba9e')

build() {
  cd "${srcdir}/vte-${pkgver}"
  #warning: type-punning to incomplete type might break strict-aliasing rules
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection
  make
}

package_vte3(){
  pkgdesc+=" GTK3"
  depends=('gtk3' 'vte-common')
  cd "${srcdir}/vte-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/lib/vte/gnome-pty-helper
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  cd "${srcdir}/vte-${pkgver}"

  make -C gnome-pty-helper DESTDIR="${pkgdir}" install
}
