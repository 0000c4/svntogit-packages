# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=1.91.92
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('at-spi2-core' 'libx11' 'atk' 'dconf')
makedepends=('intltool')
install=at-spi2-atk.install
options=('!libtool')
groups=('gnome')
source=(http://download.gnome.org/sources/${pkgname}/1.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('711b59ceca97933c1549bf2fe2ac65356f20a4f3e50bf25c440304fbcf8eb099')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install
  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
