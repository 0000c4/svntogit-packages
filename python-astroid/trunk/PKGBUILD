# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.3.2
pkgrel=0
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt' 'python-typed-ast')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('73ed8213f25a76aa60703320aaf290db33ac005e0ded26fe50c7f36bd934f98c3a58e928becc4e601d3879c19a0f0f16bf20764e3a24e227e0fb64179e468732')

build() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/astroid-astroid-$pkgver
  pytest
}

package() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
