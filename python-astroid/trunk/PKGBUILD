# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.0.1
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('cdc1b7274636b0b011a2086c34460d31dd05f0e94f80065c30277dc30bc1c70081d39a0034232c566ca2d88762b69f55c22681b188b83726e05fbf83ad601549')

build() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/astroid-astroid-$pkgver
  pytest
}

package() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
