#Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libdrm-new
pkgname=(libdrm-new libdrm-nouveau)
pkgver=2.4.34
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind')
options=('!libtool' '!emptydirs')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/libdrm/libdrm-$pkgver.tar.bz2
        no-pthread-stubs.patch
        COPYING
)
sha1sums=('861757baff4b37e564e13f5350c1b5d01c66a181'
          '2a5410baa3e6e078f9378ce486a88f41d22fd838'
          'ba3dcd636997ee0d30df14b03dae05c24ae5d094')

build() {
  cd "libdrm-$pkgver"
  patch -Np1 -i "$srcdir/no-pthread-stubs.patch"

  #libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --disable-libkms \
      --disable-intel \
      --disable-radeon 
  make
}

package_libdrm-new() {
  pkgdesc="Userspace interface to kernel DRM services - used as makedepends for xf86-video-nouveau"
  conflicts=('libdrm')
  provides=("libdrm=$pkgver")
  cd "libdrm-$pkgver"
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libdrm_nouveau.so.2*
}

package_libdrm-nouveau() {
  pkgdesc="Userspace interface to kernel DRM services for nouveau - used as depends for xf86-video-nouveau"
  depends=(libdrm)
  cd "libdrm-$pkgver"
  make DESTDIR="$pkgdir" install-libdrm_laLTLIBRARIES
  make -C nouveau DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" uninstall-libdrm_laLTLIBRARIES
  rm "$pkgdir"/usr/include/libdrm/nouveau.h  "$pkgdir"/usr/lib/pkgconfig/libdrm_nouveau.pc  "$pkgdir"/usr/lib/libdrm_nouveau.so
}
