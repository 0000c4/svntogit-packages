# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.12.32
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice')
makedepends=('intltool' 'psiconv' 'pygobject-devel'
             'gobject-introspection' 'rarian' 'python2-gobject' gtk-doc autoconf-archive git)
optdepends=('python2-gobject: for python plugin support'
            'perl: for perl plugin support'
            'psiconv: for Psion 5 file support'
            'yelp: for viewing the help manual')
_commit=0e34331361ac41a5f09ed10f4df26861be4f1160  # tags/GNUMERIC_1_12_32^0
source=("git://git.gnome.org/gnumeric#commit=$_commit"
        git://git.gnome.org/gnome-common#commit=6684e2fa5d5a4c72c9bf116f74688c0b47f350cf
        revert-warnings.patch)
sha256sums=('SKIP'
            'SKIP'
            'bcafca016b809000c2a5bf911e2e3dfa4de28f9e541d9964574cac5c7ce09e53')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNUMERIC_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  cp ../gnome-common/doc-build/{omf,xmldocs}.make .
  patch -Np0 -i ../revert-warnings.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile --enable-introspection

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
