# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.12.1
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'psiconv' 'pygobject-devel' 'gnome-doc-utils' 'gobject-introspection')
optdepends=('python2-gobject: for python plugin support'
            'perl: for perl plugin support'
            'psiconv: for Psion 5 file support'
            'yelp: for viewing the help manual')
install=gnumeric.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        gnumeric-1.12.1-automake-1.13.patch
        revert-warnings.patch)
sha256sums=('0f5a7af3acae7db643eb00e8b4fca6a9e90162259c6a73e4214cfa8c96cc3901'
            '1eb57b931023d961e9d0f3675fe96b5559270fa38c97401cb06fc7aed9c8e275'
            'bcafca016b809000c2a5bf911e2e3dfa4de28f9e541d9964574cac5c7ce09e53')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../gnumeric-1.12.1-automake-1.13.patch
  patch -Np0 -i ../revert-warnings.patch

  # Remove invalid mime type. (FS#26338)
  sed -i -e 's/zz-application\/zz-winassoc-xls;//' gnumeric.desktop.in

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-ssindex --disable-schemas-compile --enable-introspection --enable-compile-warnings=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
