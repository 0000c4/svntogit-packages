# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.8.4
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice>=0.6.1' 'gconf>=2.18.0.1-4' 'desktop-file-utils')
makedepends=('perlxml' 'pygtk' 'psiconv' 'pkgconfig' 'intltool' 'gnome-doc-utils>=0.14.1')
optdepends=('python: for python plugin support' 'psiconv: for Psion 5 file support')
install=gnumeric.install
options=('libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('338f0084f04a16f78797ad01f85d3251')
sha1sums=('2d765b0d06509e0695c4d3ec96b94f8acba88735')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
              --without-guile --with-bonobo --enable-ssindex || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

  install -d ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
