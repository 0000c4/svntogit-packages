# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Mildred <mildred593 at online dot fr>

pkgname=claws-mail-extra-plugins
pkgver=3.7.10
pkgrel=2
pkgdesc="Extra plugins for claws-mail"
url="http://www.claws-mail.org/plugins.php?branch=EXT"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('claws-mail>=3.7.10')
makedepends=('make' 'bc' 'perl>=5.10.1-5' 'libgtkhtml>=2.11.1-2' 'libnotify>=0.7'
             'libxml2>=2.7.6' 'curl>=7.19.7' 'libytnef>=1.5-2' 'libwebkit>=1.6.1'
             'pygtk>=2.16.0' 'libsoup-gnome' 'libcanberra' 'libgdata')
optdepends=('libgtkhtml: for gtkhtml2_viewer plugin'
            'perl: for perl plugin'
            'libnotify: for notification plugin'
            'libcanberra: for notification plugin'
            'dbus-core: for notification plugin'
            'libxml2: for gtkhtml2_viewer and rssyl plugins'
            'curl: for gtkhtml2_viewer, vcalendar, rssyl and spamreport plugins'
            'libarchive: for archive plugin'
            'libytnef: for tnef_parse plugin'
            'libwebkit: for the fancy webkit html plugin'
            'libsoup-gnome: for the fancy webkit html plugin'
            'python2: for python plugin'
	    'libgdata: for gdata plugin')
conflicts=('claws-gtkhtml2_viewer' 'claws-mail-acpinotifier-plugin'
           'sylpheed-claws-gtkhtml2-plugin' 'sylpheed-claws-rssyl-plugin'
           'sylpheed-claws-extra-plugins' 'claws-tnef-plugin'
           'claws-webkit-plugin-svn' 'claws-mayflower-plugin-svn')
replaces=('sylpheed-claws-extra-plugins')
options=('!libtool' '!strip')
source=(http://downloads.sourceforge.net/project/sylpheed-claws/extra%20plugins/$pkgver/claws-mail-extra-plugins-$pkgver.tar.bz2
	webkit1.6.1.patch)
md5sums=('fe782b2d4762c9727e22c1bb7979f7d6'
         '8b94fa44072331b9c3926607eb42ba26')
sha1sums=('c5200936cb4811022774e960dc3f43d19eb320cf'
          '8efd800617406dfe58a14422f243a678c582a90d')

build() {
  cd "$srcdir/claws-mail-extra-plugins-$pkgver"

  # fix for new webkit api
  pushd fancy*
  patch -Np0 -i ${srcdir}/webkit1.6.1.patch
  popd

  # fix for python2
  export PYTHON="/usr/bin/python2"

  # fix gdata plugin build
  sed -i -e "s:libgdata\ <\ 0.9.0:libgdata\ <\ 0.11.0:g" gdata_plugin-0.2/configure
 
  for dir in */; do
    [[ $dir != geolocation_plugin-* ]] && (
      cd $dir

      ./configure --prefix=/usr --disable-static
      make
    )
  done
}

package() {
  cd "$srcdir/claws-mail-extra-plugins-$pkgver"
  for dir in */; do
    [[ $dir != geolocation_plugin-* ]] && 
      make -C $dir DESTDIR="$pkgdir" install
  done

  # fix fileconflict with libical, #11485
  rm -f "$pkgdir/usr/include/ical.h"
}
