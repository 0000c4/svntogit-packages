# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Mildred <mildred593 at online dot fr>

pkgname=claws-mail-extra-plugins
pkgver=3.7.1
pkgrel=1
pkgdesc="Extra plugins for claws-mail"
url="http://www.claws-mail.org/plugins.php?branch=EXT"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('claws-mail>=3.7.1')
makedepends=('make' 'bc' 'perl>=5.10.0-4' 'libgtkhtml' 'libnotify' 'libxml2' 'curl')
optdepends=('libgtkhtml: for gtkhtml2_viewer plugin'
            'perl: for perl plugin'
            'libnotify: for notification plugin'
            'libxml2: for gtkhtml2_viewer and rssyl plugins'
            'curl: for gtkhtml2_viewer, vcalendar, rssyl and spamreport plugins'
            'libarchive: for archive plugin')
conflicts=('claws-gtkhtml2_viewer' 'claws-mail-acpinotifier-plugin' 'sylpheed-claws-gtkhtml2-plugin' 'sylpheed-claws-rssyl-plugin' 'sylpheed-claws-extra-plugins')
replaces=('sylpheed-claws-extra-plugins')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/sylpheed-claws/claws-mail-extra-plugins-$pkgver.tar.bz2")
md5sums=('a483d0554365c2409f8921eefcb69a6f')

build() {
    # remove an unwanted plugin nobody needs. it would pull ytnef dependency
    rm -rf $startdir/src/claws-mail-extra-plugins-$pkgver/tnef_parse*

    cd "$startdir/src/claws-mail-extra-plugins-$pkgver"
    for dir in *; do
        # Continue if it is not a directory or the synce plugin
        [ ! -d "$dir" ] && continue
        echo "$dir" | grep synce >/dev/null 2>/dev/null && continue
        # Else, compile
        cd "$dir"
        [ -e "$configured_file" ] || \
            ./configure --prefix=/usr --disable-static || return 1
        touch "arch-configured"
        make || return 1
        make DESTDIR=$startdir/pkg install || return 1
        cd ..
    done

 # fix fileconflict with libical, #11485
 rm -f ${pkgdir}/usr/include/ical.h
}
