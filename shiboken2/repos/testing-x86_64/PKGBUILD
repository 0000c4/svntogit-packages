# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=shiboken2
pkgname=(shiboken2 python-shiboken2)
_qtver=5.13.0
_clangver=8.0.0
pkgver=${_qtver/-/}
pkgrel=3
arch=(x86_64)
url='https://www.qt.io'
license=(GPL2 LGPL)
pkgdesc='Generates bindings for C++ libraries using CPython source code'
makedepends=(clang llvm cmake libxslt qt5-xmlpatterns python-sphinx)
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz"
         qcoreapplication-crash.patch::"https://code.qt.io/cgit/pyside/pyside-setup.git/patch/?id=80a6f91c")
sha256sums=('8e47e778a6c8ee86e9bc7dbf56371cf607e9f3c1a03a7d6df9e34f8dba555782'
            '7d36bfc604f4759f593e8d599e04e9ef713b35b3e0c373c12868b32913564303')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -Rp1 -i ../qcoreapplication-crash.patch # Revert commit that makes QCoreApplication import crash
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/shiboken2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=3
  make
}

package_shiboken2() {
  depends=(clang=$_clangver llvm libxslt qt5-xmlpatterns)

  cd build
  make DESTDIR="$pkgdir" install
# Provided in python-shiboken2
  rm -r "$pkgdir"/usr/lib/{python*,libshiboken*}
}

package_python-shiboken2() {
  depends=(python)

  cd build
  make DESTDIR="$pkgdir" install
# Provided in shiboken2
  rm -r "$pkgdir"/usr/{bin,include,lib/{cmake,pkgconfig},share}
}
