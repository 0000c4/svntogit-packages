# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=shiboken2
pkgname=(shiboken2 python-shiboken2 python2-shiboken2)
_qtver=5.11.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='http://qt-project.org/'
license=(GPL2 LGPL)
pkgdesc='Generates bindings for C++ libraries using CPython source code'
makedepends=(clang llvm cmake libxslt qt5-xmlpatterns python python2)
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("http://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('fbc412c4544bca308291a08a5173a949ca530d801f00b8337902a5067e490922')

prepare() {
  mkdir -p build{,2}
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/shiboken2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=3
  make

  cd ../build2
  cmake ../${_pkgfqn}/sources/shiboken2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=2
  make
}

package_shiboken2() {
  depends=(clang libxslt qt5-xmlpatterns)

  cd build
  make DESTDIR="$pkgdir" install
# Provided in python-shiboken
  rm -r "$pkgdir"/usr/lib/{cmake/*/*python*,python*,libshiboken*}
}

package_python-shiboken2() {
  depends=(python)

  cd build
  make DESTDIR="$pkgdir" install
# Provided in shiboken
  rm -r "$pkgdir"/usr/{bin,include,lib/{cmake/*/Shiboken2Config{,Version}.cmake,pkgconfig},share}
}

package_python2-shiboken2() {
  depends=(python2)

  cd build2
  make DESTDIR="$pkgdir" install
# Provided in shiboken
  rm -r "$pkgdir"/usr/{bin,include,lib/{cmake/*/Shiboken2Config{,Version}.cmake,pkgconfig},share}
}
