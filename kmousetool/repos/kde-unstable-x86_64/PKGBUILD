# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmousetool
pkgver=17.07.80
pkgrel=1
arch=(i686 x86_64)
pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
url='https://kde.org/applications/utilities/kmousetool/'
license=(GPL FDL)
groups=(kde-applications kdeaccessibility)
depends=(knotifications kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python libxt)
conflicts=(kdeaccessibility-kmousetool)
replaces=(kdeaccessibility-kmousetool)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('48c19599dea17c8d16d6e2bc8e44e1bbae7f1b3ddc03b4f02cef804145ddc62a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
