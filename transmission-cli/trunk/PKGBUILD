# $Id$
# Maintainer: Alexander Fehr <pizzapunk gmail com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

pkgname=transmission-cli
pkgver=1.32
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (CLI tools and daemon)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
depends=('curl')
makedepends=('intltool')
replaces=('transmission')
backup=('etc/conf.d/transmissiond')
install=transmission-cli.install
source=(http://download.m0k.org/transmission/files/transmission-$pkgver.tar.bz2
        transmissiond
        transmissiond.conf)
md5sums=('2525872f5c95886243f83ed513e3952b'
         'e2326d412e25b4449ff663a5d4015e1a'
         '7b019a710493ec4f8b54a1f528e1981d')

build() {
  cd "$srcdir/transmission-$pkgver"

  ./configure --prefix=/usr --disable-gtk || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  # Remove web interface
  rm -rf "$pkgdir/usr/share/transmission" || return 1

  install -D -m755 "$srcdir/transmissiond" "$pkgdir/etc/rc.d/transmissiond" || return 1
  install -D -m644 "$srcdir/transmissiond.conf" "$pkgdir/etc/conf.d/transmissiond" || return 1

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/transmission-cli/COPYING" || return 1
}
