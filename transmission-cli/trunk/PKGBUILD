# $Id$
# Maintainer: Alexander Fehr <pizzapunk gmail com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

pkgname=transmission-cli
pkgver=1.80
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
depends=('curl')
makedepends=('intltool')
replaces=('transmission')
backup=('etc/conf.d/transmissiond')
install=transmission-cli.install
source=(http://mirrors.m0k.org/transmission/files/transmission-$pkgver.tar.bz2
        transmissiond
        transmissiond.conf)
md5sums=('19a7d7dd60527199d05cf5e2e775929e'
         '50b7c17300bc8b38f44fb6f681ba05d0'
         '4170b71ad5d6c499a8a16c6e86b77cce')

build() {
  cd "$srcdir/transmission-$pkgver"

  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  ./configure --prefix=/usr --disable-gtk --disable-wx || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  install -D -m755 "$srcdir/transmissiond" "$pkgdir/etc/rc.d/transmissiond" || return 1
  install -D -m644 "$srcdir/transmissiond.conf" "$pkgdir/etc/conf.d/transmissiond" || return 1

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/transmission-cli/COPYING" || return 1
}
