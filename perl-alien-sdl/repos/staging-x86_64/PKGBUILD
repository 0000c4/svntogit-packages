# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

# This package must not by "any" arch.
# Requires rebuild with every change to sdl-config

pkgname=perl-alien-sdl
_realname=Alien-SDL
pkgver=1.426
pkgrel=1
pkgdesc="Building, finding and using SDL binaries"
arch=(i686 x86_64)
license=(PerlArtistic GPL)
url="http://search.cpan.org/dist/Alien-SDL/"
depends=(sdl=1.2.14 perl-file-sharedir perl-capture-tiny)
makedepends=(perl-file-which perl-text-patch)
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/F/FR/FROGGS/$_realname-$pkgver.tar.gz)
md5sums=('ab5e1167934418f4a7b3285a9d05c89b')

package() {
  cd "$srcdir/$_realname-$pkgver"

  # install module in vendor directories
  perl Build.PL installdirs=vendor destdir="$pkgdir" --with-sdl-config

  perl Build
  perl Build install
}
