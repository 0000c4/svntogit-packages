# $Id$
# Maintainer:
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=proftpd
pkgver=1.3.4b
pkgrel=1
epoch=1
pkgdesc="A high-performance, scalable FTP server"
arch=('i686' 'x86_64')
url="http://www.proftpd.org"
license=('GPL')
depends=('libldap' 'libmysqlclient' 'postgresql-libs')
backup=('etc/proftpd.conf' 'etc/conf.d/proftpd')
source=(ftp://ftp.proftpd.org/distrib/source/${pkgname}-${pkgver}.tar.gz{,.asc}
        'proftpd' 'proftpd.logrotate' 'proftpd.conf.d')
md5sums=('0871e0b93c9c3c88ca950b6d9a04aed2'
         'e5b9bd78029a15f82994efcb7ed2e9fb'
         '8177292919acb2f087ded0bb7b2dcc32'
         'ddb09eb13131becdf0e081eef413116b'
         '71d5932b0461c318ed68c2c0c2660736')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --disable-pam \
    --with-modules=mod_quotatab:mod_quotatab_sql:mod_quotatab_file:mod_tls:mod_ldap:mod_sql:mod_sql_mysql:mod_sql_postgres \
    --sysconfdir=/etc \
    --localstatedir=/var/run \
    --enable-ctrls \
    --enable-ipv6 \
    --with-includes=/usr/include/mysql:/usr/include/postgresql \
    --with-libraries=/usr/lib/mysql:/usr/lib/postgresql \
    --enable-nls

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  sed -i 's|nogroup|nobody|g' "${pkgdir}/etc/proftpd.conf"

  install -Dm644 ../proftpd.logrotate "${pkgdir}/etc/logrotate.d/proftpd"
  install -Dm644 ../proftpd.conf.d "${pkgdir}/etc/conf.d/proftpd"
  install -Dm755 ../proftpd "${pkgdir}/etc/rc.d/proftpd"
  install -Dm755 contrib/xferstats.holger-preiss "${pkgdir}/usr/bin/ftpstats"

  # /var/run is a symlink to /run now
  rm -rf "${pkgdir}/var"
}
