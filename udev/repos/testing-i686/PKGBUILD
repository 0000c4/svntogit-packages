# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=udev
pkgver=127
pkgrel=1
pkgdesc="The userspace dev tools (udev)"
arch=(i686 x86_64)
url="http://www.kernel.org/pub/linux/utils/kernel/hotplug/udev.html"
license=('GPL')
groups=('base')
depends=('glibc' 'coreutils' 'util-linux')
makedepends=('kernel26') #needed to build framebuffer blacklist
install=udev.install
backup=(etc/udev/udev.conf
        etc/scsi_id.config
        etc/udev/cdsymlinks.conf
        etc/modprobe.d/framebuffer_blacklist)
conflicts=('pcmcia-cs' 'hotplug')
replaces=('devfsd')
# older initscripts versions required start_udev
conflicts=('initscripts<2008.02')
options=(!makeflags)
source=(http://www.kernel.org/pub/linux/utils/kernel/hotplug/$pkgname-$pkgver.tar.bz2
        81-arch.rules load-modules.sh cdsymlinks.sh root-link.sh arch-udev-rules.patch
        readme-udev-arch.txt)
md5sums=('59ebde702f1ab557be15ae76d645665b' '610f75f2b05437b14f3bc3418f8e7f65'\
         '945f6d30ab458189b3c17f8e6722da1f' '8424b78e9dd772e75b4ef90814807815'\
         '2d6dc6842464f107bccc68cd505a6c31' 'c17e075cc1806bf94a3678168ddec4e1'\
         '7fc6d33bb218e752302eef0a80ff0a89')

build() {
  cd $startdir/src/$pkgname-$pkgver
  ./configure --prefix=/ --mandir=/usr/share/man --includedir=/usr/include
  make || return 1
  make DESTDIR=$startdir/pkg install

  #Non-stock rules still go in /etc
  install -D -m644 $startdir/src/81-arch.rules $startdir/pkg/etc/udev/rules.d/81-arch.rules

  # install our module loading subsystem
  install -D -m755 $startdir/src/load-modules.sh $startdir/pkg/lib/udev/load-modules.sh
  # install cdsymlinks.sh
  install -D -m755 $startdir/src/cdsymlinks.sh $startdir/pkg/lib/udev/cdsymlinks.sh
  # install root-link.sh
  install -D -m755 $startdir/src/root-link.sh $startdir/pkg/lib/udev/root-link.sh

  # install instructions
  # NOTE: We should delete this file
  install -D -m644 $startdir/src/readme-udev-arch.txt $startdir/pkg/usr/share/udev/readme-udev-arch.txt

  # add devices dir
  mkdir $startdir/pkg/lib/udev/devices
  # disable error logging to prevent startup failures printed to vc on boot
  sed -i -e 's|udev_log="err"|udev_log="0"|g' $startdir/pkg/etc/udev/udev.conf
  # install additional rules files
  for rule in $startdir/src/$pkgname-$pkgver/rules/packages/*.rules; do
      install -D -m 644 $rule $startdir/pkg/lib/udev/rules.d/
  done
  # fix standard udev rules to fit to arch
  cd $startdir/pkg/lib/udev/rules.d/
  patch -Np0 -i $startdir/src/arch-udev-rules.patch || return 1

  # disable persistent cdromsymlinks and network by default 
  # and move it to /etc/udev/rules.d
  mv $startdir/pkg/lib/udev/rules.d/75-persistent-net-generator.rules \
     $startdir/pkg/etc/udev/rules.d/75-persistent-net-generator.rules.optional
  mv $startdir/pkg/lib/udev/rules.d/75-cd-aliases-generator.rules \
     $startdir/pkg/etc/udev/rules.d/75-cd-aliases-generator.rules.optional
  # create framebuffer blacklist
  mkdir -p $startdir/pkg/etc/modprobe.d/
  for mod in $(find /lib/modules/ -name '*fb*.ko' -exec basename {} .ko \;); do 
	echo "blacklist $mod" >> $startdir/pkg/etc/modprobe.d/framebuffer_blacklist
  done
}
