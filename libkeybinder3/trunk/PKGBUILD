# $Id: PKGBUILD,v 1.13 2007/06/19 02:12:51 Snowman Exp $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=libkeybinder3
pkgver=0.3.0
pkgrel=2
pkgdesc="A library for registering global keyboard shortcuts"
arch=('i686' 'x86_64')
url="http://kaizer.se/wiki/keybinder/"
license=('MIT')
depends=('gtk3')
makedepends=('gtk-doc')
optdepends=('lua-lgi: lua bindings')
options=('!libtool')
source=(http://archive.ubuntu.com/ubuntu/pool/universe/k/keybinder-3.0/keybinder-3.0_${pkgver}.orig.tar.gz)
sha1sums=('53167d295ffa1d356bfad6f9360b90bd85e8fb26')

build() {
  cd keybinder-3.0-${pkgver}
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd keybinder-3.0-${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
