# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.org>
# Contributor: abelstr <abel@pinklf.eu>
# Contributor: Marco Lima <cipparello gmail com>

pkgname=nfs-utils
pkgver=1.3.4
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('i686' 'x86_64')
url='http://nfs.sourceforge.net'
license=('GPL2')
backup=(etc/{exports,nfsmount.conf} etc/sysconfig/nfs)
depends=('rpcbind' 'nfsidmap' 'gssproxy' 'libevent' 'device-mapper')
makedepends=('sqlite')
# http://git.linux-nfs.org/?p=steved/nfs-utils.git;a=summary
source=(http://downloads.sourceforge.net/project/nfs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.{bz2,sign}
        id_resolver.conf
        nfs-utils_env.sh
        nfs.sysconfig
        exports
        #nfs-utils.conf 
)
install=nfs-utils.install
optdepends=('sqlite: for nfsdcltrack usage'
            'python: for nfsiostat and mountstats usage')
sha1sums=('f66ce79f6d2de8907e0e2f6b00f96d7486281cc5'
          'SKIP'
          '24b3c10b47dc120b2d252cf1e5001effa8f76a62'
          '519eaaebecf423dcea156ed6e2c4f722236df0b2'
          'e9648adc732f47f24da6d71e1729da5a7697e203'
          '170a929d9c0f22edb13b656648cadf372efea841')
validpgpkeys=('E1B71E339E20A10A676F7CB69AFB1D681A125177') # Steve Dickson

prepare() {
  cd ${pkgname}-${pkgver}

  # fix hardcoded sbin path to our needs
  sed -i "s|sbindir = /sbin|sbindir = /usr/bin|g" utils/*/Makefile.am
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-gss \
    --without-tcp-wrappers \
    --with-statedir=/var/lib/nfs \
    --enable-ipv6 \
    --enable-libmount-mount \
    --enable-mountconfig \
    --with-start-statd=/usr/bin/start-statd
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
 
  install -D -m 644 utils/mount/nfsmount.conf "$pkgdir"/etc/nfsmount.conf
  
  for i in systemd/{*.service,*.mount,*.target}; do
    install -D -m 644 $i "$pkgdir"/usr/lib/systemd/system/$(basename $i)
  done

  cd ..

  # distro specific daemon flags - files taken from Fedora ( = upstream developer)
  install -D -m 755 nfs-utils_env.sh "$pkgdir"/usr/lib/nfs-utils/nfs-utils_env.sh
  install -D -m 644 nfs.sysconfig    "$pkgdir"/etc/sysconfig/nfs
  
  # fix path - this will be fixed in 1.3.5rc2 and later
  sed -i "s:libexec:lib:" "$pkgdir"/usr/lib/systemd/system/nfs-config.service

  # empty exports file  
  install -D -m 644 exports          "$pkgdir"/etc/exports

  # config file for idmappers in newer kernels
  install -D -m 644 id_resolver.conf "$pkgdir"/etc/request-key.d/id_resolver.conf

  # load kernel module - should be obsolete
#  install -D -m 644 nfs-utils.conf   "$pkgdir"/usr/lib/modules-load.d/nfs-utils.conf

  mkdir "$pkgdir"/etc/exports.d
  mkdir -m 555 "$pkgdir"/var/lib/nfs/rpc_pipefs
  mkdir "$pkgdir"/var/lib/nfs/v4recovery

}
