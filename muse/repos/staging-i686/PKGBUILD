# $Id$
# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=muse
pkgver=1.0.1
pkgrel=2
pkgdesc="A MIDI/Audio sequencer with recording and editing capabilities"
arch=('i686' 'x86_64')
url="http://muse-sequencer.org/"
license=('GPL')
depends=('qt3' 'fluidsynth' 'graphviz' 'libsamplerate')
makedepends=('jade')
optdepends=('python2: to use the scripts')
options=('!libtool')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/lmuse/$pkgname-$pkgver.tar.gz")
md5sums=('6de5bf575d1c0eba92cada745675732e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  . /etc/profile.d/qt3.sh
  unset LDFLAGS
  ./configure --prefix=/usr --enable-maintainer-mode \
                            --disable-doxy-treeview \
                            --enable-optimize \
                            --enable-vst \
                            --disable-suid-build
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' ${pkgdir}/usr/share/muse/scripts/DoNothing
  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' ${pkgdir}/usr/share/muse/scripts/*
}
