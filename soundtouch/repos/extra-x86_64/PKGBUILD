# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=soundtouch
pkgver=1.6.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('i686' 'x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
options=('!libtool')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz
        soundtouch-1.4.0-mmx-sse-compile-fix.patch
        soundtouch-1.4.0-x86_64-asm-broken.patch)
md5sums=('aa1c63d4d67b033f044a6a48d2be5bdd'
         '108cd080b4b0ebe8ed1f950ff1315e7a'
         '0d856bb5b519b18c6ba3a2e1770bdfd2')
sha1sums=('9871dcbf75a68665a3515bb9dda4727beb0358e7'
          '7a245aacadcb3003e73c1c202c8de0ecc09283ac'
          '95282cca856b3b8645fadb530ce5eb892074f193')

build() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i "${srcdir}/soundtouch-1.4.0-mmx-sse-compile-fix.patch"
  patch -p1 -i "${srcdir}/soundtouch-1.4.0-x86_64-asm-broken.patch"

  [ "${CARCH}" = "x86_64" ] && CONFIGFLAG="--with-pic"
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
