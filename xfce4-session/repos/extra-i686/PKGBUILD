# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-session
pkgver=4.10.0
pkgrel=7
pkgdesc="A session manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck' 'libsm' 'xorg-iceauth' 'upower' 'xorg-xinit'
          'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
optdepends=('gnome-keyring: for keyring support when GNOME compatibility is enabled'
            'xscreensaver: for locking screen with xflock4'
            'gnome-screensaver: for locking screen with xflock4'
            'xlockmore: for locking screen with xflock4')
replaces=('xfce-utils')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/4.10/$pkgname-$pkgver.tar.bz2
        xfce4-session-4.10.0-add-systemd-support.patch
        xfce4-session-4.10.0-use-the-async-spawn-function-of-glib.patch
        xfce4-session-4.10.0-store-the-watch-function-id.patch)
sha256sums=('bb8aa9a74c3d382840596fb4875144d66c7f3f47c8e9ee81d31e3428a72c46ce'
            'ffae61c48a4bd0cb51d422cb93f2de9567abaf29085370c455ef349b6a10234b'
            '18ed175dd4242e39161a093045c6c6aebf9a408350652bde1454fe07411cdf3c'
            '5eb5319a38f58a2d518d0e3d24b6d01c487871711fb36b55cb03a10a4591cdcf')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=8729
  patch -Np1 -i "$srcdir/xfce4-session-4.10.0-add-systemd-support.patch"

  # https://bugzilla.xfce.org/show_bug.cgi?id=9709
  patch -Np1 -i "$srcdir/xfce4-session-4.10.0-use-the-async-spawn-function-of-glib.patch"
  patch -Np1 -i "$srcdir/xfce4-session-4.10.0-store-the-watch-function-id.patch"

  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.in

  xdt-autogen
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
