# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=3.20.2
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
arch=(i686 x86_64)
license=('GPL')
url="https://wiki.gnome.org/Apps/Maps"
depends=(geocode-glib geoclue2 gjs libchamplain gnome-themes-standard libgee folks gfbgraph)
makedepends=(intltool gobject-introspection)
groups=('gnome-extra')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('e860144795339fdbb2f1239c4db092ad12beb9acaa3f3f8aa1d935c36d86bc3f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
