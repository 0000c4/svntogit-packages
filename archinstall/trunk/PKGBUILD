# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgbase=archinstall
pkgname=('archinstall' 'python-archinstall')
pkgver=2.1.0
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=('any')
url="https://github.com/Torxed/archinstall"
license=('GPL')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgbase-$pkgver.tar.gz.asc")
md5sums=('4503023e83fd7c650ba1665b4258a6f4'
         'SKIP')
sha512sums=('0ac8e759b8cae9221004e6f3f2f27a76b6923d7479ab81731d1c5abece8eab52d990dbdcc447adda1c10b9734dbcc61eccaf8a3ed1e6bdf009ebffa102ee4075'
            'SKIP')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>


build() {
	cd "$pkgbase-$pkgver"

        python setup.py build
}


package_archinstall() {
        depends=('python-archinstall')
	cd "$pkgbase-$pkgver"

        mkdir -p "${pkgdir}/usr/bin"
        
        # Install a guided profile
	cat - > "${pkgdir}/usr/bin/archinstall" <<EOF
#!/bin/sh
python -m archinstall $@
EOF

	chmod +x "${pkgdir}/usr/bin/archinstall"
}

package_python-archinstall() {
	cd "$pkgbase-$pkgver"

        python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
