# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.22.2
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server 'gsettings-desktop-schemas>=3.21.2')
makedepends=(intltool python gnome-common appstream-glib git)
groups=(gnome-extra)
_commit=12967d01bb425fd340f1d33c73f62ad884765d65  # tags/3.22.2^0
source=("git+https://git.gnome.org/browse/gnome-calendar#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
