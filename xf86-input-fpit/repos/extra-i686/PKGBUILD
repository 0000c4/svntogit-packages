# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-fpit
pkgver=1.3.0
pkgrel=6
pkgdesc="X.Org Fujitsu Stylistic Tablet PC input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel')
conflicts=('xorg-server<1.9.0')
backup=(etc/X11/xorg.conf.d/50-fpit.conf)
groups=('xorg-input-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        fpit-1.3.0-abi.patch
        50-fpit.conf
        LICENSE)
md5sums=('e5bfda480f25121e3c4222e5e1eb5880'
         '9d8f1033924deb04edc1775bc82440f3'
         '85528d3a03a55b09cc891dfd3aa321cc'
         '2d8404d3112316f04cba864c915adfac')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/fpit-1.3.0-abi.patch
  ./configure --prefix=/usr
  make
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -m644 "${srcdir}/50-fpit.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
}
