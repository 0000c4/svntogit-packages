# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeutils
pkgname=('kdeutils-ark'
         'kdeutils-kcalc'
         'kdeutils-kcharselect'
         'kdeutils-kdelirc'
         'kdeutils-kdessh'
         'kdeutils-kdf'
         'kdeutils-kfloppy'
         'kdeutils-kgpg'
         'kdeutils-ktimer'
         'kdeutils-kwallet'
         'kdeutils-okteta'
         'kdeutils-printer-applet'
         'kdeutils-superkaramba'
         'kdeutils-sweeper')
pkgver=4.2.90
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeutils')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdelibs-experimental' 'kdepimlibs'
             'kdebase-lib' 'kdebase-runtime' 'kdebindings-python' 'qca' 
             'system-config-printer>=1.1.7-3' 'libarchive' 'qimageblitz')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('f6d94afde4844f2e32fddb1069b4f19c')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeutils-ark() {
	pkgdesc='Archiving tool'
	depends=('kdebase-runtime' 'kdebase-lib' 'libarchive')
	install='kdeutils.install'
	cd $srcdir/build/ark
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ark
	make DESTDIR=$pkgdir install
}

package_kdeutils-kcalc() {
	pkgdesc='scientific calculator'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kcalc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcalc
	make DESTDIR=$pkgdir install
}

package_kdeutils-kcharselect() {
	pkgdesc='select special characters from any fonts and put them into the clipboard'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kcharselect
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcharselect
	make DESTDIR=$pkgdir install
}

package_kdeutils-kdelirc() {
	pkgdesc='a KDE frontend for the Linux Infrared Remote Control system'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kdelirc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kdelirc
	make DESTDIR=$pkgdir install
}

package_kdeutils-kdessh() {
	pkgdesc='front end to ssh'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kdessh
	make DESTDIR=$pkgdir install
}

package_kdeutils-kdf() {
	pkgdesc='like df, a graphical free disk space viewer'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kdf
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kdf
	make DESTDIR=$pkgdir install
}

package_kdeutils-kfloppy() {
	pkgdesc='format a floppy disks with this program'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/kfloppy
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfloppy
	make DESTDIR=$pkgdir install
}

package_kdeutils-kgpg() {
	pkgdesc='graphical GPG frontend'
	depends=('kdebase-runtime' 'kdelibs-experimental' 'kdepimlibs')
	install='kdeutils.install'
	cd $srcdir/build/kgpg
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kgpg
	make DESTDIR=$pkgdir install
}

package_kdeutils-ktimer() {
	pkgdesc='execute programs after some time'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/ktimer
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ktimer
	make DESTDIR=$pkgdir install
}

package_kdeutils-kwallet() {
	pkgdesc='KDE wallet management tool'
	depends=('kdebase-runtime' 'kdelibs-experimental')
	install='kdeutils.install'
	cd $srcdir/build/kwallet
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kwallet
	make DESTDIR=$pkgdir install
}

package_kdeutils-okteta() {
	pkgdesc='binary file editor'
	depends=('kdebase-runtime' 'kdelibs' 'qca')
	install='kdeutils.install'
	cd $srcdir/build/okteta
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/okteta
	make DESTDIR=$pkgdir install
}

package_kdeutils-printer-applet() {
	pkgdesc='a system tray utility that shows current print jobs'
	depends=('kdebase-runtime' 'kdelibs' 'kdebindings-python' 'system-config-printer>=1.1.7-3')
	install='kdeutils.install'
	cd $srcdir/build/printer-applet
	make DESTDIR=$pkgdir install
}

package_kdeutils-superkaramba() {
	pkgdesc='version of Karamba with python scripting'
	depends=('kdebase-runtime' 'kdelibs' 'kdebindings-python' 'qimageblitz')
	install='kdeutils.install'
	cd $srcdir/build/superkaramba
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/superkaramba
	make DESTDIR=$pkgdir install
}

package_kdeutils-sweeper() {
	pkgdesc='cleans unwanted traces the user leaves on the system'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdeutils.install'
	cd $srcdir/build/sweeper
	make DESTDIR=$pkgdir install
}

