# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-composer
epoch=1
pkgver=1.0.2
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php')
makedepends=('git')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
md5sums=('SKIP'
         'fcce0b6badce67e98b1f444946d0d6ab')

prepare() {
	cd ${srcdir}/${pkgname}

	php ${srcdir}/${pkgname}-${pkgver}.phar -n install --no-dev
}

build() {
	cd ${srcdir}/${pkgname}

	php -d phar.readonly=Off bin/compile
}

check() {
	cd ${srcdir}/${pkgname}

	# check if the upstream build and ours differ
	diff -q ${srcdir}/${pkgname}-${pkgver}.phar composer.phar

	# Install the dev dependencies to run the unit tests
	bin/composer -n install
	vendor/bin/phpunit
}

package() {
	cd ${srcdir}/${pkgname}

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -Dm755 composer.phar ${pkgdir}/usr/bin/composer
}
