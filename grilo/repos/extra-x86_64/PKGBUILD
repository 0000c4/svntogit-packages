# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo
pkgver=0.3.10
pkgrel=1
pkgdesc="Framework that provides access to various sources of multimedia content"
url="https://wiki.gnome.org/Projects/Grilo"
arch=(x86_64)
license=(LGPL)
depends=(libxml2 libsoup liboauth totem-plparser)
makedepends=(gobject-introspection gtk-doc vala git meson gtk3)
optdepends=('grilo-plugins: Plugins for grilo'
            'gtk3: Test UI')
_commit=ec6a37b7474010e60a8949b8e7a38f828e3b005e  # tags/grilo-0.3.10^0
source=("git+https://gitlab.gnome.org/GNOME/grilo.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^grilo-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
