# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgbase=unrar
pkgname=('unrar' 'libunrar')
pkgver=5.5.5
pkgrel=1
epoch=1
arch=('i686' 'x86_64')
url="http://www.rarlab.com/rar_add.htm"
license=('custom')
makedepends=('hardening-wrapper')
source=("http://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz"
        'unrar-5.3.4-soname.patch')
sha256sums=('a4553839cb2f025d0d9c5633816a83a723e3938209f17620c8c15da06ed061ef'
            '255e928160e931bde39e9da48275c394d735386a4cc2c516c5a5b515d4b9af87')

prepare() {
  cd "${srcdir}/${pkgbase}"
  patch -Np1 -i "${srcdir}/unrar-5.3.4-soname.patch"
}

build() {
  cd "${srcdir}/${pkgbase}"
  cp -a ${srcdir}/unrar ${srcdir}/libunrar
  make -C "${srcdir}"/libunrar lib libversion=${pkgver} CXXFLAGS="-fPIC ${CXXFLAGS}"
  make CXXFLAGS="-fPIC ${CXXFLAGS}" STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=('gcc-libs')
  cd "${srcdir}/${pkgbase}"
  install -Dm755 unrar "${pkgdir}/usr/bin/unrar"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=('gcc-libs')
  cd "${srcdir}/libunrar"
  install -Dm755 libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.${pkgver}"
  install -Dm644 dll.hpp "${pkgdir}/usr/include/unrar/dll.hpp"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.5"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so"
  # install license
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
