# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=libbsd
pkgver=0.5.2
pkgrel=2
pkgdesc="Provides useful functions commonly found on BSD systems like strlcpy()"
arch=('i686' 'x86_64')
url="http://libbsd.freedesktop.org"
license=('custom')
depends=('glibc')
options=('!libtool')
source=(http://libbsd.freedesktop.org/releases/$pkgname-$pkgver.tar.xz{,.asc}
        spt.patch)
md5sums=('be8b2e0dc4614699834c49693574fd1a'
         'SKIP'
         '56236fb72c8ec6cbdbd0daa5f404bccb')

prepare() {
  cd $pkgname-$pkgver

  # Paper over a firefox crash
  # http://lists.freedesktop.org/archives/libbsd/2013-June/000085.html
  patch -Np1 -i ../spt.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
