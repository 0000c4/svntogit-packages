# $Id: PKGBUILD 17170 2008-10-26 11:01:19Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=ati-dri
pkgver=7.4.1
pkgrel=1
pkgdesc="DRI drivers for ATI/AMD videochips"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('MIT')
depends=('libdrm>=2.4.9' 'libgl>=7.4.1' 'expat>=2.0.1')
makedepends=('glproto>=1.4.9' 'pkgconfig' 'libxfixes' 'libxdamage' 'libxxf86vm' 'libxext' 'dri2proto>=2.0' 'mesa>=7.4.1')
conflicts=('xf86-video-ati<6.9.0-6')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${pkgver}.tar.bz2
	7.4-branch.patch
        mesa-7.1-link-shared.patch
        LICENSE)
md5sums=('423260578b653818ba66c2fcbde6d7ad'
         'd9dbc4717b6867d2ed7abba0a84ee577'
         'f0baa948d9810f268413111ee439d24b'
         '7ba429de367a2b3d273d2230cc705771')

build() {
  cd "${srcdir}/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/7.4-branch.patch" || return 1
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=radeon,r200,r300 \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
}
