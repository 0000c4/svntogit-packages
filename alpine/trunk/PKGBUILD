# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Fabio Scotoni <fabio@esse.ch>

pkgname=alpine
pkgver=2.00
pkgrel=1
pkgdesc="The Apache-licensed PINE (a tool for reading, sending, and managing electronic messages)"
arch=('i686' 'x86_64')
url="http://www.washington.edu/alpine/"
license=('APACHE')
depends=('libldap' 'heimdal>=1.2' 'gettext')
optdepends=('aspell: for spell-checking support')
provides=('pine')
conflicts=('pine')
replaces=('pine')
options=('!makeflags')
source=(ftp://ftp.cac.washington.edu/${pkgname}/${pkgname}.tar.bz2 \
	http://staff.washington.edu/chappa/alpine/patches/${pkgname}-${pkgver}/all.patch.gz)
md5sums=('84e44cbf71ed674800a5d57eed9c1c52' 'd849335759048cf0af92f764d93c939e')
sha1sums=('dcbd3c5419954f484ccf706feaba31ce48cdebc4' '3b3f8f74ea1dcc37316c892f40ad70aa1f898d59')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../all.patch || return 1
  ./configure --prefix=/usr --without-passfile --without-tcl \
    --disable-shared --with-system-pinerc=/etc/alpine.d/pine.conf \
    --with-system-fixed-pinerc=/etc/alpine.d/pine.conf.fixed || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
