# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kfind
pkgver=17.11.90
pkgrel=1
pkgdesc="Find Files/Folders"
arch=(x86_64)
url="https://www.kde.org/applications/utilities/kfind/"
license=(LGPL)
depends=(kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin python)
optdepends=('mlocate: search using mlocate index')
conflicts=(kdebase-kfind)
replaces=(kdebase-kfind)
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('92563824d2b72c11668c28267fc321b8155d4595a4bc9cdd241edbd33e9a8cb7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
