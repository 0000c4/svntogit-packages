# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgbase=python-isort
pkgname=('python-isort' 'python2-isort')
pkgver=4.3.3
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python2-futures')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/timothycrosley/isort/archive/$pkgver.tar.gz")
sha512sums=('0bec0e035f8d8d04c92f9d35471165a4c3ea79350dc385c5ae5bafb8618e78f09c6eb1952477642c66a2113c8a7cf44e5a793a1bc2eeac22545a2ba0eda33970')

prepare() {
  cp -a isort-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/isort-$pkgver
  python setup.py build

  cd "$srcdir"/isort-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/isort-$pkgver
  python setup.py test

  cd "$srcdir"/isort-$pkgver-py2
  python2 setup.py test
}

package_python-isort() {
  depends=('python-setuptools')

  cd isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-isort() {
  depends=('python2-setuptools' 'python2-futures')

  cd isort-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/isort{,2}
}
