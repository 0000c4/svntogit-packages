# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgbase=python-isort
pkgname=('python-isort' 'python2-isort')
pkgver=4.2.14
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/timothycrosley/isort/archive/$pkgver.tar.gz")
sha512sums=('0f7f211bde55a5fff5bd431187b3235030d39596865cbb9d431497a17dd987fcfe55388a42d11ee3ef3be44e560508ede79bb1246dfb72da64648f2e0e76c2ab')

prepare() {
  cp -a isort-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/isort-$pkgver
  python setup.py build

  cd "$srcdir"/isort-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/isort-$pkgver
  python setup.py test

  cd "$srcdir"/isort-$pkgver-py2
  python2 setup.py test
}

package_python-isort() {
  depends=('python-setuptools')

  cd isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-isort() {
  depends=('python2-setuptools')

  cd isort-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/isort{,2}
}
