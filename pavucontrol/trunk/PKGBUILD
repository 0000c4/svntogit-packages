# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pavucontrol
pkgdesc="A GTK volume control for PulseAudio"
pkgver=3.0
pkgrel=3
arch=(i686 x86_64)
url="http://freedesktop.org/software/pulseaudio/pavucontrol/"
license=(GPL)
depends=(libcanberra-pulse gtkmm3 libsigc++)
makedepends=(intltool lynx)
source=($url/$pkgname-$pkgver.tar.xz unavail.patch)
sha256sums=('b3d2ea5a25fc88dcee80c396014f72df1b4742f8cfbbc5349c39d64a0d338890'
            'c184782275e90ff24b647a8d690b46e12bd9d52bdb263689e2469a2dbd164235')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../unavail.patch
}

build() {
  cd $pkgname-$pkgver
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr --enable-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
