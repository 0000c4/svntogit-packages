# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=pavucontrol
pkgver=0.99.2
pkgrel=1
pkgdesc="A GTK volume control tool for PulseAudio"
arch=(i686 x86_64)
url="http://0pointer.de/lennart/projects/pavucontrol"
license=(GPL)
depends=(gnome-icon-theme libcanberra-pulse gtkmm3 libsigc++)
makedepends=(intltool lynx)
source=(http://freedesktop.org/software/pulseaudio/$pkgname/$pkgname-$pkgver.tar.gz
        pavucontrol.desktop)
sha256sums=('4a95065a4afb2a18142b60d6f0950171198372650391275dcdcd4671ba763cdf'
            '5eab8c78e92c39a71ac1ca4c4464a8915d3a29d91e1703fd51c24652b747eebe')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-gtk3
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/pavucontrol.desktop" \
    "$pkgdir/usr/share/applications/pavucontrol.desktop"
}
