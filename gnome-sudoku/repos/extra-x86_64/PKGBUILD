# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=3.20.1
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://wiki.gnome.org/Apps/Sudoku"
arch=(x86_64 i686)
license=(GPL)
depends=(dconf gtk3 qqwing libgee json-glib)
makedepends=(intltool itstool vala)
options=(!emptydirs)
groups=(gnome-extra)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('23f27323ec8c37b37c1829510e9b6e7e8288a4109d1b7c9017f2ada07fd6fdbb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -i 's_#! /usr/bin/python_#!/usr/bin/python2_' "$pkgdir/usr/bin/gnome-sudoku"
}
