# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=3.32.0
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://wiki.gnome.org/Apps/Sudoku"
arch=(x86_64)
license=(GPL)
depends=(dconf gtk3 qqwing libgee json-glib)
makedepends=(vala yelp-tools appstream-glib git meson)
groups=(gnome-extra)
_commit=2ecbfa7bc71a5c79716517634858eaa16b4f9957  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
