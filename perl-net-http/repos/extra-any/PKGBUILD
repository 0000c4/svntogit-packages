# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-net-http
pkgver=6.02
pkgrel=1
pkgdesc="Low-level HTTP connection (client)"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.8')
conflicts=('perl-libwww<6')
url=https://metacpan.org/release/Net-HTTP
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/Net-HTTP-${pkgver}.tar.gz")
md5sums=(b0fa0a246872ff8da48c65da6f5281fc)
sha512sums=(68181b794dea14090dbf419935ad20b294f337aadade96f3d81404f40c349faedafe4e2ca77f144b25508e498c79d49ed30a82a3d2262f01114af12a57655761)
_distdir="${srcdir}/Net-HTTP-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
