# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksane
pkgver=4.6.90
pkgrel=1
pkgdesc="An image scanning library"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'sane')
makedepends=('cmake' 'automoc4')
conflicts=('kdegraphics-libs')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('26b27920f02a6e6999c7bfd08fe6632a39fe2d53')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
