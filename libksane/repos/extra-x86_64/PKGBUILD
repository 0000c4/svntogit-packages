# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksane
pkgver=15.12.3
pkgrel=1
pkgdesc="An image scanning library"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libksane"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(ktextwidgets kwallet sane)
makedepends=(extra-cmake-modules)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('110c3d09908ccbdf0117c4c59457c4d73d77f3af')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
