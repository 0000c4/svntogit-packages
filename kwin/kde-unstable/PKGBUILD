# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=5.5.95
pkgrel=3
pkgdesc='KDE Window manager'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kwin'
license=('LGPL')
depends=('qt5-multimedia' 'kscreenlocker' 'knewstuff' 'xcb-util-cursor' 'hicolor-icon-theme' 'kdecoration' 'kinit' 'plasma-framework')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools' 'libinput')
optdepends=('libinput: for kwin-wayland')
groups=('plasma')
install=${pkgname}.install
conflicts=('kdebase-workspace')
source=("http://download.kde.org/unstable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
	kwin-dummy-screen.patch::"https://quickgit.kde.org/?p=kwin.git&a=commitdiff&h=51ee5142&o=plain")
md5sums=('1d2e0d2e8aba3b1bc96674c71cd2a265'
         'c1d901c7bd17175b853ecb110f5c7586')

prepare() {
  mkdir -p build

# Fix issues with kwin-wayland
  cd $pkgname-$pkgver
  patch -p1 -i ../kwin-dummy-screen.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
