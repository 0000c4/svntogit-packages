# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca> 
# Contributor: Denis (dtonator@gmail.com)

pkgname=xmoto
pkgver=0.5.0
pkgrel=1
pkgdesc="A challenging 2D motocross platform game, where physics play an important role."
arch=('i686' 'x86_64')
url="http://xmoto.tuxfamily.org"
license=('GPL')
depends=('bzip2' 'libjpeg' 'libpng' 'lua' 'sdl_mixer' 'ode=0.10.1' 'curl>=7.16.2' 
	 'mesa' 'sqlite3' 'sdl_ttf' 'desktop-file-utils'
	'sdl_net')
install=xmoto.install
source=(http://download.tuxfamily.org/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-src.tar.gz)
md5sums=('e693ec0f949e44fb78187b8e82650869')

build() {
  cd $srcdir/xmoto-$pkgver

  # build and install 
  ./configure --prefix=/usr --disable-sdltest || return 1

  make || return 1
  make DESTDIR=$startdir/pkg mangdir=/usr/share/man/man6 install || return 1

  # install desktop file
  install -Dm0644 $srcdir/xmoto-${pkgver}/extra/xmoto.desktop \
	$pkgdir/usr/share/applications/xmoto.desktop

  # install icon for desktop file
  install -Dm0644 $srcdir/xmoto-${pkgver}/extra/xmoto.xpm \
	$pkgdir/usr/share/pixmaps/xmoto.xpm
}

