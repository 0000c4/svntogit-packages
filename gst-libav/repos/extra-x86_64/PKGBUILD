# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gst-libav
pkgver=1.0.6
pkgrel=1
pkgdesc="Gstreamer libav Plugin"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gstreamer.freedesktop.org/"
depends=('gst-plugins-base-libs' 'bzip2')
makedepends=('yasm')
options=(!libtool !emptydirs)
provides=("gst-ffmpeg=$pkgver-$pkgrel")
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('8ab222a52bf7482e913f2c9a4f490cda8f8ed1acfbc429f27451b0558b08044d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --with-libav-extra-configure="--enable-runtime-cpudetect" \
    --with-package-name="GStreamer libav Plugin (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
