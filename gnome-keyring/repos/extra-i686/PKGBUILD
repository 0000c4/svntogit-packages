#$Id$
# Maintainer: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring
epoch=1
pkgver=3.20.0+16+gbf8aa97
pkgrel=1
pkgdesc="Stores passwords and encryption keys"
url="https://wiki.gnome.org/Projects/GnomeKeyring"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gcr' 'libcap-ng')
makedepends=('intltool' 'gtk-doc' gnome-common git python)
groups=('gnome')
options=('!emptydirs')
install=gnome-keyring.install
_commit=bf8aa977e4cadadeb12e950231837b339a525d32  # master
source=("git://git.gnome.org/gnome-keyring#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-keyring \
      --with-pam-dir=/usr/lib/security --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}
