# $Id$
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: MaÃ«l Lavault <moimael@neuf.fr>

pkgbase=abiword
pkgname=('abiword' 'abiword-plugins')
pkgver=3.0.0
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.abisource.com'
makedepends=('pkgconfig' 'asio' 'boost' 'fribidi' 'goffice' 'libwmf' 'wv'
             'link-grammar' 'gtkmathview' 'aiksaurus' 'libxslt' 'enchant'
             'libots' 'libwpg' 'librsvg' 'loudmouth' 'libsoup' 'psiconv'
             'redland' 'libical')
options=('!makeflags')
_plugins=('aiksaurus' 'applix' 'clarisworks' 'command' 'docbook' 'eml' 'epub'
          'freetranslation' 'gda' 'gdict' 'gimp' 'google' 'grammar' 'kword'
          'latex' 'loadbindings' 'mathview' 'mht' 'mif' 'opendocument' 'opml'
          'openwriter' 'ots' 'pdb' 'presentation' 'sdw' 'urldict' 'wmf' 'wml'
          'urldict' 'wikipedia' 'wordperfect' 'wpg' 'xslfo')
source=(http://abisource.com/downloads/$pkgbase/$pkgver/source/$pkgbase-$pkgver.tar.gz)
md5sums=('8d9c41cff3a8fbef8d0c835c65600e65')

build() {
  cd $pkgbase-$pkgver

  ./configure --prefix=/usr \
    --enable-shared \
    --disable-static \
    --enable-clipart \
    --enable-templates \
    --disable-default-plugins \
    --disable-builtin-plugins \
    --enable-plugins="$(echo $_plugins)"

  make -j1
}

package_abiword() {
  pkgdesc='Fully-featured word processor'
  depends=('fribidi' 'wv' 'goffice' 'librsvg' 'enchant' 'desktop-file-utils'
           'redland' 'libical' 'gtk-update-icon-cache')
  install=$pkgname.install
  optdepends=('abiword-plugins')
  conflicts=("abiword-plugins<${pkgver}-${pkgrel}")

  make -C $pkgbase-$pkgver DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/abiword-${pkgver%.*}/plugins/*.so
}

package_abiword-plugins() {
  pkgdesc='Additional plugins for Abiword'
  depends=("abiword=${pkgver}-${pkgrel}" 'loudmouth' 'libwpg' 'libwmf' 'link-grammar' 
           'gtkmathview' 'aiksaurus' 'libxslt' 'libsoup' 'libots' 'libgsf')

  for plugin in ${_plugins[@]}; do
    make -C $pkgbase-$pkgver/plugins/$plugin DESTDIR="$pkgdir" install
  done
}
