# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: MaÃ«l Lavault <moimael@neuf.fr>

pkgbase=abiword
pkgname=('abiword' 'abiword-plugins')
pkgver=2.8.6
pkgrel=3
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('pkgconfig' 'asio' 'boost' 'fribidi' 'goffice' 'libwmf' 'wv' 'link-grammar' 'gtkmathview' 'aiksaurus' 'libxslt' 'enchant' 'libots' 'libwpg' 'librsvg' 'loudmouth' 'libsoup')
options=('!makeflags' '!libtool')
source=("http://www.abisource.com/downloads/${pkgbase}/${pkgver}/source/${pkgbase}-${pkgver}.tar.gz")
sha1sums=('998f69d038000b3fc027d4259548f02d67c8d0df')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --enable-clipart --enable-templates --enable-collab-backend-xmpp --enable-collab-backend-tcp --enable-collab-backend-service --disable-collab-backend-sugar --enable-plugins --without-gnomevfs --with-gio --with-goffice --disable-static --with-psiconv-config=/nothere
  make
}

package_abiword() {
  pkgdesc="A fully-featured word processor"
  depends=('fribidi' 'wv' 'goffice' 'librsvg' 'enchant')
  optdepends=('abiword-plugins')
  conflicts=("abiword-plugins<${pkgver}-${pkgrel}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i plugins/Makefile \
      -e 's/ collab / /' \
      -e 's/ wpg / /' \
      -e 's/ wmf / /' \
      -e 's/ grammar / /' \
      -e 's/ mathview / /' \
      -e 's/ aiksaurus / /' \
      -e 's/ latex / /' \
      -e 's/ ots / /' \
      -e 's/ wordperfect / /'
  make DESTDIR="${pkgdir}" install
}

package_abiword-plugins() {
  pkgdesc="Additional plugins for Abiword"
  depends=("abiword=${pkgver}-${pkgrel}" 'loudmouth' 'libwpg' 'libwmf' 'link-grammar' 'gtkmathview' 'aiksaurus' 'libxslt' 'libsoup' 'libots')

  cd "${srcdir}/${pkgbase}-${pkgver}/plugins"
  for dir in collab wpg wmf grammar mathview aiksaurus latex ots wordperfect; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done
}
