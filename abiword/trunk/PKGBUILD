# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: MaÃ«l Lavault <moimael@neuf.fr>

pkgbase=abiword
pkgname=('abiword' 'abiword-plugins')
pkgver=2.8.2
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('pkgconfig' 'fribidi' 'enchant' 'librsvg' 'wv' 'libgsf-gnome' 'aiksaurus' 'gtkmathview' 'libsoup' 'link-grammar' 'libwpd' 'psiconv' 'libxslt' 'asio' 'loudmouth')
options=('!makeflags' '!libtool')
source=(http://www.abisource.com/downloads/abiword/${pkgver}/source/${pkgbase}-${pkgver}.tar.gz)
md5sums=('b44ffbebd5bd0f5f98f34dc8c688b4ed')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  sed -e 's/png12/png14/g' -i configure plugins/rsvg/Makefile.in plugins/garble/Makefile.in || return 1
  ./configure --prefix=/usr --enable-plugins || return 1
  make || return 1
  sed -e 's/src plugins user/src user/' -i Makefile || return 1
}

package_abiword() {
  pkgdesc="A fully-featured word processor"
  depends=('fribidi' 'enchant' 'librsvg>=2.26.0-2' 'wv>=1.2.4-4' 'libgsf-gnome>=1.14.16')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  mv ${pkgdir}/usr/share/icons ${pkgdir}/usr/share/pixmaps
}

package_abiword-plugins() {
  pkgdesc="Various plugins for Abiword"
  depends=("abiword>=${pkgver}" 'aiksaurus' 'gtkmathview' 'libsoup' 'link-grammar' 'libwpd' 'psiconv' 'libxslt' 'asio' 'loudmouth')
  cd "${srcdir}/${pkgbase}-${pkgver}/plugins"
  make DESTDIR="${pkgdir}" install || return 1
}
