# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=openbabel
pkgver=2.4.1
pkgrel=1
pkgdesc="A library designed to interconvert between many file formats used in molecular modeling and computational chemistry"
arch=('i686' 'x86_64')
url="http://openbabel.org/wiki/Main_Page"
license=('GPL')
depends=('gcc-libs' 'libxml2' 'libsm')
makedepends=('cmake' 'eigen' 'wxgtk2.8' 'boost')
optdepends=('wxgtk2.8: GUI interface')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('d9defcd7830b0592fece4fe54a137b99')

prepare() {
  mkdir -p build
}

build() {
  cd build
  export CXXFLAGS+=' -std=gnu++98'
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.8
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
