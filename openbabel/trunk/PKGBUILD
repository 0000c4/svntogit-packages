# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=openbabel
pkgver=2.3.0
pkgrel=2
pkgdesc="A library designed to interconvert between many file formats used in \
molecular modeling and computational chemistry"
arch=('i686' 'x86_64')
url="http://openbabel.org/wiki/Main_Page"
license=('GPL')
depends=('gcc-libs' 'libxml2' 'eigen')
makedepends=('cmake')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('effda01ed4a31d18d8e3d08191799608')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_GUI=OFF
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
