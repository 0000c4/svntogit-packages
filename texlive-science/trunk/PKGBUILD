# $Id: PKGBUILD$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=texlive-science
pkgver=2016.42453
_revnr=${pkgver#2016.}
pkgrel=1
pkgdesc="TeX Live - Typesetting for mathematics, natural and computer sciences"
license=('GPL')
arch=(any)
depends=('texlive-core')
optdepends=('python2-pygments: for pygmentex')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip"
        "$pkgname.maps"
        "$pkgname.fmts")
options=('!emptydirs')
md5sums=('7d37b05503b4f18c7c1861c6e1232bc6'
         '752529861a7b0a133f52a92866d0c1a2'
         '7573956c7abc3de68fbacb58805f690c')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   cd "$srcdir"
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps $pkgdir/var/lib/texmf/arch/installedpkgs/
   install -m644 $pkgname.fmts $pkgdir/var/lib/texmf/arch/installedpkgs/
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   install -m755 -d $pkgdir/usr/bin
   ln -s /usr/share/texmf-dist/scripts/pygmentex/pygmentex.py $pkgdir/usr/bin/pygmentex
   ln -s /usr/share/texmf-dist/scripts/ulqda/ulqda.pl $pkgdir/usr/bin/ulqda
}
