# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=linux-docs
pkgver=5.4
pkgrel=1
pkgdesc="Kernel hacker's manual"
url="https://www.kernel.org/doc/html/latest/"
arch=(any)
license=(GPL2)
makedepends=(xmlto python-sphinx python-sphinx_rtd_theme graphviz imagemagick)
source=(https://www.kernel.org/pub/linux/kernel/v${pkgver%%.*}.x/linux-$pkgver.tar.{xz,sign})
sha256sums=('bf338980b1670bca287f9994b7441c2361907635879169c64ae78364efc5f491'
            'SKIP')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E') # Greg Kroah-Hartman

build() {
  make -C linux-$pkgver htmldocs BUILDDIR="$srcdir/html"
}

package() {
  mkdir -p "$pkgdir"/usr/share/doc/linux/{txt,html}

  # Skip hidden files (no dotglob)
  cp -r linux-$pkgver/Documentation/* "$pkgdir/usr/share/doc/linux/txt"
  cp -r html/* "$pkgdir/usr/share/doc/linux/html"
}

# vim:set sw=2 et:
