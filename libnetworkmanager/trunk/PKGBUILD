# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgname=libnetworkmanager
pkgver=0.7.0rc1
pkgrel=1
pkgdesc="The Network Manager Library"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('dbus-glib>=0.76' 'nss>=3.12')
makedepends=('pkgconfig' 'perlxml' 'wireless_tools' 'iproute' 'dhcpcd' 'hal' 'libnl>=1.1' 'wpa_supplicant' 'policykit' 'ppp')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/NetworkManager/0.7/NetworkManager-0.7.0-rc1.tar.bz2)
md5sums=('8c6a9fee3546dca99ac84c9475faec39')

build() {
  cd "${srcdir}/NetworkManager-0.7.0"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --with-distro=arch --with-crypto=nss \
	      --with-dhcp-client=dhcpcd 
  for dir in marshallers libnm-util libnm-glib include; do
    pushd ${dir} || return 1
    make || return 1
    make DESTDIR="${pkgdir}" install || return 1
    popd
  done
}
