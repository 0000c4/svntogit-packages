# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libgsf
pkgver=1.14.13
pkgrel=1
pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats."
arch=(i686 x86_64)
url="http://www.gnome.org/"
license=('GPL' 'LGPL')
depends=('libxml2>=2.7.3' 'glib2>=2.20.1' 'bzip2')
makedepends=('perlxml' 'pkgconfig' 'python')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/1.14/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b522ddfdb72cdfee3dbf3576aed8c285')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/gsf-gnome//' -e 's/thumbnailer//' Makefile.* || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --mandir=/usr/share/man --disable-static \
              --without-gnome-vfs --without-bonobo  || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -f "${pkgdir}/usr/share/man/man1/gsf-office-thumbnailer.1"
}
