# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=mail-notification
pkgver=5.4
pkgrel=7
pkgdesc="Tray icon application that informs you if you have new mail"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/mailnotify/"
license=('GPL3' 'FDL')
depends=('cyrus-sasl-plugins' 'gmime' 'libnotify>=0.7.1' 'gnome-keyring' 'hicolor-icon-theme' 'notification-daemon' 'libgnomeui')
makedepends=('gob2' 'intltool' 'evolution' 'gnome-doc-utils')
options=('!libtool' '!emptydirs')
install=mail-notification.install
source=(http://savannah.nongnu.org/download/mailnotify/${pkgname}-${pkgver}.tar.bz2
        dont-update-cache.patch
        remove-ubuntu-special-case.patch
        gmime-2.4.patch
        evolution-gtkhtml.patch
        evolution.patch
        mail-notification-5.4-icons.patch
        mail-notification-5.4-camel_headers.patch)
md5sums=('c8dc33a61251acb5474e56eab6b18f43'
         '6007bc30e789dab0a8282038e0335eb9'
         '9cadd61bbd9c324b2916ec980231e0f2'
         '447cc20f035b9cf1a391027684ce1297'
         '500b2e42fbc5078e61407923a07b6e94'
         '46568640b7f97ab9ab7b54d49bee7e8e'
         '8f602891a694ceaa91f845d97ae51b39'
         'f79939f593b2e8659e302df72c2b54b1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/dont-update-cache.patch"
  patch -Np0 -i "${srcdir}/remove-ubuntu-special-case.patch"
  patch -Np1 -i "${srcdir}/gmime-2.4.patch"
  patch -Np1 -i "${srcdir}/evolution-gtkhtml.patch"
  patch -Np0 -i "${srcdir}/evolution.patch"
  patch -Np0 -i "${srcdir}/mail-notification-5.4-icons.patch"
  patch -Np1 -i "${srcdir}/mail-notification-5.4-camel_headers.patch"

  ./jb configure prefix=/usr sysconfdir=/etc \
      localstatedir=/var destdir="${pkgdir}" \
      gconf-schemas-dir=/etc/gconf/schemas install-gconf-schemas=yes \
      cflags="${CFLAGS}" cppflags="${CXXFLAGS}" ldflags="${LDFLAGS}" \
      library-mode=0755
  ./jb build
  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 ./jb install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain mail-notification ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
