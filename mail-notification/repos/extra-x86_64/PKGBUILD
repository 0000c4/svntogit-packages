# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=mail-notification
pkgver=5.4
pkgrel=1
pkgdesc="Tray icon application that informs you if you have new mail"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/mailnotify/"
license=('GPL3' 'FDL')
depends=('eel>=2.18.0-2' 'cyrus-sasl-plugins' 'gmime>=2.2.6' 'libnotify>=0.4.4'
         'gconf>=2.18.0.1-4' 'gnome-keyring' 'hicolor-icon-theme'
         'desktop-file-utils' 'notification-daemon' 'libgnomeui')
makedepends=('gob2' 'perlxml' 'evolution>=2.12' 'evolution-data-server'
             'gnome-doc-utils>=0.12.0')
options=('!libtool' '!emptydirs')
install=mail-notification.install
source=(http://savannah.nongnu.org/download/mailnotify/${pkgname}-${pkgver}.tar.bz2
        dont-update-cache.patch
        remove-ubuntu-special-case.patch)
md5sums=('c8dc33a61251acb5474e56eab6b18f43'
         '6007bc30e789dab0a8282038e0335eb9'
         '9cadd61bbd9c324b2916ec980231e0f2')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}

  patch -Np0 -i ../dont-update-cache.patch
  patch -Np0 -i ../remove-ubuntu-special-case.patch

  ./jb configure prefix=/usr sysconfdir=/etc \
              localstatedir=/var destdir=${startdir}/pkg \
              gconf-schemas-dir=/etc/gconf/schemas install-gconf-schemas=yes \
              cflags="${CFLAGS}" cppflags="${CXXFLAGS}" ldflags="${LDFLAGS}" \
              library-mode=0755 || return 1
  ./jb build || return 1
  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 ./jb install || return 1

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas \
    ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
} 
