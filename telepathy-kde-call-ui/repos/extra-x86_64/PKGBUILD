# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-call-ui
_pkgname=ktp-call-ui
pkgver=0.4.1
pkgrel=1
pkgdesc="Voice/Video Call UI for Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals' 'qt-gstreamer')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde-telepathy')
source=("http://download.kde.org/unstable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('e6cadf3bb6820ff015e49801a3b0196827dd9ab3')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
