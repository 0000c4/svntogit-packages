# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-call-ui
_pkgname=ktp-call-ui
pkgver=19.11.80
pkgrel=1
pkgdesc='Voice/Video Call UI for Telepathy'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kdenetwork telepathy-kde)
depends=(telepathy-kde-common-internals qt-gstreamer)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig}
         telepathy-qt-0.9.8.patch)
sha256sums=('508835258cdc4ebc577916e1e2b56869e904ae59c0164e2be28ce5317aa4b3dc'
            'SKIP'
            '29ae07bbe5409e765328f68a27a528a4ff3ab93a1ca3ac190fc76a7c314594ac')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $_pkgname-$pkgver
  patch -p1 -i ../telepathy-qt-0.9.8.patch # Fix build with telepathy-qt 0.9.8
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
