# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=ibus-typing-booster
pkgver=1.5.19
pkgrel=1
pkgdesc="Predictive input method for the IBus platform"
url="https://fedorahosted.org/ibus-typing-booster/"
arch=(any)
license=(GPL3)
depends=(ibus python-gobject python-dbus python-pyenchant m17n-lib sqlite)
makedepends=(git m17n-db hunspell hunspell-{de,el,en,es,fr,he,hu,it,nl,pl,ro})
_commit=4e2b44fd93de23d92c1a6a001291d3c7f27b67d1  # master
source=("git+https://git.fedorahosted.org/git/ibus-typing-booster#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  gzip --force --best "$pkgdir"/usr/share/$pkgname/data/*.{xml,txt,json}
}

