# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=dhcpcd
pkgver=4.0.4
pkgrel=1
pkgdesc="A DHCP client daemon"
arch=('i686' 'x86_64')
license=('BSD')
url="http://roy.marples.name/dhcpcd"
groups=('base')
depends=('glibc')
backup=('etc/conf.d/dhcpcd' 'etc/dhcpcd.conf')
source=(http://roy.marples.name/downloads/dhcpcd/$pkgname-$pkgver.tar.bz2 \
	'dhcpcd.conf.d')
options=('emptydirs') # we need the empty /var/lib/dhcpcd directory
md5sums=('f57a9614e592814485c47495c53e4c30'
         '372d33485556982b64a97f301e17c5dd')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  #disable DUID usage
  #echo "#undef ENABLE_DUID" >> config.h
  
# fix installation locations
  export PREFIX=/usr 
  sed -i 's/${PREFIX}\/etc/\/etc/' Makefile || return 1
  sed -i 's/\/db/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' dhcpcd-hooks/Makefile || return 1 
  sed -i 's/${PREFIX}\/sbin/\/sbin/' Makefile  || return 1

  make || return 1
  make DESTDIR=$pkgdir install || return 1
  
  install -d $startdir/pkg/usr/sbin || return 1
  ln -sf /sbin/dhcpcd $pkgdir/usr/sbin/dhcpcd || return 1
  
# install configuration file used in /etc/rc.d/network
  install -Dm644 ../dhcpcd.conf.d $pkgdir/etc/conf.d/dhcpcd || return 1

# get the license
  install -d $pkgdir/usr/share/licenses/$pkgname || return 1
  awk '{if(FNR<27)print $0}' $srcdir/$pkgname-$pkgver/config.h \
	>> $pkgdir/usr/share/licenses/$pkgname/license || return 1

# make man pages FHS compliant
  mv -f $pkgdir/usr/man $pkgdir/usr/share/man || return 1

# set options in /etc/dhcpcd.conf
  echo noipv4ll >> $pkgdir/etc/dhcpcd.conf || return 1 # disable ip4vall
}
