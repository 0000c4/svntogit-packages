# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=16.08.1
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://community.kde.org/KDE_PIM'
license=('GPL' 'LGPL' 'FDL')
depends=(libkgapi libkolab akonadi-calendar knotifyconfig kross kalarmcal kmbox kimap kde-syndication 
         hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost python kdesignerplugin)
conflicts=(kio-pim)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
        kdebug-364342.patch::"https://quickgit.kde.org/?p=kdepim-runtime.git&a=commitdiff&h=acb496c8&hp=5f67eab0&o=plain")
sha1sums=('dca69934f85f06fd3f06b4bd1f75e233e5e4d027'
          'b8c4ac8d6aa002b2f6215707ad692ca6c4dcb01c')

prepare() {
  mkdir -p build
# Fix crash in krunner http://bugs.kde.org/show_bug.cgi?id=364342
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-364342.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
