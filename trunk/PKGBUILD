# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-mozilla-ca
pkgver=20120309
pkgrel=1
pkgdesc="Mozilla's CA cert bundle in PEM format"
arch=(any)
license=(GPL2 LGPL2.1)
options=(!emptydirs)
depends=('perl>=5.006')
url=https://metacpan.org/release/Mozilla-CA
source=("http://search.cpan.org/CPAN/authors/id/A/AB/ABH/Mozilla-CA-$pkgver.tar.gz")
md5sums=(f7fba6902335c5e068e3a576e4dce9ef)
sha512sums=(fd2c5844bc20d2b9a7669b9905e520c0aaaffae5deac95018e4da36647822aad4d3c711c222bf16dc62c0780cf4ef76a89d208e8bd7dce36de864f9453ea64db)
_dir="$srcdir/Mozilla-CA-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
