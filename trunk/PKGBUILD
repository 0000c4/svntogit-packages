# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=2.4.2
pkgrel=1
pkgdesc='Media player based on BMP'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libxml2' 'libguess' 'audacious-plugins' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('unzip: zipped skins support')
provides=('audacious-player')
replaces=('audacious-player')
install=audacious.install
source=("http://distfiles.atheme.org/$pkgname-$pkgver.tgz")
sha1sums=('a5875a52ae47213a900ef1ded8fe0db3cbd0efc7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [[ $CARCH == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
  ./configure --prefix=/usr $extraopts
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
