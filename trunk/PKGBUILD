# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=sdl2_gfx
pkgver=5
pkgrel=1
pkgdesc="SDL Graphic Primitives (Version 2)"
arch=('i686' 'x86_64')
url="http://www.ferzkopp.net/joomla/software-mainmenu-14/4-ferzkopps-linux-software/19-sdlgfx"
license=('zlib')
depends=('sdl2')
options=(!libtool)
source=("sdl2-gfx::svn://svn.code.sf.net/p/sdl2gfx/code/trunk#revision=$pkgver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/sdl2-gfx"
  svnversion | tr -d [A-z]
}

build() {
  cd "${srcdir}/sdl2-gfx"

  ./autogen.sh
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/sdl2-gfx"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
