# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=inkscape
pkgver=0.48.0
pkgrel=1
pkgdesc='Vector graphics editor using the SVG file format'
url='http://inkscape.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gc' 'gtkmm' 'poppler-glib' 'pyxml' 'libxslt' 'gsl' 'popt'
         'gtkspell' 'imagemagick' 'desktop-file-utils' 'xdg-utils')
makedepends=('boost' 'pkg-config' 'intltool')
options=('!libtool')
install=inkscape.install
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('220ade981069a91d3afab9cbff75f65c75119da2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --with-python \
    --with-perl \
    --without-gnome-vfs \
    --with-xft \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
