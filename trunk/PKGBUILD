# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
pkgver=5.23.0
pkgrel=2
pkgdesc='Support for downloading application assets from the network'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kio')
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        kdebug-364896.patch::"https://quickgit.kde.org/?p=knewstuff.git&a=commitdiff&h=fc2e7f58&o=plain")
md5sums=('2ac172c3af820c701e8e144e7f9d35db'
         'a8fbc7f6e60f5d3de3e88641d889f138')

prepare() {
  mkdir -p build

# Fix crash in Discover http://bugs.kde.org/show_bug.cgi?id=364896
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-364896.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
