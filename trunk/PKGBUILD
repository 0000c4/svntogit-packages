# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kxmlgui
pkgver=5.35.0
pkgrel=2
pkgdesc='User configurable main windows'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kglobalaccel' 'ktextwidgets' 'attica-qt5')
makedepends=('extra-cmake-modules' 'python' 'mesa')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        kdebug-356489a.patch::"https://cgit.kde.org/kxmlgui.git/patch/?id=78e572da"
        kdebug-356489b.patch::"https://cgit.kde.org/kxmlgui.git/patch/?id=1bc3a0b4")
sha256sums=('a59df51f7ae514ff94d3989707d20e761569cc819d834182b7f56810305060e8'
            'SKIP'
            '6459e6d428d072156b5d8fa3fa33be4aca9c5f03946b571ceefe48e530dc39ea'
            '7fb0ad50ee09efe753dbc823f6900625a1f6bdce6a376ec1c9baa52820767883')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

# Revert commits that break QML applications https://bugs.kde.org/show_bug.cgi?id=356489
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-356489a.patch
  patch -p1 -i ../kdebug-356489b.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
