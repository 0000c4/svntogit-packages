#Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=sdl2_image
pkgver=2.0.0
pkgrel=1
pkgdesc="A simple library to load images of various formats as SDL surfaces (Version 2)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('sdl2' 'libpng' 'libtiff' 'libjpeg' 'libwebp')
makedepends=('cmake')
options=(!libtool)
source=("http://www.libsdl.org/tmp/SDL_image/release/SDL2_image-${pkgver}.tar.gz")
sha1sums=('1a37f0f63421bd1b86dd6ddabcb04fa8f28874ce')

build() {
  cd "${srcdir}/SDL2_image-${pkgver}/"
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/SDL2_image-${pkgver}/"

  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
