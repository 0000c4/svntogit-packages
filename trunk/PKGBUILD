# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-weather
pkgver=3.32.0
pkgrel=2
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL)
depends=(gtk3 gjs libgweather geoclue2 gnome-desktop)
makedepends=(gobject-introspection appstream-glib git meson)
groups=(gnome)
_commit=0ca6ff30ad42246ac62f42d86d978f029266f724  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-weather.git#commit=$_commit"
        hack-detached.diff)
sha256sums=('SKIP'
            '47bc9707d7e768aed06ce7feafd038c4d95a8c84fee58ec3bc38a35425eb27be')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Hack around broken detached locations
  patch -Np1 -i ../hack-detached.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
