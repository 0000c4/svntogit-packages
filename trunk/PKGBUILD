# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-video-effects
pkgver=0.4.3+13+g6867955
pkgrel=1
pkgdesc="Collection of GStreamer effects for GNOME"
arch=(any)
url="https://git.gnome.org/browse/gnome-video-effects"
license=(GPL2)
depends=(gst-plugins-good gst-plugins-bad frei0r-plugins)
makedepends=(intltool gnome-common git)
_commit=68679559275ebcfe17b7591687268514b7d0b2b4  # master
groups=(gnome)
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # Add missing tag for pkgver()
  git tag -f 0.4.3 f58ea2b2c88403ec1f767da5db85e05a1babaabe
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
