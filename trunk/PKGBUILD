# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=cryptsetup
pkgver=1.3.1
pkgrel=2
pkgdesc="Userspace setup tool for transparent encryption of block devices using the Linux 2.6 cryptoapi"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper>=2.02.85-2' 'libgcrypt' 'popt')
conflicts=('mkinitcpio<0.7')
options=('!libtool' '!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        encrypt_hook
	encrypt_install)
sha256sums=('7ceb18a0c91fa1546077b41b93463dd2ec9d7f83e6fd93757fb84cc608206a6a'
            'b8269d358363eb8d67d1ffa1469068e454a89154636283bcc3edc6486b7fff4e'
            'd4380195351b70abf8fcb3cd19461879c55a7a07e4915d1f0365b295b112a573')

build() {
  cd $srcdir/$pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static --sbindir=/sbin --libdir=/lib
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver}
  make DESTDIR=$pkgdir install
  # install hook
  install -D -m644 $srcdir/encrypt_hook $pkgdir/lib/initcpio/hooks/encrypt
  install -D -m644 $srcdir/encrypt_install $pkgdir/lib/initcpio/install/encrypt
  # Fix pkgconfig location
  install -d -m755 $pkgdir/usr/lib
  mv $pkgdir/lib/pkgconfig $pkgdir/usr/lib/
}
