# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=db
pkgver=4.7.25
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=(i686 x86_64)
license=('custom')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
groups=('base')
depends=('gcc-libs' 'coreutils')
options=('!libtool')
source=(http://download-uk.oracle.com/berkeley-db/db-$pkgver.tar.gz)
install=(db.install)
md5sums=('ec2b87e833779681a0c3a814aa71359e')

build() {
  cd ${srcdir}/$pkgname-$pkgver/build_unix
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --enable-static --enable-cxx
  make LIBSO_LIBS=-lpthread || return 1
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/docs
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
