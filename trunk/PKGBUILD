# $Id: PKGBUILD 259288 2016-02-14 08:43:40Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=foomatic-db-engine
arch=('i686' 'x86_64')
pkgver=4.0.12
pkgrel=1
epoch=4
url="http://www.linuxprinting.org/foomatic.html"
pkgdesc="Foomatic - Foomatic's database engine generates PPD files from the data in Foomatic's XML database. It also contains scripts to directly generate print queues and handle jobs."
license=('GPL2')
depends=('perl' 'libxml2' "cups-filters>=1.0.42" 'bash' 'perl-xml-libxml' 'perl-clone' 'perl-dbi')
makedepends=('cups' 'ghostscript' 'a2ps' 'wget' 'perl-xml-libxml' 'foomatic-db') # 'enscript' 'net-snmp'
optdepends=('foomatic-db:	linuxprinting.org xml files database to create ppd driver files'
            #'foomatic-db-ppds:	prebuilt and compressed linuxprinting.org database files'
            #'foomatic-db-gutenprint:	gutenprint xml files database to create ppd driver files'
)
backup=('etc/foomatic/defaultspooler')
#options=('!emptydirs')

# check http://bzr.linuxfoundation.org/loggerhead/openprinting/foomatic/
# for latest commits and development, almost identical to foomatic-4.0 branch
# foomatic-db-engine::bzr+http://bzr.linuxfoundation.org/openprinting/foomatic-4.0/foomatic-db-engine/#revision=277   #2015-06-16 the same as devel tree rev.371
# foomatic-db-engine::bzr+http://bzr.linuxfoundation.org/openprinting/foomatic/foomatic-db-engine/#revision=371       #2015-06-16

source=(http://www.openprinting.org/download/foomatic/$pkgname-$pkgver.tar.gz)
md5sums=('b8049e61a3dad76044e83dde0087e0a0')

build() {
  export LIB_CUPS="/usr/lib/cups"
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  eval `perl -V:archname`
  make DESTDIR=${pkgdir} \
       INSTALLARCHLIB=/usr/lib/perl5/vendor_perl/ \
       INSTALLSITELIB=/usr/lib/perl5/vendor_perl/ \
       INSTALLSITEARCH=/usr/lib/perl5/vendor_perl/ install

  # preset default spooler
  echo cups > ${pkgdir}/etc/foomatic/defaultspooler

  # cleanup
  rm -rf ${pkgdir}/usr/lib/perl5/vendor_perl/auto #Foomatic/
}
