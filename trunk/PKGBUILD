# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=postgresql-old-upgrade
pkgver=8.4.8
_majorver=${pkgver%.*}
pkgrel=1
pkgdesc="Minimal PostgreSQL build for migrating between major versions with pg_upgrade"
arch=('i686' 'x86_64')
url="http://www.postgresql.org/"
license=('custom:PostgreSQL')
depends=("postgresql-libs>=${_majorver}" 'libxml2' 'openssl>=1.0.0')
source=(ftp://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2
        build.patch)

build() {
  cd "${srcdir}/postgresql-${pkgver}"

  # patch to remove regress/test make target (won't build with it present)
  patch -Np1 -i ../build.patch

  ./configure --prefix=/opt/pgsql-${_majorver} \
  --with-libxml --with-openssl --without-perl \
  --without-python --with-pam --without-readline \
  --with-system-tzdata=/usr/share/zoneinfo --disable-nls \
  --enable-thread-safety

  make -C src all
}

package() {
  cd "${srcdir}/postgresql-${pkgver}"

  # install
  make DESTDIR="${pkgdir}" -C src install

  # install license
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # clean up unneeded installed items
  rm -rf "${pkgdir}/opt/pgsql-${_majorver}/include/"
}

md5sums=('4603e8ea30cee97189b62b39022f2043'
         '4d74f4227dc5e12bf95b3490758d86c9')
sha256sums=('3e90ef2a86a9f4831e21ff4a574fbfb8e1b8c097af637c93ec43c64c684a0938'
            '1f020ae1d921a6f47e4889c2026c5ef69879d204e14aeac9330f89b7e6088348')
