# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-dict
pkgver=0.8.2
pkgrel=1
pkgdesc="A dictionary plugin for the Xfce panel"
arch=('x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-dict"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'librsvg' 'hicolor-icon-theme')
makedepends=('intltool')
provides=('xfce4-dict-plugin')
conflicts=('xfce4-dict-plugin')
replaces=('xfce4-dict-plugin')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('324d56128bfc2f06ad8df59a832565981ec9b360710319300185517d98b7db0a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
