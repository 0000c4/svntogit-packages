# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=kile
pkgver=2.9.92
pkgrel=3
pkgdesc="A user friendly TeX/LaTeX frontend for KDE"
arch=('x86_64')
license=('GPL2')
url="https://kile.sourceforge.net/"
depends=('ktexteditor' 'kinit' 'okular' 'khtml' 'texlive-core' 'qt5-script')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('konsole: embedded terminal')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
         kdebug-400191.patch::"https://cgit.kde.org/kile.git/patch/?id=f9de688e")
sha256sums=('177372dc25b1d109e037a7dbfc64b5dab2efe538320c87f4a8ceada21e9097f2'
            '934fcf02e8f5ceeb46dc2ecb9010f929aa7d2d8ac81d2761d1464bb2d08e8781')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-400191.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
