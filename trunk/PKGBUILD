# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20120731
pkgrel=1
pkgdesc='Outline and bitmap font editor'
arch=('i686' 'x86_64')
url='http://fontforge.sourceforge.net/'
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2' 'libspiro')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/project/fontforge/fontforge-source/fontforge_full-${pkgver}.tar.bz2"
        'stamp.patch')
sha1sums=('b44548813d937d41dd7c1005dadd86cf7339f530'
          '13603ee42e4cd8f6bcbb32638769facbaa2dde43')

# git clone git://fontforge.git.sourceforge.net/gitroot/fontforge/fontforge; cd fontforge; git archive --prefix=${pkgname}-${pkgver}/ master | xz > ../${pkgname}-${pkgver}.tar.xz
# source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz")

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/python /python2 /g' Makefile.dynamic.in
	patch -p1 -i ../stamp.patch
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-type3 \
		--enable-devicetables \
		--enable-double \
		--with-regular-link \
		--with-python=python2 \
		--enable-pyextension \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
