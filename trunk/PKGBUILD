# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=3.20.0+288+gce2340dc
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
url="https://wiki.gnome.org/Apps/Seahorse"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gcr libsecret libsoup gpgme gnome-keyring)
makedepends=(libldap yelp-tools gobject-introspection vala git meson)
provides=(x11-ssh-askpass)
_commit=ce2340dc80820a130f9a65d0084e54443cc9c82d  # master
source=("git+https://git.gnome.org/browse/seahorse#commit=$_commit"
        0001-LDAP-source-Fix-non-debug-build.patch)
sha256sums=('SKIP'
            '7c45e55f9d98510e1f9305af71d9a11d66fd15100ed29239493c94d591eb9e98')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../0001-LDAP-source-Fix-non-debug-build.patch
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
