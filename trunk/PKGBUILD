# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=4.2.0
pkgrel=1
pkgdesc="data abstraction layer; with mysql, pgsql, xml, sqlite providers"
arch=(i686 x86_64)
license=('GPL')
depends=('glib2>=2.26.0' 'libxslt' 'db>=4.8' 'ncurses>=5.7' 'libsoup>=2.32.0'
         'libmysqlclient>=5.1.50' 'postgresql-libs>=8.4.4' 'sqlite3>=3.7.2')
makedepends=('intltool' 'pkgconfig')
options=('!libtool' '!makeflags')
url="http://www.gnome-db.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/4.2/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('2e639957f50719957cea5326b0d95511794322a54f9ef8547d3241f409765441')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/python/python2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --enable-system-sqlite
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
