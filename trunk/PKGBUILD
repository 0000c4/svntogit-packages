# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.1.17
pkgrel=3
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('grilo')
makedepends=('gupnp-av' 'libgdata' 'libquvi' 'sqlite3' 'gmime' 'libgcrypt'
             'rest' 'libtracker-sparql')
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'libquvi: Youtube plugin'
            'sqlite3: Podcasts plugin'
            'gmime: Podcasts plugin'
            'sqlite3: Bookmarks plugin'
            'sqlite3: Metadata store plugin'
            'libgcrypt: Vimeo plugin'
            'rest: Blip.tv plugin'
            'libtracker-sparql: Tracker plugin')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('c77f70c02ab8a9abb7cddc909f90853d13e2a18d4b69db65a411082c3ef79b0a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # support tracker 0.12, already fixed in git master.
  sed -i 's/sparql-0.11/sparql-0.12/g' configure

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
