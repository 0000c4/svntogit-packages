# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.11.5
pkgrel=3
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(i686 x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(libyaml libxml2 libstemmer glib2)
makedepends=(meson xmlto gobject-introspection python2 gtk-doc qt5-base itstool vala)
source=("https://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz"{,.asc}
        update-appstream-cache.hook
        appstream-fix-screenshots.patch::"https://github.com/ximion/appstream/commit/207b0da3.patch"
        fix-glib-errors-1.patch::"https://github.com/ximion/appstream/commit/933f1601.patch"
        fix-glib-errors-2.patch::"https://github.com/ximion/appstream/commit/376d314b.patch")
sha256sums=('d87775f21b49424c6a6e4dae9c2d594cc228827415bcdb0aae96eb9a0cbfd4ae'
            'SKIP'
            'a6c42e4607ffd7d7dc931d608f1856e2ba7689d4caac0783e7100d86de0f951b'
            'c5268588fda6bfdd62d001e3cd9298c09035fc5bd80525d36939a0bf422b2086'
            '953a872b5e11f189231f66dade76056c7df6547aa22fe761f1fdb2fce453ef11'
            '1bbb455daf026d85539d6df35f9ea10347ebba8ea56afbf076d7fe9d7fe962e1')
validpgpkeys=(D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB) # Matthias Klumpp <matthias@tenstral.net>

prepare() {
  mkdir -p build

  cd AppStream-$pkgver
  patch -p1 -i ../appstream-fix-screenshots.patch # Fix writing screenshot info to xml
  patch -p1 -i ../fix-glib-errors-1.patch # fix Glib errors when refreshing cache
  patch -p1 -i ../fix-glib-errors-2.patch
}

build() {
  cd build

  meson ../AppStream-$pkgver \
    --prefix=/usr \
    -Denable-qt=true \
    -Denable-vapi=true
  ninja
}

package_appstream() {
  cd build

  DESTDIR="$pkgdir" ninja install

# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppStreamQt,lib/cmake,lib/libAppStreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-cache.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-cache.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build
  DESTDIR="$pkgdir" ninja install

# provided by appstream
  rm -r "$pkgdir"{/etc,/usr/{bin,include/appstream,lib/{girepository-1.0,libappstream*,pkgconfig},share}}
}
