# $Id$
# Maintainer: Kevin <kevin@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-test-pod
pkgver=1.46
pkgrel=1
pkgdesc="Check for POD errors in files"
arch=('any')
url="http://search.cpan.org/dist/Test-Pod/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/D/DW/DWHEELER/Test-Pod-${pkgver}.tar.gz)
md5sums=('fd76af5f32b4a5991a233ac9b1c4aaea')

build() {
  cd Test-Pod-${pkgver}
  perl Build.PL installdirs=vendor
  ./Build
}

check() {
  cd Test-Pod-${pkgver}
  ./Build test
}

package() {
  cd Test-Pod-${pkgver}
  ./Build install destdir="${pkgdir}"
}
