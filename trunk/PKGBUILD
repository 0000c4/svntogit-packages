# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=mkvtoolnix
pkgname=('mkvtoolnix-cli' 'mkvtoolnix-gtk')
pkgver=5.0.1
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
makedepends=('libmatroska' 'flac' 'libvorbis' 'file' 'wxgtk'\
             'boost-libs' 'lzo2' 'xdg-utils' 'boost' 'ruby')
source=("http://www.bunkus.org/videotools/${pkgbase}/sources/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('93fbbe946de6013eca699c0c2a93a4e9')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Disable automagic curl dep used for online update checking
  sed -i -e '/curl/d' configure.in
  export CURL_CFLAGS="" CURL_LIBS=""

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  autoreconf

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib
  rake
}

package_mkvtoolnix-cli() {
  pkgdesc="Set of tools to create, edit and inspect Matroska files - CLI version"
  depends=('libmatroska' 'expat' 'flac' 'libvorbis' 'file' 'boost-libs' 'lzo2')
  provides=('mkvtoolnix')
  conflicts=('mkvtoolnix')
  replaces=('mkvtoolnix')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  rake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/bin/mmg
  rm -rf "${pkgdir}"/usr/share/man/ja/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/nl/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/zh_CN/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/applications
  rm -rf "${pkgdir}"/usr/share/icons
  rm -rf "${pkgdir}"/usr/share/mime
}

package_mkvtoolnix-gtk() {
  pkgdesc="Set of tools to create, edit and inspect Matroska files - wxGTK GUI"
  depends=("mkvtoolnix-cli=${pkgver}" 'wxgtk' 'xdg-utils')
  install=mkvtoolnix.install

  cd "${srcdir}/${pkgbase}-${pkgver}"

  rake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/bin/mkv*
  rm -rf "${pkgdir}"/usr/share/doc
  rm -rf "${pkgdir}"/usr/share/locale
  rm -rf "${pkgdir}"/usr/share/man/ja/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/nl/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/zh_CN/man1/mkv*
}
