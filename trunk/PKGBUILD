# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=texmacs
pkgver=1.0.7.15
pkgrel=1
pkgdesc="Free scientific text editor, inspired by TeX and GNU Emacs. WYSIWYG editor TeX-fonts and CAS-interface (Giac, GTybalt, Macaulay 2, Maxima, Octave, Pari, Qcl, R and Yacas) in one."
arch=('i686' 'x86_64')
url="http://www.texmacs.org/"
license=('GPL')
depends=('perl' 'guile' 'texlive-core' 'python2' 'libxext' 'freetype2' 'shared-mime-info' 'desktop-file-utils')
# do not remove texlive-core dependency, as it is needed!
optdepends=('transfig: convert images using fig2ps' 'awk: conversion of some files')
#source=(ftp://ftp.texmacs.org/pub/TeXmacs/targz/TeXmacs-${pkgver}-src.tar.gz)
source=(http://www.texmacs.org/Download/ftp/tmftp/source/TeXmacs-${pkgver}-src.tar.gz)
install=texmacs.install
sha1sums=('d75d55fcf0c06bf7fd5d9afd1031b9cf8413cbd3')

build() {
  cd ${srcdir}/TeXmacs-${pkgver}-src
  sed -i 's/env python/env python2/' \
    plugins/{mathematica/bin/realpath.py,python/bin/tm_python,sage/bin/tm_sage}
  sed -i 's/"python"/"python2"/' plugins/python/progs/init-python.scm
  sed -i '/^LDPATH/d' src/makefile.in
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --libexecdir=/usr/lib \
	      --disable-qt
  make
}

package() {
  cd ${srcdir}/TeXmacs-${pkgver}-src
  make DESTDIR=${pkgdir} install

 # fix fig2ps script
 sed -i 's|${prefix}|/usr|' ${pkgdir}/usr/bin/fig2ps
}
