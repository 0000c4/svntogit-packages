# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20200516
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="https://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('9882251a96761935b07057c43485e74223705601df6a1860979290ba4272ee6e'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'f57a78bf4bff116342469de8a3487b4280a69e9d21300c607f7bbc7f426cb9f6'
            'ba4af616869d632251a1d66797fcefcd834ddcbe63d42c5c8af85a93d565c0e0'
            '340a5f2ee2a528725aa2a845146f9ac3034a4d799638a811f9445005aa006c2d'
            'c2712373abb22d0ca22730509728e754a6967a92847a77353ee22c0ba962a7cf'
            '811451a1c2a131ca5a54b26a873bd4db07b3174843004008176003b9faad99b8'
            '951ce95c1438f7ac9dc58e5979c035950aa2c0c509852757ebf41d8bdde244d8'
            'c528847a94ac571f55cfdffa6daeb11d15709c7f653c8e117756d103570645c1'
            '2bd5fc1b3df00bd5040da07d117e2a8d7baf24ab9be6e4b1c11ce52056432769')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
