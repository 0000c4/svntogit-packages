# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=lftp
pkgver=4.4.1
pkgrel=1
pkgdesc="Sophisticated command line based FTP client"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gcc-libs' 'readline>=6.2' "gnutls" "expat>=2.0.1-4" 'sh')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
url="http://lftp.yar.ru/"
backup=('etc/lftp.conf')
source=(http://ftp.yars.free.net/pub/source/lftp/${pkgname}-${pkgver}.tar.bz2
       lftp-4.3.8-gets.patch)
md5sums=('1a6de8e3922ffb51ec21642e51744b02'
         '77168a7f368b07eec7e079b55496fa97')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/lftp-4.3.8-gets.patch
  ./configure --prefix=/usr --with-gnutls \
	--without-openssl --disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/lib
}
