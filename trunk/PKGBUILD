# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=2.29.91
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libwebkit>=1.1.22' 'iso-codes>=3.10.1' 'desktop-file-utils' 'libxslt>=1.1.24' 'libnotify>=0.4.5' 'avahi>=0.6.25' 'libsoup-gnome>=2.29.91' 'nss>=3.12.4' 'ca-certificates')
makedepends=('gnome-doc-utils>=0.19.5' 'pkgconfig' 'networkmanager' 'intltool' 'startup-notification')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.29/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('20efe5f8dcd19ed1754ebc48a9e2a8c26440e8fa94e4163f2b2e4943442a2a80')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-network-manager || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain epiphany ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
