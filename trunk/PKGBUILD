# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=2.29.90
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=(i686 x86_64)
license=('LGPL')
depends=('gconf>=2.28.0' 'libsoup-gnome>=2.29.90' 'gnome-icon-theme')
makedepends=('pkgconfig' 'intltool' 'gtk-doc')
options=('!libtool' '!emptydirs')
conflicts=('gnome-applets<2.22.0')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.29/${pkgname}-${pkgver}.tar.bz2
        01_gettext_not_xml.patch)
sha256sums=('a79fed235f6bbc3d92f175f64ffe2e1e1e6f513bfed2c3898524e882f0b53493'
            '64c181675714a411c2bc31d254cf5ad4a128fba539839b41783540e978a1de20')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/01_gettext_not_xml.patch" || return 1
  libtoolize --force || return 1
  gtkdocize || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-locations-compression || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgweather ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
