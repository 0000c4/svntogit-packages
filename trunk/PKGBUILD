# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.32.1
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas gnome-control-center libdazzle)
makedepends=(python appstream-glib git meson gtk-doc)
optdepends=('evolution: ICS file import')
groups=(gnome)
_commit=f9f46a2f3540b26bebf552bba138ef80f1c03a83  # tags/3.32.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#commit=$_commit"
        null-icaltime.diff)
sha256sums=('SKIP'
            'c1aa738a4ff275f725d0aa5406312600503b2b59270448a9e6b30b82a924dc27')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../null-icaltime.diff
}

build() {
  arch-meson $pkgname build -D documentation=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
