# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: arjan <arjan@archlinux.org>
# Contributor: Brice Carpentier <brice@dlfp.org>
# Contributor: Andreas Zwinkau <beza1e1@web.de>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=empathy
pkgver=2.24.0
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('enchant>=1.4.2' 'telepathy-mission-control>=4.61' 'iso-codes' 'evolution-data-server>=2.24.0')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils>=0.14.0' 'gnome-panel>=2.24.0')
optdepends=('telepathy-gabble: XMPP/Jabber'
            'telepathy-butterfly: MSN'
	    'telepathy-idle: IRC'
	    'telepathy-salut: Link-local XMPP'
	    'telepathy-sofiasip: SIP'
	    'telepathy-haze: libpurple')
options=(!libtool !emptydirs)
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('89e6dc430ad198c8044f261645d8bc6c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-voip=yes \
              --libexecdir=/usr/lib/empathy || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
       DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
