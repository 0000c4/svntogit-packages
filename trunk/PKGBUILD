# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing () gmail () com>

pkgname=clazy
pkgver=1.3
pkgrel=3
pkgdesc="Qt oriented code checker based on clang framework"
url="https://www.kdab.com/"
license=(GPL2)
depends=(clang)
makedepends=(cmake llvm)
arch=(x86_64)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        clazy-clang7.patch::"https://cgit.kde.org/clazy.git/patch/?id=c1074481")
sha256sums=('0a9470d653e1c73e06db1421fa922f6ea286408de5abc98e4743b9aee5655188'
            'SKIP'
            '39f57c125c69dcbbdd2549090137afed386926f538a54cbaaea57c6a50d03b58')
validpgpkeys=(949014B23D24354DFD548E5457416A0ADCEF0EFE) # Sergio Martins <sergio.martins@kdab.com>

prepare() {
  mkdir -p build

  sed -e 's|man/man1|share/man/man1|g' -i $pkgname-$pkgver/CMakeLists.txt
  cd $pkgname-$pkgver
  patch -p1 -i ../clazy-clang7.patch # Fix build with clang 7
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
