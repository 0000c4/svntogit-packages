# $Id: PKGBUILD,v 1.31 2009/03/14 02:03:57 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=2.91.91
pkgrel=1
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!libtool' '!emptydirs')
depends=('gstreamer0.10-base-plugins' 'totem-plparser' 'cdrkit' 'cdrdao'
         'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'
         'libcanberra' 'dvd+rw-tools' 'dconf')
makedepends=('gtk-doc' 'intltool' 'libburn' 'libisofs' 'nautilus'
             'gobject-introspection' 'namcap')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'gstreamer0.10-bad-plugins: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
install=brasero.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('2483535fa34aa81d1537fee8c79e06c52a15a5ccb9281ae40d6987ecd5b320f6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-caches \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
