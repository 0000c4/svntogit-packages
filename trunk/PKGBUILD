# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pipewire
pkgver=0.1.4
pkgrel=1
pkgdesc="Server and user space API to deal with multimedia pipelines"
url="http://pipewire.org"
license=(LGPL2.1)
arch=(i686 x86_64)
depends=(gstreamer gst-plugins-base v4l-utils)
makedepends=(git meson doxygen graphviz xmltoman ffmpeg jack2 libva)
optdepends=(ffmpeg jack2 libva)
_commit=b7e334e55ddbdd9afe811c043e782ae31596899e  # tags/0.1.4
source=("git+https://github.com/PipeWire/pipewire.git#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  git cherry-pick -n ff363cc7bf1db8da7ffe3423de0fb85fd1531bc2
}

build() {
  cd build
  meson setup --prefix=/usr --sysconfdir=/etc --buildtype=release ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
