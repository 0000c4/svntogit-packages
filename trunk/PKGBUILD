# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdm
pkgver=3.1.90
pkgrel=1
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm' 'etc/pam.d/gdm-autologin' 'etc/gdm/custom.conf')
groups=('gnome-extra')
options=('!libtool')
depends=('libcanberra' 'libxklavier' 'gnome-session' 'upower' 'accountsservice' 'consolekit' 'xorg-xrdb' 'nss')
makedepends=('intltool' 'gnome-doc-utils' 'xorg-server')
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        fix_external_program_directories.patch
        gdm.pam
        gdm-autologin.pam
        gdm)
sha256sums=('3fb9c52b8a18f39539c53d84645b89836de25a3eb3a0b84388e76a4f988178bf'
            'c02af756912f744a0b32c05f2b552fd425b4d30cf9f8fe22e04be8746c325fc0'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix_external_program_directories.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm \
      --localstatedir=/var \
      --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
      --disable-scrollkeeper \
      --disable-static \
      --without-tcp-wrappers
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -r "${pkgdir}/etc/gconf/"

  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm"
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin"
  sed -e 's/include\s*system-auth/required\tpam_unix.so/' -i "${pkgdir}/etc/pam.d/gdm-welcome"

  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/"

  rmdir "${pkgdir}/var/gdm"
  chmod 1770 "${pkgdir}/var/log/gdm"
  rm -rf "${pkgdir}/var/run"
}
