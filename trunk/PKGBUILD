# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.32.0
pkgrel=2
pkgdesc="Clocks applications for GNOME"
url="https://wiki.gnome.org/Apps/Clocks"
arch=(x86_64)
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop geoclue2 geocode-glib gsound)
makedepends=(vala gobject-introspection yelp-tools git meson)
groups=(gnome)
_commit=0cbad1af4366da6934e3d14c0713e6da985fd928  # tags/3.32.0-fixed^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-clocks.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Fixup tag
  git tag -d 3.32.0-fixed
  git tag -f 3.32.0 0cbad1af4366da6934e3d14c0713e6da985fd928
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
