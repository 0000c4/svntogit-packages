# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=3.0.0.0
pkgrel=1
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('vte3' 'gdl' 'autogen' 'devhelp' 'gtksourceview3' 'glade' 'libgda'
         'subversion' 'gnome-icon-theme' 'hicolor-icon-theme' 'vala' 'graphviz'
         'python2' 'shared-mime-info' 'desktop-file-utils')
makedepends=('gnome-doc-utils' 'intltool')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*.*}/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('f0b7b4ad77479e02f181f52adf1b2b866d6ed1463f401dac905bb55c606492c9')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package(){
  cd "${srcdir}/anjuta-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed -i "1s|#!/usr/bin/python$|&2|" \
    $pkgdir/usr/share/anjuta/project/{pygtk,python}/src/main.py
}
