# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=0.23
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/devhelp/"
depends=('gconf>=2.26.0' 'libglade>=2.6.4' 'libwnck>=2.26.0' 'libwebkit>=1.1.3')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
install="devhelp.install"
source=(http://ftp.gnome.org/pub/GNOME/sources/devhelp/0.23/devhelp-${pkgver}.tar.bz2)
md5sums=('704c0c90616aeb1c52ca3af1df93fde6')

build() {
  cd "${srcdir}/devhelp-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain devhelp ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
