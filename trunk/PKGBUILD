# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=2.32.0
pkgrel=1
pkgdesc="The Control Center for GNOME"
arch=('i686' 'x86_64')
depends=('metacity>=2.30.2' 'libxss' 'desktop-file-utils' 'gnome-settings-daemon>=2.32.0' 'libgnomekbd>=2.31.92' 'libcanberra>=0.25' 'sound-theme-freedesktop' 'libunique>=1.1.6' 'gnome-menus>=2.30.4' 'evolution-data-server>=2.32.0' 'librsvg>=2.31.0')
makedepends=('gnome-doc-utils>=0.20.1' 'pkgconfig' 'intltool')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('d7b09e7e07333da8b468a1b2af7a64cd12e1bf2bd50c57e2c00e4c30642f102f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-aboutme \
      --disable-scrollkeeper --disable-update-mimedb
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-control-center-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  # --disable-update-mimedb?
  rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
