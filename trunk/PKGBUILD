# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=1.2.8
pkgrel=1
pkgdesc="NetworkManager VPN plugin for PPTP"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=(x86_64)
license=(GPL)
_pppver=2.4.7
depends=(libnm libsecret "ppp=$_pppver" pptpclient)
makedepends=(nm-connection-editor intltool python git)
_commit=baacc757de8c3f49516ceeae1ca9748c077116a5  # tags/1.2.8^0
source=("git+https://gitlab.gnome.org/GNOME/NetworkManager-pptp.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd NetworkManager-pptp
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd NetworkManager-pptp
  git tag -d 1.2.7-dev  # Fixup pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd NetworkManager-pptp
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd NetworkManager-pptp
  make DESTDIR="$pkgdir" install
}
