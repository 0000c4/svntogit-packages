# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=3.8.0
pkgrel=2
pkgdesc="Gnome Display Manager"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
options=('!libtool')
depends=(gnome-shell gnome-session polkit-gnome upower xorg-xrdb xorg-server)
makedepends=(itstool intltool gnome-doc-utils gobject-introspection)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz
        fix_external_program_directories.patch)
sha256sums=('4a0894ae50d1f090c6ccc450b74142243fff9ee17e2924c0f363ea51b3ba6bd5'
            'a878680734e35c1d66252cbcfe678c3961b5ff0fa16302f8796a5e7e65ffe4a2')

build() {
  cd $pkgbase-$pkgver
  patch -Np1 -i "$srcdir/fix_external_program_directories.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var \
    --disable-static \
    --disable-schemas-compile \
    --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
    --with-check-accelerated-directory=/usr/lib/gnome-session \
    --with-authentication-agent-directory=/usr/lib/polkit-gnome \
    --without-tcp-wrappers \
    --without-console-kit \
    --with-systemd \
    --with-default-pam-config=exherbo

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

check() {
  cd $pkgbase-$pkgver
  make check
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession)
  groups=(gnome)
  install=gdm.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  chmod 1770 "$pkgdir/var/log/gdm"
  chmod 700 "$pkgdir/var/lib/gdm/.config/dconf"
  rm -r "$pkgdir/var/run" "$pkgdir/var/gdm"

### Split libgdm
  make -C gui/libgdm DESTDIR="$pkgdir" uninstall
}

package_libgdm() {
  pkgdesc="GDM support library"
  depends=(systemd)

  cd $pkgbase-$pkgver
  make -C gui/libgdm DESTDIR="$pkgdir" install
}
