# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.net>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=jack
_longname=jack-audio-connection-kit
pkgver=0.121.3
pkgrel=2
pkgdesc="A low-latency audio server"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('libsamplerate' 'readline')
makedepends=('doxygen' 'libffado' 'celt')
optdepends=('libffado: FireWire support'
            'celt: NetJACK driver')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-realtime.conf)
options=('!libtool')
provides=("$_longname=$pkgver")
conflicts=("$_longname")
replaces=("$_longname")
install=$pkgname.install
source=("http://jackaudio.org/downloads/$_longname-$pkgver.tar.gz"
        "99-realtime.conf"
        "40-hpet-permissions.rules")
md5sums=('35f470f7422c37b33eb965033f7a42e8'
         'bb312fa74956066046d6a599ac623c98'
         '6a1017041c024cea57da9bd726b664d7')

build() {
  cd "$srcdir/$_longname-$pkgver"

  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "$srcdir/$_longname-$pkgver"

  make DESTDIR="$pkgdir" install

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -Dm644 "$srcdir/99-realtime.conf" \
    "$pkgdir/etc/security/limits.d/99-realtime.conf"

  install -Dm644 "$srcdir/40-hpet-permissions.rules" \
    "$srcdir/lib/udev/rules.d/40-hpet-permissions.rules"
}

# vim:set ts=2 sw=2 et:
