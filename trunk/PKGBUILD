# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

# reminder: scons moving to py3 any time soon

pkgname=scons
pkgver=3.0.0
pkgrel=2
pkgdesc="Extensible Python-based build utility"
arch=('any')
url="http://scons.org"
license=('MIT')
depends=('python')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        scons-support-python2-syntax.patch::"https://github.com/SConsProject/scons/commit/2e0de3c5.patch")
md5sums=('1c99878d0eaf9b768b50721e913142d3'
         '7bcd903c70839301194931046e81b9b3')

prepare() {
  cd $pkgname-$pkgver
  patch -p2 -i ../scons-support-python2-syntax.patch # support Python 2 syntax in Sconsripts
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python setup.py install --standard-lib \
                          --prefix=/usr \
                          --install-data=/usr/share \
                          --optimize=1 \
                          --root="$pkgdir"

  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
