# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=3.2.2.1
pkgrel=1
pkgdesc="A BitTorrent program for KDE"
arch=('i686' 'x86_64')
url="http://ktorrent.org"
license=('GPL2')
depends=('kdelibs>4' 'gmp' 'qca' 'libxft' 'kdebase-runtime' 'taglib')
# 'kdebase-runtime' is needed for some of the icons in the interface to show
makedepends=('autoconf' 'perl' 'automoc4' 'cmake' 'kdepimlibs' 'kdebase-workspace')
# 'kdebase-workspace' is needed to enable plasmoid building
source=(http://ktorrent.org/downloads/3.2.2/${pkgname}-3.2.2-1.tar.bz2 \
	httpserver.patch)
install=${pkgname}.install
options=('libtool')
md5sums=('03750ee39d602c5093af6e0cc8ded819'
         '1f25b94615b0bbfa0457c99fb20de3e6')

build() {
	cd ${srcdir}/${pkgname}-3.2.2

	# build time patch
	patch -Np0 -i ${srcdir}/httpserver.patch || return 1

	cmake .	-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	make || return 1
	make DESTDIR=${pkgdir} install 
}
