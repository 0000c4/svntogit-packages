# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=poppler
pkgver=0.10.2
_dataver=0.2.1
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('libjpeg' 'gcc-libs>=4.3.2' 'cairo>=1.8.6' 'libxml2>=2.6.32-5' 'fontconfig>=2.6.0')
makedepends=('pkgconfig')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/${pkgname}-${pkgver}.tar.gz
	http://poppler.freedesktop.org/poppler-data-${_dataver}.tar.gz)
md5sums=('a802a178d12f453f1d0176d67f923c7d'
         'bca78b8786746c7b48b324283992d658')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-cairo-output \
	      --enable-xpdf-headers \
	      --enable-libjpeg --enable-zlib \
	      --disable-poppler-qt4 \
	      --disable-poppler-glib \
	      --disable-poppler-qt \
	      --disable-gtk-test || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/poppler-data-${_dataver}" || return 1
  make prefix=/usr DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
