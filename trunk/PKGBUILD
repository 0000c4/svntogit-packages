# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=vte
pkgname=(vte vte3)
pkgver=0.27.5
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool')
makedepends=('pygtk' 'intltool' 'gobject-introspection' 'gtk2' 'gtk3' 'namcap' 'pygobject-devel')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/vte/0.27/vte-${pkgver}.tar.bz2)
sha256sums=('47541d480c50c95b53eb6bf1187561ae7eebc19556aec0c5dd281b5cb5c316c0')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  mkdir build-gtk2
  pushd build-gtk2
  PYTHON=python2 ../configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=2.0 \
      --disable-schemas-compile
  make
  popd

  mkdir build-gtk3
  pushd build-gtk3
  PYTHON=python2 ../configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte3 \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=3.0 \
      --disable-schemas-compile
  make
  popd
}

package_vte(){
  pkgdesc+=" for GTK2"
  depends=('gtk2')
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk2
  make DESTDIR="${pkgdir}" install
}

package_vte3(){
  pkgdesc+=" for GTK3"
  depends=('gtk3')
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk3
  make DESTDIR="${pkgdir}" install
}
