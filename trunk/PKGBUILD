# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgbase=calligra
pkgname=('braindump'
         'calligra-devtools'
         'calligra-extras'
         'calligra-filters'
         'calligra-handbook'
         'karbon'
         'calligra-libs'
         'calligra-plan'
         'calligra-plugins'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words')
pkgver=3.0.0
pkgrel=4
arch=('i686' 'x86_64')
url='http://www.calligra-suite.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('extra-cmake-modules' 'kcmutils' 'kdoctools' 'python' 'kdelibs4support' 'knotifyconfig'
             'kdesignerplugin' 'kross' 'ktexteditor' 'threadweaver' 'kactivities' 'khtml' 'boost'
             'libwpg' 'poppler-qt5' 'kdiagram' 'kreport' 'eigen' 'marble' 'openjpeg' 'pstoedit' 'libspnav'
             'libvisio' 'libetonyek' 'libodfgen' 'libwps' 'gsl' 'openexr' 'akonadi-contacts' 'kcalcore'
             'vc' 'qca-qt5' 'git')
groups=('calligra')
source=("http://download.kde.org/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.gz")
md5sums=('e1380e00acb92db59d4a24ee856acaa9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNMAINTAINED=ON
  make
}

package_calligra-filters() {
  pkgdesc="Filters for the Calligra office suite"
  depends=('calligra-sheets' 'calligra-words' 'karbon' 'libodfgen')
  optdepends=('libwpg: Corel WordPerfect Graphics image importer'
              'libwps: Microsoft Works file word processor format import'
              'libvisio: Microsoft Visio import filter'
              'libetonyek: Apple Keynote import filter'
              'pstoedit: EPS to SVG filter'
              'poppler: PDF to SVG filter'
              'khtml: HTML to ODS filter'
              'openjpeg: PDF to SVG filer')
  cd build/filters
  make DESTDIR="$pkgdir" install
}

package_braindump() {
  pkgdesc="Notes and idea gathering"
  depends=('qt5-webkit' 'hicolor-icon-theme' 'calligra-libs')
  conflicts=('calligra-braindump')
  replaces=('calligra-braindump')

  cd build/braindump
  make DESTDIR="$pkgdir" install
}

package_calligra-libs() {
  pkgdesc="Libraries for the Calligra office suite"
  depends=('akonadi-contacts' 'ilmbase' 'kactivities' 'qca-qt5')

  for _d in interfaces libs pics servicetypes; do
    cd "$srcdir"/build/$_d
    make DESTDIR="$pkgdir" install
  done
}

package_calligra-plugins() {
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-filters' 'libspnav' 'kdiagram')

  cd build/plugins
  make DESTDIR="$pkgdir" install
}

package_karbon() {
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs' 'hicolor-icon-theme')
  optdepends=('calligra-filters: import/export filters'
              'calligra-extras: extra calligra tools')
  conflicts=('calligra-karbon')
  replaces=('calligra-karbon')

  cd build/karbon
  make DESTDIR="$pkgdir" install
}

package_calligra-plan() {
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words' 'kreport' 'kcalcore' 'khtml' 'kdiagram')

  cd build/plan
  make DESTDIR="$pkgdir" install
}

package_calligra-stage() {
  pkgdesc="Write presentation documents"
  depends=('calligra-libs' 'qt5-webkit' 'hicolor-icon-theme')

  cd build/stage
  make DESTDIR="$pkgdir" install
}

package_calligra-sheets() {
  pkgdesc="Write spreadsheet documents"
  depends=('calligra-libs' 'gsl' 'kdelibs4support' 'kcmutils' 'knotifyconfig' 'hicolor-icon-theme')
  optdepends=('calligra-filters: import/export filters'
              'calligra-plugins: extra calligra plugins'
              'calligra-extras: extra calligra tools')

  cd build/sheets
  make DESTDIR="$pkgdir" install
}

package_calligra-words() {
  pkgdesc="Word Processor"
  depends=('calligra-libs' 'hicolor-icon-theme')
  optdepends=('calligra-extras: extra calligra tools')

  cd build/words
  make DESTDIR="$pkgdir" install
}

package_calligra-handbook() {
  pkgdesc="Documentation for Calligra"
  options=('docs')

  cd build/doc/calligra
  make DESTDIR="$pkgdir" install
}

package_calligra-extras() {
  pkgdesc="Calligra suite extras"
  depends=('calligra-libs')

  cd build/extras
  make DESTDIR="$pkgdir" install

  cd ../data
  make DESTDIR="$pkgdir" install
}

package_calligra-devtools() {
  pkgdesc="Calligra developer tools"
  depends=('calligra-sheets' 'calligra-words' 'poppler-qt5')

  cd build/devtools
  make DESTDIR="$pkgdir" install
}
