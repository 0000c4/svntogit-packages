# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.28.0+2+gfabc580
pkgrel=1
pkgdesc="Remove colored balls from the board by forming lines"
url="https://wiki.gnome.org/Apps/Five%20or%20more"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=fabc5807ff020247b5c03f0ae5a09f2c85457e33  # gnome-3-28
source=("git+https://git.gnome.org/browse/five-or-more#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
