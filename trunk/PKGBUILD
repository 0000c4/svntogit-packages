# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.9.14
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('udev' 'libusb' 'polkit' 'pm-utils' 'dbus-glib' 'libimobiledevice')
makedepends=('pkg-config' 'intltool' 'docbook-xsl' 'gobject-introspection')
replaces=('devicekit-power')
options=('!libtool')
source=($url/releases/${pkgname}-${pkgver}.tar.xz)
sha256sums=('81eb44c0453b264a737d32f763a31d5b1776f050a47d5be85fc5e9caf874a4c5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/upower --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
