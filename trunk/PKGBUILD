# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=feh
pkgver=1.10
pkgrel=1
pkgdesc='Fast, lightweight image viewer which uses imlib2'
arch=('i686' 'x86_64')
url='https://derf.homelinux.org/~derf/projects/feh/'
license=('MIT')
depends=('libxinerama' 'giblib' 'perl')
makedepends=('libxt')
source=("https://derf.homelinux.org/~derf/projects/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('c42095ed6a7636fe9d1ec90d3994f4a57af84681')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's:/usr/local:/usr:g' config.mk
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
