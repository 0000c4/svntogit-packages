# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=blinken
pkgver=17.04.2
pkgrel=1
pkgdesc="Memory Enhancement Game"
url="https://kde.org/applications/education/blinken/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui' 'phonon-qt5' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-blinken')
conflicts=('kdeedu-blinken')
source=("https://download.kde.org/stable/applications/${pkgver}/src/blinken-${pkgver}.tar.xz"{,.sig})
sha256sums=('2a82b23838cf9ef5efbc72894db3fb565eccfa35a94101deb7f2e706fb5bff8a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
