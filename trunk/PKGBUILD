# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=2.2
pkgrel=5
pkgdesc="Media player based on BMP"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libsamplerate' 'libxml2' 'desktop-file-utils' 'audacious-plugins')
optdepends=('unzip: Zipped skins support')
provides=('audacious-player')
replaces=('audacious-player')
install=audacious.install
source=(http://distfiles.atheme.org/$pkgname-$pkgver.tgz 
	audacious-2.2-coverart-2.patch
	audacious-2.2-set_tuple_cb.patch)
md5sums=('ad0f7484692c529acfa91ce784170478'
         'd659c93402ec3ea326854590aceab7c7'
         'ac9bbbd4c8b4bb39cbfa6ef579708528')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/audacious-2.2-coverart-2.patch || return 1
  patch -Np1 -i $srcdir/audacious-2.2-set_tuple_cb.patch || return 1

  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr \
      --enable-samplerate \
      --disable-sse2 || return 1
  elif [[ $CARCH == "x86_64" ]]; then
    ./configure --prefix=/usr \
      --enable-samplerate || return 1
  fi

  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  #fix for: tarball installs softlinks that point into buildroot
  rm -f "$pkgdir/usr/bin/audtool"
  ln -s /usr/bin/audtool2 "$pkgdir/usr/bin/audtool"

  rm -f "$pkgdir/usr/bin/audacious"
  ln -s /usr/bin/audacious2 "$pkgdir/usr/bin/audacious"
}
