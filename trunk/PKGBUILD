# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue
pkgver=2.5.5
pkgrel=2
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/GeoClue/"
license=(LGPL)
depends=(libsoup json-glib libmm-glib avahi geocode-glib)
makedepends=(systemd gobject-introspection git vala meson gtk-doc libnotify)
optdepends=('libnotify: Demo Agent')
provides=("geoclue2=$pkgver-$pkgrel")
conflicts=(geoclue2)
replaces=(geoclue2)
backup=(etc/geoclue/geoclue.conf)
_commit=9519d67c7a9a0e97a75115f9ff99d4fb0bb81b21  # tags/2.5.5^0
source=("git+https://gitlab.freedesktop.org/geoclue/geoclue.git#commit=$_commit"
        0001-config-Make-the-Mozilla-API-key-configurable.patch
        0002-config-Don-t-warn-when-wifi-URL-unset.patch
        0003-config-Don-t-crash-when-submission-url-unset.patch)
sha256sums=('SKIP'
            '9ab573d38a555f4e95c44031ba14b50e0ae929dd297335db38402d7cf6bf14c0'
            '057104bbe3e08d6c258af2a3c61b5c81e1e9cf72c14646eaac2029d94d675ad1'
            '38b3d34093afbb048bfab2c10df0873ebb4b8209eee9ff7c5fb43d3406351e15')

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact heftig@archlinux.org for
# more information.
_mozilla_api_key=e05d56db0a694edc8b5aaebda3f2db6a

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../0001-config-Make-the-Mozilla-API-key-configurable.patch
  git apply -3 ../0002-config-Don-t-warn-when-wifi-URL-unset.patch
  git apply -3 ../0003-config-Don-t-crash-when-submission-url-unset.patch
}

build() {
  arch-meson $pkgname build \
    -D dbus-srv-user=geoclue \
    -D dbus-sys-dir=/usr/share/dbus-1/system.d \
    -D mozilla-api-key="$_mozilla_api_key"
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  echo 'u geoclue - "Geoinformation service" /var/lib/geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  echo 'd /var/lib/geoclue 0755 geoclue geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set sw=2 et:
