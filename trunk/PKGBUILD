# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=3.0.1
pkgrel=1
pkgdesc="A scriptable screen reader and magnifier"
arch=(any)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
install=orca.install
options=('!libtool')
depends=('pygtk' 'python2-gconf' 'python2-libgnome' 'python-wnck' 'dbus-python'
         'at-spi2-core' 'gnome-speech' 'gnome-mag' 'brltty' 'hicolor-icon-theme'
         'pyxdg' 'pyatspi')
makedepends=('intltool' 'gnome-doc-utils')
replaces=('gnopernicus')
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('cb040777af2e8c7b9a91cfe41249906d1ba314a21beed185ea5ca4ab56755be8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
