# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=iptables
pkgver=1.4.6
pkgrel=1
pkgdesc="A Linux kernel packet control tool"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.netfilter.org/"
depends=('glibc' 'bash')
options=('!libtool')
source=(http://www.iptables.org/projects/iptables/files/${pkgname}-${pkgver}.tar.bz2 \
        iptables ip6tables empty.rules simple_firewall.rules iptables.conf.d)
backup=(etc/conf.d/iptables)
md5sums=('c67cf30e281a924def6426be0973df56'
         '89401d6f0cf1de46a455b7be6720a58b'
         '6e0e88c2ed0c3715d1409ee3258a0046'
         '14186bbafe21bb0638c0cb8e0903c829'
         'e53a83bb4d8ac8b7eadd7bd58294751d'
         'a9020ee7586417b87c026d435b4be50d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # http://bugs.archlinux.org/task/17046
  sed -ie '87 i libxt_RATEEST.so: libxt_RATEEST.oo' extensions/GNUmakefile.in
  sed -ie '88 i \\t${AM_VERBOSE_CCLD} ${CCLD} ${AM_LDFLAGS} -lm -shared ${LDFLAGS} -o $@ $<;\n' extensions/GNUmakefile.in

 ./configure --prefix=/usr --with-kernel=usr/src/linux-$(uname -r) \
	--libexecdir=/usr/lib/iptables --sysconfdir=/etc \
	--with-xtlibdir=/usr/lib/iptables \
	--enable-devel --enable-libipq

  make || return 1
  make DESTDIR=${pkgdir} install || return 1 
  
  install -D -m755 ../iptables ${pkgdir}/etc/rc.d/iptables
  install -D -m755 ../ip6tables ${pkgdir}/etc/rc.d/ip6tables
  install -D -m644 ../empty.rules ${pkgdir}/etc/iptables/empty.rules
  install -D -m644 ../simple_firewall.rules ${pkgdir}/etc/iptables/simple_firewall.rules
  install -D -m644 ../iptables.conf.d ${pkgdir}/etc/conf.d/iptables
}
