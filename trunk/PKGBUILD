# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=3.3.4
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf' 'libnotify')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus-sendto' 'gobject-introspection')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        61-gnome-bluetooth-rfkill.rules)
sha256sums=('045e4232b3adbb9219bde406c853ba8d652bb58ce7ea85323b51fc8c786abeea'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -m644 -D "$srcdir/61-gnome-bluetooth-rfkill.rules" \
                     "$pkgdir/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
  install -d "$pkgdir/etc/ld.so.conf.d"
  echo "/usr/lib/gnome-bluetooth" > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf"
}
