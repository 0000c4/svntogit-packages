# $Id$
# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.24.2
pkgrel=1
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('autogen>=5.9.5' 'devhelp>=0.21' 'gnome-build>=2.24.1' 'vte>=0.17.4' 'graphviz>=2.20.3' 'subversion>=1.5.4' 'gtksourceview2>=2.4.1' 'glade>=3.5.4' 'libglade>=2.6.3' 'libxslt>=1.1.24' 'desktop-file-utils')
makedepends=('gnome-doc-utils>=0.14.1')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
md5sums=('d1db75bc1c8d4ab4c9d180767e24e39a')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-plugin-subversion --enable-plugin-glade \
    --enable-plugin-class-inheritance --enable-plugin-valgrind \
    --enable-plugin-sourceview --disable-gtk-doc \
    --enable-debug --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
   
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
