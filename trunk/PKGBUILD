# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-python
pkgver=5.4.3
pkgrel=1
pkgdesc="Python language and documentation plugin for KDevelop"
arch=(x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop python)
makedepends=(extra-cmake-modules)
optdepends=('python-pycodestyle: for Python style checking')
source=("https://download.kde.org/stable/kdevelop/$pkgver/src/kdev-python-$pkgver.tar.xz"{,.sig})
sha256sums=('d992b2648fe520fac595feb66377af7b42fb2e36da52162c21f851b9fbba93c7'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'  # Sven Brauch <mail@svenbrauch.de>
              '364EFA5F639572907D392999C64CF56B13CACE5D'  # Kevin Funk <kfunk@kde.org>
              'E191FD5BE6F46870F09E82B2024E7FB43D015474'  # Friedrich W. H. Kossebau <kossebau@kde.org>
              '0A48BC961075B4BA8523E3790A345FB086E797D9') # https://share.kde.org/s/8dKaJTw8pR8iiwJ    

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdev-python-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
