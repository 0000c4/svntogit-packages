# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.9.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify hicolor-icon-theme desktop-file-utils)
makedepends=(intltool docbook-xsl gnome-settings-daemon)
options=('!libtool' '!emptydirs')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('704bc3afc01c120a2651b8000f64f53308e6aaa5e155cda3700af27cd32d6f04')

prepare() {
  cd $pkgname-$pkgver
  sed -i -e 's/LIBLZMA_REQUIRED=5.1.0/LIBLZMA_REQUIRED=5.0.5/g' configure*
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
