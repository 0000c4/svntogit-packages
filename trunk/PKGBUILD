# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2
pkgver=0.25.1
pkgrel=2
epoch=1
pkgdesc='A linkable library for Git'
arch=('i686' 'x86_64')
url='https://libgit2.github.com/'
depends=(zlib openssl libssh2 curl)
makedepends=(cmake python)
license=('GPL2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        https://github.com/libgit2/libgit2/commit/f47db3c799b4f6b63ee8021e0c93d00d5f125c9a.patch)
sha1sums=('c65238d0e0a698b202a3a886d003228cac6dacc3'
          'b1e349e98d98ac0f745750203b2e105a2932b9fd')

prepare() {
  patch -d"$pkgname-$pkgver" -p1 -i ../f47db3c799b4f6b63ee8021e0c93d00d5f125c9a.patch
}

build() {
  cd "$pkgname-$pkgver"
  export LANG=en_US.UTF-8
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
