# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.1.90
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('at-spi2-core' 'libx11' 'atk' 'dconf')
makedepends=('intltool')
install=at-spi2-atk.install
options=('!libtool')
groups=('gnome')
source=(http://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('218090dc38ea5614ec86e3f388c3c504f94d129df4eee6d4c2f5066b257e1faf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
