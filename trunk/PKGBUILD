# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libsoup
pkgname=('libsoup' 'libsoup-gnome')
pkgver=2.28.1
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
makedepends=('glib2>=2.22.2' 'gnutls>=2.8.3' 'libxml2>=2.7.3' 'libproxy>=0.2.3' 'sqlite3>=3.6.18' 'gconf>=2.28.0' 'gnome-keyring>=2.28.1' 'intltool' 'pkgconfig')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.28/${pkgbase}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('615f154998a65c1d9611a0e6c16c1f30df799da83437f1ff7b4baa57e5067a80')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static || return 1
  make || return 1
}

package_libsoup() {
  pkgdesc="GNOME HTTP Library - base library"
  depends=('glib2>=2.21.6' 'libxml2>=2.7.3' 'gnutls>=2.8.3')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  rm -f "${pkgdir}"/usr/lib/libsoup-gnome-2.4.*
  rm -f "${pkgdir}/usr/lib/pkgconfig/libsoup-gnome-2.4.pc"
  rm -rf "${pkgdir}/usr/include/libsoup-gnome-2.4"
}

package_libsoup-gnome() {
  pkgdesc="GNOME HTTP Library - GNOME libraries"
  depends=("libsoup=${pkgver}" 'libproxy>=0.2.3' 'gnome-keyring>=2.28.0')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}"  install || return 1
  rm -f "${pkgdir}"/usr/lib/libsoup-2.4.*
  rm -f "${pkgdir}/usr/lib/pkgconfig/libsoup-2.4.pc"
  rm -rf "${pkgdir}/usr/include/libsoup-2.4"
  rm -rf "${pkgdir}/usr/share"
}
