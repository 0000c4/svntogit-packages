# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(python-gobject python2-gobject)
pkgver=2.90.3
pkgrel=1
arch=('i686' 'x86_64')
url="https://live.gnome.org/PyGObject"
license=('LGPL')
depends=('glib2' 'gobject-introspection')
makedepends=(python python2 python-cairo python2-cairo)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz)
options=('!libtool')
sha256sums=('8e14f8db0780ad26a8ce6583da30313463ca40519b35f5cda8780f02bd6ddca4')

build() {
  cd "${srcdir}"
  cp -a "${pkgbase}-${pkgver}" python2-build

  (
    cd python2-build
    export PYTHON=/usr/bin/python2
    ./configure --prefix=/usr
    make
  )

  (
    cd "${pkgbase}-${pkgver}"
    ./configure --prefix=/usr
    make
  )
}

package_python-gobject() {
  pkgdesc="Python 3 bindings for GObject"
  depends+=('python' 'python-cairo')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_python2-gobject() {
  pkgdesc="Python 2 bindings for GObject"
  depends+=('python2' 'python2-cairo')
  conflicts=('python-gobject')

  cd "${srcdir}/python2-build"
  make DESTDIR="${pkgdir}" install
}
