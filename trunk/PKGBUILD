# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=3.0.0
pkgrel=2
pkgdesc="Cross-platform QtWebEngine browser"
arch=(x86_64)
url="https://www.falkon.org/"
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg)
makedepends=(extra-cmake-modules qt5-tools kwallet libgnome-keyring)
optdepends=('kwallet: kf5 kwallet integration' 'libgnome-keyring: gnome keyring integration')
replaces=(qupzilla)
source=(https://download.kde.org/stable/$pkgname/${pkgver%.*}/src/$pkgname-$pkgver.tar.xz{,.sig}
        falkon-qt5.11a.patch::"https://cgit.kde.org/falkon.git/patch/?id=17d45d71"
        falkon-qt5.11b.patch::"https://cgit.kde.org/falkon.git/patch/?id=9f0817ca"
        falkon-qt5.11c.patch::"https://cgit.kde.org/falkon.git/patch/?id=ad2466f2")
sha256sums=('4e42a091e6ae434d7c3146adb876a8a5dc29d9354560087a905a0bd7fb58d7a6'
            'SKIP'
            'e5c297dcc9e62da8e2d8dd0e52eaefc6234f84b26b0aa513582f9f7215e595a3'
            'f4325dc12c949ec2dc22035747024462c9727c5169c814c6a1641fe3c3c02560'
            'dce1905befeba0e6450445ac04ba6302f7b17e77c6fd554d450cf0f7331c8cd0')
validpgpkeys=(3CB7B8416EF6778B409FDC43EBC3FC294452C6D8) # David Rosca <nowrep@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  # Backport fixes for Qt 5.11
  patch -p1 -i ../falkon-qt5.11a.patch
  patch -p1 -i ../falkon-qt5.11b.patch
  patch -p1 -i ../falkon-qt5.11c.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
