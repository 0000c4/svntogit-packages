# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-dbd-sqlite
pkgver=1.56
pkgrel=1
pkgdesc="Self-contained RDBMS in a DBI driver"
arch=('x86_64')
url="http://search.cpan.org/dist/DBD-SQLite"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'sqlite')
source=(http://www.cpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-${pkgver}.tar.gz)
sha512sums=('1c9be3557691f76a9e543fe4bd6656a6b82f53fba4bc05261a55affa0a38b4eaa1cf876da975234c9c09985ba25b21f6f7a735f1cc47104169edf7fdd3a2e314')

build() {
  cd DBD-SQLite-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd DBD-SQLite-${pkgver}
  make test
}

package() {
  cd DBD-SQLite-${pkgver}
  make install DESTDIR="${pkgdir}"

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
