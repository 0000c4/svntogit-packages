# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-logger-qt
pkgver=0.6.0
pkgrel=1
pkgdesc="Qt bindings for telepathy logger"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/network/telepathy/telepathy-logger-qt'
license=('GPL')
depends=('telepathy-qt' 'telepathy-logger' 'qt-gstreamer')
makedepends=('automoc4' 'cmake' 'python2' 'doxygen' 'boost')
source=("http://download.kde.org/unstable/kde-telepathy/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('3e58f7a25ad192d9a62b52784492b317b77a5ffa')

build() {
  mkdir build 
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
