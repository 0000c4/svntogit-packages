# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dconf-editor
pkgver=3.30.2
pkgrel=1
pkgdesc="dconf Editor"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(GPL3)
groups=(gnome-extra)
depends=(gtk3 dconf)
makedepends=(vala meson appstream-glib git)
_commit=b76505ab13fabe78148c098b33250f812d292bcc  # tags/3.30.2^0
source=("git+https://gitlab.gnome.org/GNOME/dconf-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
