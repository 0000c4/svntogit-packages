# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=('akonadi' 'akonadi-server')
pkgver=1.2.1
pkgrel=3
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url='http://www.akonadi-project.org'
license=('LGPL')
makedepends=('shared-mime-info' 'boost' 'mysql>=5.1.41' 'soprano'
	'pkgconfig' 'cmake' 'automoc4')
source=("http://download.akonadi-project.org/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f9c1d000844f31c67360078ddf60bec2')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
  make || return 1
}

package_akonadi(){
  desc="Libraries for the Akonadi PIM storage service"
  depends=('shared-mime-info' 'boost' 'soprano')

  cd ${srcdir}/build/libs/
  make DESTDIR=${pkgdir} install || return 1
  
  # install cmake files
  install -d ${pkgdir}/usr/lib/cmake/Akonadi
  install -Dm644 ../AkonadiConfig{,Version}.cmake \
    ${pkgdir}/usr/lib/cmake/Akonadi || return 1
}

package_akonadi-server(){
  desc="Akonadi PIM storage service"
  depends=('akonadi' 'mysql')
  install=akonadi.install

  cd ${srcdir}/build/server
  make DESTDIR=${pkgdir} install || return 1

  # install mime file
  install -d ${pkgdir}/usr/share/mime/packages
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/akonadi-mime.xml \
    ${pkgdir}/usr/share/mime/packages
}
