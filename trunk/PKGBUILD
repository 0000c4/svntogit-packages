# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgbase=sip
pkgname=('sip' 'python-sip' 'python2-sip')
pkgver=4.13.3
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
makedepends=('python' 'python2')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/${pkgbase}-${pkgver}.tar.gz")
md5sums=('76192829cc42ec558db46e4f9e1d8ba9')

build() {
  cd "${srcdir}"
  cp -r ${pkgbase}-${pkgver} python2-${pkgbase}-${pkgver}

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
  
  ### Python2 version ###
  cd "${srcdir}/python2-${pkgbase}-${pkgver}"
  python2 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  depends=('glibc')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C sipgen

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-sip() {
  pkgdesc="Python 3.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C siplib
  
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-sip() {
  pkgdesc="Python 2.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python2')

  cd "${srcdir}/python2-${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C siplib
  
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
