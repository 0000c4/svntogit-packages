# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=liferea
pkgver=1.6.5
pkgrel=2
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf' 'libwebkit' 'libxslt' 'libglade' 'sqlite3' 'hicolor-icon-theme')
makedepends=('pkg-config' 'intltool' 'lua' 'libnotify')
optdepends=('lua: for lua scripting support'
            'libnotify: for notification support')
options=('!libtool' '!emptydirs')
install=liferea.install
changelog=ChangeLog
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz
        libnotify-0.7.patch)
md5sums=('b5a2c0803eec5d0aff0b9ca4114eaf16'
         'fca97e3316c1c51e0179c4c7dcce47a2')
sha1sums=('f24dbd0e81c5a8309f0d19853211fc02ff818477'
          '52e53dd11a0ffe62304c4f17e80bfe1412cfbf29')

build() {
  cd "${srcdir}/liferea-${pkgver}"
  patch -Np1 -i "$srcdir/libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
