# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jason Chu <jason@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=cscope
pkgver=15.8
pkgrel=1
pkgdesc="A developers tool for browsing program code"
arch=('i686' 'x86_64')
url="http://cscope.sourceforge.net/"
license=('BSD')
depends=('ncurses' 'sh')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7114b5838b3edcb3f5821efd444fff06')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's|/usr/local/lib/cs|/usr/lib/cs|' contrib/ocs

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
  
	install -d ${pkgdir}/usr/lib/cs
	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

	cd contrib/xcscope
	install -Dm644 xcscope.el "${pkgdir}"/usr/share/emacs/site-lisp/xcscope.el
	install -m755 cscope-indexer "${pkgdir}"/usr/bin
}
