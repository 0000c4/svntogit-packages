# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname='perl-text-csv'
_dist="Text-CSV"
pkgver=1.33
pkgrel=1
pkgdesc="comma-separated values manipulator"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=()
url='http://search.mcpan.org/dist/Text-CSV'
source=("http://search.mcpan.org/CPAN/authors/id/M/MA/MAKAMAKA/${_dist}-${pkgver}.tar.gz")
md5sums=('358e2f2ddb6c7746c91e49a7db97b634')
sha512sums=('3b30242c71719aeb20077490b752718bb25320dc1510b5053d5e21678cf7011e16eac2b578138115399f8dbdfb5519cbb913fc42b042355e35e39491ed520b8e')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
