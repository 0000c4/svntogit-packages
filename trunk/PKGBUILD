# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=4.2.1
pkgrel=2
pkgdesc="A powerful BitTorrent client for KDE"
arch=('i686' 'x86_64')
url='http://ktorrent.org/'
license=('GPL2')
depends=('libktorrent' 'kdebase-workspace' 'taglib')
makedepends=('automoc4' 'cmake' 'boost')
source=("http://ktorrent.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'httpserver.patch' 'magnet.patch')
install=${pkgname}.install
sha1sums=('3f52c4a10011a49d304c08a3bb1290a022e0c53f'
          'cef22b24af540e1bfcf13da60e4846ade106c303'
          'f0156489364e5590b0b539cfe2abc929c1922ef8')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# build time patch
	patch -p0 -i "${srcdir}/httpserver.patch"
	patch -p1 -i "${srcdir}/magnet.patch"

	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
}
