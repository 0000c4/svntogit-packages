# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-shell-extensions
pkgver=3.22.0+1+gf99b42e
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
depends=(gnome-shell)
makedepends=(intltool git gnome-common)
optdepends=('nautilus: gnome-classic session')
groups=(gnome)
_commit=f99b42e73232f794b00e8487fcd595661ebd4fba
source=("git://git.gnome.org/gnome-shell-extensions#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnome-session --enable-extensions=all
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
