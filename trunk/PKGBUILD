# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-tetravex
pkgver=3.35.92+6+g65f0423
pkgrel=1
pkgdesc="Complete the puzzle by matching numbered tiles"
url="https://wiki.gnome.org/Apps/Tetravex"
arch=(x86_64)
license=(GPL)
depends=(gtk3)
makedepends=(meson gobject-introspection vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=65f04234e5bc55239d0ed62fb0b801c75211f425  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-tetravex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D build_cli=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
