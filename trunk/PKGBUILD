# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=17.04.2
pkgrel=2
pkgdesc='UML modeller'
arch=(i686 x86_64)
url='https://kde.org/applications/development/umbrello'
license=(GPL)
depends=(hicolor-icon-theme ktexteditor qt5-webkit)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdesdk-umbrello)
replaces=(kdesdk-umbrello)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        umbrello-exec-line.patch::"https://cgit.kde.org/umbrello.git/patch/?id=54f5b361")
sha256sums=('618a90643d2041b57a0d1efeef1fa22a73174052946ef1ccbb40258db68b1d4a'
            'SKIP'
            '56d1f1a6c20b607f6332a060bfcb591ad4aa3658821af380ac4f96252694ac69')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix desktop exec line
  patch -p1 -i ../umbrello-exec-line.patch
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

