# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gupnp
pkgver=0.16.1
pkgrel=1
pkgdesc="An object-oriented UPNP framework"
arch=('i686' 'x86_64')
url="http://gupnp.org/"
license=('LGPL')
depends=('gssdp')
makedepends=('gobject-introspection' 'gtk-doc')
optdepends=('python2: gupnp-binding-tool')
options=('!libtool')
source=(http://gupnp.org/sites/all/files/sources/$pkgname-$pkgver.tar.gz)
sha256sums=('f01a1f4fd36ce161a3df29fa83e1a0a2fb40d3c9f30f6b403e7791688ad24cfe')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '1s|^#!.*python$|&2|' tools/gupnp-binding-tool

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
