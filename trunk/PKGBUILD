# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=xf86-video-intel
pkgname=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
pkgver=2.19.0
pkgrel=5
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util>=0.3.9' 'libxfixes' 'systemd-tools')
makedepends=('xorg-server-devel>=1.12.0' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'libxrender')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/archive/individual/driver/${pkgbase}-${pkgver}.tar.bz2)
sha1sums=('131934cf5e90bd48c883804cc644c4cb8b1de100')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  mkdir build-{UXA,SNA}

  pushd build-UXA
  ../configure --prefix=/usr --enable-dri --enable-uxa
  make
  popd

  pushd build-SNA
  ../configure --prefix=/usr --enable-dri --enable-sna --enable-vmap
  make
  popd
}

package_xf86-video-intel-uxa() {
	
  pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers with UXA acceleration"
  provides=('xf86-video-intel')
  replaces=('xf86-video-intel')
  conflicts=('xf86-video-intel-sna' 'xorg-server<1.12.0' 'xf86-video-i810' 'xf86-video-intel-legacy')
  
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build-UXA
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_xf86-video-intel-sna() {

  pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers with SNA acceleration"
  provides=('xf86-video-intel')
  conflicts=('xf86-video-intel-uxa' 'xorg-server<1.12.0' 'xf86-video-i810' 'xf86-video-intel-legacy')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build-SNA
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
