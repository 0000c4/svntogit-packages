# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gnome-tweak-tool
pkgver=3.0.3
pkgrel=1
pkgdesc="A tool to customize advanced GNOME 3 options."
arch=(any)
url="https://live.gnome.org/GnomeTweakTool"
license=('GPL')
depends=('gconf' 'gsettings-desktop-schemas' 'pygobject')
makedepends=('pygobject-devel' 'intltool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('c055cb760a52aaa2ab1ca01f611b4881462576e7a91cdaf463cfd8de18e2fc03')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gnome-tweak-tool
}

# vim:set ts=2 sw=2 et:
