# $Id$
# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio
pkgver=1.1.0
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library"
arch=('x86_64')
license=('GPL3')
url="https://www.gnu.org/software/libcdio/"
depends=('libcddb' 'ncurses' 'gcc-libs')
source=(https://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('785c32494e4770c38bef09b1a545ef3acec8ccfbf2b799cb7e70dc380cbcf164'
            'SKIP')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-vcd-info --enable-cpp-progs --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -C test check
}

package() {
  cd ${pkgname}-${pkgver}
  make -j1 DESTDIR="${pkgdir}" install
  
  sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
    $pkgdir/usr/include/cdio/cdio_config.h
}
