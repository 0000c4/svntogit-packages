# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-dbd-sqlite
pkgver=1.60
pkgrel=1
pkgdesc="Self-contained RDBMS in a DBI driver"
arch=('x86_64')
url="http://search.cpan.org/dist/DBD-SQLite"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'sqlite')
source=("https://www.cpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-$pkgver.tar.gz")
sha512sums=('b5fd203898815ada13ec04477fa0ad90ec2f53761d9b1ed7cce7d06a674e31d3810e745658aea150a3a58fc18adb889e5e50e795e8575318ab4cc757c1ff1c9e')

build() {
  cd DBD-SQLite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd DBD-SQLite-$pkgver
  make test
}

package() {
  cd DBD-SQLite-$pkgver
  make install DESTDIR="$pkgdir"
}
