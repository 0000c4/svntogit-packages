# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=sed
pkgver=4.2.2
pkgrel=3
pkgdesc='GNU stream editor'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/sed'
license=('GPL3')
groups=('base' 'base-devel')
depends=('acl' 'sh')
makedepends=('gettext')
install=sed.install
source=("ftp://ftp.gnu.org/pub/gnu/sed/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('4111de4faa3b9848a0686b2f260c5056'
         '86a5ab72f414d4cb38126e8e27cf0101')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
