# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=3.4.0
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gconf' 'libsoup-gnome' 'gnome-icon-theme')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'gnome-common')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        gettext-not-xml.patch)
sha256sums=('db8fdfc022fffd1ef97615c19af278fb6e1fca5ebf4ff2da5e255d54869c9e18'
            '3808da5c27bad1152eee824db2cead21de599af198eca017a18e1b0adb366330')

build() {
    cd "$pkgname-$pkgver"

    patch -Np1 -i "$srcdir/gettext-not-xml.patch"
    gtkdocize
    autoreconf -fi

    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
        --enable-locations-compression
    make
}

package() {
    cd "$pkgname-$pkgver"
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

    install -m755 -d "$pkgdir/usr/share/gconf/schemas"
    gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain libgweather "$pkgdir"/etc/gconf/schemas/*.schemas
    rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
