# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=apricots
pkgver=0.2.6
pkgrel=9
pkgdesc="A simple 2D flying/bombing game similar to basic side scrollers"
url="http://www.fishies.org.uk/apricots.html"
arch=(x86_64)
license=(GPL)
depends=(gcc-libs sdl freealut hicolor-icon-theme)
#source=("http://www.fishies.org.uk/$pkgname-$pkgver.tar.gz"
backup=(etc/apricots.cfg)
source=("https://sources.archlinux.org/other/apricots/$pkgname-$pkgver.tar.gz"
        apricots.desktop apricots.png
        apricots-0.2.6-freealut.patch)
sha512sums=('cf8a988d316382469e4b13a86315c760b702b3d490398cf2fe96f119ba743c1ff707c09228dbbf4eb601189fece708913a621b04c44e9d93162639ccbd502cf5'
            '9f2255f440fef17dccb38474edc5bef9f9770d026bbbc074e0a4dbd4bcc6fb639e00e7c6f370c67f478d6bc0d869f3c40bcfc0b493a08bff531fd754b7282d75'
            '77061be211c4f03e21af3b6c0f43d1be6b1d2b323415d7e0c03daf1b29d6010ac69b1f7e27057ff399f91987f6b10b8e78f66d669cb81f396ae2cfc209b41983'
            '002cc098dc0239c2fb771560d0957b93242d03055ade9a02e015e8a1e867ef06feb66499e3a39d1fea5261ede98b7193e256a0fc2cccb785df4e709cd3ca6efc')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../apricots-0.2.6-freealut.patch
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make LIBTOOL=/usr/bin/libtool
}

package() {
  make -C $pkgname-$pkgver install prefix="$pkgdir/usr" LIBTOOL=/usr/bin/libtool

  mkdir "$pkgdir/etc"
  mv "$pkgdir/usr/share/apricots/apricots.cfg" "$pkgdir/etc"
  ln -sr "$pkgdir/etc/apricots.cfg" "$pkgdir/usr/share/apricots"

  install -Dt "$pkgdir/usr/share/applications" -m644 apricots.desktop
  install -Dt "$pkgdir/usr/share/icons/hicolor/24x24/apps" -m644 apricots.png
}
