# $Id$
# Maintainer: damir <damir@archlinux.org>
# Maintainer/Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-xml-twig
_realname=XML-Twig
pkgver=3.39
pkgrel=1
pkgdesc="A module for easy processing of XML"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
license=('GPL' 'PerlArtistic')
depends=('perl-xml-parser>=2.23' 'perl-text-iconv' 'perl>=5.12.0')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/M/MI/MIROD/${_realname}-${pkgver}.tar.gz)
md5sums=('c3f42ee3f09fe7b0601172ddb15bfe53')

build() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL -y INSTALLDIRS=vendor
  make
}

check() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd  "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
