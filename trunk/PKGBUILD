# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-docs
pkgname=(vulkan-html-docs)
pkgver=1.2.135
pkgrel=1
epoch=1
_pkgname=Vulkan-Docs
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(asciidoctor python3 dblatex source-highlight cmake ghostscript git)
groups=(vulkan-devel)
source=("https://github.com/KhronosGroup/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('b07acfdd90d3a18a74e19b4ebc64b20607e4d096614c0b4aa600dd46ccfc9abe')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./makeAllExts html manhtmlpages styleguide registry
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation"
  cd "${_pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/doc/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  mv -v out/man "${pkgdir}/usr/share/doc/vulkan/"
  mv -v out/html/* "${pkgdir}/usr/share/doc/vulkan/"
  mv -v out/registry.html "${pkgdir}/usr/share/doc/vulkan/"
  mv -v out/styleguide.html "${pkgdir}/usr/share/doc/vulkan/"

  install -m644 copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}
