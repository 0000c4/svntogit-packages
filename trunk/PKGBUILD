# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgbase=sip
pkgname=('sip' 'python-sip' 'python2-sip' 'python-pyqt5-sip' 'python2-pyqt5-sip')
pkgver=4.19.9
pkgrel=2
arch=('x86_64')
url='http://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
makedepends=('python' 'python2')
source=("http://sourceforge.net/projects/pyqt/files/sip/sip-$pkgver/sip-$pkgver.tar.gz")
sha256sums=('38a646ba483821ca4a6be7fa33e8634eb74812114e7081eccc4c3a8a6c92e438')

prepare() {
  cp -a sip-$pkgver{,-py2}
  cp -a sip-$pkgver{,-pyqt5}
  cp -a sip-$pkgver{,-py2-pyqt5}
}

build() {
  cd "$srcdir"/sip-$pkgver
  python configure.py CFLAGS="$CFLAGS" LFLAGS="$LDFLAGS"
  make

  cd "$srcdir"/sip-$pkgver-py2
  python2 configure.py CFLAGS="$CFLAGS" LFLAGS="$LDFLAGS"
  make

  cd "$srcdir"/sip-$pkgver-pyqt5
  python configure.py CFLAGS="$CFLAGS" LFLAGS="$LDFLAGS" --sip-module PyQt5.sip
  make

  cd "$srcdir"/sip-$pkgver-py2-pyqt5
  python2 configure.py CFLAGS="$CFLAGS" LFLAGS="$LDFLAGS" --sip-module PyQt5.sip
  make
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  depends=('glibc')

  cd sip-$pkgver
  make DESTDIR="$pkgdir" install -C sipgen

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-sip() {
  pkgdesc="Python 3.x SIP bindings for C and C++ libraries"
  depends=('python')

  cd sip-$pkgver
  make DESTDIR="$pkgdir" install -C siplib

  install -Dm644 sipconfig.py "$pkgdir"/usr/lib/python3.6/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "$pkgdir"/usr/lib/python3.6/site-packages/sipdistutils.py

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sip() {
  pkgdesc="Python 2.x SIP bindings for C and C++ libraries"
  depends=('python2')

  cd sip-$pkgver-py2
  make DESTDIR="$pkgdir" install -C siplib

  install -Dm644 sipconfig.py "$pkgdir"/usr/lib/python2.7/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "$pkgdir"/usr/lib/python2.7/site-packages/sipdistutils.py

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-pyqt5-sip() {
  pkgdesc="Python 3.x SIP bindings for C and C++ libraries (PyQt5 private copy)"
  depends=('python')

  cd sip-$pkgver-pyqt5
  make DESTDIR="$pkgdir" install -C siplib

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
 
package_python2-pyqt5-sip() {
  pkgdesc="Python 2.x SIP bindings for C and C++ libraries (PyQt5 private copy)"
  depends=('python2')

  cd sip-$pkgver-py2-pyqt5
  make DESTDIR="$pkgdir" install -C siplib

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

