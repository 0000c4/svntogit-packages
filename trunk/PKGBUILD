# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-bad
pkgver=1.9.90+14+gfb36608
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(LGPL)
depends=(mjpegtools gst-plugins-base-libs curl chromaprint libmms faad2 celt libdca libdvdnav
         libmodplug libgme wayland libofa openjpeg2 libwebp libsrtp gnutls glu sbc rtmpdump
         libgudev graphene)
makedepends=(schroedinger libexif libdvdread libvdpau libmpeg2 python valgrind wildmidi librsvg
             gobject-introspection gtk-doc gtk3 clutter libtiger ladspa openal libusb
             qt5-declarative qt5-x11extras qt5-wayland vulkan-headers vulkan-icd-loader zbar
             autoconf-archive git libfdk-aac faac soundtouch spandsp fluidsynth neon)
options=(!emptydirs)
_commit=fb36608ccd95004b3c2b38d6451c009fbad899b6  # master
source=("git://anongit.freedesktop.org/gstreamer/gst-plugins-bad#commit=$_commit"
        "gst-common::git://anongit.freedesktop.org/gstreamer/common")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer Bad Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --with-gtk=3.0 \
    --enable-experimental --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  # don't have a camera for the camerabin test
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
