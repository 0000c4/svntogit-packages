# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: David Runge <dave@sleepmap.de>

pkgname=libgit2-glib
pkgver=0.99.0
pkgrel=1
pkgdesc="GLib wrapper for libgit2"
url="https://wiki.gnome.org/Projects/Libgit2-glib"
license=('LGPL2.1')
arch=('x86_64')
depends=('glib2' 'libgit2')
makedepends=('gobject-introspection' 'gtk-doc' 'meson' 'python-gobject' 'vala'
             'git')
_commit=0a55b5a078af476d522d89e60cd0a49e8722f066  # tags/v.0.99.0^0
source=("git+https://gitlab.gnome.org/GNOME/libgit2-glib.git#commit=$_commit"
        build.diff)
sha256sums=('SKIP'
            '8f2a5e245f70f7685ba3692074ed1d372cf766b4a490f67b101f68f054e22289')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/^v\.?//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # build fixes
  git apply -3 ../build.diff
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=('libgit2.so')
  provides+=('libgit2-glib-1.0.so')

  DESTDIR="$pkgdir" meson install -C build

  # strip $pkgdir from embedded paths:
  python -m compileall -d "/usr/lib" "$pkgdir/usr/lib"
  python -O -m compileall -d "/usr/lib" "$pkgdir/usr/lib"

  install -vDm 644 $pkgname/{AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
