# $Id: PKGBUILD 290221 2017-03-09 16:12:45Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-dev-utils
pkgver=17.04.1
pkgrel=1
pkgdesc='Small utilities for developers using KDE/Qt libs/frameworks'
url='https://www.kde.org/applications/development/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kparts qt5-tools)
makedepends=(extra-cmake-modules python)
conflicts=(kdesdk-dev-utils)
replaces=(kdesdk-dev-utils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b60ed02ac6071d1e1722957e987f04027e08feb79aab130be97e24a1200e84d0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
