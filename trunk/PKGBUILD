# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.9.1
pkgrel=2
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/Folks"
license=(LGPL2.1)
depends=(telepathy-glib libgee libxml2 evolution-data-server libsocialweb libzeitgeist
         libtracker-sparql)
makedepends=(gobject-introspection intltool)
install=folks.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('102b1867355d16e73a837871a058dda13c74fa385908ddda1623893ab0baee4a')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-schemas-compile --enable-tracker-backend
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
