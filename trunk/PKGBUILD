# $Id$
# Maintainer: Jürgen Hötzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
_pkgver=3.6a
pkgver=${_pkgver/-/_}
pkgrel=3
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('openssl' 'sqlite3')
makedepends=('java-environment' 'emacs' 'zip')
optdepends=('emacs')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${pkgname}${_pkgver}.tar.gz)
md5sums=('484c09cd0e0f84c96999d889de35cd1e')

build() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --disable-gstreamer \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --dotnet=no \
    --jvm=yes
  make build compile-bee
}

package() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install install-bee
  make -C manuals DESTDIR="${pkgdir}" install-bee
  chmod 644 "${pkgdir}"/usr/lib/bigloo/${_pkgver%-*}/*.a
}
