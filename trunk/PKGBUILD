# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=3.22.0
pkgrel=1
pkgdesc="Collection of plugins for the gedit Text Editor"
arch=(x86_64 i686)
license=(GPL)
url="https://wiki.gnome.org/Apps/Gedit/ShippedPlugins"
depends=(gedit python-dbus python-cairo)
makedepends=(intltool vte3 libgit2-glib itstool gucharmap gobject-introspection vala yelp-tools git)
optdepends=('gucharmap: for charmap plugin'
            'vte3: for embedded terminal'
            'libgit2-glib: for git plugin')
options=('!emptydirs')
_commit=19bfe94e46b03b94271b58d0287b3def4ab6f941  # tags/3.22.0^0
source=("git://git.gnome.org/gedit-plugins#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
