# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=ed
pkgver=1.5
pkgrel=3
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/ed/ed.html"
depends=('glibc' 'sh')
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/ed/${pkgname}-${pkgver}.tar.gz)
md5sums=('e66c03d7e4c67b025d5b6093ec678267')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/ \
              --mandir=/usr/share/man \
              "CFLAGS=$CFLAGS" \
              "LDFLAGS=$LDFLAGS"

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make "DESTDIR=$pkgdir" install

  # nuke info pages
  rm -r "$pkgdir/usr/share"
}
