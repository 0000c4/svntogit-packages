# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.8.2
pkgrel=3
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('libxfce4ui' 'thunar>=1.2.0' 'garcon' 'hicolor-icon-theme' 'libwnck>=2.30.0')
makedepends=('intltool' 'xfce4-panel>=4.8.0')
optdepends=('xfce4-panel: panel menu plugin')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
options=('!libtool')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.8/${pkgname}-${pkgver}.tar.bz2
        fix-desktop-drag-and-drop.patch)
md5sums=('ed25d59f478afda552d121e96657d16f'
         '86d464bcd8ffae9f048454f49603658a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../fix-desktop-drag-and-drop.patch
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-gio-unix \
	--enable-thunarx \
	--enable-exo \
	--enable-notifications \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
