# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon-vlc
pkgver=0.3.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://phonon.kde.org"
pkgdesc="Phonon VLC backend"
license=('LGPL')
depends=('vlc')
makedepends=('cmake' 'automoc4' 'phonon')
provides=('phonon-backend')
source=("http://download.kde.org/stable/phonon-backend-vlc/${pkgver}/src/phonon-backend-vlc-${pkgver}.tar.bz2")
md5sums=('51e4be81516e7d8d8ff9ffa4009c567f')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../phonon-backend-vlc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
