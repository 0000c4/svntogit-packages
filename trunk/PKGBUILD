# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=mkvtoolnix
pkgver=4.7.0
pkgrel=1
pkgdesc="Set of tools to create, edit and inspect Matroska files"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('libmatroska' 'flac' 'libvorbis' 'file' 'boost-libs' 'lzo2' 'xdg-utils')
makedepends=('wxgtk' 'boost' 'ruby')
optdepends=('wxgtk: mkvmerge,mkvinfo and mmg GUI')
install=mkvtoolnix.install
source=("http://www.bunkus.org/videotools/${pkgname}/sources/${pkgname}-${pkgver}.tar.bz2")
md5sums=('68200debce9570fc14d934a3d6a750b5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib
  ./drake
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./drake DESTDIR="${pkgdir}" install
}
