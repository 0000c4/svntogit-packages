# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.25+27+gd3bccc0
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'docbook-xml' 'perl' 'source-highlight')
makedepends=('dblatex' 'git' 'yelp-tools')
optdepends=('dblatex: PDF support')
url="http://www.gtk.org/gtk-doc/"
_commit=d3bccc0aa153a349f6c9e816a111330572971a2e
source=("git://git.gnome.org/gtk-doc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/GTK_DOC_//' -e 's/_/\./g' -e 's/-/+/g'
}
prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
