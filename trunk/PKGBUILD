# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=f2fs-tools
pkgver=1.7.0
pkgrel=1
pkgdesc="Tools for Flash-Friendly File System (F2FS)"
arch=('i686' 'x86_64')
url='http://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/about/'
depends=('util-linux')
license=('GPL')
source=("https://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/snapshot/$pkgname-$pkgver.tar.gz"
	'f2fs-tools-allow-to-build-without-libselinux.patch::https://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/patch/?id=b0a2386089fd6efade6b89094325ed8a9f8c6fff')
sha256sums=('33d454c2e95aabef5659949c4fff15f6c9877b48349e64411de502bc62b0cbd4'
        '4ac1dcf67c0b9b4dbca12602cd9d2180363d67e721a667bf14ad4147f0bbf4f6')

prepare() {
	cd "${pkgname}-${pkgver}"

	patch -Np1 < "${srcdir}/f2fs-tools-allow-to-build-without-libselinux.patch"
}

build() {
	cd "${pkgname}-${pkgver}"

	autoreconf -fi
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" sbindir=/usr/bin install
}
