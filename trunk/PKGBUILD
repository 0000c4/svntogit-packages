# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=opensp
pkgver=1.5.2
pkgrel=6
pkgdesc="A library and a set of tools for validating, parsing and manipulating SGML and XML documents"
arch=('i686' 'x86_64')
url="http://openjade.sourceforge.net/"
license=('BSD')
depends=('sgml-common' 'perl')
makedepends=('xmlto' 'docbook-xsl')
conflicts=('jade')
source=("http://downloads.sourceforge.net/project/openjade/opensp/$pkgver/OpenSP-$pkgver.tar.gz")
sha1sums=('b4e903e980f8a8b3887396a24e067bef126e97d5')

build() {
  cd OpenSP-$pkgver
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-nls \
    --enable-http \
    --enable-default-catalog=/etc/sgml/catalog \
    --enable-default-search-path=/usr/share/sgml:/usr/share/xml \
    --enable-xml-messages
  make
}

check() {
  cd OpenSP-$pkgver
  make check || /bin/true
}

package() {
  cd OpenSP-$pkgver
  make DESTDIR="${pkgdir}" install
  
  # add symlinks for compatibility with jade, see FS#49775
  ln -v -sf onsgmls   $pkgdir/usr/bin/nsgmls
  ln -v -sf osgmlnorm $pkgdir/usr/bin/sgmlnorm
  ln -v -sf ospam     $pkgdir/usr/bin/spam
  ln -v -sf ospcat    $pkgdir/usr/bin/spcat
  ln -v -sf ospent    $pkgdir/usr/bin/spent
  ln -v -sf osx       $pkgdir/usr/bin/sx
  ln -v -sf osx       $pkgdir/usr/bin/sgml2xml
  ln -v -sf libosp.so $pkgdir/usr/lib/libsp.so
  
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
