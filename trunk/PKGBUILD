# Contributor: iztok pizorn <pizorn@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
pkgname=wpa_supplicant
pkgver=0.5.10
_madwifi_ver=0.9.4
pkgrel=3
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=('i686' 'x86_64')
makedepends=('kernel26>=2.6.27' 'kernel26<2.6.28')
depends=('openssl' 'dbus-core>=1.2.4')
optdepends=('wpa_supplicant_gui: wpa_gui program')
license=('GPL')
groups=('base')
backup=('etc/wpa_supplicant.conf')
url="http://hostap.epitest.fi/wpa_supplicant"
source=(http://hostap.epitest.fi/releases/wpa_supplicant-$pkgver.tar.gz
        config
	http://downloads.sourceforge.net/sourceforge/madwifi/madwifi-${_madwifi_ver}.tar.gz)
md5sums=('9e7b99da67c47d81121368c6d580d069'
         '94fb8071fc440dca8294c9591486921f'
         '399d20de8d855a59f20058857c2178ad')

build() {
  cd "${srcdir}"
  mv madwifi-${_madwifi_ver} madwifi
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp ../config ./.config
  sed -i 's@/usr/local@$(PREFIX)@g' Makefile
  sed -i 's@dynamic_eap_methods@@g' Makefile
  make || return 1
  make PREFIX=/usr DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/etc"
  install -m644 wpa_supplicant.conf "${pkgdir}/etc/wpa_supplicant.conf" || return 1
  install -d -m755 ${pkgdir}/usr/share/man/man{5,8} || return 1
  install -m644 doc/docbook/wpa_supplicant.conf.5 "${pkgdir}/usr/share/man/man5/" || return 1
  install -m644 doc/docbook/{wpa_background,wpa_cli,wpa_passphrase,wpa_supplicant}.8 "${pkgdir}/usr/share/man/man8/" || return 1

  install -m755 -d "${pkgdir}/usr/share/dbus-1/system-services"
  install -m644 dbus-wpa_supplicant.service "${pkgdir}/usr/share/dbus-1/system-services/fi.epitest.hostap.WPASupplicant.service" || return 1
  sed -e 's/sbin/usr\/sbin/' -i "${pkgdir}/usr/share/dbus-1/system-services/fi.epitest.hostap.WPASupplicant.service" || return 1
  install -m755 -d "${pkgdir}/etc/dbus-1/system.d"
  install -m644 dbus-wpa_supplicant.conf "${pkgdir}/etc/dbus-1/system.d/fi.epitest.hostap.WPASupplicant.conf" || return 1
}
