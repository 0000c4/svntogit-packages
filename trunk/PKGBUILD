# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.8.2
pkgrel=2
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
depends=('webkitgtk3' 'json-glib' 'libnotify' 'rest' 'hicolor-icon-theme' 'libsecret')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl')
options=(!libtool)
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        imap-smtp.patch
        smtp-auth-plain.patch
        drop-google-password-interface-1.patch
        drop-google-password-interface-2.patch
        drop-google-password-interface-3.patch)
sha256sums=('12611a002043c8acc29e9800ec009e3e19736bdb6237d246e848d7c7909fe826'
            '2b413d1f401647bd143b7dc6dd4b6d6660ff47ef9a11ccbc691b7c13de5cba69'
            'e7250be7c078053345699250433b7164751d3e457155bd35508c7d1660f459be'
            '33435c10aa1ba3fbb6fd633dc098cd1da50629f1d602521ca83be5379f2ebe48'
            'c837f9be4e5e072a06ea44076e6b9a1519c66c95b16d7a55b1dba105fded317f'
            '18dfad8ed978af1068fd057bbe323006c349ac4ea77c742aa06f1011dc92326e')

prepare() {
  cd $pkgname-$pkgver
  # Upstream 3.8 branch
  patch -Np1 -i ../imap-smtp.patch
  patch -Np1 -i ../smtp-auth-plain.patch
  
  # Backport from Ubuntu
  patch -Np1 -i ../drop-google-password-interface-1.patch
  patch -Np1 -i ../drop-google-password-interface-2.patch
  patch -Np1 -i ../drop-google-password-interface-3.patch
}
 
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexec=/usr/lib/gnome-online-accounts
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
