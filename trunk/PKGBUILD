# Maintainer: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-langcjk
pkgver=2012.28079
_revnr=${pkgver#2012.}
pkgrel=2
pkgdesc="TeX Live - CJK (Chinese, Japanese, Korean) macros and fonts"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-lang')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs')
options=(!strip)
install=texlive.install
md5sums=('2b47c29e4e1a3c56b4542ba724a7190f'
         '0a21c8f3406ac404459b588e3e8eb3ea')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps $pkgdir/var/lib/texmf/arch/installedpkgs/
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   # hack for uplatex format generation
   ln -s ../../platex/config/hyphen.cfg "${pkgdir}/usr/share/texmf-dist/tex/uplatex/config/hyphen.cfg"
   # scripts links
   install -d -m 755 "$pkgdir/usr/bin"
   ln -s /usr/share/texmf-dist/scripts/uptex/convbkmk.rb "$pkgdir/usr/bin/convbkmk"
   ln -s /usr/share/texmf-dist/scripts/jfontmaps/updmap-setup-kanji.pl "$pkgdir/usr/bin/updmap-setup-kanji"
}
