# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gnome-tweak-tool
pkgver=2.91.92
pkgrel=2
pkgdesc="A tool to customize advanced GNOME 3 options."
arch=(any)
url="https://live.gnome.org/GnomeTweakTool"
license=('GPL')
depends=('gconf' 'gsettings-desktop-schemas' 'pygobject')
makedepends=('pygobject-devel')
source=(http://download.gnome.org/sources/$pkgname/2.91/$pkgname-$pkgver.tar.bz2)
sha256sums=('db2d38b56bad1acaff3aff80eb8792b2bb679464c1fc7579ae94eb1d03650cf6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gnome-tweak-tool
}

# vim:set ts=2 sw=2 et:
