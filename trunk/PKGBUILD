# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=composer
pkgver=1.7.0
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php')
makedepends=('git')
checkdepends=('git' 'mercurial' 'parallel')
replaces=('php-composer')
conflicts=('php-composer')
provides=('php-composer')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
sha512sums=('SKIP'
            'c969880bcd242270b97301baa2290f3f8f37c6d4e4b599b09b6e7f020bcb762612be55a398336228e9bc5e43e6f7a26b5a95d20334de852fa2f6b6e15a029afa')

prepare() {
	cd ${srcdir}/${pkgname}

	php ${srcdir}/${pkgname}-${pkgver}.phar -n install --no-dev --prefer-dist
}

build() {
	cd ${srcdir}/${pkgname}

	php -d phar.readonly=Off bin/compile
}

check() {
	cd ${srcdir}/${pkgname}

	# check if the upstream build and ours differ
	diff -q ${srcdir}/${pkgname}-${pkgver}.phar composer.phar

	git config --global user.email "${USER}@${HOST}"
	git config --global user.name "${USER}"

	# Install the dev dependencies to run the unit tests
	bin/composer -n install --prefer-dist

	ls -d tests/Composer/Test/* | parallel --gnu --keep-order vendor/bin/phpunit {} \;
}

package() {
	cd ${srcdir}/${pkgname}

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -Dm755 composer.phar ${pkgdir}/usr/bin/composer
}
