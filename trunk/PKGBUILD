# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.20.0
pkgrel=3
pkgdesc='Advanced embeddable text editor'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kparts' 'libgit2')
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        'pkgbuild-syntax-highlight.patch'
	ktexteditor-selection.patch::"https://quickgit.kde.org/?p=ktexteditor.git&a=commitdiff&h=f7f330b2&o=plain")
md5sums=('4e4cdc9d49dd3a3a865d91ffaec392ba'
         '3a03efe92f1403991c51f2859ee092f6'
         '4ddfd71ab48c7e1a7f25333c8bdd0f39')

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}"/pkgbuild-syntax-highlight.patch
# Fix text selection
  patch -p1 -i ../ktexteditor-selection.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
