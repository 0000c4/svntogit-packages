# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgname=network-manager-applet
pkgver=0.8.3.996
pkgrel=2
pkgdesc="GNOME frontends to NetWorkmanager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager' 'libgnome-keyring' 'polkit-gnome' 'gtk2'
         'notification-daemon' 'libnotify' 'gnome-icon-theme'
         'mobile-broadband-provider-info')
makedepends=('intltool' 'gnome-bluetooth')
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool')
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/network-manager-applet/0.8/network-manager-applet-${pkgver}.tar.bz2
        0001-editor-fix-usage-of-nm_connection_update_secrets.patch)
sha256sums=('717d8a6fb905e8759978d2e77828300edf7145240f65bc2a5d80adabc349510e'
            'da41e8b7d36f35cb8adab03874601c90f08d3ac22401839fc77b954588c9d38a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix editing system connections
  patch -Np1 -i "$srcdir/0001-editor-fix-usage-of-nm_connection_update_secrets.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-maintainer-mode
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nm-applet ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
