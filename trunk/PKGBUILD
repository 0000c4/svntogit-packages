# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : Damir Perisa <damir.perisa@bluewin.ch>

pkgname=celestia
pkgver=1.6.1
pkgrel=1
pkgdesc="Real-time space simulation"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.shatters.net/celestia/"
depends=('gtk2' 'libtheora' 'lua' 'gtkglext' 'glut' 'libxmu' 'mesa' 'libjpeg')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/celestia/${pkgname}-${pkgver}.tar.gz
	celestia-1.5.1-gcc44.patch celestia-1.6.1-gentoo.patch)
sha1sums=('1f0fdf4525a8b9d6c708e1f6383babed670d18e7'
          '5ab2769e239a89468bb8ffb5740cbeec6018143c'
          'aebf894630fdba4c4d429622f5e2b8da56d8b61e')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # build patch
    patch -Np0 < ${srcdir}/celestia-1.5.1-gcc44.patch
    patch -Np0 -i ${srcdir}/celestia-1.6.1-gentoo.patch

    ./configure --prefix=/usr \
                --with-lua=/usr \
		--datadir=/usr/share \
                --with-gtk
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}

