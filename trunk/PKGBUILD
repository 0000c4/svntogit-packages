# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-nettool
pkgver=3.8.0
pkgrel=1
pkgdesc="A Collection of GNOME3 Networking Tools."
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libgtop dnsutils iputils dconf whois hicolor-icon-theme)
makedepends=(yelp-tools intltool nmap net-tools)
optdepends=('nmap: Port scanning'
            'net-tools: Network statistics')
groups=(gnome-extra)
url="http://www.gnome.org"
install=gnome-nettool.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        printf.patch)
sha256sums=('3a3480dcef181684f3e37d779cfd43f9b21920412aff78501de1695faf194a28'
            '4b06c0748650996100f710a2d7c90afc8fb75bbdd1984f243f42318d0985229e')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../printf.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
