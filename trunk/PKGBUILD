# $Id$
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-mail-spf
_realname=Mail-SPF
pkgver=2.8.0
pkgrel=2
pkgdesc="Perl module that provides SPF support"
arch=('any')
license=('custom')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-error' 'perl-netaddr-ip' 'perl-uri' 'perl-net-dns' 'perl')
makedepends=('perl-net-dns-resolver-programmable')
conflicts=('perl-mail-spf-query')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/J/JM/JMEHNLE/mail-spf/${_realname}-v${pkgver}.tar.gz")
md5sums=('4f5bbd59058da680bfd9ff18e31489b7')

prepare() {
  cd ${_realname}-v${pkgver}
  sed -i 's|/usr/sbin|/usr/bin|' Build.PL
}

build() {
  cd ${_realname}-v${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-v${pkgver}
  make test
}

package() {
  cd ${_realname}-v${pkgver}
  make DESTDIR="${pkgdir}"  sbin=/usr/bin install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
