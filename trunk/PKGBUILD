# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=moc
pkgver=2.4.4
pkgrel=4
pkgdesc="An ncurses console audio player with support for the mp3, ogg, and wave formats"
arch=('i686' 'x86_64')
url="http://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libsamplerate' 'libtool')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug')
optdepends=('speex: for using the speex plugin' 
	    'ffmpeg: for using the ffmpeg plugin'
	    'taglib: for using the musepack plugin' 
	    'libmpcdec: for using the musepack plugin' 
            'wavpack: for using the wavpack plugin' 
	    'libmodplug: for using the modplug plugin')
options=('!libtool')
source=(ftp://ftp.daper.net/pub/soft/moc/stable/${pkgname}-${pkgver}.tar.bz2 \
        gcc-undefined-symbols.diff curl-streams.diff)
md5sums=('647c770a5542a4ae5437386807a89796'
         'efacb8559e9145e15b0c25f8fa2a9d79'
         'bb9be1e80762851c57075c99a612af2f')
sha1sums=('e56ee13aa17c177f0afc42efe3804ebbbf46d4db'
          'a811a4ac7e049914aab528d3f06a6be6634c2720'
          'a58ca952d7f3b0ede19b95c53da96c090d0a94c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../gcc-undefined-symbols.diff
  patch -p1 -i ../curl-streams.diff
  sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' decoder_plugins/ffmpeg/ffmpeg.c
  sed -i 's|avcodec_decode_audio|avcodec_decode_audio2|' decoder_plugins/ffmpeg/ffmpeg.c
# Disabling aac to use the external ffmpeg to play them (FS#13164)
  ./configure --prefix=/usr --without-rcc --without-aac \
    --with-oss  --with-alsa  --with-jack --with-mp3 \
    --with-musepack --with-vorbis --with-flac --with-wavpack \
    --with-sndfile --with-modplug --with-ffmpeg --with-speex \
    --with-samplerate --with-curl
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
