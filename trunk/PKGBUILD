# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.25.3
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
url="https://wiki.gnome.org/Accessibility"
arch=(i686 x86_64)
license=(GPL2)
depends=(at-spi2-core libx11 libxml2 atk)
makedepends=(git meson)
_commit=d360bc28c5b041d5920a28a40a8bf110b74e3b84  # tags/AT_SPI2_ATK_2_25_3^0
source=("git+https://git.gnome.org/browse/at-spi2-atk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname \
    --sysconfdir=/etc
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
