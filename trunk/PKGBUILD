# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=20.08.0
pkgrel=2
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('p7zip: 7Z format support' 'unrar: RAR decompression support' 'unarchiver: RAR format support'
            'lzop: LZO format support' 'lrzip: LRZ format support')
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         CVE-2020-24654.patch::"https://invent.kde.org/utilities/ark/-/commit/8bf8c5ef.patch")
sha256sums=('7627ffa17466d31dfdedabaa07b491ce14b46041d04f8b20316a0fa731fab098'
            'SKIP'
            'd5dfd82fc8dccea75f59f10942c9a42e2325f3c5e3c062cc7112677b77c2f72b')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../CVE-2020-24654.patch
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

