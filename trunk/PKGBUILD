# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-widgets
pkgver=16.04.0
pkgrel=1
pkgdesc="Widgets for Baloo"
arch=(i686 x86_64)
url='https://projects.kde.org/baloo-widgets'
license=(LGPL)
depends=(baloo)
makedepends=(extra-cmake-modules kdoctools python)
replaces=(baloo4-widgets)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('438024b01dade3daeb952a1326e6199c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

