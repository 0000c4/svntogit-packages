# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=17.04.1
pkgrel=2
pkgdesc="A fast and easy to use image viewer for KDE"
url='https://kde.org/applications/graphics/gwenview/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kactivities' 'baloo' 'libkipi' 'libkdcraw' 'kparts' 'kitemmodels')
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdegraphics-gwenview')
conflicts=('kdegraphics-gwenview')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-379615.patch::"https://cgit.kde.org/gwenview.git/patch/?id=6ce5d2f8")
sha256sums=('90d595035d3f69c21791830ac99a2419227c50f30a1ccbb453c5055e4227d011'
            'SKIP'
            '7062bf139eb1b3312414eab9c8fa132aefbff930227b61de50717416376bc13d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

# fix data loss when importing pictures
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-379615.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
