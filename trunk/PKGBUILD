# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=3.32.0
pkgrel=2
pkgdesc="A character map application"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/gnome-characters"
license=(GPL2)
depends=(gjs gtk3 libunistring gnome-desktop)
makedepends=(meson gobject-introspection appstream-glib git gperf)
groups=(gnome)
_commit=329e8a8929408086b5de72657c0d564a592e872a  # tags/v3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-characters.git#commit=$_commit"
        0001-searchProvider-Fix-deprecation-fixes.patch)
sha256sums=('SKIP'
            '21896401a98f28a296973ea3d4c11206dafc00d324a4e3166b028d505116d52c')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/gnome-characters/issues/49
  git apply -3 ../0001-searchProvider-Fix-deprecation-fixes.patch
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
