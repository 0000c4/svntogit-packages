# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dconf-editor
pkgver=3.22.0+9+g8f28aa1
pkgrel=1
pkgdesc="dconf Editor"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(GPL3)
groups=(gnome)
depends=(gtk3 dconf)
makedepends=(vala intltool appstream-glib git)
_commit=8f28aa1a07fe9871bcdbd1cf1af4adb9c1da1aef
source=("git://git.gnome.org/dconf-editor#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
