# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: daniel g. siegel <dgsiegel@gmail.com> 

pkgname=libsmbios
pkgver=2.2.26
pkgrel=1
pkgdesc="A library for providing access to as much BIOS information as possible"
arch=(i686 x86_64)
url="http://linux.dell.com/libsmbios/main/index.html"
license=('GPL' 'custom')
depends=('gcc-libs')
makedepends=('libxml2' 'python2' 'chrpath')
optdepends=('python2: tools')
source=(http://linux.dell.com/libsmbios/download/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool')
md5sums=('8ae63da74912deffa5b6b2602879c7a9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build
  cd build
  PYTHON=python2 ../configure --prefix=/usr --sysconfdir=/etc --disable-static
  mkdir -p out/libsmbios_c
  mkdir -p out/libsmbios_c++
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  chrpath -d "${pkgdir}/usr/sbin/smbios-sys-info-lite"

  cp -a ../src/include/* "${pkgdir}/usr/include/"
  cp -a out/public-include/* "${pkgdir}/usr/include/"

  rm -rf "${pkgdir}/etc/yum"
  rm -rf "${pkgdir}/usr/lib/yum-plugins"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../COPYING-OSL "${pkgdir}/usr/share/licenses/${pkgname}/"

}
