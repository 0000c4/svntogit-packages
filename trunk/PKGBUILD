# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=perl-archive-zip
pkgver=1.63
pkgrel=1
pkgdesc="Provide a perl interface to ZIP archive files"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/Archive-Zip/"
depends=('perl')
checkdepends=('perl-test-mockmodule')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PH/PHRED/Archive-Zip-${pkgver}.tar.gz)
sha512sums=('53876bbda170ab8184f80bb43794d348aff1b82ba650694158ddef28b27b87f5d4ac1e4cb11be2c82c939e09787478543faec116ac76f0802064da667b4ebac2')

build() {
  cd Archive-Zip-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Archive-Zip-${pkgver}
  make test
}


package() {
  cd Archive-Zip-${pkgver}
  make DESTDIR="${pkgdir}" install
}
