# $Id$
# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.30.1.0
pkgrel=1
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libunique>=1.1.6' 'gconf>=2.28.1' 'vte>=0.24.1' 'gdl>=2.30.0' 'autogen>=5.9.9' 'devhelp>=2.28.1' 'gtksourceview2>=2.10.1' 'glade>=3.6.7' 'libgda>=4.0.8' 'subversion>=1.6.9' 'gnome-icon-theme>=2.30.2' 'perl-locale-gettext')
makedepends=('gnome-doc-utils>=0.19.5' 'intltool')
conflicts=('gnome-build')
replaces=('gnome-build')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('3c64d80723e2a60311405e83b1b0cab94d07003a579f47cdb1f8073dcfb4e3c1')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
   
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain anjuta ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
