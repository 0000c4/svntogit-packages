# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-3d
pkgname=(python-pyqt6-3d)
pkgver=6.0.0
pkgrel=1
pkgdesc="Python bindings for Qt3D"
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqt3d/intro"
license=(GPL3)
groups=(pyqt6)
depends=(qt6-3d python-pyqt6)
makedepends=(sip pyqt-builder)
source=("https://pypi.python.org/packages/source/P/PyQt6-3D/PyQt6_3D-$pkgver.tar.gz"
         fix-build.patch)
sha256sums=('df00e17640a5bb209992e66a6913bd5acf5384d3be3e01b369ec3422aae4c6ac'
            '7015a382361e27d209fbbb35b395c3fd8da82197a043b32cdc4d472af45aef40')

prepare() {
  patch -d PyQt6_3D-$pkgver -p1 -i ../fix-build.patch
}

build() {
  cd PyQt6_3D-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake-qt6
  cd build
  make
}

package_python-pyqt6-3d(){
  cd PyQt6_3D-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
