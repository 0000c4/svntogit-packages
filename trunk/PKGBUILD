# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=putty
pkgver=0.69
pkgrel=1
pkgdesc="A terminal integrated SSH/Telnet client"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
depends=('gtk3')
validpgpkeys=('0054DDAA8ADA15D2768A6DE79DFE2648B43434E4') # PuTTY Releases <putty@projects.tartarus.org>
source=("https://the.earth.li/~sgtatham/putty/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "putty-${pkgver}.tar.gz.sig::https://the.earth.li/~sgtatham/putty/${pkgver}/putty-${pkgver}.tar.gz.gpg")
sha256sums=('b7dad241ff01b0cbb9dc4c1471ec7cacf8f08d98a581aeb2f336da3c0eb96ad1'
            'dbe1caa557a91c4072191eb4890fdc675c0264457d298c0b71da1a4eb84acc30')

build() {
  cd ${pkgname}-${pkgver}/unix
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}/unix
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
