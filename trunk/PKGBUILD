# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=2.91.93
pkgrel=1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system."
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=('GPL')
depends=('libnotify' 'gconf' 'libcanberra' 'gnome-icon-theme' 'upower'
         'libgnome-keyring' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-control-center')
options=(!emptydirs)
install=gnome-power-manager.install
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-power-manager/2.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('ad85a7382f3bfeb8a355cfb7d0c8ee65b23f7ffdb76071e471a6c88a38a326e9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --disable-scrollkeeper \
      --with-dbus-services=/usr/share/dbus-1/services \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
