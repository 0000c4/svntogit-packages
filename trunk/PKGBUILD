# $Id$
# Maintainer: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

pkgname=slim
pkgver=1.3.2
pkgrel=1
pkgdesc='Desktop-independent graphical login manager for X11'
arch=('i686' 'x86_64')
url='http://slim.berlios.de/'
license=('GPL2')
depends=('pam' 'libxmu' 'libpng' 'libjpeg>=8' 'libxft')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim')
install=slim.install
source=("http://download.berlios.de/${pkgname}/${pkgname}-$pkgver.tar.gz"
        'slim'
        'slim.logrotate'
        'slim.pam'
        'slim-restart.patch'
        'slim-tty-slowness.patch'
        'slim-gcc44.patch')
sha1sums=('e421d5487732c8317f8f591906661e014b036358'
          '6fe0ba83509af634bce47be34e30995965bffc79'
          'b969cc902c1d9915a5609141a652c77b2732407b'
          'aff213af9ade7fe05db54fcd9a193e3dcb002aa9'
          'e9b5b624da2063d4e0cbc68de44a3eae22d509cb'
          '213fefe8533c845ea8c40585b6a8097820d5e5d2'
          '51121d451116c768d0fc027ff1ea70aaaef036e7')

build() {
  cd "$srcdir/${pkgname}-$pkgver"
  
  sed -i -e 's/png12/png14/g' Makefile # libpng 1.4.0
  patch -p1 < ../slim-gcc44.patch
  patch -p1 < ../slim-tty-slowness.patch # fix sluggish tty after slim start
  patch -p1 < ../slim-restart.patch # restart X server if killed
  # See http://developer.berlios.de/patch/?func=detailpatch&patch_id=2378&group_id=2663

  make USE_PAM=1
}

package() {
  cd "$srcdir/${pkgname}-$pkgver"

  make DESTDIR="$pkgdir" MANDIR=/usr/share/man install

  install -D -m755 ../slim "$pkgdir/etc/rc.d/slim"
  install -D -m644 ../slim.logrotate "$pkgdir/etc/logrotate.d/slim"
  install -D -m644 ../slim.pam "$pkgdir/etc/pam.d/slim"

  # Provide sane defaults
  sed -i 's|#xserver_arguments.*|xserver_arguments   -nolisten tcp vt07|' "$pkgdir/etc/slim.conf"
  sed -i 's|/var/run/slim.lock|/var/lock/slim.lock|' "$pkgdir/etc/slim.conf"
}
