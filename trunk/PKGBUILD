# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=2.90.1.git20110928
pkgrel=1
pkgdesc="An iTunes-like music playback and management application"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.rhythmbox.org"
depends=('libgpod' 'gvfs-afc' 'totem-plparser' 'libmusicbrainz3' 'libmtp' 'lirc-utils' 'libwebkit3' 'libdmapsharing' 'media-player-info' 'python2-gobject'
         'python-gnomekeyring' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'json-glib' 'desktop-file-utils' 'libpeas' 'python-mako' 'grilo' 'dconf')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.20.1' 'intltool' 'brasero' 'gnome-common' 'gtk-doc' 'gobject-introspection')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: cd burning')
options=('!libtool' '!emptydirs' '!strip')
install=rhythmbox.install
#source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.13/${pkgname}-${pkgver}.tar.bz2)
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('b95b2aed0cd485198fbefdcb8dbf2a1ca3d4eb7d31b1e02d610fb16384191e8a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CFLAGS="$CFLAGS -g -O1"
  export CXXFLAGS="$CXXFLAGS -g -O1"
  PYTHON=/usr/bin/python2 ./autogen.sh
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/rhythmbox \
      --localstatedir=/var --disable-static \
      --enable-daap --enable-python \
      --with-mdns=avahi --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
