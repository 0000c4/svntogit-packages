# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hunspell-es
pkgname=(hunspell-es_any hyphen-es mythes-es)
pkgver=2.3
pkgrel=2
epoch=1
arch=(any)
url="https://github.com/sbosio/rla-es"
license=(LGPL3)
_countries=(
  'AR "Argentina"'
  'BO "Bolivia"'
  'CL "Chile"'
  'CO "Colombia"'
  'CR "Costa Rica"'
  'CU "Cuba"'
  'DO "Dominican Republic"'
  'EC "Ecuador"'
  'ES "Spain"'
  'GT "Guatemala"'
  'HN "Honduras"'
  'MX "Mexico"'
  'NI "Nicaragua"'
  'PA "Paraguay"'
  'PE "Peru"'
  'PR "Puerto Rico"'
  'PY "Paraguay"'
  'SV "El Salvador"'
  'UY "Uruguay"'
  'VE "Venezuela"'
)

source=(https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_ANY.oxt)
noextract=(es_ANY.oxt)

for _co in "${_countries[@]}"; do
  _code=${_co%% *}
  noextract+=(es_$_code.oxt)
  source+=("https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_$_code.oxt")
  _pkgname=hunspell-es_${_code,,}
  pkgname+=($_pkgname)
  eval "package_$_pkgname() {
    _package $_co
  }"
done

sha256sums=('428148e473091229ab3918d9282ee0fbeeee39e4c842f592a7aa3924a31452fd'
            '8d0952c6eb3bf40bcbc60cd8ea8f876f8e0c4ff714b65e06556ee06bfeaa0a7e'
            '97e3b37efb3bb0149405bee38666b75a44d4c683a3f0b3b77ff442299a5987a2'
            '15c99c78cbeae09a02a74a8eff0beda219ae639e2673100fad9cec9ad93aca99'
            'bc876b22df92211791655fee3eb2f74a9c52e10cc1f2143239fd5326c0a1fefa'
            '5f834d9a6570b351702adeaa0d160c0514b4510147623d8c70cc1d3e5c49dcfe'
            '2051ce3aea6b8e50b3ec305b6915320ced630b2f6e89c27126b33bd5d58f8476'
            'a63fd1ad3db6dc1bfd1585320c18775fd58df4f7e7fc3dce9903284053536412'
            '7c48d2845312bcd83c5f85cf20672fcdbeb632ab7fde5403bd393c61fafc3b55'
            '837fe70fe5dade69a5991f5088e68368a139ec76655bb551ebfc1c2ae1c4a71a'
            '1fdf774bff8e923e03177beb5a8b2d8c47760274617adf59a78904bdc22356f5'
            'b7710785e7079b62609ab800fa1fe9f4de337454fbac15bc5d10955a2af298ab'
            'ec4fb742a1bce1cb7de92ca544bb9583c7956dcf562465ca9d41fd6eff44c492'
            '9d3990fb1f3686fa2a0880ba42bc6e3a9ca9702500cb5d9a2a16e226a8859514'
            'b631d583bf153d02d117724cfeace26269eb2c24f390bc3d989ae32bc7b59e5f'
            '8960855f46f0a5febcc433882f5fc29a7d85a7861e0f1005b8d97c51434aa59b'
            '46652a01289460926fabf01ee89cdd162bd22499e3dd48cc2e9b31cee8b3a232'
            'fbc2dc7ac633cc81592d4ac5f3170415dbb786669e3a93f8b72b9a2b7fec9f78'
            'ea3ccd70b22f57d31ecfd6c4cf5e8545c59f8b0bff558619f824a0116bfdeaa4'
            'c516f12a801161497c765d58021a04f7596e9957c22d915527596c76b5bd7ec3'
            'b53bffc7b041831df47babefc667a5a00f01ba5bad77f264219b05eaf8670bc8')

_package() {
  pkgdesc="Spanish ($2) hunspell dictionary"
  depends=(hunspell)
  conflicts=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es_$1.oxt

  install -dm755 "${pkgdir}/usr/share/hunspell"
  install -Dm644 es_$1.dic es_$1.aff "$pkgdir/usr/share/hunspell"

  install -d "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hunspell-es_any() {
  pkgdesc="Spanish (generic) hunspell dictionary"
  depends=(hunspell)
  conflicts=(hunspell-es)
  replaces=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt

  install -Dm644 es_ANY.dic es_ANY.aff -t "$pkgdir"/usr/share/hunspell

  install -d "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
  
  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hyphen-es() {
  pkgdesc="Spanish hyphenation rules"
  depends=(hyphen)

  install -Dm644 hyph_es_ANY.dic -t "$pkgdir"/usr/share/hyphen/

  pushd "$pkgdir"/usr/share/hyphen/
  for _co in "${_countries[@]}"; do
    ln -s hyph_es_ANY.dic hyph_es_${_co%% *}.dic
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
  
  install -Dm644 README_hyph_es_ANY.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_mythes-es() {
  pkgdesc="Spanish thesaurus"
  depends=(libmythes)

  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt
  install -Dm644 th_es_ES_v2.* -t "$pkgdir"/usr/share/mythes

  pushd "$pkgdir"/usr/share/mythes/
  for _co in "${_countries[@]}"; do
    ln -s th_es_ES_v2.idx "th_es_"${_co%% *}"_v2.idx" || true
    ln -s th_es_ES_v2.dat "th_es_"${_co%% *}"_v2.dat" || true
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
  for file in "$pkgdir"/usr/share/mythes/*; do
    ln -sv /usr/share/mythes/$(basename $file) .
  done
  popd
  
  install -Dm644 README_th_es_ES.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}
