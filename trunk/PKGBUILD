# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-python
pkgver=5.0.3
pkgrel=1
pkgdesc="Python language and documentation plugin for KDevelop"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop python)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/kdevelop/$pkgver/src/kdev-python-$pkgver.tar.xz"{,.sig}
        kdev-python3.6.patch::"https://cgit.kde.org/kdev-python.git/patch/?h=5.0-python36&id=e79ea8fa&id2=72f97de7")
sha1sums=('26ff325e2ae1cff4ded3fa4396a6b4dbff5706d6'
          'SKIP'
          '019f837b94fa41cb18e55018ad151554e16d7c5a')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6') # Sven Brauch <mail@svenbrauch.de>

prepare() {
  mkdir -p build

# Support Python 3.6
  cd kdev-python-$pkgver
  patch -p1 -i ../kdev-python3.6.patch
}

build() {
  cd build
  cmake ../kdev-python-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
