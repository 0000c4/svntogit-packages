# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.34.0
pkgrel=1
epoch=1
pkgdesc="Music player and management application"
url="https://wiki.gnome.org/Apps/Music"
arch=(x86_64)
license=(GPL)
depends=(grilo grilo-plugins tracker-miners libdmapsharing libmediaart gtk3 gvfs
         python-gobject python-cairo gst-plugins-base python-requests libdazzle)
makedepends=(gobject-introspection git meson yelp-tools appstream-glib)
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
groups=(gnome)
_commit=7ef36ed4b3a00c5af3f0438e16afa45fb8ec83c7  # tags/3.34.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-music.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git"
        "git+https://gitlab.gnome.org/mschraal/gfm.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.subprojects/libgd.url "$srcdir/libgd"
  git config --local submodule.subprojects/gfm.url "$srcdir/gfm"
  git submodule update
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  # appdata style violations
  meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
