# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-anyevent
pkgver=7.01
pkgrel=1
pkgdesc='the DBI of event loop programming'
epoch=3
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl 'perl-ev>=4.00' 'perl-guard>=1.02' 'perl-async-interrupt>=1.0')
optdepends=('perl-net-ssleay: TLS/SSL connections with AnyEvent::Handle'
            'perl-json-xs: send or receive JSON with AnyEvent::Handle')
url=https://metacpan.org/release/AnyEvent
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/AnyEvent-$pkgver.tar.gz")
md5sums=(f26c1d03d7f5fe7d82e6885e5887bf8f)
sha512sums=(3f98f917c6298a2f22e7f295ac8596afd03ef279511d3a25e602a9bfc401e0a380345b58cc9932bdb04765377031d0ac865de9caf9f8bb6ccedb0f0a8c2bad93)
_dir="$srcdir/AnyEvent-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
