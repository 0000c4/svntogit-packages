# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=liferea
pkgver=1.12.6
pkgrel=1
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('x86_64')
url="https://lzone.de/liferea"
license=('GPL')
depends=('webkit2gtk' 'libpeas' 'python-gobject')
makedepends=('intltool' 'gobject-introspection')
optdepends=('libsecret: for keyring plugin support'
            'python-cairo: for the tray icon plugin'
	    'gst-plugins-base: for media player plugin support'
	    'gst-plugins-good: for media player plugin support'
	    'gst-plugins-bad: for media player plugin support')
options=('!emptydirs')
source=(https://github.com/lwindolf/liferea/releases/download/v$pkgver/$pkgname-${pkgver}b.tar.bz2)
sha256sums=('2da4adba18bdc7294d5a3c850f594894863034ca9dc1b9448ac0971deb0bf90e')

build() {
  cd $pkgname-${pkgver}b
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-${pkgver}b
  make DESTDIR="${pkgdir}" install
}
