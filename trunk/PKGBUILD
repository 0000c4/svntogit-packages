# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.6.5
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://www.freetype.org/"
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
makedepends=('libx11')
optdepends=('libx11: Some demo programs')
provides=('libfreetype.so')
install=freetype2.install
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2{,.sig}
        http://download.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.bz2{,.sig}
        http://download.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.bz2{,.sig}
        0001-Enable-table-validation-modules.patch
        0002-Enable-subpixel-rendering.patch
        0003-Make-subpixel-hinting-mode-configurable.patch
        0004-Keep-default-interpreter-version-as-35.patch)
sha1sums=('5db38ae2308c5a2c519c8e071e98b70d61585201'
          'SKIP'
          'e62cc983baf103b111f7608ee1b06f12b1774a1c'
          'SKIP'
          '6562596b3fc8c18a7beb5509b2e4a7daf7ee1d48'
          'SKIP'
          'e1fde7854d2a64868a5eef07415ad23c339fc630'
          '7da3af8e1e002e39a247c37a05a10beb576007d6'
          'c64ba9d37732fc75de7817d1d679a3e38efdb4cb'
          '1f30feeba9c51c9ec745bd29ef07beccf9544338')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

prepare() {
  # Rename source dir to allow building the demos
  mv freetype-${pkgver} freetype2

  cd freetype2
  patch -Np1 -i ../0001-Enable-table-validation-modules.patch
  patch -Np1 -i ../0002-Enable-subpixel-rendering.patch

  # Provide a way to set the default subpixel hinting mode
  # at runtime, without depending on the application to do so.
  patch -Np1 -i ../0003-Make-subpixel-hinting-mode-configurable.patch

  # Keep the classic subpixel hinting mode as default, as expected
  # by upstream for the 2.6 series
  patch -Np1 -i ../0004-Keep-default-interpreter-version-as-35.patch

  cd ../ft2demos-${pkgver}

  # Suppress RPATH
  sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
  cd freetype2
  ./configure --prefix=/usr --disable-static
  make

  # Build demos
  cd ../ft2demos-${pkgver}
  make
}

check() {
  cd freetype2
  make -k check
}

package() {
  cd freetype2
  make DESTDIR="${pkgdir}" install

  # Package docs
  install -dm755 "${pkgdir}/usr/share/doc"
  cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"

  # Package demos
  cd ../ft2demos-${pkgver}
  for _i in bin/{f,t}t*; do
    libtool --mode=install install $_i "$pkgdir/usr/bin/"
  done
}
