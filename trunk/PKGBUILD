# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=k3b
pkgver=17.04.3
pkgrel=1
epoch=1
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('i686' 'x86_64')
url="https://userbase.kde.org/K3b"
license=('GPL')
depends=('libkcddb' 'kcmutils' 'knotifyconfig' 'libmad' 'kfilemetadata' 'knewstuff'
         'libmpcdec' 'libdvdread' 'libburn' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'emovix: for bootable multimedia cd/dvd support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for cd ripping support')
groups=(kde-applications kdemultimedia)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d3d5ef74df0f6a15e4d0238d502d49af09909f4392fe80f35e5cc7a223983028'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DK3B_ENABLE_PERMISSION_HELPER=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
