# $Id: PKGBUILD 262594 2016-03-26 20:49:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=3.0
pkgrel=2
pkgdesc="Edit and paint images"
arch=(i686 x86_64)
url="http://krita.org"
license=(LGPL)
depends=(kio kitemmodels gsl libraw opencolorio exiv2 openexr openjpeg fftw curl boost-libs hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python boost eigen vc0.7 poppler-qt5)
optdepends=('poppler-qt5: PDF filter')
conflicts=(calligra-krita)
replaces=(calligra-krita)
source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tgz"
        krita-fix-build.patch::"https://quickgit.kde.org/?p=krita.git&a=commitdiff&h=100a148e&o=plain")
md5sums=('beb7140471d1b6b0ae92d664743c067f'
         'ebb9982d57cecd52c86bc72bab395889')

prepare() {
  mkdir -p build

# Fix build
  cd $pkgname-$pkgver
  patch -p1 -i ../krita-fix-build.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DPACKAGERS_BUILD=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
