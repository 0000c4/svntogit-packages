# $Id: PKGBUILD 261838 2016-03-16 07:36:15Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-stats
pkgver=5.34.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A library for accessing the usage data collected by the activities system"
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kactivities')
makedepends=('extra-cmake-modules' 'boost')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1920c36a04461d0a3bc7f059bb7a2cfa64ce135d7db642d40c95a7a621c1cab5'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
