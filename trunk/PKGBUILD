# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname=vpnc
pkgver=0.5.3
pkgrel=4
pkgdesc="VPN client for cisco3000 VPN Concentrators"
url="http://www.unix-ag.uni-kl.de/~massar/vpnc/"
license=('GPL')
depends=('libgcrypt' 'openssl' 'iproute2' 'net-tools')
optdepends=('openresolv: Let vpnc manage resolv.conf')
arch=('i686' 'x86_64')
source=("http://www.unix-ag.uni-kl.de/~massar/vpnc/$pkgname-$pkgver.tar.gz"
        'vpnc.conf'
        'vpnc.rc'
        fix-iproute-syntax.patch)
backup=('etc/vpnc/default.conf')
md5sums=('4378f9551d5b077e1770bbe09995afb3'
         'a3f4e0cc682f437e310a1c86ae198e45'
         'c5885162ac198eaa36da9a01e7c0c55b'
         '193a35773594667a2782701aff372551')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  # Build hybrid support
  sed -i 's|^#OPENSSL|OPENSSL|g' Makefile
  
  # Fix vpnc-script
  sed 's|/sbin/resolvconf|/usr/sbin/resolvconf|g' -i vpnc-script.in
  patch -p1 -i "${srcdir}"/fix-iproute-syntax.patch

  make PREFIX=/usr
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install

  install -D -m644 "${srcdir}"/vpnc.conf "${pkgdir}"/etc/vpnc/default.conf
  install -D -m755 "${srcdir}"/vpnc.rc "${pkgdir}"/etc/rc.d/vpnc
  rm -f "${pkgdir}"/etc/vpnc/vpnc.conf
}
