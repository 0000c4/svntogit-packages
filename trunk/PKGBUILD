# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.32.0
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils ctags
         libpeas vte3 vala python-jedi autoconf-archive sysprof flatpak gspell libdazzle
         template-glib jsonrpc-glib python-sphinx webkit2gtk glade)
makedepends=(llvm gobject-introspection gtk-doc yelp-tools appstream-glib git mm-common meson)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-code-assistance: Legacy assistance services'
            'python-lxml: documentation support for Python auto-completion')
groups=(gnome-extra)
_commit=1c20f5b5c5a322d45ca5bb795c95ccb0d46892c5  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D help=true
  ninja -C build
}

check() {
  # some tests need an installed gnome-builder
  xvfb-run meson test -C build || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
