# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=vlc
pkgver=0.9.9a
pkgrel=6
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL2')
depends=('a52dec' 'fluidsynth' 'zvbi' 'libdvbpsi' 'lirc-utils' 'libdca' 'hal' 'libproxy'
         'sdl_image' 'libdvdnav>=4.1.3' 'lua' 'libxv' 'libv4l' 'libcddb' 'smbclient' 'libmatroska'
	 'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg>=0.5' 'libshout' 'libmad' 'qt' 'fribidi'
	 'libmpeg2' 'libmodplug' 'avahi' 'ttf-dejavu')
makedepends=('pkgconfig' 'live-media>=2009.04.20')
optdepends=('libnotify')
options=('!libtool' 'force')
source=(http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c8d832deeef7f4072a73506f8d033198')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i -e 's:/usr/share/fonts/truetype/freefont/FreeSerifBold.ttf:/usr/share/fonts/TTF/DejaVuSerif-Bold.ttf:' modules/misc/freetype.c
  [ "${CARCH}" = "i686" ] && EXTRAFEATURES="--enable-loader --enable-live555 --with-live555-tree=/usr/lib/live-media"
  [ "${CARCH}" = "x86_64" ] && EXTRAFEATURES="--enable-fast-install --enable-live555 --with-live555-tree=/usr/lib/live-media"
  ./configure --prefix=/usr \
              --enable-dvdread \
	      --enable-jack \
              --enable-dvdnav \
              --disable-rpath \
              --enable-qt4 \
              --enable-faad \
              --enable-alsa \
              --enable-skins2 \
              --enable-dvb \
              --enable-v4l \
              --enable-theora \
              --enable-flac \
              --enable-snapshot \
              --enable-hal \
              --enable-dbus \
              --enable-ogg \
              --enable-dbus-control \
              --enable-shared \
              --enable-nls \
              --enable-lirc \
              --enable-shout \
              --enable-pvr \
              --enable-release \
              --program-suffix= \
	      --enable-realrtsp ${EXTRAFEATURES} || return 1
  CFLAGS="-fPIC"
  make || return 1
  make DESTDIR=${pkgdir}/ install || return 1
  
  for res in 16 32 48 128; do
    install -D -m644 ${srcdir}/vlc-${pkgver}/share/vlc${res}x${res}.png \
		     ${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png || return 1
  done
  rm -rf ${pkgdir}/usr/lib/mozilla
}
