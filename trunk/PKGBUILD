# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=5.1.0
pkgrel=3
pkgdesc="A powerful BitTorrent client for KDE"
arch=(x86_64)
url='https://www.kde.org/applications/internet/ktorrent/'
license=(GPL2)
depends=(libktorrent kcmutils knotifyconfig kross)
makedepends=(extra-cmake-modules kdoctools boost plasma-workspace kdnssd taglib kde-syndication kplotting kdewebkit)
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin'
            'kde-syndication: syndication plugin' 'kdewebkit: search plugin, syndication plugin' 'kplotting: statistics plugin')
source=("http://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
        ktorrent-qt5.11.patch::"https://cgit.kde.org/ktorrent.git/patch/?id=16691987"
        ktorrent-qt5.11b.patch::"https://cgit.kde.org/ktorrent.git/patch/?id=c66233cd"
        ktorrent-qt5.11c.patch::"https://cgit.kde.org/ktorrent.git/patch/?id=672c5076")
sha256sums=('3c57882f78006dcec3c5fb243479e995cc541e51e6acded5bf98a09900c486a3'
            'SKIP'
            '653d4665807e007ab1d64217e4ca3ec1c94eca912e09b5b7616fa0fdc76a5423'
            'ea4372920e7741d6accde2a9127761c801eb2548d3e262e0da799742e542a61e'
            '9e63246e5a975f22dfd9cc2aae41153c676a1420daffd1de0a2e6c6a39d2fb82')
validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../ktorrent-qt5.11.patch # Fix build with Qt 5.11
  patch -p1 -i ../ktorrent-qt5.11b.patch
  patch -p1 -i ../ktorrent-qt5.11c.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
