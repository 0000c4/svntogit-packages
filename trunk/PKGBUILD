# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libetpan
pkgver=1.4
pkgrel=1
pkgdesc="A portable middleware for email access"
arch=('i686' 'x86_64')
url="http://www.etpan.org/libetpan.html"
license=("custom:etpan")
depends=('libsasl' 'curl' 'expat')
source=(https://github.com/dinhviethoa/${pkgname}/archive/${pkgver}.tar.gz)
md5sums=('30a99dc58ff783157d8415587a3427b8')

build() {
  cd ${pkgname}-${pkgver}
  
  # generate automake files that are not included in the tarball
  libtoolize --force --copy
  aclocal -I m4
  autoheader
  autoconf
  automake --add-missing --foreign --force --copy
  #autoreconf -vfi

  ./configure --prefix=/usr \
    --disable-static \
    --disable-db
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/$pkgname/license.txt
}
