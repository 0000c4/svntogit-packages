# $Id$
# Maintainer:

pkgname=freetds
pkgver=1.00.83
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='http://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('openssl' 'unixodbc')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/$pkgname-$pkgver.tar.bz2")
sha512sums=('2f5442cdcbd27dab36b596c630d61b2ee95a375601ef2bd121509d4f667896a7c74af1cb5d16a1d7ff1b7ab0cac12e2655e8d7305a67b169f6f19652398e6f76')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/freetds --enable-msdblib \
    --with-tdsver=7.0 --with-unixodbc=/usr --with-openssl
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
