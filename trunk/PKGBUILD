# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
 
pkgname=gthumb
pkgver=2.10.10
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
arch=(i686 x86_64)
license=('GPL')
url="http://gthumb.sourceforge.net/"
depends=('libgphoto2>=2.4.3' 'libgnomeui>=2.24.0' 'libxtst' 'libxxf86vm' 'desktop-file-utils')
makedepends=('perlxml' 'pkgconfig' 'gnome-doc-utils>=0.14.0')
options=('!libtool' '!emptydirs')
install=gthumb.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.10/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b3344b31f82830fdcf86865f4ecb7a95')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
