# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=qhull
pkgver=2019.1
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=(x86_64)
url="http://www.qhull.org/"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=("http://www.qhull.org/download/qhull-2019-src-7.3.2.tgz")
sha256sums=('2b7990558c363076261564f61b74db4d0d73b71869755108a469038c07dc43fb')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() { 
  cd build

  make DESTDIR="$pkgdir" install

  # Install license
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
