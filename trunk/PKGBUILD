# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=vte3
pkgname=(vte3 vte-common)
pkgver=0.31.0
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
makedepends=('intltool' 'gobject-introspection' 'gtk3')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/vte/${pkgver%.*}/vte-$pkgver.tar.xz
make_alt_work.patch::http://git.gnome.org/browse/vte/patch/?id=b73782a28894e25ed146271f9d6c6775a6836199)
sha256sums=('3e5925acc9d3eefe80a11683492d94b85bb4fe0b3c33d204c123f411c0efd60f'
            '8307623b13bcaf214cc25207ee7afaf65575d334623460311c24437fbbae14d3')

build() {
  cd "vte-$pkgver"
  #warning: type-punning to incomplete type might break strict-aliasing rules
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  patch -Np1 -R -i "$srcdir/make_alt_work.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection
  make
}

package_vte3(){
  depends=('gtk3' 'vte-common')
  cd "vte-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/lib/vte/gnome-pty-helper
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  cd "vte-$pkgver"

  make -C gnome-pty-helper DESTDIR="$pkgdir" install
}
