# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pavucontrol
pkgdesc="PulseAudio Volume Control"
pkgver=3.0+31+g1d4280a
pkgrel=1
arch=(x86_64)
url="http://freedesktop.org/software/pulseaudio/pavucontrol/"
license=(GPL)
depends=(libcanberra-pulse gtkmm3 libsigc++)
makedepends=(intltool lynx git)
optdepends=("pulseaudio: Audio backend")
_commit=1d4280a0d4435faebfbf3ad3dc14f346000a6d1d  # master
source=("git+https://anongit.freedesktop.org/git/pulseaudio/pavucontrol#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./bootstrap.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk3
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
