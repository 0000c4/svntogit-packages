# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-epub-thumbnailer
pkgver=1.5+3+g0694740
pkgrel=1
pkgdesc="Thumbnailer for EPub and MOBI books"
url="https://gitlab.gnome.org/GNOME/gnome-epub-thumbnailer"
license=(GPL)
arch=(x86_64)
_commit=06947404d8660becc7f4665e7409964862704f4b  # master
depends=(gdk-pixbuf2 libarchive libxml2)
makedepends=(git)
source=("git+https://gitlab.gnome.org/GNOME/gnome-epub-thumbnailer.git#commit=$_commit")
sha256sums=('SKIP')
 
pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-compile-warnings=minimum
  make
}

check() {
  make -C $pkgname check
}
 
package() {
  DESTDIR="$pkgdir" make -C $pkgname install
}

# vim:set sw=2 et:
