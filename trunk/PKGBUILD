# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=mpfr
_basever=3.0.0
_patchlevel=p8
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.mpfr.org/"
depends=('gmp>=5.0')
options=('!libtool')
install=mpfr.install
source=(http://www.mpfr.org/mpfr-current/mpfr-${_basever}.tar.xz
        mpfr-${_basever}-${_patchlevel}.patch)
md5sums=('8ab3bef2864b8c6e6a291f5603141bbd'
         'ac4591dea9abdd7573cd05a2fce2bb14')

build() {
  cd "${srcdir}/${pkgname}-${_basever}"
  patch -Np1 -i ${srcdir}/${pkgname}-${_basever}-${_patchlevel}.patch
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_basever}"
  make DESTDIR="${pkgdir}" install
}
