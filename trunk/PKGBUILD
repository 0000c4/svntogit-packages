# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Jason Taylor <jftaylor21@gmail.com>

pkgbase=lapack
pkgname=('blas' 'lapack')
pkgver=3.3.1
_sover=3.3.0
pkgrel=1
url="http://www.netlib.org/lapack"
makedepends=('gcc-fortran')
arch=('i686' 'x86_64')
license=("custom")
source=(http://www.netlib.org/${pkgbase}/${pkgbase}-${pkgver}.tgz
        lapack-3.1.1-make.inc.patch
        Makefile.blas
        Makefile.lapack
        LICENSE.blas)
sha1sums=('3ce437e05f0e9a3c8148ec41922f6efeef0ebdac'
          '71bf7696f1c841339163e82d863dd62e484eb1cf'
          '47f46f83ad9a82b6539e040298a143baee6543fd'
          '128a258fde1b923c0b0b958a8a8ae8aa7657e44b'
          '2491a151a37f0162b25fc4e4e9a8ac444b574a76')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/lapack-3.1.1-make.inc.patch"
  cp -f INSTALL/make.inc.gfortran make.inc
  cp -f "${srcdir}/Makefile.blas" BLAS/SRC/Makefile
  cp -f "${srcdir}/Makefile.lapack" SRC/Makefile

  export FC=gfortran

  pushd BLAS/SRC
  make clean
  FFLAGS="${CFLAGS/-O?/-O0}" make dcabs1.o
  FFLAGS="${CFLAGS} -fPIC" CFLAGS="${CFLAGS} -fPIC" make shared
  cp libblas.so.${_sover} "${srcdir}/${pkgbase}-${pkgver}/"

  popd
  ln -s libblas.so.${_blasver} libblas.so

  pushd INSTALL
  make clean
  make NOOPT="${CFLAGS/-O?/-O0} -fPIC" OPTS="${CFLAGS} -fPIC"
  popd
  # make sure no files with '_', *vxx.o *vfx.o and *2.O as defined in *ASRC
  #  are in OBJS
  pushd SRC
  make clean
  
  make FFLAGS="${CFLAGS} -fPIC" CFLAGS="${CFLAGS} -fPIC" shared
  cp liblapack.so.${_sover} "${srcdir}/${pkgbase}-${pkgver}/"
  popd
}

package_blas() {
  pkgdesc="Basic Linear Algebra Subprograms"
  depends=('gcc-libs')
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 "${srcdir}/${pkgbase}-${pkgver}/libblas.so.${_sover}" \
    "${pkgdir}/usr/lib/"
  ln -sf libblas.so.${_sover} "${pkgdir}/usr/lib/libblas.so"
  ln -sf libblas.so.${_sover} "${pkgdir}/usr/lib/libblas.so.3"
  
  install -m755 -d "${pkgdir}/usr/share/licenses/blas"
  install -m644 "${srcdir}/LICENSE.blas" \
    "${pkgdir}/usr/share/licenses/blas/LICENSE"
}

package_lapack() {
  pkgdesc="Linear Algebra PACKage"
  depends=("blas=${pkgver}")
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 "${srcdir}/${pkgbase}-${pkgver}/liblapack.so.${_sover}" \
    "${pkgdir}/usr/lib/"
  ln -sf liblapack.so.${_sover} "${pkgdir}/usr/lib/liblapack.so"
  ln -sf liblapack.so.${_sover} "${pkgdir}/usr/lib/liblapack.so.3"

  install -m755 -d "${pkgdir}/usr/share/licenses/lapack"
  install -m644 "${srcdir}/${pkgbase}-${pkgver}/LICENSE" \
    "${pkgdir}/usr/share/licenses/lapack/"
}
sha1sums=('3ce437e05f0e9a3c8148ec41922f6efeef0ebdac'
          '71bf7696f1c841339163e82d863dd62e484eb1cf'
          '47f46f83ad9a82b6539e040298a143baee6543fd'
          '128a258fde1b923c0b0b958a8a8ae8aa7657e44b'
          '2491a151a37f0162b25fc4e4e9a8ac444b574a76')
