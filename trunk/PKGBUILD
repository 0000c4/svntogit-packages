# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcolorchooser
pkgver=16.08.2
pkgrel=1
pkgdesc="Color Chooser"
url="http://kde.org/applications/graphics/kcolorchooser/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
conflicts=(kdegraphics-kcolorchooser)
replaces=(kdegraphics-kcolorchooser)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
        no-native-color-picker.patch::"https://quickgit.kde.org/?p=kcolorchooser.git&a=commitdiff&h=1f35c6d3&o=plain")
sha1sums=('a4f3e15d1c9a1925cb1a8f427ab8e493a8fb331f'
          'e649287a430bd8c96fa92c5c9d875ea76507acc5')

prepare() {
  mkdir -p build

# Fix running in GTK desktops
  cd $pkgname-$pkgver
  patch -p1 -i ../no-native-color-picker.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
