# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libpeas
pkgver=1.3.0
pkgrel=1
pkgdesc="A GObject-based plugins engine"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('gtk3' 'hicolor-icon-theme' 'gobject-introspection')
makedepends=('gtk-doc' 'intltool' 'vala' 'python2-gobject' 'gjs' 'seed' 'glade')
optdepends=('gjs: gobject-based plugin engine - gjs runtime loader'
            'seed: gobject-based plugin engine - seed runtime loader')
options=('!libtool')
install=libpeas.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz)
sha256sums=('aefff05cdd39d7a88986af54f2980034857482605e777ce5345ba3b5653fe79d')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var --enable-vala
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
