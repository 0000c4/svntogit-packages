# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.15
pkgrel=2
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope')  # 'python'
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        automake-1.15-perl-regex.patch
        automake-1.15-gzip-envvar.patch
        automake-1.15-dejagnu-testcase.patch)
md5sums=('9a1ddb0e053474d9d1105cfe39b0c48d'
         'SKIP'
         'ec24c629cad74c88555ee499c38dbd6e'
         'fa71c83d3f9d32d9a4007152b829074c'
         'ffa4dd8eb78cea82c3009d76087598b1')
validpgpkeys=('E1622F96D2BB4E58018EEF9860F906016E407573')   # Stefano Lattarini


prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix regex issue with recent perl
  patch -p1 -i $srcdir/automake-1.15-perl-regex.patch

  # fix testsuite issues with recent gzip
  patch -p1 -i $srcdir/automake-1.15-gzip-envvar.patch

  # fix testsuite issue with resent dejagnu
  patch -p1 -i $srcdir/automake-1.15-dejagnu-testcase.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=$CHOST --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
