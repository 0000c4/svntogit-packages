# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gdk-pixbuf-xlib
pkgver=0+23
pkgrel=1
pkgdesc="Deprecated Xlib integration for GdkPixbuf"
arch=(x86_64)
url="https://gitlab.gnome.org/Archive/gdk-pixbuf-xlib"
license=(LGPL2.1)
depends=(gdk-pixbuf2 libx11)
makedepends=(gtk-doc git meson)
_commit=3116b8ae55501cf48d16970aa2b50a5530e15223  # master
source=("git+$url.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  #git describe --tags | sed 's/-/+/g'
  echo "0+$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libgdk_pixbuf-2.0.so)
  provides+=(libgdk_pixbuf_xlib-2.0.so)

  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
