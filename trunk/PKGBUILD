# Maintainer: Guillaume ALAUX <guillaume@archlinuxorg>
pkgname=junit
pkgver=4.11
pkgrel=1
pkgdesc='automated testing framework for Java'
arch=('any')
url='http://www.junit.org/'
license=('CPL')
depends=('java-runtime-headless')
makedepends=('apache-ant')
source=(https://github.com/KentBeck/${pkgname}/archive/r${pkgver}.tar.gz)
md5sums=('bf62095e510f50baf0962af329438647')

build() {
  cd "${srcdir}/${pkgname}-r${pkgver}"

  ant dist -Dversion-status=''
}

package() {
  cd "${srcdir}/${pkgname}-r${pkgver}/${pkgname}${pkgver}"

  install -dm755 ${pkgdir}/usr/share/java/${pkgname}
  install -m644 \
    ${pkgname}-${pkgver}.jar \
    ${pkgname}-dep-${pkgver}.jar \
    ${pkgdir}/usr/share/java/${pkgname}
  ln -s ${pkgname}/${pkgname}-${pkgver}.jar ${pkgdir}/usr/share/java/${pkgname}.jar
  ln -s ${pkgname}/${pkgname}-dep-${pkgver}.jar ${pkgdir}/usr/share/java/${pkgname}-dep.jar
}
