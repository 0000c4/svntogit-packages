# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-pl-parser
pkgver=3.26.4
pkgrel=1
pkgdesc="Simple GObject-based library to parse and save a host of playlist formats"
url="https://gitlab.gnome.org/GNOME/totem-pl-parser"
license=(LGPL)
arch=(x86_64)
depends=(libarchive libquvi libxml2)
makedepends=(gobject-introspection git gtk-doc meson)
checkdepends=(gvfs)
provides=("totem-plparser=$pkgver-$pkgrel")
conflicts=(totem-plparser)
replaces=("totem-plparser<=3.26.3-1")
_commit=ea7238580d0d94a439295eb92890f6229a51c382  # tags/V_3_26_4^0
source=("git+https://gitlab.gnome.org/GNOME/totem-pl-parser.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^V_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-gtk-doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
