# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwordquiz
pkgver=16.04.1
pkgrel=1
pkgdesc="Flash Card Trainer"
url="http://kde.org/applications/education/kwordquiz/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('knotifyconfig' 'libkeduvocdocument' 'knewstuff' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdeedu-kwordquiz')
replaces=('kdeedu-kwordquiz')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kwordquiz-${pkgver}.tar.xz")
sha1sums=('87366c86ee79e4e8b1292a750003a40045c4a709')

build() {
  mkdir -p build
  cd build
  cmake ../kwordquiz-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
