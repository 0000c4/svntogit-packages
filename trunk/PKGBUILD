# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation
pkgver=1.7.5
pkgrel=2
pkgdesc="A user-friendly and fully-featured IRC client"
arch=(x86_64)
url="https://konversation.kde.org"
depends=(knotifyconfig kemoticons kparts kidletime qca-qt5 hicolor-icon-theme perl)
optdepends=('python: python scripting support' 'keditbookmarks: for managing bookmarks')
makedepends=(extra-cmake-modules kdoctools)
license=(GPL2 FDL)
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         konversation-qt-5.13.patch::"https://cgit.kde.org/konversation.git/patch/src/irc?id=4d003661")
sha256sums=('60bf7533062b5fc63a37105461b4776437f4e24859e8ddaed1d48c4ba1470940'
            'SKIP'
            '2b734869ae5e04644aceff8e02d1b24c8b705bd984f68f8fc4c0c9c042851afb')
validpgpkeys=(DA6B31C4B98A877DBB2EF9A292220328C632316E) # Eike Hein <hein@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../konversation-qt-5.13.patch # Fix build with Qt 5.13
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
