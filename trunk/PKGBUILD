# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor : Markus Meissner <markus@meissna.de>

pkgname=glhack
pkgver=1.2
pkgrel=2
pkgdesc="A port of Nethack, a single player dungeon exploration game in 2D"
arch=('i686' 'x86_64')
url="http://glhack.sourceforge.net/"
license=('custom')
depends=('sdl' 'libpng' 'mesa')
options=('!makeflags')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('b4c68e3ab689610ca638b7660c1b05bd')

build(){
  cd $srcdir/$pkgname-$pkgver
  sed -i -e 's|GAMEDIR  = $(PREFIX)/lib/games/$(GAME)|GAMEDIR  = $(PREFIX)/share/$(GAME)|g' Makefile || return 1
  sed -i -e 's|SHELLDIR = $(PREFIX)/games|SHELLDIR = $(PREFIX)/bin|g' Makefile || return 1
  sed -i -e 's|VARDIR   = /var/lib/games/glhack|VARDIR   = $(PREFIX)/share/$(GAME)|g' Makefile || return 1
  make   || return 1
  install -d $pkgdir/usr/share/man/man{5,6}
  sed -i "s|/usr/man/man6|$pkgdir/usr/share/man/man6|" doc/Makefile || return 1
  sed -i -e "s|PREFIX[\t]* = /usr|PREFIX   = $pkgdir/usr|g" Makefile || return 1
  sed -i -e 's|s;/usr/games/lib/nethackdir;$(GAMEDIR);|s;/usr/games/lib/nethackdir;/usr/share/$(GAME);|g' Makefile || return 1
  make install || return 1
  install -D -m644 dat/license $pkgdir/usr/share/licenses/$pkgname/license
}
