# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=3.0.1
pkgrel=2
pkgdesc="The GNOME shell and file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gtk3' 'hicolor-icon-theme' 'gvfs' 'desktop-file-utils' 'gsettings-desktop-schemas' 'shared-mime-info')
makedepends=('intltool' 'gobject-introspection')
replaces=('gnome-volume-manager')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        places-sidebar.patch  progress-ui-handler.patch)
sha256sums=('f759d615c880caebf84290bc00c354488266bd7292479d88b8811a9c4f3106e8'
            'ddf020bf8c5ede07fb737b37561d0e557b105c2b96dfa56d9a9f5110ecfbc041'
            '5b6f911b1d3e0317c370c674bc52f6521bdd785647087863d33d3b60ddf00433')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #fixed for 3.0.2
  patch -Np1 -i "${srcdir}/places-sidebar.patch"
  patch -Np1 -i "${srcdir}/progress-ui-handler.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
