# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
 
pkgname=libportal
pkgver=0.1
pkgrel=1
pkgdesc="GIO-style async APIs for most Flatpak portals"
url="https://github.com/flatpak/libportal"
arch=(x86_64)
license=(LGPL)
depends=(glib2)
makedepends=(git meson gtk-doc)
_commit=a7a1edcaeceb53652b7d76adadfec390f7b09178  # tags/0.1^0
source=("git+https://github.com/flatpak/libportal#commit=$_commit"
        0001-filechooser-Don-t-try-to-free-the-method-name-it-s-s.patch)
sha256sums=('SKIP'
            '934374cb20a3bb76df2c1982a56a15e1c9808a1dfa46273db6529dd6213bde1f')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../0001-filechooser-Don-t-try-to-free-the-method-name-it-s-s.patch
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
