# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.20.2
pkgrel=1
pkgdesc="Single sign-on framework for GNOME"
url="https://wiki.gnome.org/Projects/GnomeOnlineAccounts"
arch=(i686 x86_64)
license=(LGPL)
depends=(webkit2gtk json-glib libnotify rest libsecret telepathy-glib krb5 gcr)
makedepends=(intltool gobject-introspection gtk-doc)
optdepends=('gvfs-goa: Virtual file systems (e.g. OwnCloud)'
            'gvfs-google: Google Drive')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('4b555d9d2587d49148929e836ff02d5dda80c4138f69036fa214b71383cdc1dd')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --enable-media-server \
    --enable-kerberos \
    --enable-lastfm \
    --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
