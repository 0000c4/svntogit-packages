# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Raymano <re1212122000@yahoo.com>

pkgname=soundtouch
pkgver=1.4.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('i686' 'x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs>=4.4.2')
options=('!libtool')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz
        soundtouch-1.4.0-mmx-sse-compile-fix.patch
        soundtouch-1.4.0-x86_64-asm-broken.patch)
md5sums=('fc4bb10401624899efe4fb554d4fd3ed'
         'f0204136c90ce4f6d4761073151eb4dc'
         '0d856bb5b519b18c6ba3a2e1770bdfd2')

build() {
  cd "${srcdir}/${pkgname}"
  patch -Np1 -i "${srcdir}/soundtouch-1.4.0-mmx-sse-compile-fix.patch" || return 1
  patch -Np1 -i "${srcdir}/soundtouch-1.4.0-x86_64-asm-broken.patch" || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install || return 1
}
