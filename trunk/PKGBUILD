# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
pkgver=0.30.1
pkgrel=1
pkgdesc="Compiler for the GObject type system"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('23add78e5c6a5e6df019d4a885c9c79814c9e0b957519ec8a4f4d826c4e5df2c')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --enable-vapigen
    make
}

check() {
    cd $pkgname-$pkgver
#    make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
