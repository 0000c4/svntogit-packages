# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd
pkgver=5.1.4
pkgrel=2
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/dhcpcd/"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
depends=('glibc' 'sh')
backup=('etc/conf.d/dhcpcd' 'etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.bz2" \
        'dhcpcd.conf.d' 'fs17838.patch')
md5sums=('d18afc66efbea9a28e541e9b10fd284f'
         '372d33485556982b64a97f301e17c5dd'
         'bc07eb80a90972f70fc37131aabc56a3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix FS#17838
  patch -Np1 -i ${srcdir}/fs17838.patch || return 1

  # configure variables
  ./configure --libexecdir=/usr/lib/dhcpcd --dbdir=/var/lib/dhcpcd

  # Build
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # Create Binary Symlink
  install -d ${pkgdir}/usr/sbin || return 1
  ln -sf /sbin/dhcpcd ${pkgdir}/usr/sbin/dhcpcd || return 1

  # Install Configuration File used in /etc/rc.d/network
  install -D -m644 ../dhcpcd.conf.d $pkgdir/etc/conf.d/$pkgname || return 1

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname || return 1
  awk '{if(FNR<27)print $0}' ${srcdir}/${pkgname}-${pkgver}/configure.h \
	>> ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf || return 1 # Disable ip4vall
}
