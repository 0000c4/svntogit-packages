# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gupnp
pkgver=0.20.15
pkgrel=1
pkgdesc="An object-oriented UPNP framework"
arch=(i686 x86_64)
url="http://gupnp.org/"
license=(LGPL)
depends=(gssdp util-linux)
makedepends=(gobject-introspection gtk-doc vala) 
optdepends=('python2: gupnp-binding-tool')
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('0446b42d0d5b8adfcdca78832f2ce6173ab33793fca055afc97aec1fcc579fe2')

prepare() {
  cd $pkgname-$pkgver
  sed -i '1s|^#!.*python$|&2|' tools/gupnp-binding-tool
}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-context-manager=linux
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
