# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-net-dns
pkgver=1.11
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Net-DNS/"
depends=('perl-digest-hmac' 'perl-net-ip')
checkdepends=('perl-test-pod' 'perl-mime-base32' 'perl-net-dns-sec')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-socket6: IPv6 support')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-$pkgver.tar.gz)
sha512sums=('b6539e462e39a00f8a00f4eeddbb496207094e36b37cd05799cb34435c8d8555173cbcc3ecfaff13068b11108cef453e0de8f99544203a3303f6d77739467be1')

build() {
  cd Net-DNS-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-DNS-${pkgver}
  make test
}

package() {
  cd Net-DNS-${pkgver}
  make DESTDIR="${pkgdir}" install
}
