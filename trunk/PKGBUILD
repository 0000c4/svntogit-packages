# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gdk-pixbuf-xlib
pkgver=2.40.1
pkgrel=1
pkgdesc="Deprecated Xlib integration for GdkPixbuf"
arch=(x86_64)
url="https://gitlab.gnome.org/Archive/gdk-pixbuf-xlib"
license=(LGPL2.1)
depends=(gdk-pixbuf2 libx11)
makedepends=(gtk-doc git meson)
_commit=dc22ea36f69755007c66877284596df270532cc1  # master
source=("git+$url.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f 2.40.1 dc22ea36f69755007c66877284596df270532cc1
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
