# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=vinagre
pkgver=2.26.0
pkgrel=1
pkgdesc="A VNC Client for the GNOME Desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/vinagre/"
install=vinagre.install
depends=('gnome-keyring>=2.26.0' 'gtk-vnc>=0.3.8' 'avahi>=0.6.24')
makedepends=('gnome-doc-utils>=0.16.0' 'intltool' 'gnome-panel>=2.26.0')
optdepends=('gnome-panel')
groups=('gnome-extra')
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/vinagre/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('6930bec2968c96dfb3407d6492ae42d4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-scrollkeeper \
  	      --libexecdir=/usr/lib/vinagre || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain vinagre ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
