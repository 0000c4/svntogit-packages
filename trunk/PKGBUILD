# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Alan Young <harleypig@gmail.com>

pkgname=perl-module-build
pkgver=0.4220
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="http://search.cpan.org/dist/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-cpan-meta' 'perl-inc-latest')
checkdepends=('perl-par-dist' 'perl-archive-zip')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/Module-Build-$pkgver.tar.gz)
sha512sums=('20f69663bc922db7c57fc2c153cd117d46f11fa89ea5045dc11e66b861a9d93c12ec0b1dc65dcdadf581608d99ad2197fed2edee06be2228f72d5e0c5775efa0')

build() {
  cd Module-Build-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Module-Build-$pkgver
  perl Build test
}

package() {
  cd Module-Build-$pkgver
  perl Build install destdir="$pkgdir"
}
