# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=make
pkgver=4.1
pkgrel=3
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'sh' 'guile')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        make-4.1-NULL-ttyname.patch)
md5sums=('57a7a224a822f94789a587ccbcedff69'
         'SKIP'
         '91cfbd6ca26b0f59b800d1a81154c080')
validpgpkeys=('3D2554F0A15338AB9AF1BB9D96B047156338B6D4')   # Paul Smith


prepare() {
  cd ${pkgname}-${pkgver}

  # upstream commit 292da6f6
  patch -p1 -i ${srcdir}/make-4.1-NULL-ttyname.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # features/archives ....................................... FAILED (3/10 passed)
  # caused by binutils configured with --enable-deterministic-archives
  make -k check || true
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
