# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kimagemapeditor
pkgver=19.04.1
pkgrel=1
arch=(x86_64)
pkgdesc="HTML Image Map Editor"
url='https://www.kde.org'
license=(GPL LGPL FDL)
groups=(kde-applications kdewebdev)
depends=(khtml hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdewebdev-kimagemapeditor)
replaces=(kdewebdev-kimagemapeditor)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d85d2f3d043a29e56f4234ce24dd75545e06c2812d5fe45cafde4c3dbe280533'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
