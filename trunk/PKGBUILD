# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=procps-ng
pkgver=3.3.8
pkgrel=2
pkgdesc='Utilities for monitoring your system and its processes'
url='http://gitorious.org/procps'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
groups=('base')
depends=('ncurses')
backup=('etc/sysctl.conf')
source=("${pkgname}-${pkgver}.tar.gz::http://gitorious.org/procps/procps/archive-tarball/v${pkgver}"
        'sysctl.conf')
sha1sums=('63aed46ba4b5b05698beb25d2aa3eee6779d7f45'
          'bc4e929a0ddcdcd627e8d15d30abcf185aa33fab')

conflicts=('procps')
provides=('procps')
replaces=('procps')

options=('!libtool')

prepare() {
	cd "${srcdir}/procps-procps"
	echo ${pkgver} > .tarball-version
	./autogen.sh
}

build() {
	cd "${srcdir}/procps-procps"
	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--disable-static
	make
}

package() {
	cd "${srcdir}/procps-procps"
	make DESTDIR="${pkgdir}" install
	install -D -m644 ../sysctl.conf "${pkgdir}/etc/sysctl.conf"

	# provided by util-linux
	rm "${pkgdir}/usr/bin/kill"
	rm "${pkgdir}/usr/share/man/man1/kill.1"
}
