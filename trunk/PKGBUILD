# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>

pkgname=hefur
pkgver=0.3
pkgrel=4
pkgdesc='A standalone and lightweight BitTorrent tracker'
arch=('i686' 'x86_64')
url='http://github.com/abique/hefur'
license=('MIT')
makedepends=('cmake')
depends=('gnutls' 'xz')
source=("https://github.com/downloads/abique/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'hefurd.service'
        'Check-that-added-torrents-from-the-disk-are-still-on.patch'
        'Replace-by-amp-in-stat.html.patch')
md5sums=('446ca4e659ce0ca3c8f32b0990ee7f5f'
         '731d5be04d74a52541450b5127abeff3'
         'f912a75467a30251403923c0a7abe049'
         'f11af93e9a8074c8ba73f7a47e978362')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/Check-that-added-torrents-from-the-disk-are-still-on.patch
	patch -p1 -i ${srcdir}/Replace-by-amp-in-stat.html.patch
	mkdir build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}/build
	DESTDIR=${pkgdir} make install
	install -D -m644 ${srcdir}/hefurd.service ${pkgdir}/usr/lib/systemd/system/hefurd.service
	install -D -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/hefur/LICENSE
	install -d -m755 ${pkgdir}/var/lib/hefurd
}
