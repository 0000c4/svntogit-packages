# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=portmidi
pkgver=217
pkgrel=4
pkgdesc="Platform independent library for real-time MIDI input/output"
arch=('i686' 'x86_64')
url="http://portmedia.sourceforge.net/"
license=('GPL')
depends=('alsa-lib')
makedepends=('cmake' 'java-environment')
optdepends=('java-runtime: for using pmdefaults')
source=(http://downloads.sourceforge.net/project/portmedia/${pkgname}/${pkgver}/${pkgname}-src-${pkgver}.zip
        portmidi-217-build-fix.patch)
sha1sums=('f45bf4e247c0d7617deacd6a65d23d9fddae6117'
          '7fe10874ea8689e1ba51e8f50da38e79d10b76e1')

prepare() {
  cd ${pkgname}
  # build fix for "pm_java/CMakeLists.txt" (fixes FS#27118)
  patch -p1 -i "${srcdir}/portmidi-217-build-fix.patch" 
}

build() {
  cd ${pkgname}
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
    -DPORTMIDI_ENABLE_TEST=OFF -DPORTMIDI_ENABLE_JAVA=ON
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install

  # Fix missing symlink for libporttime.so (FS#21528).
  ln -s libportmidi.so "${pkgdir}/usr/lib/libporttime.so"
}
