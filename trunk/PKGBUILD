# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-logs
pkgver=3.28.5
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Logs"
license=(GPL)
depends=(systemd gtk3 gsettings-desktop-schemas)
makedepends=(appstream-glib git meson itstool docbook-xsl)
groups=(gnome)
_commit=1330a8bd43310a9a85db0deb262f71c643c5c5b4  # tags/gnome-logs-3.28.5^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-logs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-logs-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D tests=true -D man=true
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
