# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.28.2
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 exempi libpeas librsvg dconf)
makedepends=(intltool itstool gobject-introspection gtk-doc gnome-common git meson)
optdepends=('eog-plugins: Additional features')
groups=(gnome)
options=(!emptydirs)
_commit=fb856db7e4d9fee24b8d953f6c28eec9a02ff11f  # tags/3.28.2^0
source=("git+https://git.gnome.org/browse/eog#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
