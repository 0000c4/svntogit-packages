# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.4.7
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=(i686 x86_64)
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
backup=('etc/conf.d/memcached')
source=(http://memcached.googlecode.com/files/$pkgname-$pkgver.tar.gz
        memcached.conf
		memcached.sh)
changelog=ChangeLog
sha256sums=('925837a4cf440e3cb451e338413ed9ad1429fd0d80ba4d903955f5fed8da7be6'
            '0e57a0f528b387f1f04f628ec74856c615d82d44f4d6bc041c33b3814ce13d0a'
            '72efa639c5a39c7c14f07cc51731ebbf82299870d9cf31cf3aaa981bc084b4eb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 755 scripts/memcached-tool $pkgdir/usr/bin/memcached-tool
  install -D -m 644 "$srcdir/memcached.conf" $pkgdir/etc/conf.d/memcached
  install -D -m 755 "$srcdir/memcached.sh" $pkgdir/etc/rc.d/memcached
}
