# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=(glibmm glibmm-docs)
pkgver=2.64.0
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libsigc++)
makedepends=(git mm-common perl-xml-parser clang meson)
checkdepends=(glib-networking)
options=(!emptydirs)
_commit=5e4b8f37de7110f4905ca11961dfe372151df095  # tags/2.64.0^0
source=("git+https://gitlab.gnome.org/GNOME/glibmm.git#commit=$_commit"
        header.diff)
sha256sums=('SKIP'
            'dd735cb3509bbb3400c150ab64f4791385df5393df3de964f0fcf548dd7d626c')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase

  # Missing includes
  git cherry-pick -n c6e078c3e7d457a9db39073af30af0c65b5145c1

  # Missing header
  git apply -3 ../header.diff
}

build() {
  arch-meson $pkgbase build -D maintainer-mode=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_glibmm() {
  DESTDIR="$pkgdir" meson install -C build

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_glibmm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}
