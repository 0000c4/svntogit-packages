# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=icedtea-web
pkgname=('icedtea-web' 'icedtea-web-doc')
pkgver=1.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')
makedepends=('openjdk6' 'zip' 'xulrunner')
install=$pkgname.install
source=(http://icedtea.classpath.org/download/source/$pkgname-$pkgver.tar.gz
        fix-man-location.patch)
md5sums=('f6124dbaef16650f46df5125d8ed7407'
         '58e305aa0d27d59f543d9cfddc6e175a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i $srcdir/fix-man-location.patch
  autoreconf -v
  ./configure --prefix=/usr/lib/jvm/java-6-openjdk \
      --datarootdir=/usr/share
  make
}

package_icedtea-web() {

  pkgdesc="provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project"
  depends=('openjdk6' 'gtk2')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # Install desktop files.
  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 javaws.png ${pkgdir}/usr/share/pixmaps
  install -m644 {javaws,itweb-settings}.desktop ${pkgdir}/usr/share/applications
  # remove splitted doc files
  rm -vrf ${pkgdir}/usr/share/doc
}

package_icedtea-web-doc() {

  pkgdesc="icedtea-web browser plugin + Java WebStart - documentation files"

  cd "$srcdir/$pkgname-$pkgver"
  install -m755 -d $pkgdir/usr/lib/jvm/java-6-openjdk/jre/lib
  make DESTDIR="$pkgdir" install-data-local
  # remove javaws about and man page
  rm -vrf ${pkgdir}/usr/lib
  rm -vrf ${pkgdir}/usr/share/man
}

