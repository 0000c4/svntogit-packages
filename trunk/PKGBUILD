# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmime
pkgver=18.08.1
pkgrel=1
pkgdesc="Library for handling mail messages and newsgroup articles"
arch=(x86_64)
url="https://kontact.kde.org/"
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('dd14d7813199f4f859cb112dc34113269196c62fd9f05359e889913eb9d55d02'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
