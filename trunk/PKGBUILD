# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=jemalloc
pkgver=4.2.1
pkgrel=1
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('i686' 'x86_64')
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('glibc')
provides=('libjemalloc.so')
optdepends=('perl: for jeprof')
source=("https://github.com/jemalloc/jemalloc/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('5630650d5c1caab95d2f0898de4fe5ab8519dc680b04963b38bb425ef6a42d57')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name \*.a -type f -exec chmod 644 '{}' \;
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
