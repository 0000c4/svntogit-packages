# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=k3b
pkgver=17.04.0
pkgrel=1
epoch=1
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('i686' 'x86_64')
url="https://userbase.kde.org/K3b"
license=('GPL')
depends=('libkcddb' 'kcmutils' 'knotifyconfig' 'libmad' 'kfilemetadata' 'knewstuff'
         'libmpcdec' 'libdvdread' 'cdrtools' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'emovix: for bootable multimedia cd/dvd support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for cd ripping support')
groups=(kde-applications kdemultimedia)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('832b7a94769b4b0c33c0935adeb36e47c297eeb4801650da9b7ab1668b464a73'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
