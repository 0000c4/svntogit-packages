# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=3.8.2
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'clutter-gtk' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs' '!libtool')
install=swell-foop.install
url="https://live.gnome.org/Swell%20Foop"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('33b72da1f5378801ae41cb13f3bc35ce44368c104327f68dd51f45653ca62cb9')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
