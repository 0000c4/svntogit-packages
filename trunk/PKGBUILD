# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=ekiga
pkgver=3.0.2
pkgrel=1
pkgdesc="VOIP/Videoconferencing app with full SIP and H.323 support (GnomeMeeting expanded and renamed)"
url="http://www.ekiga.org"
license=(GPL)
arch=(i686 x86_64)
depends=('opal=3.4.4' 'libgnomeui>=2.24.0' 'evolution-data-server>=2.24.3' 'libsigc++2.0>=2.2.3' 'libxv' 'libnotify>=0.4.5')
makedepends=('pkgconfig' 'perlxml' 'gnome-doc-utils>=0.14.2')
provides=('gnomemeeting')
replaces=('gnomemeeting')
options=(!emptydirs)
groups=('gnome-extra')
install=ekiga.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/3.0/${pkgname}-${pkgver}.tar.bz2)
md5sums=('8007925925f50b2ba89bd821f623ae32')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
	      --disable-scrollkeeper --enable-dbus || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
