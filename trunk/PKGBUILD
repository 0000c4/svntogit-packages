# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Contributor: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-xml-libxml
pkgver=2.0133
pkgrel=1
pkgdesc="Interface to the libxml library"
arch=('x86_64')
url="http://search.cpan.org/dist/XML-LibXML"
license=('GPL')
depends=('libxml2' 'perl-xml-sax' 'perl-xml-namespacesupport')
checkdepends=('perl-test-pod' 'perl-test-leaktrace' 'perl-cpan-changes' 'perl-uri')
provides=('perl-libxml' 'perl-xml-libxml-common')
replaces=('perl-libxml' 'perl-xml-libxml-common')
options=('!emptydirs')
install=perl-xml-libxml.install
source=(https://www.cpan.org/authors/id/S/SH/SHLOMIF/XML-LibXML-$pkgver.tar.gz)
sha512sums=('ba4685fe99e2b540461663212a43b0eb8c59e19f6b49d8c7e7738bc6b373c2b0506d8190cdd50fb13edb65db543da4095af3d70c2a6916d3fca0a738d2c9b1eb')

build() {
  cd XML-LibXML-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-LibXML-$pkgver
  make test
}

package() {
  cd XML-LibXML-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"/
}
