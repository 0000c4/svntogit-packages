# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=3.20.0
pkgrel=2
pkgdesc="System power information and statistics"
arch=(i686 x86_64)
url="http://projects.gnome.org/gnome-power-manager/"
license=(GPL)
depends=(gtk3 upower dconf)
makedepends=(intltool)
options=(!emptydirs)
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('159c23a6ed5ffd54324947adb2a32caff6d6e2210f96bdf3961edfcad8058a47')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
