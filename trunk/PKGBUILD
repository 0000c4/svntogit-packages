# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=tracker-miners
pkgver=2.3.2
pkgrel=1
pkgdesc="Collection of data extractors for Tracker/Nepomuk"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(tracker gst-plugins-base-libs exempi flac libexif libcue libgrss libgsf libgxps libiptcdata
         libosinfo libvorbis poppler-glib totem-plparser giflib libgexiv2 gupnp-dlna upower
         libseccomp)
makedepends=(intltool git meson vala)
checkdepends=(python-gobject gst-plugins-good gst-plugins-base gst-libav)
groups=(gnome)
_commit=8df1ae1a835caffa3cd3ead0e0c7ffa015e8cd9c  # tags/2.3.2^0
source=("git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package() {
  depends+=(libtracker-{miner,sparql}-2.0.so)
  DESTDIR="$pkgdir" meson install -C build
}
