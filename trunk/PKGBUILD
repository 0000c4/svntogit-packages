# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=modemmanager
_realname=ModemManager
pkgver=0.4.997
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=('GPL2')
depends=('dbus-glib' 'udev' 'ppp')
makedepends=('intltool')
options=('!libtool')
source=(http://download.gnome.org/sources/${_realname}/0.4/${_realname}-${pkgver}.tar.xz)
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
sha256sums=('88fa2f098b9fbbfb4e91e78e200404ce6dd6258102a197a635a3bd789302c6e4')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
