# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=1.2.3
_pkgver=4.2.3
pkgrel=2
pkgdesc="PHP language and documentation plugin for KDevelop/Quanta"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop-pg-qt')
makedepends=('cmake' 'automoc4')
replaces=('kdevelop-php-docs')
conflicts=('kdevelop-php-docs')
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        "http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-docs-${pkgver}.tar.bz2")
sha1sums=('b0dc43cf01d2005f2ad66872ce6b49b093a7ceb4'
          'e399465638457fe015e920227e3aca5a53716804')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make

  # kdevelop-php-docs
  cd "${srcdir}"
  mkdir build-docs
  cd build-docs
  cmake ../${pkgname}-docs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  # kdevelop-php-docs
  cd "${srcdir}"/build-docs
  make DESTDIR="${pkgdir}" install
}
