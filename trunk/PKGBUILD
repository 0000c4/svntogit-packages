# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdebugsettings
pkgver=15.12.2
pkgrel=1
pkgdesc='An application to enable/disable qCDebug'
arch=(i686 x86_64)
url='http://projects.kde.org/kdebugsettings'
license=(GPL)
depends=(kconfigwidgets kio)
makedepends=(extra-cmake-modules python)
groups=(kde-applications kdeutils)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('534cc69a1356e2fecdc771145a0df29e1e99c417af1c70dae91a0e1734ea7b06')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

