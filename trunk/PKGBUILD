# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.24.0+7+g4c598a652
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(i686 x86_64)
depends=(gnome-online-accounts nss krb5 libgweather libical db libgdata)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost git cmake)
license=(GPL)
_commit=4c598a65295a9c828438c8ddc5219a48ce4a3dc7  # gnome-3-24
source=("git+https://git.gnome.org/browse/evolution-data-server#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_DATA_SERVER_//;s/_/./g;s/-/+/g'
}

build() {
  cd $pkgname

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DLIBEXEC_INSTALL_DIR=/usr/lib/evolution-data-server -DSYSCONF_INSTALL_DIR=/etc -DENABLE_INTROSPECTION=ON -DENABLE_VALA_BINDINGS=ON -DENABLE_UOA=OFF -DENABLE_GOOGLE_AUTH=ON -DENABLE_GTK_DOC=ON .
  make
}

check() {
  cd $pkgname
  # libedata-book tests fail because they try to
  # mkdir /usr/lib/evolution-data-server/camel-providers
  make check -k || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
