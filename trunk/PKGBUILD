# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxft
pkgver=2.3.0
pkgrel=1
pkgdesc="FreeType-based font drawing library for X"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
options=('!libtool')
source=("${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2")
sha1sums=('687f38a810b9ae58e1169c295d9d438370235e23')

build() {
  cd "${srcdir}/libXft-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
