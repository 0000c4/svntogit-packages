# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-docs
pkgname=(vulkan-headers vulkan-html-docs)
pkgver=1.0.39
pkgrel=1
epoch=1
_pkgname=Vulkan-Docs
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(asciidoc python3 dblatex source-highlight cmake git)
groups=(vulkan-devel)
#source=("https://github.com/KhronosGroup/${_pkgname}/archive/v${pkgver}-core.tar.gz"
source=(Vulkan-Docs-${pkgver}-core::git+https://github.com/KhronosGroup/Vulkan-Docs.git#commit=ca4abe0
        git+https://github.com/leethomason/tinyxml2.git#commit=c8dad95
        git+https://github.com/KhronosGroup/Vulkan-Hpp.git#commit=ae2e22e)
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd Vulkan-Hpp
  git submodule init
  git config submodule.tinyxml2.url $srcdir/tinyxml2
  git submodule update
}

build() {
  # we need this to build hpp headers
  cd Vulkan-Hpp
  cmake ./ && make

  ./VulkanHppGenerator "${srcdir}/${_pkgname}-${pkgver}-core/src/spec/vk.xml"

  cd "${srcdir}/${_pkgname}-${pkgver}-core/doc/specs/vulkan"

  make all
}

package_vulkan-headers() {
  pkgdesc="Vulkan header files"
  cd "${_pkgname}-${pkgver}-core"

  install -dm755 "${pkgdir}/usr/include/vulkan"
  install -dm755 "${pkgdir}/usr/share/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  install -m644 src/vulkan/vk_platform.h ${pkgdir}/usr/include/vulkan/
  install -m644 src/vulkan/vulkan.h ${pkgdir}/usr/include/vulkan
  install -m644 ${srcdir}/Vulkan-Hpp/vulkan/vulkan.hpp ${pkgdir}/usr/include/vulkan
  install -m644 src/spec/vk.xml ${pkgdir}/usr/share/vulkan

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation"
  cd "${_pkgname}-${pkgver}-core"

  install -dm755 "${pkgdir}/usr/share/doc/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  mv -v out/1.0/*.html "${pkgdir}/usr/share/doc/vulkan/"
  mv -v out/1.0/html/* "${pkgdir}/usr/share/doc/vulkan/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}
