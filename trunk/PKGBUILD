# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=mpg123
pkgver=1.9.2
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2')
depends=('glibc' 'libtool>=2.2' 'alsa-lib')
makedepends=('esound' 'sdl')
optdepends=('esound: for additional audio support' 'sdl: for additional audio support')
conflicts=('mpg321')
provides=('mpg321')
replaces=('mpg321')
options=('libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        mpg123-1.9.2-libtool.patch)
md5sums=('5313b1bcc0274b4d7c394397c205bb20'
         '570ac56f363011237cc5b9ed8cbdcf9e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/mpg123-1.9.2-libtool.patch || return 1
  ./configure --prefix=/usr \
              --with-audio="alsa esd sdl" || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
