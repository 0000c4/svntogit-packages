# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jason A. Donenfeld <Jason@zx2c4.com>

pkgname=wireguard-dkms
pkgver=1.0.20200330
pkgrel=2
pkgdesc='next generation secure network tunnel - module sources'
arch=('x86_64')
url='https://www.wireguard.com/'
license=('GPL')
depends=('dkms')
provides=('WIREGUARD-MODULE')
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE') # Jason A. Donenfeld <Jason@zx2c4.com>
source=("https://git.zx2c4.com/wireguard-linux-compat/snapshot/wireguard-linux-compat-${pkgver}.tar"{.xz,.asc}
        '0001-queueing-skb_reset_redirect-change-has-been-backport.patch')
sha256sums=('2d57b239605be2ee0e4c2da935ff1a23e9ed8bb3ee692e10ae032ae50f280bef'
            'SKIP'
            '64206515571285962d5fe8378c8dd3a8527561fe2f8807410f4117d3e19cbe6a')

prepare() {
	cd wireguard-linux-compat-${pkgver}/

	patch -Np1 < ../0001-queueing-skb_reset_redirect-change-has-been-backport.patch
}

package() {
	cd wireguard-linux-compat-${pkgver}/

	make -C src/ \
		DESTDIR="${pkgdir}/" \
		DKMSDIR="/usr/src/wireguard-${pkgver}/" \
		dkms-install
}
