# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.0.12
pkgrel=1
pkgdesc="Cantarell font family"
arch=('any')
url="http://abattis.org/cantarell/"
license=('GPL2')
depends=('fontconfig')
install=cantarell-fonts.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver::3}/$pkgname-${pkgver}.tar.xz
        21-cantarell-hinting.conf)
sha256sums=('3d509e1117dd92a6b80ef8b2586c89e178dc21a03c7c61f5c50772def5c4934b'
            '7da2d00e68d9eeab071cb861fe7da35098d5164008f638a5a9c289f717924bd0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-configdir=/etc/fonts/conf.avail
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../21-cantarell-hinting.conf \
    "$pkgdir/etc/fonts/conf.avail/21-cantarell-hinting.conf"
  install -d "$pkgdir/etc/fonts/conf.d"
  ln -s ../conf.avail/21-cantarell-hinting.conf "$pkgdir/etc/fonts/conf.d"
  ln -s ../conf.avail/31-cantarell.conf "$pkgdir/etc/fonts/conf.d"
}

# vim:set ts=2 sw=2 et:
