# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdazzle
pkgver=3.26.1
pkgrel=1
pkgdesc="A library to delight your users with fancy features"
url="https://git.gnome.org/browse/libdazzle"
arch=(x86_64)
license=(LGPL)
depends=(gtk3)
makedepends=(git meson vala gobject-introspection gtk-doc)
_commit=05543e750afbfa1d84ec06135c18fd687dd78f0e  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/libdazzle#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname -Denable_gtk_doc=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
