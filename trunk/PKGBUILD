# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=akonadi
pkgver=17.04.3
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url='https://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('kio' 'kitemmodels' 'mariadb')
makedepends=('extra-cmake-modules' 'postgresql' 'qt5-tools' 'boost' 'python' 'kdesignerplugin')
optdepends=('postgresql: PostgreSQL backend')
conflicts=('akonadi-client')
provides=('akonadi-client')
replaces=('akonadi-client')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('89e0476892a9b43ae0686e059f6caa0e2bba47955920e3424f5315b1cba7ad31'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
