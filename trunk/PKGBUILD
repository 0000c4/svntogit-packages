# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.2.9
pkgrel=2
pkgdesc="GObject wrapper for libusb1"
url="https://github.com/hughsie/libgusb"
arch=(i686 x86_64)
license=(LGPL2.1)
depends=(glib2 glibc libusb)
makedepends=(gobject-introspection python2 vala gtk-doc git)
_commit=1a9ae96944de71ca0898d820f2ff325e864c5162  # tags/gusb_0_2_9^0
source=("git+https://github.com/hughsie/libgusb#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gusb_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-gtk-doc \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
