# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica
pkgver=0.4.1
pkgrel=1
pkgdesc='A Qt library that implements the Open Collaboration Services API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/attica'
license=('LGPL')
depends=('qt')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('b90983ec5d79e5ddcbc9146fa23cab72')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
