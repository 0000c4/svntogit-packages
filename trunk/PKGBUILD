# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
pkgname=libarchive
pkgver=2.8.5
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat')
source=("http://libarchive.googlecode.com/files/libarchive-${pkgver}.tar.gz"
        issue-185.patch)
md5sums=('9caf51dcf6213e9c9f5a1c27448b9c90'
         '3f6019fa86d15644f84cf059711141ce')
sha256sums=('13993e0ffbd121ccda46ea226b1f8eac218de0fa8da7d8b1f998093d5c32a72d'
            'c5f583e5ad0bd95542051fad374430cbc2704d03798a7918043813b95268eb05')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 < ../issue-185.patch
  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
