# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-terminal
pkgver=0.8.0
pkgrel=2
pkgdesc="A modern terminal emulator primarily for the Xfce desktop environment"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'vte3' 'hicolor-icon-theme')
makedepends=('intltool')
conflicts=('terminal')
replaces=('terminal')
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        0001-Set-window-hints-properly-showes-columns-rows-when-r.patch)
sha256sums=('e915c05a3d07b66d153e1ba8614f904f9c12b2dd8372b27d24c0d339743d5b0a'
            'f746972fc29ca7de603ebf5cb9faaa5a12aa12f053380a8acc29d73dbb2dd262')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=12793
  patch -Np1 -i ../0001-Set-window-hints-properly-showes-columns-rows-when-r.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
