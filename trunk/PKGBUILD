# $Id: PKGBUILD 295962 2017-05-13 12:58:56Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-desktop-portal-kde
pkgver=5.10.0
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal using Qt/KF5'
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(ki18n knotifications)
makedepends=(extra-cmake-modules python)
provides=(xdg-desktop-portal-impl)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('25ad494b81e49f0fd205b78aa7d0b2460b2697d232d0f728e3f311cad4d8a2a8'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
