# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=qhull
pkgver=2012.1
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=('i686' 'x86_64')
url="http://www.qhull.org/"
license=('custom')
depends=('gcc-libs')
makedepends=('cmake')
source=("http://www.qhull.org/download/$pkgname-$pkgver-src.tgz")
options=('!libtool')
md5sums=('ed81aa19b203aa0fdb75b41fe6f7a40f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir srcbuild
  cd srcbuild
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DDOC_INSTALL_DIR=/usr/share/doc \
    -DMAN_INSTALL_DIR=/usr/share/man/man1 -DLIB_INSTALL_DIR=/usr/lib ..
  make
}

package() { 
  cd "$srcdir/$pkgname-$pkgver/srcbuild"

  make DESTDIR=$pkgdir install

  # Install license
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
