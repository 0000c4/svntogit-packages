# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=3.28.0+3+g1a1f835
pkgrel=1
pkgdesc="Clear hidden mines from a minefield"
url="https://wiki.gnome.org/Apps/Mines"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(itstool gobject-introspection yelp-tools appstream-glib vala git meson)
groups=(gnome-extra)
_commit=1a1f835f5ae191d5aa228caf0829fe6a4136392b  # master
source=("git+https://git.gnome.org/browse/gnome-mines#commit=$_commit"
        icon-fixes.diff)
sha256sums=('SKIP'
            '8f8667d9d6fe5ad9d789c1842653ae0b49b9eaa82b378d6ee7b7e2a8abffae57')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../icon-fixes.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
