# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=flatpak
pkgver=0.6.12
pkgrel=2
pkgdesc="Application deployment framework for desktop apps"
url="http://flatpak.org"
arch=(i686 x86_64)
license=(GPL)
depends=(dbus systemd glib2 libsoup polkit xorg-xauth libgsystem ostree fuse
         json-glib libseccomp libarchive libelf libcap)
makedepends=(intltool python libxslt gobject-introspection gtk-doc git)
install=flatpak.install
_commit=41d2e453622409e4387c36d64a72af589d87e345  # tags/0.6.12^0
source=("git+https://github.com/flatpak/flatpak#commit=$_commit"
        "git://git.gnome.org/libglnx"
        "git+https://github.com/projectatomic/bubblewrap")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  # CVE-2016-8659
  git cherry-pick -n da0204f4c81e7cf66943ac07317a48cad28ecf95

  git submodule init
  git config --local submodule.libglnx.url "$srcdir/libglnx"
  git config --local submodule.bubblewrap.url "$srcdir/bubblewrap"
  git submodule update

  sed -i '/locale\/C\./d' tests/make-test-runtime.sh

  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd $pkgname
  git describe | sed 's/-/+/g'
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --sbindir=/usr/bin --libexecdir=/usr/lib/$pkgname --disable-static \
    --enable-gtk-doc --with-priv-mode=setuid

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  # https://github.com/flatpak/flatpak/issues/267
  # https://github.com/flatpak/flatpak/issues/268
  make -k check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  # Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
