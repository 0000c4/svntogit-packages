# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gdm
pkgver=3.6.1
pkgrel=1
pkgdesc="Gnome Display Manager"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
        etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf)
groups=(gnome)
options=('!libtool')
depends=(libcanberra gnome-session gnome-settings-daemon metacity upower accountsservice systemd
         xorg-xrdb nss polkit-gnome xorg-server)
makedepends=(itstool intltool gnome-doc-utils xorg-server gobject-introspection)
optdepends=('gnome-shell: new login interface'
            'fprintd: fingerprint authentication')
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        fix_external_program_directories.patch)
sha256sums=('aff69dde55e11b540145002c95cf966cd16376910283b385520431ced10c5031'
            'a878680734e35c1d66252cbcfe678c3961b5ff0fa16302f8796a5e7e65ffe4a2')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/fix_external_program_directories.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var \
    --disable-static \
    --disable-schemas-compile \
    --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
    --with-check-accelerated-directory=/usr/lib/gnome-session \
    --with-authentication-agent-directory=/usr/lib/polkit-gnome \
    --without-tcp-wrappers \
    --without-console-kit \
    --with-systemd \
    --with-default-pam-config=exherbo

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make -j1 # race condition building gdm-client.c, does not wait for gdm-client-glue.h
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  chmod 1770 "$pkgdir/var/log/gdm"
  chmod 700 "$pkgdir/var/lib/gdm/.config/dconf"
  rm -r "$pkgdir/var/run" "$pkgdir/var/gdm"
}
