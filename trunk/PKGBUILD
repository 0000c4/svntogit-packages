# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=zenity
pkgver=3.28.1
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
url="https://git.gnome.org/browse/zenity"
arch=(x86_64)
license=(LGPL)
depends=(webkit2gtk libnotify)
makedepends=(itstool yelp-tools gettext gnome-common git)
_commit=7fee2da8076affd41113f7e7d6e8e61f9ff036ab  # tags/3.28.1^0
source=("git+https://git.gnome.org/browse/zenity#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^ZENITY_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
