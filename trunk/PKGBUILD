# $Id: PKGBUILD,v 1.31 2009/03/14 02:03:57 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=0.9.1
pkgrel=2
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
depends=('gstreamer0.10-base' 'totem-plparser' 'libbeagle' 'cdrkit' 'cdrdao') 
makedepends=('gnome-doc-utils' 'intltool' 'libburn' 'libisofs' 'nautilus') 
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'gst-plugin-bad: video project'
            'vcdimager: video project')
install=brasero.install
source=(http://ftp.gnome.org/pub/gnome/sources/brasero/0.9/brasero-${pkgver}.tar.bz2
        nautilus_language.patch)
md5sums=('1b9432a5ac81a7b6d09b68887994f5c5'
         'abcd1637c587fbb3bb2ec150ac3a6575')
options=('!libtool' '!emptydirs')

build() {
  cd ${srcdir}/brasero-${pkgver}

  patch -Np1 -i $srcdir/nautilus_language.patch

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-caches || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
