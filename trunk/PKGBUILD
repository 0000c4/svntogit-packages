# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.0.23
pkgrel=3
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.22.99" 'dialog' 'pciutils' 'ncurses')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2
        alsa
        alsaconf.patch
        alsa.conf.d)
backup=(etc/conf.d/alsa)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ../alsaconf.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m755 ../alsa ${pkgdir}/etc/rc.d/alsa
  install -D -m644 ../alsa.conf.d ${pkgdir}/etc/conf.d/alsa

  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}
md5sums=('cb0cf46029ac9549cf3a31bff6a4f4e1'
         '3ef812cae83053d01cd560284efad71f'
         '62ebef77d5617d0036012cc541dcf23c'
         '4c8ae9f976c7df5e271a4cb91ccc7767')
