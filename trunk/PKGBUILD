# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer
pkgver=28347
pkgrel=5
pkgdesc="A movie player for linux"
arch=('i686' 'x86_64')
depends=('libxxf86dga' 'libxv' 'libmad' 'giflib' 'cdparanoia' 'gtk2'
         'sdl' 'lame' 'libtheora' 'xvidcore' 'zlib' 'libmng' 'libxss'
         'libgl' 'smbclient' 'aalib' 'jack-audio-connection-kit' 'libcaca'
         'x264>=20090416' 'faac' 'lirc-utils' 'ttf-dejavu' 'fribidi' 'libxvmc')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'live-media' 'libdca' 'mesa')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(ftp://ftp.archlinux.org/other/mplayer/${pkgname}-${pkgver}.tar.bz2
        http://www.mplayerhq.hu/MPlayer/skins/Blue-1.7.tar.bz2)
md5sums=('41fb540025a51930d318e99728a19c66' 'e4e2020d11b681aac898103b3ba723c4')

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS

  cd ${srcdir}/${pkgname}

  ./configure --prefix=/usr --enable-gui --disable-arts --enable-x11 \
      --enable-runtime-cpudetection --confdir=/etc/mplayer --disable-nas \
      --enable-gl --enable-tv-v4l1 --enable-tv-v4l2 --enable-largefiles \
      --disable-liblzo --disable-speex --disable-openal \
      --enable-fribidi --disable-libdv --disable-musepack \
      --language=all --disable-esd --disable-mga \
      --disable-libamr_nb --enable-lirc --enable-radio --enable-radio-capture \
      --with-extraincdir=/usr/lib/live-media --enable-freetype \
      --enable-xvmc || return 1

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make || return 1
  make -j1 DESTDIR=${pkgdir} install || return 1
  cp etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font
  mv ${srcdir}/Blue ${pkgdir}/usr/share/mplayer/skins/default
}
