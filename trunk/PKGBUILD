# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd <judd@archlinux.org>

pkgname=quota-tools
pkgver=3.16
pkgrel=1
pkgdesc="Tools to manage kernel-level quotas in Linux"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/linuxquota/"
license=('GPL' 'BSD')
depends=('e2fsprogs' 'tcp_wrappers')
backup=('etc/warnquota.conf' 'etc/quotatab' 'etc/quotagrpadmins')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/linuxquota/quota-${pkgver}.tar.gz LICENSE)
md5sums=('6d1467458a092c4754abef92e7fa6ebe' 'dd792440e684043e4e4ef80963d0237b')
sha1sums=('bf90bb867fe847408c31bfa47a739235ba95287c' '57297bdc9e638c500506169bbbe12eb89bcf7d07')

build() {
  cd ${startdir}/src/${pkgname}
  ./configure --prefix=/usr --sysconfdir=/etc
  make || return 1
  make ROOTDIR=${startdir}/pkg install || return 1
  install -D -m644 ../LICENSE ${startdir}/pkg/usr/share/licenses/${pkgname}/LICENSE

  # remove conflicts
  # glibc
  rm ${startdir}/pkg/usr/include/rpcsvc/rquota.h || return 1
  rm ${startdir}/pkg/usr/include/rpcsvc/rquota.x || return 1
  # man-pages
  rm ${startdir}/pkg/usr/share/man/man2/quotactl.2 || return 1
}
