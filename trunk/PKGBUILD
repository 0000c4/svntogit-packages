# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyasn1-modules
pkgname=('python-pyasn1-modules' 'python2-pyasn1-modules')
pkgver=0.1.5
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=('any')
license=('BSD')
url="http://pypi.python.org/pypi/pyasn1-modules"
makedepends=('python-pyasn1' 'python2-pyasn1' 'python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/pyasn1-modules/pyasn1-modules-$pkgver.tar.gz")
sha512sums=('0d7f631fdbcb4c89ccf65b91c106483f536daadd099f5b3fb9b144bbcfcb0619bf818fa18715b8f61baf10699421a1af80f003781a5ebac28ebb4f4e7f677c1a')

prepare() {
  cp -a pyasn1-modules-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyasn1-modules-$pkgver
  python setup.py build

  cd "$srcdir"/pyasn1-modules-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyasn1-modules-$pkgver
  PYTHONPATH=. python tests/__main__.py

  cd "$srcdir"/pyasn1-modules-$pkgver-py2
  PYTHONPATH=. python2 tests/__main__.py
}

package_python-pyasn1-modules() {
  depends=('python-pyopenssl' 'python-pyasn1')

  cd pyasn1-modules-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyasn1-modules() {
  depends=('python2-pyopenssl' 'python2-pyasn1')

  cd pyasn1-modules-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
