# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ffi-checklib
pkgver=0.24
pkgrel=2
pkgdesc="Check that a library is available for FFI"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/FFI-CheckLib"
depends=('perl')
checkdepends=('perl-test-exit' 'perl-test2-suite')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/FFI-CheckLib-$pkgver.tar.gz")
sha512sums=('5036c80c5f76a447ffeec017b69253f89c5dc44ec07c97ec48ebf03e4e5f5cae970aa6a1f1ecc865f568cf84ca85cd73212d4763f79791eadcf1c8d676689000')

build() {
  cd FFI-CheckLib-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd FFI-CheckLib-$pkgver
  make test
}

package() {
  cd FFI-CheckLib-$pkgver
  make DESTDIR="$pkgdir" install
}
