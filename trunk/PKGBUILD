# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-libwww
pkgver=6.03
pkgrel=2
pkgdesc="The World-Wide Web library for Perl"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.8' 'perl-encode-locale' 'perl-file-listing>=6'
         'perl-html-parser' 'perl-http-cookies>=6' 'perl-http-daemon>=6'
         'perl-http-date>=6' 'perl-http-negotiate>=6' 'perl-lwp-mediatypes>=6'
         'perl-net-http>=6' 'perl-uri>=1.10' 'perl-www-robotrules>=6'
         'perl-http-message>=6')
optdepends=('perl-lwp-protocol-https: for https:// url schemes')
url=https://metacpan.org/release/libwww-perl
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/libwww-perl-${pkgver}.tar.gz")
md5sums=(e8f0ad4a990e13db41cdf6d4a4adbb18)
sha512sums=(1be85a3129724aee25bed7e3b80ccf66488fc9e50f095426635e8d2aaa80e29ee6a18e034d715320b9ea6f2f619c7821b3326c85426749a48a1187467acc9076)
_distdir="${srcdir}/libwww-perl-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
