# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=2.8
pkgrel=1
pkgdesc="A library for manipulating block devices"
arch=('i686' 'x86_64')
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
depends=('glib2' 'libsystemd')
makedepends=('cryptsetup' 'dmraid' 'gobject-introspection' 'kmod' 'libbytesize' 'libsystemd' 'nss'
	         'parted' 'python' 'volume_key')
optdepends=('dmraid: For Device Mapper plugin'
			'parted: For FS plugin'
			'lvm2: For LVM plugin'
			'volume_key: For crypto plugin'
			'btrfs-progs: For BTRFS plugin'
			'libbytesize: For BTRFS plugin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz")
sha512sums=('d3e72b41aaab3837df9383d3997fdfbc983ca90fa7268895eda5d78825ff475f60a3d6183fc22187cd9836fca619b433a0e78045c31040498483d1ba9f9ad25e')

prepare() {
  sed -i 's/-ldmraid //' $pkgname-$pkgver-1/src/lib/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver-1
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make DESTDIR="$pkgdir" install
}
