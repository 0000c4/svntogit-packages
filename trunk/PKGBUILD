# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-plugins-base
pkgname=(gst-plugins-base-libs gst-plugins-base)
pkgver=1.10.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(LGPL)
makedepends=(pkgconfig gstreamer orc libxv alsa-lib cdparanoia libvisual libvorbis
             libtheora pango gobject-introspection opus gtk-doc autoconf-archive git)
options=(!emptydirs)
_commit=73e5a830c089767f018f3c8824df1fd58302fe06  # tags/1.10.2^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-plugins-base#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase

  git cherry-pick -n 8b9ebd8f8844fb720243b099bf743100dff5acea

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer Base Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --enable-experimental --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make

  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgbase
  make check
}

package_gst-plugins-base-libs() {
  pkgdesc="GStreamer Multimedia Framework Base Plugin libraries"
  depends=(gstreamer orc libxv)

  cd $pkgbase
  make DESTDIR="$pkgdir" install
}

package_gst-plugins-base() {
  depends=("gst-plugins-base-libs=$pkgver" alsa-lib cdparanoia libvisual libvorbis
           libtheora pango opus)

  cd $pkgbase
  make -C gst-libs DESTDIR="$pkgdir" install
  make -C ext DESTDIR="$pkgdir" install
  make -C gst-libs DESTDIR="$pkgdir" uninstall
}
