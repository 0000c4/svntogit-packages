# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Original Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# New Version - Contributor: Bob Finch <w9ya@arrl.net>
# with help from J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar> aka Angelus

pkgname=wxgtk
pkgver=2.8.10
pkgrel=1
pkgdesc="wxGTK - GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2>=2.12.11' 'mesa')
makedepends=('libgnomeprintui')
#source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
md5sums=('88b867bc118a183af56efc67014bdf27')
sha1sums=('1e4bee16a423efeef665ed70e82b6e7dc5468cf4')

build() {
#  cd ${srcdir}/wxPython-src-${pkgver}
  cd ${srcdir}/wxGTK-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
	  --enable-graphics_ctx --with-gnomeprint --disable-optimize || return 1
  make || return 1
  make -C locale allmo || return 1
  make DESTDIR=${pkgdir} install || return 1
  cd contrib/src
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 ../../docs/licence.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
