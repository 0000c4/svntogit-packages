# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gpaste
pkgver=3.36.3
pkgrel=1
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD)
arch=(x86_64)
depends=(gtk3)
makedepends=(vala appstream-glib gobject-introspection gnome-shell
             gnome-control-center git meson)
optdepends=("wgetpaste: Upload clipboard contents")
_commit=73838e7f3ddb068dc988225787636816925f8cb5  # tags/v3.36.3^0
source=("git+https://github.com/keruspe/gpaste#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f v3.36.2 3224441e260f81112544fcc90609b5da6843ba40  # fixup tag
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}
