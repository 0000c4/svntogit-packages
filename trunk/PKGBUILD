# $Id$
# Contributor: Sarah Hay <sarah@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libmpeg2
pkgver=0.5.1
pkgrel=2
pkgdesc="Library for decoding MPEG-1 and MPEG-2 video streams."
arch=('i686' 'x86_64')
url="http://libmpeg2.sourceforge.net/"
depends=('glibc')
makedepends=('sdl' 'libxv')
optdepends=('sdl:	requiered for mpeg2dec'
            'libxv:	requiered for mpeg2dec')
source=(http://libmpeg2.sourceforge.net/files/${pkgname}-${pkgver}.tar.gz)
license=('GPL2')
options=(!libtool)
provides=('mpeg2dec')
md5sums=('0f92c7454e58379b4a5a378485bbd8ef')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --enable-shared --disable-static
  make	OPT_CFLAGS="${CFLAGS}" \
	MPEG2DEC_CFLAGS="${CFLAGS}" \
	LIBMPEG2_CFLAGS=""
  make DESTDIR="${pkgdir}" install
}
