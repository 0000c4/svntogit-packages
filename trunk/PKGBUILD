# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=dcraw
pkgver=9.17
pkgrel=1
pkgdesc='Decodes any raw image from any digital camera'
url='http://www.cybercom.net/~dcoffin/dcraw/'
arch=('i686' 'x86_64')
license=('custom')
depends=('lcms' 'jasper')
source=("http://www.cybercom.net/~dcoffin/dcraw/archive/${pkgname}-${pkgver}.tar.gz")
sha1sums=('11a0b20b8f46950dd94e6c1ada313807049a6b27')

build() {
	cd "${srcdir}/${pkgname}"
	gcc ${CFLAGS} ${LDFLAGS} \
		-o dcraw dcraw.c \
		-lm -ljasper -ljpeg -llcms \
		-DLOCALEDIR=\"/usr/share/locale/\"
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm755 dcraw "${pkgdir}"/usr/bin/dcraw
	install -Dm644 dcraw.1 "${pkgdir}"/usr/share/man/man1/dcraw.1
	
	for i in dcraw_*.1; do j=${i#dcraw_}; k=${j%.1}
		install -Dm644 $i "${pkgdir}"/usr/share/man/$k/man1/dcraw.1
	done

	for i in dcraw_*.po; do j=${i#dcraw_}; k=${j%.po}
		install -d "${pkgdir}"/usr/share/locale/$k/LC_MESSAGES
		msgfmt -o "${pkgdir}"/usr/share/locale/$k/LC_MESSAGES/dcraw.mo $i
	done

	install -d "${pkgdir}"/usr/share/licenses/${pkgname}
	head -25 dcraw.c > "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
