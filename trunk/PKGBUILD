# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-build
pkgver=1.69
pkgrel=1
pkgdesc="Build external dependencies for use in CPAN"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Build"
depends=('perl-capture-tiny' 'perl-ffi-checklib' 'perl-file-chdir' 'perl-file-which'
         'perl-path-tiny')
makedepends=('perl-test2-suite')
checkdepends=('perl-alien-base-modulebuild' 'perl-alien-cmake3' 'perl-env-shellwords'
              'perl-pkgconfig' 'perl-pkgconfig-libpkgconf' 'perl-readonly' 'perl-sort-versions')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Build-$pkgver.tar.gz")
sha512sums=('5acb98ad910516184da9424646cbb298f5cd9b6ca84b15a03d19507ac7a145fd77041a782df211d127442f7ef97d279ee224ecf2107ff1d73fff3cc2f99655f1')

build() {
  cd Alien-Build-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-Build-$pkgver
  make test
}

package() {
  cd Alien-Build-$pkgver
  make DESTDIR="$pkgdir" install
}
