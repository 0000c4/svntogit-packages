# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=18.08.0
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=(x86_64)
url='https://community.kde.org/KDE_PIM'
license=(GPL LGPL FDL)
depends=(libkolabxml kdav akonadi-calendar knotifyconfig kalarmcal kmbox pimcommon akonadi-notes kdelibs4support)
makedepends=(extra-cmake-modules kdoctools boost kdesignerplugin)
conflicts=(kio-pim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        gmail-auth-dialog.patch::"https://cgit.kde.org/kdepim-runtime.git/patch/?id=6752a169")
sha256sums=('59cdebec7121f094688cca97835b910901252d8150e62f1fd262e8d9f55c322c'
            'SKIP'
            '0f5363c0b293f9a302dea13c707ab6b23d2719769ce7abc9714e50bf7cb521b1')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../gmail-auth-dialog.patch # Fix Gmail authentication dialog popping up continuously
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
