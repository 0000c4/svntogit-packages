# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcoreaddons
pkgver=5.21.0
pkgrel=2
pkgdesc='Addons to QtCore'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-base' 'shared-mime-info' 'gamin')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
install=${pkgname}.install
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        kdebug-362161.patch::"https://quickgit.kde.org/?p=kcoreaddons.git&a=commitdiff&h=78212436&o=plain")
md5sums=('f3823cbe368197f64ec692e3e6688865'
         'f38d38a9ea2962918fd8b08f627efccb')

prepare() {
  mkdir -p build

# Fix krandom regression https://bugs.kde.org/show_bug.cgi?id=362161
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-362161.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -D_KDE4_DEFAULT_HOME_POSTFIX=4 \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
