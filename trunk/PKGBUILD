# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libmediaart
pkgver=1.9.1
pkgrel=1
pkgdesc="Library tasked with managing, extracting and handling media art caches"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('vala' 'gobject-introspection' 'git' 'gtk-doc')
url="https://github.com/curlybeast/libmediaart"
_commit=015a7f81bb3eb0d6f30f1e15062dbca388927f1e  # tags/1.9.1^0
source=("git+https://git.gnome.org/browse/libmediaart#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --enable-gdkpixbuf --disable-qt --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
