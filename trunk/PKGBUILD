# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=dmraid
pkgver=1.0.0.rc16+CVS
pkgrel=1
pkgdesc="Device mapper RAID interface"
url="http://people.redhat.com/~heinzm/sw/dmraid/"
depends=('device-mapper>=2.0.54')
arch=('i686' 'x86_64')
license=('GPL')
source=(http://ftp.archlinux.org/other/dmraid/$pkgname-$pkgver.tar.bz2
        #http://people.redhat.com/~heinzm/sw/dmraid/src/$pkgname-$pkgver.tar.bz2
        dmraid_install
        dmraid_hook)
install=dmraid.install
md5sums=('c0de87073de96b311d26ab0170b2a753'
         'd01908b414e1686c0f3233ff37de78a5'
         '4d1c6af4fb043abe7acaa6658ef68fcf')

build() {
  cd "$srcdir"/$pkgname/$pkgver

  ./configure --enable-led --enable-intel_led
  make || return 1
  make DESTDIR="$pkgdir" mandir=/usr/share/man includedir=/usr/include install || return 1
  mkdir -p "$pkgdir"/var/lock/dmraid
  chmod 1777 "$pkgdir"/var/lock/
  install -D -m644 "$srcdir"/dmraid_install "$pkgdir"/lib/initcpio/install/dmraid
  install -D -m644 "$srcdir"/dmraid_hook "$pkgdir"/lib/initcpio/hooks/dmraid
  # fix permissions
  chmod 644 "$pkgdir"/lib/libdmraid.a
  chmod 644 "$pkgdir"/usr/include/dmraid/*
}
