# Maintainer: Firmicus <firmicus ατ gmx δοτ net>

pkgname=texlive-core
pkgver=2008.10429
pkgrel=1
pkgdesc="TeX Live core distribution"
license=('GPL')
arch=('i686' 'x86_64')
depends=('texlive-bin')
optdepends=('perl' 'ruby' 'ghostscript' 'psutils')
makedepends=('lzma-utils')
groups=('texlive-most')
conflicts=('tetex')
provides=('tetex')
url='http://tug.org/texlive/'
source=("http://dev.archlinux.org/~francois/texlive/src/$pkgname-src.zip")
install=texlive.install
backup=(usr/share/texmf-config/tex/context/config/cont-usr.tex)
md5sums=('498890380ff97432cb581543848198bf')

build() {
   cd $startdir/src
   for p in *.tar.lzma; do 
	   lzma --force -dc $p | tar xf - || return 1
   done
   install -m755 -d $startdir/pkg/usr/share || return 1
   find texmf-dist -type d -exec install -d -m755 $startdir/pkg/usr/share/'{}' \; || exit 1
   find texmf-dist -type f -exec install -m644 '{}' $startdir/pkg/usr/share/'{}' \; || exit 1

   # copy config file to texmf-config
   mkdir -p $startdir/pkg/usr/share/texmf-config/tex/context/config
   cp -a $startdir/pkg/usr/share/texmf-dist/tex/context/config/cont-usr.tex \
   	$startdir/pkg/usr/share/texmf-config/tex/context/config/cont-usr.tex
  
  # remove obsolete stuff (we don't provide the omega and aleph binaries anyway)
  rm -rf $startdir/pkg/usr/share/texmf-dist/omega
  rm -f $startdir/pkg/usr/share/texmf-dist/tex/plain/config/{omega,aleph}.ini
}
