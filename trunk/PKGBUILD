# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fvwm
pkgver=2.6.0
pkgrel=1
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'readline' 'librsvg')
conflicts=('fvwm-devel')
source=(ftp://ftp.fvwm.org/pub/fvwm/version-2/${pkgname}-${pkgver}.tar.bz2 fvwm2.desktop)
md5sums=('e4b4b065b2185be9071a9a55ac49b39a'
         'e52a6ef25a936dcf3ad0e58f2e9ed0ea')
sha1sums=('7c6ed1075c3faa1cd13c2ebc31f720d971564f5e'
          'e0924164329e7eb14e0d6137d8ff18cf6230191c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke-library
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/fvwm"
  install -D -m644 sample.fvwmrc/* "${pkgdir}/etc/fvwm"
  rm -f "${pkgdir}"/etc/fvwm/Makefile*
  install -D -m644 ../fvwm2.desktop "${pkgdir}/usr/share/xsessions/fvwm2.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
