# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=kseexpr
pkgver=4.0.0.0
pkgrel=1
pkgdesc="An embeddable expression evaluation engine (Krita fork)"
arch=(x86_64)
url="https://krita.org"
license=(Apache)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools)
replaces=(seexpr-krita)
source=("https://download.kde.org/stable/kseexpr/${pkgver%.*}/$pkgname-$pkgver.tar.gz")
sha256sums=('3b2bfad1a60afb5efcea2c16e424203696e0440401e7169dec1db6df27ef2228')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
