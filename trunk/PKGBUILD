# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=3.4.2
pkgrel=1
pkgdesc="a VNC server for the GNOME desktop"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libnotify' 'libxtst' 'libsm' 'libsoup' 'telepathy-glib' 'gtk3' 'libgnome-keyring' 'avahi' 'desktop-file-utils')
makedepends=('intltool' 'networkmanager')
groups=('gnome-extra')
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('b58f7f7e1ce207246dfe98ff777c665cbcbc52abab0cbafc0a1b4f5fc5b3e0c8')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --enable-gnome-keyring \
      --enable-avahi \
      --disable-http-server
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
