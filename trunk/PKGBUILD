# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=deluge
pkgver=1.3.5
pkgrel=4
pkgdesc="A BitTorrent client with multiple user interfaces in a client/server model"
arch=('any')
url="http://deluge-torrent.org/"
license=('GPL3')
depends=(python2-xdg libtorrent-rasterbar twisted python2-pyopenssl
         xdg-utils python2-chardet desktop-file-utils hicolor-icon-theme
         python2-distribute)
makedepends=(intltool pygtk librsvg python2-mako)
optdepends=('python2-notify: libnotify notifications'
            'pygtk: needed for gtk ui'
            'librsvg: needed for gtk ui'
            'python2-mako: needed for web ui')
install=deluge.install
_commit=dcd85e64c0870306bfe412d9dfa675152f799dcb
source=(http://git.deluge-torrent.org/deluge/snapshot/$pkgname-$_commit.tar.bz2
        deluged.service deluge-web.service)
md5sums=('a37bce6070b8cb6a652c262af9736f55'
         'b55cb839a37e8315da4a0f6c99a75fd3'
         '2a73f62c04e8b147c731fbb50666e846')

build() {
  cd $pkgname-$_commit
  python2 setup.py build
}

package() {
  cd $pkgname-$_commit
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 deluge/data/pixmaps/deluge.svg "$pkgdir/usr/share/pixmaps/deluge.svg"

  _dir="$pkgdir/usr/lib/python2.7/site-packages/deluge/ui"
  sed -i '1s/python$/&2/' "$_dir"/{Win32IconImagePlugin.py,web/gen_gettext.py}

  cd ..
  install -Dm644 deluged.service "$pkgdir/usr/lib/systemd/system/deluged.service"
  install -Dm644 deluge-web.service "$pkgdir/usr/lib/systemd/system/deluge-web.service"
 
  install -d "$pkgdir/srv"
  install -d -m 775 -o 125 -g 125 "$pkgdir/srv/deluge"
}
