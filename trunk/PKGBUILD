# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-video-ati
pkgver=6.14.0
pkgrel=3
pkgdesc="X.org ati video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(libpciaccess libdrm udev pixman ati-dri)
makedepends=('xorg-server-devel' 'libdrm' 'xf86driproto' 'mesa')
conflicts=('xorg-server<1.10.0')
groups=('xorg-drivers' 'xorg')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	ati-fix-build-1.10.patch)
md5sums=('b64bf8939f9f8fb518baf858f8b6ca73'
         '99b8c07957c4155da4c0edb0744710ae')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/ati-fix-build-1.10.patch
  ./configure --prefix=/usr --enable-dri
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
