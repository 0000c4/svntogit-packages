# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=at-spi2-atk
pkgver=0.3.92
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus-core>=1.4.0' 'dbus-glib=>0.86' 'atk>=1.30.0' 'gconf>=2.31.91')
makedepends=('intltool')
install=at-spi2-atk.install
options=('!libtool')
source=(http://download.gnome.org/sources/${pkgname}/0.3/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('cf7ef59ee28d4d98b7faf93f17dc77ee1eb5fa84c21d4e0ca731fc9ca0d5bd53')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain at-spi2-atk ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
