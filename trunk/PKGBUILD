# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=feh
pkgver=1.11
pkgrel=3
pkgdesc='Fast, lightweight image viewer which uses imlib2'
arch=('i686' 'x86_64')
url='http://derf.homelinux.org/projects/feh/'
license=('MIT')
depends=('libxinerama' 'giblib' 'perl')
makedepends=('libxt')
source=("${url}${pkgname}-${pkgver}.tar.bz2"
        'keystate.patch')
sha1sums=('cc4209aa197938389b3d6628e75404eff52475cc'
          '1a3d7f07cc2a7b508480de0eda9e3014ad599543')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../keystate.patch
  make PREFIX=/usr
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
