# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.8.3
pkgrel=1
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('exo>=0.6.0' 'libxfce4ui' 'libnotify>=0.7.1' 'libxklavier' 'tango-icon-theme' 'gnome-icon-theme' 'gtk-engines' 'gtk-xfce-engine')
makedepends=('intltool')
optdepends=('libcanberra: for sound control')
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.8/${pkgname}-${pkgver}.tar.bz2
	xfce4-settings-4.6.0.patch)
md5sums=('4669298cc8a4abf0e5a410016b575030'
         '3046732e5d73c9939e7674602eadb1e2')
sha1sums=('98431633ba3ec2a4a10182bc7266904d9256949b'
          '18f5eaf1fd86b957bae2e41fbdd83edef5437b3c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # enable Tango icon theme, clearlooks theme and font hinting by default(taken from Fedora)
  patch -Np1 -i ${srcdir}/xfce4-settings-4.6.0.patch || return 1
    
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-xrandr \
	--enable-xcursor \
	--enable-libnotify \
	--enable-libxklavier \
	--enable-pluggable-dialogs \
	--enable-sound-settings \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
