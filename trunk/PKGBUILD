# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=amtk
pkgver=5.0.2
pkgrel=1
pkgdesc="Actions, Menus and Toolbars Kit for GTK+ applications"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Amtk"
license=(LGPL2.1)
depends=(gtk3)
makedepends=(autoconf-archive git gobject-introspection gtk-doc)
_commit=6fc28285d80f5ea3ec69069c7e1336e2b8ff5d5c  # tags/5.0.2^0
source=("git+https://gitlab.gnome.org/GNOME/amtk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc --disable-Werror
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -C $pkgname check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
