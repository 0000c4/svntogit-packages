# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=9.0.3+2+g4ec9d55
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(i686 x86_64)
license=(GPL)
depends=(dconf gtk3)
makedepends=(gtk-doc intltool gobject-introspection itstool docbook-xsl
             python2 appdata-tools unicode-character-database unzip gnome-common git)
groups=(gnome)
_commit=4ec9d55793142429bf29fe96ed5576b4cb428742  # unicode-9
source=("git+https://git.gnome.org/browse/gucharmap#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-introspection \
      --with-unicode-data=/usr/share/unicode-character-database
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
