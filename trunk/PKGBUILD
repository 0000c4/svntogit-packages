# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux at free.fr>
# Contributor: Bartlomiej Piotrowski <nospam at bpiotrowski dot pl>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=discount
pkgver=2.2.3.a
_pkgver=2.2.3a
pkgrel=1
pkgdesc='A Markdown implementation written in C'
arch=(x86_64)
url="https://www.pell.portland.or.us/~orc/Code/discount/"
license=('custom:BSD')
source=("http://www.pell.portland.or.us/~orc/Code/$pkgname/$pkgname-$_pkgver.tar.bz2")
sha256sums=('75f5d5fda2e9607a5c77455296b41e5dd436389e20f35c37bea395d35aea0954')
depends=(glibc)
conflicts=(markdown)
provides=(markdown)
replaces=(markdown)

build() {
  cd $pkgname-$_pkgver
  ./configure.sh --prefix=/usr --enable-all-features --with-fenced-code --shared
  make
}

package() {
  cd $pkgname-$_pkgver
  # fix ldconfig call
  sed -e 's|/sbin/ldconfig|/sbin/ldconfig -n|' -i librarian.sh

  make DESTDIR="$pkgdir" install.everything
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
