# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi' 'plasma-wayland-session')
pkgver=5.5.1
pkgrel=4
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
# note on libxdamage:
# not detected by namcap because libgl depends on it
# but nvidia providing libgl does not depend on libxdamage
depends=('kjsembed' 'knotifyconfig' 'libxdamage' 'libksysguard' 'libkscreen' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot'
         'xorg-xmessage' 'xorg-xprop' 'milou' 'breeze' 'prison-frameworks' 'kwin')
makedepends=('extra-cmake-modules' 'kdoctools' 'gpsd' 'baloo'
             'krunner' 'kxmlrpcclient' 'networkmanager-qt')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgbase}-${pkgver}.tar.xz" 'kde.pam'
	fix-empty-tray-icons.patch::"https://quickgit.kde.org/?p=plasma-workspace.git&a=commitdiff&h=a0578e6e5&o=plain"
	fix-panel-click.patch::"https://quickgit.kde.org/?p=plasma-workspace.git&a=commitdiff&h=1390b4&o=plain")
md5sums=('9007fe743784f6b1f04956cbd3429fec'
         '378ee33a9ec2870a1a899f2e05ee00d4'
         'f2f5593a1bf02ef2371876557352f0a5'
         'b720190712f8b4387206d5f2328223f6')

prepare() {
  mkdir -p build

  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -e 's:qtpaths:qtpaths-qt5:' -i startkde/start*.cmake

# Workaround for empty tray icons at startup https://bugs.kde.org/show_bug.cgi?id=352055
  patch -p1 -i ../fix-empty-tray-icons.patch
# Fix mouse interaction with the panel http://bugs.kde.org/show_bug.cgi?id=354651
  patch -p1 -i ../fix-panel-click.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation')
  conflicts=('kdebase-workspace')

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "${pkgdir}"/usr/lib/drkonqi
  rm -r "${pkgdir}"/usr/share/drkonqi

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kxmlrpcclient' 'gdb')

  cd build/drkonqi
  make DESTDIR="${pkgdir}" install
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=('plasma-workspace' 'qt5-wayland' 'kwayland-integration' 'xorg-server-xwayland')
  groups=()

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
