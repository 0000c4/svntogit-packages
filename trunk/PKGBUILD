# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-libxml2
pkgver=0.07
pkgrel=1
pkgdesc="Install the C libxml2 library on your system"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Libxml2"
depends=('libxml2' 'perl-alien-build')
makedepends=('perl-html-parser' 'perl-sort-versions' 'perl-test2-suite' 'perl-uri')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Libxml2-$pkgver.tar.gz")
sha512sums=('8ef4b42597980452b15ff3d2664c1765dffe2cdca9f52465166ad211c1bcdb59b0b65bcb9f12b7832643d324176a996a6592a5312d2f83a208dd18da3c5bd222')

build() {
  cd Alien-Libxml2-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-Libxml2-$pkgver
  make test
}

package() {
  cd Alien-Libxml2-$pkgver
  make DESTDIR="$pkgdir" install
}
