# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dolphin-plugins
pkgver=16.04.1
pkgrel=1
pkgdesc="Extra Dolphin plugins"
arch=(i686 x86_64)
url="https://www.kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(dolphin)
makedepends=(extra-cmake-modules kdoctools ktexteditor)
optdepends=('ktexteditor: Mercurial plugin')
conflicts=(kdesdk-dolphin-plugins)
replaces=(kdesdk-dolphin-plugins)
groups=(kde-applications kdesdk)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('79e7ab48c4dcadc253af530cdcfbacaa')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
