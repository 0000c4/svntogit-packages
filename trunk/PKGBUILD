# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=kile
pkgver=2.9.92
pkgrel=1
pkgdesc="A user friendly TeX/LaTeX frontend for KDE"
arch=('x86_64')
license=('GPL2')
url="https://kile.sourceforge.net/"
depends=('ktexteditor' 'kinit' 'okular' 'khtml' 'texlive-core')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('konsole: embedded terminal')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('177372dc25b1d109e037a7dbfc64b5dab2efe538320c87f4a8ceada21e9097f2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
