# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpimtextedit
pkgver=19.12.0
pkgrel=1
pkgdesc="A textedit with PIM-specific features"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kio grantlee syntax-highlighting)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6caffca187ca21929d8becb83da6a173fb6c1a21ad07817b070100579eac1bd1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
