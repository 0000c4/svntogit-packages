# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=folks
pkgname=(folks folks-tracker)
pkgver=0.6.3
pkgrel=3
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/Folks"
license=('LGPL2.1')
depends=('telepathy-glib' 'libgee' 'libxml2' 'evolution-data-server' 'libsocialweb')
makedepends=('gobject-introspection' 'vala' 'intltool' 'libtracker-sparql')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/0.6/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('e21810966f300b31e8cd51cca9851606213345eb933584997b49326c43b46e1b')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  # Change tracker version
  grep -lr sparql-0.10 . | xargs sed -i 's/sparql-0.10/sparql-0.12/g'

  # Fix the sed command to work with LANG=C
  sed -i '/dlname=/s/A-Za-z0-9\.-+/A-Za-z0-9./' folks/Makefile.*

  ./configure --prefix=/usr --enable-tracker-backend
  make
}

package_folks() {
  optdepends=('folks-tracker: Tracker backend')

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

### Split folks-tracker
  cd "$pkgdir"
  find . -name '*tracker*' \! -type d | while IFS= read -r _file; do
    mkdir -p "$srcdir/folks-tracker/${_file%/*}"
    mv "$_file" "$srcdir/folks-tracker/$_file"
  done
}

package_folks-tracker() {
  pkgdesc="Tracker backend for folks"
  depends=('folks' 'tracker')

  mv "$srcdir"/folks-tracker/* "$pkgdir"
}

# vim:set ts=2 sw=2 et:
