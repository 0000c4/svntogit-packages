# $Id$
# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.24.0.1
pkgrel=1
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('autogen>=5.9.5' 'devhelp>=0.21' 'gnome-build>=2.24.0' 'vte>=0.17.4' 'graphviz>=2.18' 'subversion>=1.5.2' 'gtksourceview2>=2.4.0' 'glade>=3.5.2' 'libxslt>=1.1.23' 'desktop-file-utils')
makedepends=('gnome-doc-utils>=0.14.0')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
md5sums=('98cfa3707923c339b987f87f659d1e4f')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-plugin-subversion --enable-plugin-glade \
    --enable-plugin-class-inheritance --enable-plugin-valgrind \
    --enable-plugin-sourceview --disable-gtk-doc \
    --enable-debug --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
   
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
