# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=mtr
pkgname=(mtr mtr-gtk)
pkgver=0.80
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('ncurses' 'gtk2')
source=(ftp://ftp.bitwizard.nl/mtr/${pkgbase}-${pkgver}.tar.gz)
url="http://www.bitwizard.nl/mtr/"
md5sums=('fa68528eaec1757f52bacf9fea8c68a9')

build() {
    install -d  "${srcdir}/"{mtr-cli,mtr-gtk}
    cd "${srcdir}/mtr-cli"
    ../${pkgbase}-${pkgver}/configure --prefix=/usr --disable-gtktest --without-gtk
    make

    cd "${srcdir}/mtr-gtk"
    ../${pkgbase}-${pkgver}/configure --prefix=/usr --enable-gtk2
    make
}

package_mtr() {
    pkgdesc="Combines the functionality of traceroute and ping into one tool (CLI version)"
    depends=('ncurses>=5.7')
    cd "${srcdir}/mtr-cli"
    make DESTDIR="${pkgdir}" install
}

package_mtr-gtk() {
    pkgdesc="Combines the functionality of traceroute and ping into one tool (GTK version)"
    depends=('ncurses>=5.7' "gtk2>=2.22.0")
    conflicts=("mtr")
    provides=("mtr=${pkgver}")

    cd "${srcdir}/mtr-gtk"
    make DESTDIR="${pkgdir}" install
}
