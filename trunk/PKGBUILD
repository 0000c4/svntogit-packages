# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=avahi
pkgver=0.6.27
pkgrel=1
pkgdesc='A multicast/unicast DNS-SD framework'
arch=('i686' 'x86_64')
url='http://www.avahi.org/'
license=('LGPL')
depends=('expat' 'libdaemon' 'glib2' 'dbus' 'libcap' 'gdbm')
optdepends=('pygtk: avahi-discover'
            'dbus-python: avahi-discover'
            'gtk2: avahi-discover-standalone'
            'libglade: avahi-discover-standalone'
            'pygtk: avahi-bookmarks'
            'twisted: avahi-bookmarks')
makedepends=('qt' 'pygtk' 'mono' 'libglade' 'intltool' 'dbus-python' 'gtk-sharp-2')
backup=(etc/avahi/avahi-daemon.conf etc/avahi/services/{sftp-,}ssh.service)
install=avahi.install
conflicts=('howl' 'mdnsresponder')
provides=('howl' 'mdnsresponder')
replaces=('howl' 'mdnsresponder')
options=('!libtool')
source=("http://www.avahi.org/download/avahi-${pkgver}.tar.gz"
        'avahi-daemon-dbus.patch'
        'gnome-nettool.png')
sha1sums=('e763bbeba92fd5b3ba3e2af5fc85aaf99b406c8b'
          '41e9f23efa0b5a5f7b0f14a86cfb0677ece84c61'
          'cf56387c88aed246b9f435efc182ef44de4d52f3')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's/Requires.private/Requires/' avahi-qt4.pc.in
  sed -i 's/netdev/network/g' avahi-daemon/avahi-dbus.conf
  patch -p0 < ../avahi-daemon-dbus.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-qt3 \
    --disable-gtk3 \
    --disable-monodoc \
    --disable-doxygen-doc \
    --disable-xmltoman \
    --enable-compat-libdns_sd \
    --enable-compat-howl \
    --with-distro=archlinux \
    --with-avahi-priv-access-group=network \
    --with-autoipd-user=avahi \
    --with-autoipd-group=avahi

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # howl and mdnsresponder compatability
  cd "${pkgdir}"/usr/include
  ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h
  ln -s avahi-compat-howl howl
  cd "${pkgdir}"/usr/lib/pkgconfig
  ln -s avahi-compat-howl.pc howl.pc

  install -D -m 644 "${srcdir}"/gnome-nettool.png "${pkgdir}"/usr/share/pixmaps/gnome-nettool.png
}
