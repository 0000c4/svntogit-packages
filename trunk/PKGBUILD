# $Id$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=zziplib
pkgver=0.13.66
pkgrel=2
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file"
arch=('i686' 'x86_64')
url="http://zziplib.sourceforge.net"
license=('LGPL' 'MPL')
depends=('zlib')
makedepends=('python2' 'xmlto' 'zip')
source=($pkgname-$pkgver.tar.gz::"https://github.com/gdraheim/zziplib/archive/v$pkgver.tar.gz"
        CVE-2017-5979.patch)
sha256sums=('59b18c7c4ed348ba8d63fa7e194e6b012cd94197265b7a7b3afb539d8206bd7d'
            '6c649cc35eb040dc9f667faa1484e61fdb8600eccc293d79dca5a3cd8fdb1ee4')
sha512sums=('893885d85293269fd8ff14d61eaae5f7d07689a16dd9c07c1ae8d46ea2b2f94a13d6aab19670efa7716cafe5e9f8efb1cbc1254bd9e860c836faa35736bdbe20'
            'b11e940f6d0d0806e6408a06c465180c5a250449ea837108663049a0f395c2d8b5ff30614fa364a56f2686dd1ee2da120aa47dfb7d80698db43c00ae7a5ebd27')

prepare() {
  cd ${pkgname}-${pkgver}
  # extracted from opensuse
  patch -p1 < "${srcdir}/CVE-2017-5979.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

# fix permissions
  chmod -s "${pkgdir}/usr/share/man/man3"
# chmod 644 "${pkgdir}"/usr/share/man/man3/*
  chown -R root:root "${pkgdir}/usr/share/man/man3"
}

# vim: set ts=2 sw=2 et:
