# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgit2-glib
pkgver=0.27.7
pkgrel=1
pkgdesc="GLib wrapper for libgit2"
url="https://wiki.gnome.org/Projects/Libgit2-glib"
license=(LGPL2.1)
arch=(x86_64)
depends=(glib2 libgit2)
makedepends=(gobject-introspection python-gobject python2 vala gtk-doc git meson)
_commit=97becd4ca4642d8a03b2153df65e01f220d80757  # tags/v0.27.7^0
source=("git+https://gitlab.gnome.org/GNOME/libgit2-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  cp -a "$pkgdir"/usr/lib/python{3.7,2.7}
  python3 -m compileall "$pkgdir/usr/lib/python3.7"
  python3 -O -m compileall "$pkgdir/usr/lib/python3.7"
  python2 -m compileall "$pkgdir/usr/lib/python2.7"
  python2 -O -m compileall "$pkgdir/usr/lib/python2.7"
}
