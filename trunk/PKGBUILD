# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.13.90
pkgrel=1
pkgdesc="A low-level configuration system."
arch=(i686 x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('glib2' 'gtk-update-icon-cache')
makedepends=('vala' 'gtk3' 'intltool' 'docbook-xsl')
optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('8661a45700d2530d1e07b735809a3a5e3df019cc8064f0f80919417fc59ee288')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd "$pkgname-$pkgver"
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
