# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=0.4.4
pkgrel=1
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://projects.kde.org/projects/extragear/libs/libkgapi'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdepimlibs' 'qjson')
makedepends=('cmake' 'automoc4' 'boost')
replaces=('akonadi-google' 'libkgoogle')
conflicts=('akonadi-google' 'libkgoogle')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a26ddd6da675075f7529652f36f5c9f5')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
