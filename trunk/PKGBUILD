# $Id: PKGBUILD 319871 2018-03-23 19:45:34Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: said
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=kamoso
pkgver=18.04.1
pkgrel=2
pkgdesc="A webcam recorder from KDE community"
url="https://userbase.kde.org/Kamoso"
arch=(x86_64)
license=(GPL)
groups=(kde-applications kdemultimedia)
depends=(purpose gst-plugins-good gst-plugins-bad qt5-quickcontrols kirigami2)
makedepends=(extra-cmake-modules kdoctools boost python)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-394881.patch::"https://cgit.kde.org/kamoso.git/patch/?id=70d5cf37")
sha256sums=('4725043190557eae4ecbb676676054edc9d7c0d49d3289703f53f47f8841a0e5'
            'SKIP'
            '1daed78c6879fd1e161a7eebd645404ab6d9a9ab7c185581fbe16ed4c91639da')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-394881.patch # Fix crash
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

