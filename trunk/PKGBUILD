# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-libav
pkgver=1.16.1
pkgrel=1
pkgdesc="GStreamer open-source multimedia framework FFmpeg plugin"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(GPL)
depends=(gst-plugins-base-libs bzip2 ffmpeg)
makedepends=(python gtk-doc git yasm meson)
provides=("gst-ffmpeg=$pkgver-$pkgrel")
_commit=fe08c26641592f058b6fa0da9a1f86fc0b24cb0c  # tags/1.16.1^0
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-libav.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D package-name="GStreamer FFmpeg Plugin (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
