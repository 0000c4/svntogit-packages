# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributer: Ben <contrasutra@myrealbox.com>

pkgname=sound-juicer
pkgver=2.28.0
pkgrel=1
pkgdesc="A cd ripper application"
arch=(i686 x86_64)
license=('GPL')
depends=('musicbrainz>=2.1.5' 'gnome-media>=2.28.0' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'brasero>=2.28.0' 'libcdio>=0.81' 'gvfs>=1.4.0' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils>=0.18.0' 'pkgconfig')
options=(!emptydirs)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
groups=('gnome-extra')
install=sound-juicer.install
url="http://www.burtonini.com/blog/computers/sound-juicer"
sha256sums=('850ac22b164c414625892386b3a3f1163c1d61d078892928064eface3e12ead8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-hal \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain sound-juicer ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
