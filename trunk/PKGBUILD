# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-renderer
pkgver=0.6.0
pkgrel=2
pkgdesc="Library to discover and manipulate Digital Media Renderers"
arch=(x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git)
_commit=334baba53e253957637b86764347dcc27cf091ab  # tags/0.6.0
source=("git+https://github.com/01org/dleyna-renderer#commit=$_commit"
        gupnp-1.2.diff)
sha256sums=('SKIP'
            '91ee8a1bf00d94865f2caef3b16da2c74c42ff37e6620718592304617d27b4f1')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../gupnp-1.2.diff
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  CFLAGS+=" -Wno-deprecated-declarations"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
