# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Daniel Balieiro <daniel@balieiro.com>
# Contributor: Rodrigo L. M. Flores <mail@rodrigoflores.org>

pkgname=telepathy-gabble
pkgver=0.18.3+5+gfe084b7
pkgrel=1
pkgdesc="A Jabber/XMPP connection manager for Telepathy"
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org"
groups=(telepathy)
license=(LGPL2.1)
depends=(telepathy-glib libsoup libnice sqlite)
makedepends=(libxslt python2 gnome-common git)
_commit=fe084b7ef77184927e7740583a395eb5106f0151  # telepathy-gabble-0.18
source=("git://anongit.freedesktop.org/telepathy/telepathy-gabble#commit=$_commit"
        "git://anongit.freedesktop.org/wocky")
install=telepathy-gabble.install
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^telepathy-gabble-//;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.lib/ext/wocky.url "$srcdir/wocky"
  git submodule update

  sed -i '1s/python$/&2/' plugins/telepathy-gabble-xmpp-console
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --libexecdir=/usr/lib/telepathy \
    --disable-Werror --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
