# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-log-log4perl
pkgver=1.36
pkgrel=1
pkgdesc='Log4j implementation for Perl'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Log-Log4perl
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHILLI/Log-Log4perl-$pkgver.tar.gz")
md5sums=(ae6a51735b0b0e6661d2e66d30d91fa5)
sha512sums=(ac940b5de1ed3d04a3dfa06625c7311f31b0439786fe49fe56fe31a1bfbe78be378240a71fd79bd766e23600798621742f0572ddf1bb6bde7623832030e9ed3c)
_dir="$srcdir/Log-Log4perl-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
