# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=putty
pkgver=0.61
pkgrel=1
pkgdesc="A terminal integrated SSH/Telnet client"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
depends=('gtk2')
source=(http://the.earth.li/~sgtatham/putty/${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('db0e37f6b82ea62f0ace87927d29b2a4')
sha1sums=('cbb82fd392142e3455e859ca8bc7b1e61b246204')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/unix"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/unix"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
