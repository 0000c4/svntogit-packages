# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=mkinitcpio-busybox
pkgver=1.25.1
pkgrel=1
pkgdesc='Base initramfs tools'
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
options=('!buildflags')
source=("https://busybox.net/downloads/busybox-$pkgver.tar.bz2"
        'glibc-2.16.patch'
        'config')
sha256sums=('27667e0f2328fdbd79cfd622e4453e5c57e58f781c5da97c9be337d93aa2a02e'
            'fb5b6e2a0de4db5401322e5c2474ad8ce6a58615dad45b7109cfe045baf2c88d'
            'd916c75c3c4627e1155c9dc40bab5fe7890fbf45d6a3bcda4936f1c5b80f7d7f')

prepare() {
  cd "busybox-$pkgver"

  local safeflags="-march=${CARCH/_/-} -mtune=generic -Os -pipe -fno-strict-aliasing"

  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="'"$safeflags"'"|' "$srcdir/config" >.config

  patch -Np1 <"$srcdir/glibc-2.16.patch"
}

build() {
  make -C "busybox-$pkgver"
}

package() {
  install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/usr/lib/initcpio/busybox"
}

# vim:set ts=2 sw=2 et:
