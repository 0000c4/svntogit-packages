# $Id$
# Maintainer: tobias <tobias funnychar archlinux.org>

pkgname=xfconf
pkgver=4.6.1
pkgrel=4
pkgdesc="a simple client-server configuration storage and query system"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
# http://www.xfce.org/documentation/requirements
# keep dbus and gtk2, see also http://bugs.archlinux.org/task/14536
depends=( "libxfce4util>=4.6.1" "dbus-glib>=0.80" 'dbus>=1.2.16' 'gtk2>=2.18.5')
makedepends=('pkgconfig' 'perl-extutils-depends' 'perl-extutils-pkgconfig' 'glib-perl')
options=('!libtool' '!emptydirs')
replaces=('libxfce4mcs' 'xfce-mcs-manager' 'xfce-mcs-plugins')
conflicts=('libxfce4mcs' 'xfce-mcs-manager' 'xfce-mcs-plugins')
source=(http://mocha.xfce.org/archive/xfce-${pkgver}/src/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d4d3018767ea9eb4b57cc57d4038023b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var --disable-static --with-perl-options=INSTALLDIRS="vendor" 
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${startdir}/pkg install || return 1
  # remove unneeded dynloader bootstrap file
  rm ${pkgdir}/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.bs 
}
