# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=polari
pkgver=3.26.0+7+ga67955a
pkgrel=1
pkgdesc="An IRC Client for GNOME"
url="https://wiki.gnome.org/Apps/Polari"
arch=(i686 x86_64)
license=(GPL)
depends=(gjs gtk3 telepathy-glib telepathy-idle telepathy-mission-control telepathy-logger gspell)
makedepends=(gobject-introspection appdata-tools yelp-tools appstream-glib git meson)
groups=(gnome-extra)
_commit=a67955a2fac43a66375a4bd2fc1e819af0b43452  # master
source=("git+https://gitlab.gnome.org/GNOME/polari.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
