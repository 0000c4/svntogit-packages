# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-pylint
pkgver=2.5.1
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="https://pylint.org"
license=('GPL')
depends=('python-astroid' 'python-mccabe' 'python-isort' 'python-setuptools' 'python-toml')
makedepends=('python-pytest-runner' 'python-pytest-benchmark')
checkdepends=('mpdecimal')
optdepends=('tk: Pylint GUI'
            'graphviz: To have other output formats than dot or vcg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pylint/archive/pylint-$pkgver.tar.gz")
sha512sums=('71ea5b1b04c7fa72d1097743b8561548ccf4623112ad284e11dfcb83670041288e6b6dac186d4fbfd72697797dd695449c3f20d7a84da04648ca64f854f5db45')

build() {
  cd "$srcdir"/pylint-pylint-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pylint-pylint-$pkgver
  # https://github.com/PyCQA/pylint/issues/3198
  python setup.py pytest --addopts="-k 'not test_by_module_statement_value'"
}

package() {
  cd pylint-pylint-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 man/* "$pkgdir"/usr/share/man/man1
}
