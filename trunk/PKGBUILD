# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=inkscape
pkgver=0.48.1
pkgrel=2
pkgdesc='Vector graphics editor using the SVG file format'
url='http://inkscape.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
makedepends=('boost' 'pkg-config' 'intltool')
depends=('gc' 'gtkmm' 'poppler-glib' 'pyxml' 'libxslt' 'gsl' 'popt' 'python2'
         'gtkspell' 'imagemagick' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('python2-numpy: some extensions'
            'python-lxml: some extensions and filters')
options=('!libtool')
install=inkscape.install
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('a43467119925aee70d1d63d746f9846abb4dea91')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --with-python \
    --with-perl \
    --without-gnome-vfs \
    --with-xft \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking

  sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
  sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
  sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
  sed -i 's|\<python -c |python2 -c|g' share/extensions/uniconv*.py
  sed -i 's|"python"|"python2"|g' src/main.cpp

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
