# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=3.32.0
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(geocode-glib geoclue2 gjs libchamplain libgee folks gfbgraph)
makedepends=(gobject-introspection git meson yelp)
groups=(gnome)
_commit=c85fd42e2ae820e4bbbe15f0737a96e2ba49ba3a  # tags/v3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
