# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-weather
pkgver=3.20.2
pkgrel=2
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL)
depends=(gtk3 gjs libgweather geoclue2 gnome-desktop)
makedepends=(intltool gobject-introspection autoconf-archive appstream-glib git)
groups=(gnome-extra)
_commit=dc0de147404ca887471990c54f6cd94afe27dcd2  # tags/3.20.2^0
source=("git+https://git.gnome.org/browse/gnome-weather#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
