# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=udisks2
pkgver=1.98.0
pkgrel=1
pkgdesc="Disk Management Service, version 2"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL2')
depends=('glib2' 'udev' 'polkit' 'libatasmart' 'eject')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection')
optdepends=('parted: partition management'
            'gptfdisk: GUID partition table support')
options=(!libtool)
source=(http://udisks.freedesktop.org/releases/udisks-$pkgver.tar.bz2)
sha256sums=('c3e93ed0f678bb4999ea1209ede83717f086b0464a998889b47ee0017d3f8d78')

build() {
  cd "udisks-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "udisks-$pkgver"
  make DESTDIR="$pkgdir" install \
    bash_completiondir=/usr/share/bash-completion/completions
}
