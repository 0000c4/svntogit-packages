# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=mkinitcpio-busybox
pkgver=1.17.2
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
source=(http://busybox.net/downloads/busybox-${pkgver}.tar.bz2
        config)
sha256sums=('7a4c9d59098734c86c3bfef421719c3239ca213bba071e284e2352ff4a01cac6'
            'd2131815c28b987b0f7bf9065e7cf567c656438fbe0d44b685dc7dba387a1ca9')

build() {
  cd "${srcdir}/busybox-${pkgver}"

  unset CFLAGS CXXFLAGS
  if [ ${CARCH} = "x86_64" ]; then
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=x86-64 -mtune=generic -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  else
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=i686 -mtune=generic -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  fi
  make || return 1
}

package() {
  install -D -m755 "${srcdir}/busybox-${pkgver}"/busybox "${pkgdir}/lib/initcpio/busybox"
}
# vim:set ts=2 sw=2 et:
