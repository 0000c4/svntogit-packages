# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kturtle
pkgver=17.04.1
pkgrel=1
pkgdesc="Educational Programming Environment"
url="https://kde.org/applications/education/kturtle/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(knewstuff hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools)
conflicts=(kdeedu-kturtle)
replaces=(kdeedu-kturtle)
source=("https://download.kde.org/stable/applications/$pkgver/src/kturtle-$pkgver.tar.xz"{,.sig})
sha256sums=('ea689c98666a8b4a2591ce23cee1aa2e9ac6ff2698e7600ba780e41c05101045'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
