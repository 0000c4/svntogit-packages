# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=noto-fonts-emoji
_pkgver=2016-10-20
_commit=f09b63d
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto emoji fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(Apache)
depends=(fontconfig)
makedepends=(cairo optipng python2-fonttools nototools imagemagick zopfli python2-setuptools)
#source=("https://github.com/googlei18n/noto-emoji/archive/v$_pkgver-license-apache.tar.gz")
source=($pkgname-$pkgver.zip::"https://github.com/googlei18n/noto-emoji/archive/$_commit.zip")
md5sums=('52568bbd4752b74ba24d060f2b74e85e')

prepare() {
  cd noto-emoji-*
  find -name '*.py' | xargs sed -e 's|/usr/bin/env python|/usr/bin/env python2|' -i
  sed -e 's|python|python2|g' -e 's|ttx "$<"|ttx2 "$<"|g' -i Makefile
}

build() {
  cd noto-emoji-*
  make
}

package() {
  cd noto-emoji-*
  mkdir -p "$pkgdir"/usr/share/fonts/noto
  install -m644 NotoColorEmoji.ttf fonts/NotoEmoji-Regular.ttf "$pkgdir"/usr/share/fonts/noto
}
