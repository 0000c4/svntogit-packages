# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.3.91
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-desktop' 'libexif' 'lcms2' 'desktop-file-utils' 'exempi' 'libpeas' 'librsvg' 'gnome-icon-theme' 'dconf')
makedepends=('gtk-doc' 'intltool')
install=eog.install
groups=('gnome-extra')
options=('!emptydirs' '!libtool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('20900a333f0fdf63fc458218586d00a298f44c49ee8b8e6f0f31f38f7e9cfd96')

build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove it from Xfce menu that ships its own thingy
  echo "NotShowIn=XFCE" >> $pkgdir/usr/share/applications/eog.desktop
}
