# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=json-glib
pkgver=1.3.2
pkgrel=1
pkgdesc="JSON library built on GLib"
url="https://wiki.gnome.org/Projects/JsonGlib"
arch=(i686 x86_64)
license=(GPL)
depends=(glib2)
makedepends=(gobject-introspection git gtk-doc meson)
_commit=730cc7f4f69913eb1702fd2a52229d678d1e8c90  # tags/1.3.2^0
source=("git+https://gitlab.gnome.org/GNOME/json-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname \
    --libexecdir=/usr/lib \
    -Denable-gtk-doc=true \
    -Denable-man=true
  ninja
}

check() {
  cd build
  meson test || [[ $CARCH == i686 ]]
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
