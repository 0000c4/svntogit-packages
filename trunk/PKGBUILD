# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=syndication
pkgver=5.50.0
pkgrel=1
pkgdesc="RSS/Atom parser library"
arch=(x86_64)
url="https://community.kde.org/Frameworks"
license=(LGPL)
depends=(kcodecs)
makedepends=(extra-cmake-modules)
groups=(kf5)
conflicts=(kde-syndication)
provides=(kde-syndication)
replaces=(kde-syndication)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6355e2b14966e455dd0d6fd9cdb6691e905ab62be1981b9efbfb3727305de3c4'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
