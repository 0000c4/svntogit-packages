# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=dmraid
pkgver=1.0.0.rc14
pkgrel=4
pkgdesc="Device mapper RAID interface"
url="http://people.redhat.com/~heinzm/sw/dmraid/"
depends=(device-mapper)
arch=('i686' 'x86_64')
license=('GPL')
source=(http://people.redhat.com/~heinzm/sw/dmraid/src/old/$pkgname-$pkgver.tar.bz2
        dmraid_install
        dmraid_hook)
options=(force)
install=dmraid.install
md5sums=('a0e7d0bb5394e15c878ac86afd72c08e'
         'db9a72cbf07560059d407c874f646240'
         '2b2719d5bf463f411701241bba3d8120')

build() {
  cd $srcdir/$pkgname/$pkgver
  # create static bin
  ./configure --enable-static_link --prefix=/usr 
  make || return 1
  make DESTDIR=$pkgdir exec_prefix=$pkgdir/usr install
  mv $pkgdir/usr/sbin/dmraid $pkgdir/usr/sbin/dmraid.static

  # create dynamic linked bin
  ./configure --prefix=/usr 
  make || return 1
  make DESTDIR=$pkgdir exec_prefix=$pkgdir/usr install
  mkdir -p $pkgdir/var/lock/dmraid
  chmod 1777 $pkgdir/var/lock/
  install -D -m755 $srcdir/dmraid_install $pkgdir/lib/initcpio/install/dmraid
  install -D -m755 $srcdir/dmraid_hook $pkgdir/lib/initcpio/hooks/dmraid
  # fix .a file
  chmod 644 $pkgdir/usr/lib/libdmraid.a
  # fix manpage
  mkdir -p $pkgdir/usr/share/man
  mv $pkgdir/usr/man/man8 $pkgdir/usr/share/man/
  rm -rf $pkgdir/usr/man
}
