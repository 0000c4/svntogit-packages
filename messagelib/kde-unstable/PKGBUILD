# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=16.03.90
pkgrel=1
pkgdesc="KDE PIM messaging library"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport kmbox libgravatar akonadi-notes kidentitymanagement)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools)
conflicts=('libkdepim<16')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('c9625fca07c9c5a316ebe3bfbedf035a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
