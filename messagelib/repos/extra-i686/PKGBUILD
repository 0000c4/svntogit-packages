# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=16.08.1
pkgrel=3
pkgdesc="KDE PIM messaging library"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport libgravatar akonadi-notes kidentitymanagement qt5-webkit)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools kdesignerplugin)
conflicts=('libkdepim<16')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz" CVE-2016-7967-7968.patch)
md5sums=('90b85c4cc36d245a2a0e65cacaef1c27'
         '58786e8dde5be850ac568eed0e8b6958')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../CVE-2016-7967-7968.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
