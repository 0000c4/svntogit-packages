# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=python-logilab-astng
pkgname=('python2-logilab-astng')
pkgver=0.24.0
pkgrel=2
pkgdesc="Extend python's abstract syntax tree"
arch=('any')
url="http://www.logilab.org/project/logilab-astng"
license=('LGPL')
makedepends=('python2-distribute' 'python2-logilab-common')
source=(ftp://ftp.logilab.org/pub/astng/logilab-astng-${pkgver}.tar.gz)
sha1sums=('7883e6b87ae9defa33feb469924fcd9943214036')

build() {
  cd "${srcdir}/logilab-astng-${pkgver}"

  python2 setup.py build
}

package_python2-logilab-astng() {
  depends=('python2-logilab-common')
  cd "${srcdir}/logilab-astng-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  # Resolve conflict with python-logilab-common
  rm -f "${pkgdir}"/usr/lib/python2.7/site-packages/logilab/__init__.py

  find "${pkgdir}"/usr/lib/python2.7/site-packages/logilab/astng/ -type f -exec chmod ugo+r {} \;
}
