# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=python-logilab-astng
pkgname=('python2-logilab-astng')
pkgver=0.24.1
pkgrel=1
pkgdesc="Extend python's abstract syntax tree"
arch=('any')
url="http://www.logilab.org/project/logilab-astng"
license=('LGPL')
depends=('python2-logilab-common')
makedepends=('python2-distribute')
conflicts=('python-logilab-astng<=0.24.0-1')
replaces=('python-logilab-astng<=0.24.0-1')
source=(ftp://ftp.logilab.org/pub/astng/logilab-astng-${pkgver}.tar.gz)
sha1sums=('067922aed15f3d3025c04e6532c65c420ed86497')

build() {
  cd "${srcdir}/logilab-astng-${pkgver}"

  python2 setup.py build
}

package_python2-logilab-astng() {
  cd "${srcdir}/logilab-astng-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  # Resolve conflict with python-logilab-common
  rm -f "${pkgdir}"/usr/lib/python2.7/site-packages/logilab/__init__.py

  find "${pkgdir}"/usr/lib/python2.7/site-packages/logilab/astng/ -type f -exec chmod ugo+r {} \;
}
