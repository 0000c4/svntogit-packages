# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>

pkgbase=python2-httplib2
pkgname=(python-httplib2 python2-httplib2)
_pkgbasename=httplib2
pkgver=0.7.1
pkgrel=2
pkgdesc="A comprehensive HTTP client library, supporting many features"
url="http://code.google.com/p/httplib2/"
license=('MIT')
arch=('any')
makedepends=('python' 'python2')
provides=('httplib2')
replaces=('httplib2')
source=(http://httplib2.googlecode.com/files/${_pkgbasename}-${pkgver}.tar.gz
        COPYING godaddy-certificate.patch)
md5sums=('03e798858cebbdde189b02ed97e85a45'
         'd65321d20730e3d9291dd63b43fdcda9'
         '21417aa815bbc67c72e84eb15c08d5b7')

build() {
  cd "${srcdir}"
  (cd "${_pkgbasename}-${pkgver}" ; patch -p1 -i "${srcdir}/godaddy-certificate.patch")
  cp -Rpv "${_pkgbasename}-${pkgver}" "${pkgname[0]}-$pkgver"
  cp -Rpv "${_pkgbasename}-${pkgver}" "${pkgname[1]}-$pkgver"
}

package_python-httplib2() {
  depends=('python')

  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}"
  install -D -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-httplib2() {
  depends=('python2')

  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
  install -D -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
