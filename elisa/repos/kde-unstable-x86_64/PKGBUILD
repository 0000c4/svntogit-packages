# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=20.03.80
pkgrel=1
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
groups=(kde-applications kdemultimedia)
depends=(baloo kirigami2 kcmutils vlc) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b0cbafdfaaca9b5f87af40fa386181cad0fd165ead4270c790e865d10e68a640'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
