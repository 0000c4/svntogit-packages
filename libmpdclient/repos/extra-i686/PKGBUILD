# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=libmpdclient
pkgver=2.8
pkgrel=1
pkgdesc='Asynchronous API library for interfacing MPD in the C, C++ & Objective C languages'
url='http://www.musicpd.org/libs/libmpdclient/'
license=('custom:BSD')
arch=('i686' 'x86_64')
source=("http://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('7e23c760296fe47e9e4108a0eba6fef08cfe4db8')

options=('!libtool')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --disable-static --disable-documentation
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}"
	install -d usr/share/licenses/libmpdclient
	ln -s ../../doc/libmpdclient/COPYING usr/share/licenses/libmpdclient/COPYING
}
