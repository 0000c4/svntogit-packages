# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Gan Lu <rhythm.gan@gmail.com>

pkgname=scim
pkgver=1.4.11
pkgrel=1
pkgdesc='Input method user interface and development platform'
url='http://www.scim-im.org/projects/scim'
license=('GPL')
arch=('i686' 'x86_64')
options=('!libtool')
makedepends=('intltool')
depends=('gtk2' 'libtool')
backup=('etc/scim/config' 'etc/scim/global')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'wtf.patch')
sha1sums=('dd93094f62e5707db88d4f0e7ebd3392ae75b942'
          '14d2be6c8f9f4af6494c3e806d3cadef5803597a')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../wtf.patch # revert broken commit 303

	./bootstrap
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-static \
		--with-gnu-ld \
		--with-x \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}
