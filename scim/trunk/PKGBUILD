# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Gan Lu <rhythm.gan@gmail.com>

pkgname=scim
pkgver=1.4.10
pkgrel=1
pkgdesc='Input method user interface and development platform'
url='http://www.scim-im.org/projects/scim'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gtk2' 'gcc-libs')
makedepends=('intltool')
backup=('etc/scim/config' 'etc/scim/global')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/${pkgname}_$pkgver.tar.gz")
sha1sums=('bf33a6ceb199453fed909a61f54e971653fe5abf')

install=install

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./bootstrap
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-static \
		--with-gnu-ld \
		--with-x \

	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}
