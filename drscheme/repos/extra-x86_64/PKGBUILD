# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=drscheme
pkgver=4.0.2
pkgrel=1
pkgdesc="PLT Scheme Environment"
arch=('i686' 'x86_64')
url="http://www.drscheme.org/"
license=('LGPL')
depends=('gcc-libs' 'cairo' 'desktop-file-utils' 'libjpeg' 'mesa' 'openssl>=0.9.8h' 'libxaw' 'libxmu' 'libxft')
options=('!makeflags' 'force')
install=${pkgname}.install
source=(http://download.plt-scheme.org/bundles/${pkgver}/plt/plt-${pkgver}-src-unix.tgz drscheme.desktop nonstandard-install.patch paths-fix.patch)
md5sums=('a5abe7c45bb2af0bf537e49a5f2e2a68' '41ba9c74aa541308aa6f67b262387c6c'\
         'd2cc43a6e8496e2940af80b55ec04d8f' '9ae64fb6eb8513f5954abb0fa7ac817a')
sha1sums=('4e1c8f2b689d6711d60acaa59750ee40b08900b3'
          '6b8247da9ce5f1e83f35bc14f90e8c34aae0e0d5'
          '4e394162f2dc4f59e752b97a05b4673871d25f81'
          '4d62f1ea0a57382fb2fae1336421698783a49ef5')

build() {
  cd ${srcdir}/plt-${pkgver}
  patch -p1 < ../nonstandard-install.patch || return 1
  patch -p1 < ../paths-fix.patch || return 1
  cd src
  ./configure --prefix=/usr --enable-noopt  || return 1
  make || return 1
  make  DESTDIR=${pkgdir} install  || return 1
  install -D -m644 ../collects/icons/plt.xpm ${pkgdir}/usr/share/pixmaps/drscheme.xpm || return 1
  install -D -m644 ${srcdir}/drscheme.desktop ${pkgdir}/usr/share/applications/drscheme.desktop || return 1
}
