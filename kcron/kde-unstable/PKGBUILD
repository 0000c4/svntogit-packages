# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcron
pkgver=16.07.80
pkgrel=1
pkgdesc='Configure and schedule tasks'
url='http://userbase.kde.org/KCron'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kio' 'cron')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdeadmin-kcron')
replaces=('kdeadmin-kcron')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kcron-${pkgver}.tar.xz")
sha1sums=('34d1a051d02a5be8cb2a4862f525f6e58572f6ef')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
