# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdenetwork
pkgver=4.2.1
pkgrel=1
pkgdesc="KDE Network related programs"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdebase-workspace' 'libvncserver' 'libidn' 'libotr' 'qca' 'rdesktop' 'libmsn' 'ortp' 'speex' 'ppp' 'libxdamage')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost')
options=('docs')
install='kdenetwork.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('17ba9800af0bfcf20a49337624f9946a')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_Decibel=OFF \
		-DWITH_LibMeanwhile=OFF
	make
	make DESTDIR=$pkgdir install
}
