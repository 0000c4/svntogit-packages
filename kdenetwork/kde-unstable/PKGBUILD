# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdenetwork
pkgname=('kdenetwork-filesharing'
         'kdenetwork-kdnssd'
         'kdenetwork-kget'
         'kdenetwork-kopete'
         'kdenetwork-kppp'
         'kdenetwork-krdc'
         'kdenetwork-krfb')
pkgver=4.2.91svn979380
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost'
             'kdelibs-experimental' 'kdebase-workspace' 'kdebase-lib' 'libvncserver' 'libotr' 'qca' 'qca-ossl'
             'libmsn' 'ppp' 'libv4l' 'libidn' 'rdesktop' 'qimageblitz' 'libxss')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('1da32b7de0d3832376b9da5c897e4913')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_Decibel=OFF \
		-DWITH_LibMeanwhile=OFF
	make
}

package_kdenetwork-filesharing() {
	pkgdesc='network file sharing'
	depends=('kdebase-runtime' 'kdelibs' 'smbclient')
	install='kdenetwork.install'
	cd $srcdir/build/filesharing
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kdnssd() {
	pkgdesc='Zeroconf'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/kdnssd
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kget() {
	pkgdesc='an advanced download manager'
	depends=('kdebase-workspace' 'kdebase-lib')
	install='kdenetwork.install'
	cd $srcdir/build/kget
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kget
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kopete() {
	pkgdesc='Instant Messaging client'
	depends=('kdebase-runtime' 'kdepimlibs' 'qca' 'libotr' 'libmsn' 'libidn' 'qimageblitz' 'libxss' 'libv4l')
	install='kdenetwork.install'
	cd $srcdir/build/kopete
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kopete
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kppp() {
	pkgdesc='dialer and front end for pppd'
	depends=('kdebase-runtime' 'kdelibs' 'ppp')
	install='kdenetwork.install'
	cd $srcdir/build/kppp
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kppp
	make DESTDIR=$pkgdir install
}

package_kdenetwork-krdc() {
	pkgdesc='a client for Desktop Sharing'
	depends=('kdebase-runtime' 'kdelibs' 'libvncserver' 'rdesktop')
	cd $srcdir/build/krdc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/krdc
	make DESTDIR=$pkgdir install
}

package_kdenetwork-krfb() {
	pkgdesc='Desktop Sharing server, allow others to access your desktop via VNC'
	depends=('kdebase-runtime' 'kdelibs' 'libvncserver')
	cd $srcdir/build/krfb
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/krfb
	make DESTDIR=$pkgdir install
}
