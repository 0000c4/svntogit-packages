# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kcalc
pkgver=15.03.90
pkgrel=1
pkgdesc='Scientific Calculator'
url='http://kde.org/applications/utilities/kcalc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('knotifications' 'kxmlgui')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdeutils-kcalc')
replaces=('kdeutils-kcalc')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kcalc-${pkgver}.tar.xz")
sha1sums=('bb66ed36fcbf6f3b3678c5b6d34d22966be6a01e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcalc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
