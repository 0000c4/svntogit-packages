# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libgsf-gnome
pkgver=1.14.14
pkgrel=1
pkgdesc="Adds gnome support to libgsf"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://www.gnome.org/"
depends=('gnome-vfs>=2.24.1' 'libbonobo>=2.24.1' "libgsf>=${pkgver}")
makedepends=('intltool' 'pkgconfig')
options=('!libtool' '!emptydirs')
install=libgsf-gnome.install
source=(http://ftp.gnome.org/pub/GNOME/sources/libgsf/1.14/libgsf-${pkgver}.tar.bz2)
md5sums=('69e9c57d48539782f72c55824a3c6859')

build() {
  cd "${srcdir}/libgsf-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static  || return 1

  sed -e 's|$(top_builddir)/gsf/libgsf-1.la|-lgsf-1|g' \
      -i gsf-gnome/Makefile thumbnailer/Makefile || return 1
  make -C gsf-gnome || return 1
  make -C thumbnailer || return 1
  make -C gsf-gnome DESTDIR="${pkgdir}" install || return 1
  make -C thumbnailer DESTDIR="${pkgdir}" install || return 1
  make -C doc DESTDIR="${pkgdir}" install-manpageDATA || return 1
  rm -f ${pkgdir}/usr/share/man/man1/gsf.1
  rm -f ${pkgdir}/usr/share/man/man1/gsf-vba-dump.1
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 libgsf-gnome-1.pc "${pkgdir}/usr/lib/pkgconfig/" || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
