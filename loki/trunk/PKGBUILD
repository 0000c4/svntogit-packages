# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=loki
pkgver=1.2.0
pkgrel=1
pkgdesc='like Prometheus, but for logs'
url='https://github.com/grafana/loki'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
source=($pkgname-$pkgver.tar.gz::https://github.com/grafana/loki/archive/v1.2.0.tar.gz)
sha512sums=('2a522b0c5b7bddbdf810c229dbd21a2fcac3717d1fc940bfa98f74f5e86000cb36ec17ae2ef900a7b302997860a9d8b904f1e23d18964365579d6a4a39665918')

build() {
  cd loki-$pkgver

  LDFLAGS="-extldflags $LDFLAGS"

  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "$LDFLAGS" \
    ./cmd/loki

  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "$LDFLAGS" \
    ./cmd/promtail
}

check() {
  cd loki-$pkgver

  go test ./...
}

package() {
  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin loki promtail

}
