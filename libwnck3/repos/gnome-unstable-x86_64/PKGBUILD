# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=libwnck3
_pkgbasename=libwnck
pkgver=2.91.6
pkgrel=1
pkgdesc="Window Navigator Construction Kit (GTK+3)"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'startup-notification' 'libxres')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.91/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('da397528a9fa80bfa236d5e6b871896fd72e48b6eab0a056ce05231e94766217')

build() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
