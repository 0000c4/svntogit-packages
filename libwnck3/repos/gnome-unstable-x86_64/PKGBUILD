# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=libwnck3
_pkgbasename=libwnck
pkgver=2.91.92
pkgrel=1
pkgdesc="Window Navigator Construction Kit (GTK+3)"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'startup-notification' 'libxres')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.91/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('64b4c114620df2e78935775226968133820f35fb43a0804dde1391c478548a28')

build() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
