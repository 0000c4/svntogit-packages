# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
pkgver=3.32.0
pkgrel=3
pkgdesc="Library to manage X windows and workspaces (via pagers, tasklists, etc.)"
url="https://gitlab.gnome.org/GNOME/libwnck"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 startup-notification libxres)
makedepends=(gobject-introspection gtk-doc git meson)
_commit=d3a714db6af5c1a146effb8d8f827b8973bbb91c  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/libwnck.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
    cd libwnck
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd libwnck

    # Fix startup-notification use
    git cherry-pick -n 0d9ff7db63af568feef8e8c566e249058ccfcb4e

    # Fix gettext
    git cherry-pick -n a5c0a3353bf015e4ba88f7ba4eef41a4610aa3d7
}

build() {
    arch-meson libwnck build -D gtk_doc=true
    ninja -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=4 et:
