# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
_pkgbasename=libwnck
pkgver=3.14.1
pkgrel=1
pkgdesc="Window Navigator Construction Kit (GTK+3)"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'startup-notification' 'libxres')
makedepends=('intltool' 'gobject-introspection' 'python2')
url="http://www.gnome.org/"
source=(https://download.gnome.org/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('bb643c9c423c8aa79c59973ce27ce91d3b180d1e9907902278fb79391f52befa')

build() {
    cd "$_pkgbasename-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "$_pkgbasename-$pkgver"
    make DESTDIR="$pkgdir" install
}
