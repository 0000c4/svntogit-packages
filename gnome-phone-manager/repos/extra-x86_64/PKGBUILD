# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-phone-manager
pkgver=0.68
pkgrel=2
pkgdesc="Control your mobile phone from your GNOME desktop."
arch=('i686' 'x86_64')
url="http://live.gnome.org/PhoneManager"
license=('GPL')
depends=('evolution-data-server' 'gnokii' 'gnome-bluetooth' 'gnome-icon-theme' 'libcanberra')
makedepends=('intltool' 'gnome-common')
options=('!emptydirs' '!libtool')
install=gnome-phone-manager.install
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-phone-manager/${pkgver}/${pkgname}-${pkgver}.tar.xz
        phonemgr-gnome-bluetooth-3.3.patch)
sha256sums=('3777b62ee36492274532ece965f4688600457014f17a6f21dd01fb63bbb77f8f'
            'a69c1d2d1d25eca4f32215d8a8ca3c8e4a048af76d7a291b86b0aa7f0f65c452')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../phonemgr-gnome-bluetooth-3.3.patch
  autoreconf -fi
  LIBS+="-lgthread-2.0" ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-phone-manager "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
