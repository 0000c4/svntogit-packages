# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libtasn1
pkgver=4.6
pkgrel=2
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('glibc' 'texinfo')
install=libtasn1.install
source=(http://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig}
        multi-byte-fix.diff)
md5sums=('454a7f80362f38bd2335b6e9d95f57a6'
         'SKIP'
         'd785762c19c4b2685d9034ed8ca1b3bc')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') #Nikos Mavrogiannopoulos <nmav@gnutls.org>

prepare() {
  cd ${pkgname}-${pkgver}
  # http://git.savannah.gnu.org/gitweb/?p=libtasn1.git;a=commit;h=e4478fb4a0186f9d59969bf93ed8fa6074729b43
  patch -Np1 -i ${srcdir}/multi-byte-fix.diff
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
      --with-packager=Archlinux \
      --with-packager-bug-reports="http://bugs.archlinux.org/" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
