# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ksmtp
pkgver=17.12.0
pkgrel=2
pkgdesc="Job-based library to send email through an SMTP server"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kmime kio)
makedepends=(extra-cmake-modules python)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-388068.patch::"https://cgit.kde.org/ksmtp.git/patch/?id=ec2afd27"
        kdebug-387926.patch::"https://cgit.kde.org/ksmtp.git/patch/?id=5199ed07")
sha256sums=('d84b2e4ddaa5506d977b3bc5608ef89d5ba772f51811febbff78a1e0661c314e'
            'SKIP'
            'c445ed8e7284be8de3117d3174be99729d1234ed624313f24ced66e99594b287'
            'a9fe4d38faf0447a1ee96c52bdec7d14e1017657baab94d8650e926c28685e41')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-388068.patch # Fix duplicate authentication
  patch -p1 -i ../kdebug-387926.patch # Send the correct hostname with the HELO/EHLO command
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
