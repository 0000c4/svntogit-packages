# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdebugsettings
pkgver=16.03.80
pkgrel=1
pkgdesc='An application to enable/disable qCDebug'
arch=(i686 x86_64)
url='http://projects.kde.org/kdebugsettings'
license=(GPL)
depends=(kdbusaddons ki18n kitemviews kcompletion kcoreaddons)
makedepends=(extra-cmake-modules python)
groups=(kde-applications kdeutils)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('0a1628746a3b1d604e4a705bef44fe5df5539a5ce86520438b77b74b45523bbc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

