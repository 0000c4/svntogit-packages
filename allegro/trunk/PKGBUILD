# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=4.4.2
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming"
arch=('i686' 'x86_64')
url="http://alleg.sourceforge.net/"
license=('custom')
depends=('jack' 'libxpm' 'libxxf86vm' 'libxxf86dga' 'libxcursor' 'libpng')
makedepends=('cmake' 'mesa')
source=(http://downloads.sourceforge.net/alleg/${pkgname}-${pkgver}.tar.gz
        LICENSE)
md5sums=('4db71b0460fc99926ae91d223199c2e6'
         'cd97e2992e8e66b9e6a449d832dc9c7a')

build() {
  cd "${srcdir}"
  mkdir build && cd build

  cmake "../${pkgname}-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWANT_DOCS=OFF

  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
