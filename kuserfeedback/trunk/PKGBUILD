# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kuserfeedback
pkgver=0.9.90
pkgrel=1
pkgdesc="Framework for collecting user feedback for applications via telemetry and surveys"
arch=(x86_64)
url="https://kde.org/products/frameworks/"
license=(GPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools clang qt5-charts qt5-svg qt5-declarative)
optdepends=('qt5-declarative: QML bindings' 'qt5-charts: User Feedback console' 'qt5-svg: User Feedback console')
source=("https://download.kde.org/unstable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('90440cd54f11df3e9c896c543484e36cc5ba1f8ce2797fd6326d0bfcbed7c49e'
            'SKIP')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
