# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=cryptsetup
pkgver=1.4.2
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using dm-crypt"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper>=2.02.85-2' 'libgcrypt' 'popt')
conflicts=('mkinitcpio<0.7')
options=('!libtool' '!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2.asc
        encrypt_hook
        encrypt_install)
sha256sums=('1fe80d7b19d24b3f65d2e446decfed859e2c4d17fdf7c19289d82dc7cd60dfe7'
            '4e6dbece8d1baad861479aca70d0cf30887420da9b5eab45d65d064c656893ed'
            'e4c00e2da274bf4cab3f72a0de779790a11a946d36b83144e74d3791e230b262'
            'cba1dc38ff6cc4d3740d0badfb2b151bb03d19e8e9fa497569ac2fb6f4196e0e')

build() {
  cd "${srcdir}"/$pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
}
