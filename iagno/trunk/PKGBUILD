# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=iagno
pkgver=3.34.0
pkgrel=1
pkgdesc="Dominate the board in a classic version of Reversi"
url="https://wiki.gnome.org/Apps/Iagno"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gsound librsvg)
makedepends=(meson gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=eef2f6e57dc014dc19c20060c744ac4a2d263335  # tags/3.34.0^0
source=("git+https://gitlab.gnome.org/GNOME/iagno.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
