# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Original Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# New Version - Contributor: Bob Finch <w9ya@arrl.net>
# with help from J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar> aka Angelus

pkgname=wxgtk
pkgver=2.8.8
pkgrel=1
pkgdesc="wxGTK - GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2>=2.10.9' 'mesa')
makedepends=('libgnomeprintui')
source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
md5sums=('6c73103878bd2a44b42a7b5fbb4c5605')
sha1sums=('ba33ca7efad2847c538d3df524d31b8b4011ad8a')

build() {
  cd ${srcdir}/wxGTK-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
	  --enable-graphics_ctx --with-gnomeprint --disable-optimize || return 1
  make || return 1
  make -C locale allmo || return 1
  make DESTDIR=${pkgdir} install || return 1
  cd contrib/src
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 ../../docs/licence.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
