# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

pkgname=xf86-video-nouveau
pkgver=1.0.13
pkgrel=3
pkgdesc="Open Source 2D acceleration driver for nVidia cards"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/"
license=('GPL')
depends=('libsystemd' 'mesa')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23' 'systemd')
conflicts=('xorg-server<1.19' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
groups=('xorg-drivers')
source=(https://xorg.freedesktop.org/archive/individual/driver/$pkgname-$pkgver.tar.bz2{,.sig}
        DDX-Consider-CRTCs-disabled-when-DPMS-is-off.patch)
sha256sums=('6d9242ba139c3df7afefffb455573b52f4427920b978161c00483c64a6da47cb'
            'SKIP'
            '47bb07ec5f2a0abe113899907fc0eb634a592c78e3562ab7770256f30a1a05fd')
validpgpkeys=('B97BD6A80CAC4981091AE547FE558C72A67013C3') # Maarten Lankhorst <maarten@debian.org>

prepare() {
  cd $pkgname-$pkgver

  # Fix dpms with kernel 4.10.x https://bugs.freedesktop.org/show_bug.cgi?id=99922
  patch -Np1 -i ../DDX-Consider-CRTCs-disabled-when-DPMS-is-off.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
