# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=mkvtoolnix
pkgver=4.9.1
pkgrel=2
pkgdesc="Set of tools to create, edit and inspect Matroska files - CLI version"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('file' 'boost-libs' 'libmatroska' 'lzo2' 'expat' 'flac' 'libvorbis')
makedepends=('boost' 'ruby')
source=("http://www.bunkus.org/videotools/${pkgname}/sources/${pkgname}-${pkgver}.tar.bz2")
md5sums=('5981f266e6d25da4a49f7464e4a97cdf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Disable automagic curl dep used for online update checking
  sed -i -e '/curl/d' configure.in
  export CURL_CFLAGS="" CURL_LIBS=""

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  autoreconf

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib \
              --disable-gui
  rake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  rake DESTDIR="${pkgdir}" install
}
