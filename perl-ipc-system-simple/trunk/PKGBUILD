# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-ipc-system-simple
pkgver=1.26
pkgrel=1
pkgdesc="Run commands simply, with detailed diagnostics"
arch=('any')
url="https://search.cpan.org/dist/IPC-System-Simple"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/J/JK/JKEENAN/IPC-System-Simple-$pkgver.tar.gz")
sha512sums=('cf9a0ba99ecb080010f9da995d5985501dc61fb4254c4ba0c0033000e5f2678f728feb8cf3f96324439fcc77dbd76da6a509e0b75203ef37a8ca11bf1a2aa2bb')

build() {
  cd IPC-System-Simple-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IPC-System-Simple-$pkgver
  make test
}

package() {
  cd IPC-System-Simple-$pkgver
  make DESTDIR="$pkgdir" install
}
