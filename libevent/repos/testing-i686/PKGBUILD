# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=libevent
pkgver=2.0.17
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
options=('!libtool')
source=(https://github.com/downloads/libevent/libevent/libevent-$pkgver-stable.tar.gz{,.asc})

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
}
md5sums=('dad64aaaaff16b5fbec25160c06fee9a'
         'ef4a32a96df3941fed58fbe39b4eab9d')
