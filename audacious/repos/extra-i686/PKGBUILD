# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=audacious
pkgver=3.1.1
pkgrel=1
pkgdesc='Lightweight, GTK-based advanced audio player focused on audio quality'
url='http://audacious-media-player.org/'
license=('GPL3')
arch=('i686' 'x86_64')
depends=('gtk3' 'dbus-glib' 'libguess' 'libsm' 'audacious-plugins'
         'hicolor-icon-theme' 'gtk-update-icon-cache' 'desktop-file-utils')
optdepends=('unzip: zipped skins support')
source=("http://distfiles.atheme.org/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('fa5629d68ac1b12d563e8c0b4be083dea7c7924c')

provides=('audacious-player')
replaces=('audacious-player')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
