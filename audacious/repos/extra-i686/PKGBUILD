# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=audacious
pkgver=3.0.2
pkgrel=1
pkgdesc='Lightweight, GTK-based advanced audio player focused on audio quality'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL3')
depends=('gtk3' 'libmcs' 'dbus-glib' 'libguess' 'libsm' 'audacious-plugins')
optdepends=('unzip: zipped skins support'
            'hicolor-icon-theme: for icons'
            'gtk-update-icon-cache: for icons'
            'desktop-file-utils: desktop integration')
source=("http://distfiles.atheme.org/${pkgname}-${pkgver}.tar.gz")
sha1sums=('68799076acafb151668285e8d9498048633d7b60')

provides=('audacious-player')
replaces=('audacious-player')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	[[ ${CARCH} == x86_64 ]] && _extraopts='' || _extraopts='--disable-sse2'
	./configure --prefix=/usr ${_extraopts}
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
