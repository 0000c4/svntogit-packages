# $Id$
# Maintainer: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious
pkgver=1.5.1
pkgrel=1
pkgdesc="Media player based on BMP"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libsamplerate' 'libxml2' 'desktop-file-utils')
optdepends=('unzip: Zipped skins support')
provides=('audacious-player')
replaces=('audacious-player')
install=audacious.install
source=(http://distfiles.atheme.org/audacious-$pkgver.tbz2)
md5sums=('ea7a8ee2e8f1a301b40ccf40788ab0eb')

build() {
  cd "$srcdir/audacious-$pkgver"

  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr --enable-samplerate --disable-sse2 || return 1
  elif [[ $CARCH == "x86_64" ]]; then
    ./configure --prefix=/usr --enable-samplerate || return 1
  fi

  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}
