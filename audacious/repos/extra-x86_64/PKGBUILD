# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=audacious
pkgver=3.0
pkgrel=3
pkgdesc='Lightweight, GTK-based advanced audio player focused on audio quality'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL3')
depends=('gtk3' 'libmcs' 'dbus-glib' 'libguess' 'audacious-plugins' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('unzip: zipped skins support')
source=("http://distfiles.atheme.org/${pkgname}-${pkgver}.tar.gz")
sha1sums=('d32de719f5b09440d0f6c802a5140a32b3827475')

provides=('audacious-player')
replaces=('audacious-player')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	[[ ${CARCH} == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
	./configure --prefix=/usr ${extraopts}
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
