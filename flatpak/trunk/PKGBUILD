# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=flatpak
pkgver=0.10.4
pkgrel=1
pkgdesc="Linux application sandboxing and distribution framework (formerly xdg-app)"
url="http://flatpak.org"
arch=(x86_64)
license=(LGPL2.1)
depends=(dbus systemd glib2 libsoup polkit libxau ostree fuse json-glib libseccomp libarchive
         python bubblewrap appstream-glib)
makedepends=(intltool gobject-introspection gtk-doc git docbook-xsl xmlto)
checkdepends=(valgrind)
install=flatpak.install
_commit=c576d284dc123be15c7003fee6a8184eddeaf462  # tags/0.10.4^0
source=("git+https://github.com/flatpak/flatpak#commit=$_commit"
        "git+https://github.com/projectatomic/bubblewrap"
        "git+https://git.gnome.org/browse/libglnx"
        flatpak-bindir.sh flathub.flatpakrepo)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'cf20e4831bceaaee74e9298b4ad7c4d2d51085d43f62d1c6a2d2977d2f421e00'
            '3371dd250e61d9e1633630073fefda153cd4426f72f4afa0c3373ae2e8fea03a')

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.bubblewrap.url "$srcdir/bubblewrap"
  git config --local submodule.libglnx.url "$srcdir/libglnx"
  git submodule update

  # https://github.com/flatpak/flatpak/issues/267
  sed -i '/locale\/C\./d' tests/make-test-runtime.sh

  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --disable-document-portal \
    --enable-gtk-doc \
    --enable-p2p \
    --with-system-bubblewrap \
    --with-priv-mode=setuid \
    --with-dbus-config-dir=/usr/share/dbus-1/system.d

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  # Xattr tests fail on tmpfs
  make -k check || :
}

package() {
  depends+=(xdg-desktop-portal)

  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/etc/profile.d" -m644 ../flatpak-bindir.sh
  install -Dt "$pkgdir/usr/share/flatpak" -m644 ../flathub.flatpakrepo

  # Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
