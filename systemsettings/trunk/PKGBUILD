# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.11.2
pkgrel=2
pkgdesc='KDE system settings'
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kirigami2 kcmutils kactivities-stats)
makedepends=(extra-cmake-modules kdoctools python khtml)
optdepends=('khtml: for the tree view')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        systemsettings-dbus-notification.patch::"https://cgit.kde.org/systemsettings.git/patch/?id=14906076")
sha256sums=('31332049291ce54a93fcfa2d2281313005e3993b49daadbd7411221143cbec85'
            'SKIP'
            '92f344bcfc7e6fc141ea33094f5f4a56e59e21c7e53298f54c0f58546aebfff8')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../systemsettings-dbus-notification.patch # Fix KDEInit error when launching from panel
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
