# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gpgmepp
pkgver=16.08.0
pkgrel=1
pkgdesc="C++ bindings/wrapper for gpgme"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(gpgme qt5-base)
makedepends=(extra-cmake-modules python boost)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('857bae4c2d9efddb3f51877f6fe39f54')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
