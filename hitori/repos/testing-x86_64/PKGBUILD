# $Id$
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=hitori
pkgver=3.22.0+1+gbb07c7c
pkgrel=1
pkgdesc="GTK+ application to generate and let you play games of Hitori"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Hitori"
license=(GPL)
depends=(gtk3)
groups=(gnome-extra)
makedepends=(intltool itstool yelp-tools autoconf-archive appstream-glib git)
_commit=bb07c7cada0f93186ca4d65a93ca3a5116f6d57b  # master
source=("git://git.gnome.org/hitori#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}
