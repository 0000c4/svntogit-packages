# $Id$
# Maintainer:
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=perl-bit-vector
_cpanname=Bit-Vector
pkgver=7.2
pkgrel=3
pkgdesc='Efficient bit vector, set of integers and "big int" math library'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('perl-carp-clan' 'perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/A/AN/ANDK/${_cpanname}-${pkgver}.tar.gz
        Bit-Vector-destroy.patch)
md5sums=('bc6ae34738b8f889290bf50099ff8aed'
         'c86a55b84cb05dd04004ee8f56ee2ee3')

prepare() {
  cd "${srcdir}/${_cpanname}-$pkgver"

  # https://rt.cpan.org/Public/Bug/Display.html?id=84250
  patch -Np1 -i "${srcdir}/Bit-Vector-destroy.patch"
}

build() {
  cd "${srcdir}/${_cpanname}-$pkgver"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_cpanname}-$pkgver"
  make test
}

package() {
  cd "${srcdir}/${_cpanname}-$pkgver"
  make DESTDIR="${pkgdir}" install
}
