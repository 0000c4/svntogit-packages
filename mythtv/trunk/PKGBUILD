# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: <kleptophobiac@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=mythtv
pkgver=0.22
pkgrel=7
pkgdesc="A Homebrew PVR project"
arch=('i686' 'x86_64')
url="http://www.mythtv.org/"
license=('GPL')
groups=('pvr')
depends=('mysql-clients' 'qt' 'lame' 'lirc-utils' 'ffmpeg' 'libxvmc' 'fribidi' 
         'perl-soap-lite' 'perl-date-manip' 'perl-xml-sax' 'perl-math-round' 
         'perl-net-upnp' 'perl-dbd-mysql' 'perl-time-hires' 'libavc1394' 'wget' 
         'libiec61883' 'mysql-python' 'mesa' 'libxinerama')
optdepends=('xmltv: to download tv listings'
            'mythplugins: plugins for mythtv')
backup=('etc/conf.d/mythbackend')
install=mythtv.install
source=(ftp://ftp.osuosl.org/pub/mythtv/${pkgname}-${pkgver}.tar.bz2 
        mythbackend 
        archlinux.conf.d.mythbackend)
md5sums=('e8f8b5b6a51cd7be700e215b2a1bf2c0' 
         'feadcc9ad064d93d6dceab1efc0bd9ed' 
         'bb8e4033d82428d827570fae9ba15e6a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ARCH=${CARCH/_/-}
  ./configure --prefix=/usr --cpu=${ARCH} \
              --enable-mmx \
              --enable-audio-oss \
              --enable-audio-alsa \
              --disable-audio-jack \
              --disable-audio-pulse \
              --disable-audio-arts \
              --enable-vdpau \
              --enable-dvb \
              --enable-lirc \
              --enable-joystick-menu \
              --enable-v4l \
              --enable-ivtv \
              --enable-dvb \
              --dvb-path=/usr/include \
              --enable-xvmc \
              --enable-ffmpeg \
              --enable-xvmc-pro \
              --enable-xvmc-vld \
              --enable-opengl-vsync \
              --enable-firewire \
              --enable-fribidi 

  qmake mythtv.pro
  make || return 1
  # basic install
  make INSTALL_ROOT="${pkgdir}" install || return 1
  install -D -m0755 ${srcdir}/mythbackend "${pkgdir}/etc/rc.d/mythbackend" || return 1
  # config file
  install -D -m644 ${srcdir}/archlinux.conf.d.mythbackend "${pkgdir}/etc/conf.d/mythbackend" || return 1
}
