# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=1.23.1
pkgrel=1
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('x86_64')
url="https://github.com/libuv/libuv"
license=('custom')
depends=('glibc' 'libnsl')
makedepends=('python-sphinx')
source=("https://github.com/libuv/libuv/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5c3170dea49d7954f5780bc94d5c021a8e1bf91e2d17188335825f1dc1098a8826d5eb9dac4be318ec68275888d0dfa9579a841d43de0c3520c89ab0dc70e5ab')

build() {
	cd $pkgname-$pkgver
	./autogen.sh
	./configure --prefix=/usr
	make
	make man -C docs
}

check() {
	cd $pkgname-$pkgver
	make check
}

package() {
	cd $pkgname-$pkgver

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	install -Dm644 AUTHORS \
		"$pkgdir"/usr/share/doc/$pkgname/AUTHORS
	install -Dm644 README.md \
		"$pkgdir"/usr/share/doc/$pkgname/README.md
	install -Dm644 ChangeLog \
		"$pkgdir"/usr/share/doc/$pkgname/ChangeLog
	install -Dm644 docs/build/man/libuv.1 \
		"$pkgdir"/usr/share/man/man1/libuv.1
}
