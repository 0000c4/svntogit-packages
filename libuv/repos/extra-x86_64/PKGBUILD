# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=1.19.0
pkgrel=2
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('x86_64')
url="https://github.com/libuv/libuv"
license=('custom')
depends=('glibc')
makedepends=('python-sphinx')
source=("https://github.com/libuv/libuv/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
		https://github.com/libuv/libuv/commit/fd049399aa4ed8495928e375466970d98cb42e17.patch)
sha512sums=('b9dfe1df09b67dae27a9aa3a3eb8f3ef31aedf9c95c1361034defab21a017e3978a2e2db502847a0c372a34bffe176acd96616096aa692ef2f35cc854ac8e211'
            '196a5a8be10c05924b88375d4a1d667b0cbcafb3f8ea7f0d16b7fc8d7d843114d71ff0373f79d816fc43964e8bc00797f400745efdb82b60706966f0dce45470')

prepare() {
	cd $pkgname-$pkgver
	# https://github.com/libuv/libuv/pull/1717
	patch -R -p1 -i ../fd049399aa4ed8495928e375466970d98cb42e17.patch
}

build() {
	cd $pkgname-$pkgver
	./autogen.sh
	./configure --prefix=/usr
	make
	make man -C docs
}

check() {
	cd $pkgname-$pkgver
	make check
}

package() {
	cd $pkgname-$pkgver

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	install -Dm644 AUTHORS \
		"$pkgdir"/usr/share/doc/$pkgname/AUTHORS
	install -Dm644 README.md \
		"$pkgdir"/usr/share/doc/$pkgname/README.md
	install -Dm644 ChangeLog \
		"$pkgdir"/usr/share/doc/$pkgname/ChangeLog
	install -Dm644 docs/build/man/libuv.1 \
		"$pkgdir"/usr/share/man/man1/libuv.1
}
