# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=('oxygen-icons'
         'oxygen-icons-svg')
pkgver=4.10.0
pkgrel=1
pkgdesc="The Oxygen Icon Theme"
arch=('any')
url='http://www.oxygen-icons.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('e7f9b5fd8fe0e6f6f9c0d744424134497328e65e')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_oxygen-icons() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}

package_oxygen-icons-svg() {
	pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"
	cd $srcdir/${pkgbase}-${pkgver}
	find scalable -type f ! -name '*.sh' -exec \
		install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
