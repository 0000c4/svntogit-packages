# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=('oxygen-icons'
         'oxygen-icons-svg')
pkgver=4.2.91svn979380
pkgrel=1
pkgdesc="The Oxygen Icon Theme"
arch=('i686' 'x86_64')
url='http://www.oxygen-icons.org/'
license=('LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('fcb5d4026c455cc94c1c818e480dc30f')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_oxygen-icons() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}

package_oxygen-icons-svg() {
	pkgdesc="${pkgdesc} (Scalable Vector Graphics)"
	cd $srcdir/${pkgname}-${pkgver}
	for i in $(find scalable -type f ! -name '*.sh'); do
		install -D -m644 "${i}" "${pkgdir}/usr/share/icons/oxygen/${i}"
	done
}