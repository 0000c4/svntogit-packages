# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=juk
pkgver=17.12.2
pkgrel=1
pkgdesc='A jukebox, tagger and music collection manager'
url='https://kde.org/applications/multimedia/juk/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdemultimedia)
depends=(kio taglib hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdemultimedia-juk)
replaces=(kdemultimedia-juk)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        juk-column-sorting.patch::"https://cgit.kde.org/juk.git/patch/?id=81c0dcc2")
sha256sums=('92028c3e45aa25b542a17fb0263c73e33065314e6ba7928b3d190eafbd1645f1'
            'SKIP'
            'd36b7b773c1725202056d2438347c82c6cd77d7e932316d0429693e9123623a8')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../juk-column-sorting.patch # Fix column sorting
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
