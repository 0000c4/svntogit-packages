# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce-utils
pkgver=4.6.2
pkgrel=2
pkgdesc="Utilities for Xfce"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("libxfcegui4>=$pkgver" 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
optdepends=('perl:	enables migration script for 4.4 configuration files'
            'xscreensaver|gnome-screensaver|xlockmore: for locking screen with xflock4')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.6/${pkgname}-${pkgver}.tar.bz2
	http://git.xfce.org/xfce/xfce-utils/patch/?id=cae311cf6453c888a262b435648a194517ce76f3
	 )
md5sums=('5d23407700d7e8c9751e17a5bc955109'
         'b1b40010d7df2e413061118b60443700')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix comments in config file http://bugs.archlinux.org/task/20675
  patch -Np1 -i ${srcdir}/'?id=cae311cf6453c888a262b435648a194517ce76f3' || return 1
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
