# $Id$
# Maintainer: aurelien <aurelien@archlinux.org>

pkgname=xfce-utils
pkgver=4.4.3
pkgrel=1
pkgdesc="Utilities for Xfce"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('libxfcegui4>=4.4.3' 'libxfce4mcs>=4.4.3' 'bash' 'dbh' 'hicolor-icon-theme')
makedepends=('xfce-mcs-manager>=4.4.3' 'dbus-glib' 'pkgconfig')
optdepends=('dbus-glib:		to work with xfrun4'
            'xlock:		to work with xflock4 or'
            'xscreensaver:	to work with xflock4')
options=('!libtool')
install=${pkgname}.install
source=(http://www.xfce.org/archive/xfce-${pkgver}/src/${pkgname}-${pkgver}.tar.bz2)
md5sums=('6d6ae1f048e1dc1348ad050498af5a18')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var --disable-static
  make || return 1
  make DESTDIR=${pkgdir} install
  install -D -m644 gdm/xfce.desktop ${pkgdir}/etc/X11/sessions/xfce4.desktop
}
