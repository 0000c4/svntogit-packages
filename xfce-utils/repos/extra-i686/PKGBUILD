# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce-utils
pkgver=4.6.2
pkgrel=3
pkgdesc="Utilities for Xfce"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("libxfcegui4>=$pkgver" 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
optdepends=('perl:	enables migration script for 4.4 configuration files'
            'xscreensaver: for locking screen with xflock4'
	    'gnome-screensaver: for locking screen with xflock4'
	    'xlockmore: for locking screen with xflock4')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.6/${pkgname}-${pkgver}.tar.bz2
	#http://git.xfce.org/xfce/xfce-utils/patch/?id=cae311cf6453c888a262b435648a194517ce76f3
	startxfce4_fix.diff)
md5sums=('5d23407700d7e8c9751e17a5bc955109'
         '03536fb1315837ada2f27467cc640846')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix comments in config file http://bugs.archlinux.org/task/20675
  patch -Np1 -i ${srcdir}/startxfce4_fix.diff
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --enable-dbus --disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
