# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=avidemux
pkgver=2.4.3
pkgrel=2
pkgdesc="A graphical tool to edit video (filter/re-encode/split)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://fixounet.free.fr/avidemux/"
depends=('libxv' 'libvorbis' 'libxml2' 'alsa-lib' 'lame' 'xvidcore' 'faad2>=2.6.1' 'faac' 'sdl' 'x264>=20090108')
makedepends=('cmake' 'libxslt' 'gtk2' 'qt')
optdepends=('gtk2: for using the GTK GUI' 'qt: for using the QT4 GUI')
options=('!makeflags')
source=(http://download.berlios.de/avidemux/${pkgname}_${pkgver}.tar.gz avidemux.desktop \
        avidemux-2.4-i18n.patch avidemux-2.4.3-x264.patch)
md5sums=('ff8e6ac186321e7e8bec9f8177df3724' 'a9cf864c209782307afda5bc6a33a0cd'\
         'c76ab4a1434689b5be2084c4af66298c' '632d9935e4102454f4f61c86e572e62d')
sha1sums=('d3a4dc2c07884916c45f750229d02fbdeb3cedde'
          'e575817d59cd992c2b056c1ff978c23f7fa0e50b'
          '32557519caeed3b236f48b40d5ee8953b2d03197'
          '06fb7f651ff8b3c866785c8a9e65ced1ec395d03')

build() {
  cd ${srcdir}/${pkgname}_${pkgver}
  patch -p1 < ../avidemux-2.4-i18n.patch || return 1
  patch -p1 < ../avidemux-2.4.3-x264.patch || return 1

  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -D NO_GTK=0 -D NO_QT4=0 .. || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -D -m644 ../avidemux_icon.png ${pkgdir}/usr/share/pixmaps/avidemux.png
  install -D -m644 ${srcdir}/avidemux.desktop ${pkgdir}/usr/share/applications/avidemux.desktop
  install -D -m644 ../man/avidemux.1 ${pkgdir}/usr/share/man/man1/avidemux.1
}
