# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=avidemux
pkgver=2.5.0
pkgrel=2
pkgdesc="A graphical tool to edit video (filter/re-encode/split)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://fixounet.free.fr/avidemux/"
depends=('libxv' 'libxml2' 'sdl')
makedepends=('cmake' 'libxslt' 'gtk2' 'qt' 'jack-audio-connection-kit' 'libdca' 'esound' \
             'libvorbis' 'alsa-lib' 'lame' 'xvidcore' 'faad2' 'faac' 'x264' 'libsamplerate' 'subversion')
optdepends=('gtk2: for using the GTK GUI' \
            'qt: for using the QT4 GUI' \
            'lame, faac: for the corresponding audio encoder plugin' \
            'faad2, libdca : for the corresponding audio decoder plugin' \
            'esound, jack-audio-connection-kit: for the corresponding audio device plugin' \
            'x264, xvidcore: for the corresponding video encoder plugin')
options=('!makeflags')
source=(ftp://ftp.archlinux.org/other/avidemux/avidemux_2.5_svn5205.tbz \
        avidemux-2.5-i18n.patch)
md5sums=('f0d89accf46e599e0b329322f3add740' '0adb7cee81e06bfc454baf1d8fbcdd64')
sha1sums=('5179f095e74ee3329bb867bdd4e8663fc4488c06' 'd510ca55cd1b9c162b4c3bcd224a80b6a18421ef')

build() {
  cd "${srcdir}/${pkgname}_${pkgver}"
#  patch -p1 < ../avidemux-2.5.0-gcc-4.4.patch || return 1
  patch -p1 < ../avidemux-2.5-i18n.patch || return 1
###  patch -p1 < ../avidemux-2.5.0-format-strings.patch || return 1
###  patch -p1 < ../avidemux_2.5.0-underlinking.patch || return 1
###  patch -p1 < ../avidemux_2.5.0-wrong-include.patch || return 1
#  patch -p1 < ../avidemux-plugins-2.5.0-gcc-4.4.patch || return 1

  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -D NO_GTK=0 -D NO_QT4=0 .. || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

# plugin build expects libraries to be already installed; we fake a prefix
# in build/ by symlinking all libraries to build/lib/
  mkdir -p lib
  cd lib
  find ../avidemux -name '*.so*' | xargs ln -sft .  || return 1
  cd ../../plugins                 
  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D AVIDEMUX_SOURCE_DIR=${srcdir}/avidemux_${pkgver} \
    -D AVIDEMUX_CORECONFIG_DIR=${srcdir}/avidemux_${pkgver}/build/config \
    -D AVIDEMUX_INSTALL_PREFIX=${srcdir}/avidemux_${pkgver}/build -D CMAKE_BUILD_TYPE=Release .. || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  ln -s /usr/lib/ADM_plugins/videoEncoder/libADM_vidEnc_xvid.so "${pkgdir}/usr/lib/libADM_vidEnc_xvid.so"
  ln -s /usr/lib/ADM_plugins/videoEncoder/libADM_vidEnc_x264.so "${pkgdir}/usr/lib/libADM_vidEnc_x264.so"

  install -D -m644 ../../avidemux_icon.png "${pkgdir}/usr/share/pixmaps/avidemux.png" || return 1
  install -D -m644 ../../avidemux2.desktop "${pkgdir}/usr/share/applications/avidemux.desktop" || return 1
  install -D -m644 ../../man/avidemux.1 "${pkgdir}/usr/share/man/man1/avidemux.1" || return 1
}
