# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cairo-perl
pkgver=1.082
pkgrel=2
pkgdesc="Perl wrappers for cairo"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('cairo' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Cairo-${pkgver}.tar.gz
        testsuite.patch)
md5sums=('cfd61e519ff20023979c255d4040fe06'
         '6a3ceac304a5a53f8fdd5652d98f6855')

build() {
  cd "${srcdir}/Cairo-${pkgver}"
  patch -Np1 -i "${srcdir}/testsuite.patch"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Cairo-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/Cairo-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
