# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=0.9.2
pkgrel=2
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod>=3' 'util-linux>=2.21' 'libarchive' 'coreutils'
         'bash' 'findutils' 'grep' 'filesystem>=2011.10-1' 'file' 'gzip' 'systemd-tools')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"
        0001-Update-module-filter-to-be-aware-of-hyphens-in-the-M.patch
        0002-shutdown-don-t-alert-udev-on-disable-VGs.patch)
install=mkinitcpio.install
sha256sums=('1c388ab1df4ecdf6204602c66220a79adc2c564956fdf68deb7dde9bab331c28'
            '5e5f83a2f9805ce1b09602f31fd4aca93fa7085a40c74bc559cf25c29506b057'
            'e32a7955b94e378698268f9cfd07fb0285a4efbdc4b26ea40714e7f1a54705a1')

package() {
  cd "$pkgname-$pkgver"

  patch -Np1 <"$srcdir"/0001-Update-module-filter-to-be-aware-of-hyphens-in-the-M.patch
  patch -Np1 <"$srcdir"/0002-shutdown-don-t-alert-udev-on-disable-VGs.patch

  make DESTDIR="$pkgdir" install

  # compat symlink
  install -dm755 "$pkgdir/sbin"
  ln -s /usr/bin/mkinitcpio "$pkgdir/sbin/mkinitcpio"
}

