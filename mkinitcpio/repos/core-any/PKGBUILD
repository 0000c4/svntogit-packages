# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=0.12.0
pkgrel=2
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.21' 'libarchive' 'coreutils'
         'bash' 'findutils' 'grep' 'filesystem>=2011.10-1' 'file' 'gzip' 'systemd-tools')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        0001-consolefont-fix-reference-to-consolefont-variable.patch)
install=mkinitcpio.install
sha256sums=('ed9bd16999e75b42c8ad78a62cd744f95aca1d1078bb1ecff94c9d31d6765761'
            '9db84177b3e976dc732fe24d565c07e0977d809a32cbcaaa5f48364fbe6157d2'
            '014c2337870338b8635e6e25115a183de0b8936f54cbfd1589f6604dc42c96e0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 <"$srcdir/0001-consolefont-fix-reference-to-consolefont-variable.patch"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

