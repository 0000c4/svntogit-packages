# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=0.8.4
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('mkinitcpio-busybox>=1.19.4-2' 'kmod>=3' 'util-linux>=2.21' 'libarchive' 'coreutils'
         'bash' 'findutils' 'sed' 'grep' 'filesystem>=2011.10-1' 'file' 'gzip')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('e7a171a8a52d4e7c40bcbd515877b143')
sha256sums=('0b90ea5bd592fa5892160430edc1cf59c467e6302694f08a4ea95633bd8bc2dc')

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # compat symlink
  install -dm755 "$pkgdir/sbin"
  ln -s /usr/bin/mkinitcpio "$pkgdir/sbin/mkinitcpio"
}

