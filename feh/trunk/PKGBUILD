# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=feh
pkgver=1.10.1
pkgrel=1
pkgdesc='Fast, lightweight image viewer which uses imlib2'
arch=('i686' 'x86_64')
url='https://derf.homelinux.org/~derf/projects/feh/'
license=('MIT')
depends=('libxinerama' 'giblib' 'perl')
makedepends=('libxt')
source=("${url}${pkgname}-${pkgver}.tar.bz2")
sha1sums=('2784c80f58e8d5ecc2ed05d6473ec6a574aa25cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's:/usr/local:/usr:g' config.mk
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
