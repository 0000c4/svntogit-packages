# $Id: PKGBUILD 153258 2012-03-12 16:05:19Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-good
pkgver=1.0.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libpulse' 'jack' 'libsoup' 'gst-plugins-base-libs' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout' 'libvpx' 'gdk-pixbuf2' 'libcaca' 'libavc1394' 'libiec61883')
makedepends=('gstreamer' 'speex' 'flac' 'libraw1394')
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.xz)

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Good Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
}

check() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}
md5sums=('f711832cd8a9b033eb424fabe89ca9d1')
