# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: David Runge <dave@sleepmap.de>

pkgname=libgit2-glib
pkgver=0.99.0+2+g0c28c3a
pkgrel=1
pkgdesc="GLib wrapper for libgit2"
url="https://wiki.gnome.org/Projects/Libgit2-glib"
license=('LGPL2.1')
arch=('x86_64')
depends=('glib2' 'libgit2')
makedepends=('gobject-introspection' 'gtk-doc' 'meson' 'python-gobject' 'vala'
             'git')
_commit=0c28c3af2c83e84a0a368ea9ff12502ee3cbbbde  # master
source=("git+https://gitlab.gnome.org/GNOME/libgit2-glib.git#commit=$_commit"
        build.diff)
sha256sums=('SKIP'
            'ab872e2de395d3280aaa8da84524a09b1b0ba2800a40b53c1422fb2647e1ec44')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/^v\.?//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # build fixes
  git apply -3 ../build.diff
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=('libgit2.so')
  provides+=('libgit2-glib-1.0.so')

  DESTDIR="$pkgdir" meson install -C build

  # strip $pkgdir from embedded paths:
  python -m compileall -d "/usr/lib" "$pkgdir/usr/lib"
  python -O -m compileall -d "/usr/lib" "$pkgdir/usr/lib"

  install -vDm 644 $pkgname/{AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
