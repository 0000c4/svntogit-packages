# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: David Runge <dave@sleepmap.de>

pkgname=libgit2-glib
pkgver=0.28.0.1
pkgrel=1
pkgdesc="GLib wrapper for libgit2"
url="https://wiki.gnome.org/Projects/Libgit2-glib"
license=('LGPL2.1')
arch=('x86_64')
depends=('glib2' 'libgit2')
makedepends=('gobject-introspection' 'gtk-doc' 'meson' 'python-gobject' 'vala')
source=("https://gitlab.gnome.org/gnome/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('f180c3030b33930133b2dd66aecf64ec354433c603323da810ff84f542243350')

prepare() {
  mv -v "${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  arch-meson build -D gtk_doc=true
  ninja -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  meson test -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  local py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  DESTDIR="$pkgdir" meson install -C build
  # strip $pkgdir from embedded paths:
  python -m compileall -d "/usr/lib/python${py_ver}" \
    "${pkgdir}/usr/lib/python${py_ver}"
  python -O -m compileall -d "/usr/lib/python${py_ver}" \
    "${pkgdir}/usr/lib/python${py_ver}"
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
