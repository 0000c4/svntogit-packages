# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgit2-glib
pkgver=0.27.8
pkgrel=1
pkgdesc="GLib wrapper for libgit2"
url="https://wiki.gnome.org/Projects/Libgit2-glib"
license=(LGPL2.1)
arch=(x86_64)
depends=(glib2 libgit2)
makedepends=(gobject-introspection python-gobject python2 vala gtk-doc git meson)
_commit=423aad961068321604ce98c8b65761ddaf6b80f0  # tags/v0.27.8^0
source=("git+https://gitlab.gnome.org/GNOME/libgit2-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  cp -a "$pkgdir"/usr/lib/python{3.7,2.7}
  for _v in 2.7 3.7; do
    python$_v -m compileall -d /usr/lib/python$_v "$pkgdir/usr/lib/python$_v"
    python$_v -O -m compileall -d /usr/lib/python$_v "$pkgdir/usr/lib/python$_v"
  done
}
