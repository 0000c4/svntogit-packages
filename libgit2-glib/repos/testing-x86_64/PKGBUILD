# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgit2-glib
pkgver=0.25.0
pkgrel=2
pkgdesc="GLib wrapper for libgit2"
url="http://wiki.gnome.org/Projects/Libgit2-glib"
license=(LGPL2.1)
arch=(i686 x86_64)
depends=(glib2 libgit2)
makedepends=(gobject-introspection python2 vala gtk-doc)
source=("https://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz"
        'https://git.gnome.org/browse/libgit2-glib/patch/?id=995b33c745085439d3cb3fed85dbfdb89cb876a9')
sha256sums=('4a256b9acfb93ea70d37213a4083e2310e59b05f2c7595242fe3c239327bc565'
            'd773b17e11da49a944dc13fe4ea74db7b3f6df6aee4781538d0ce18474490f7f')

prepare() {
  cd "$pkgname-$pkgver"

  # Import patch for libgit 0.26.0 compatibility.
  patch -p1 -i '../?id=995b33c745085439d3cb3fed85dbfdb89cb876a9'
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-gtk-doc

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
