# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calculator
pkgver=3.28.0
pkgrel=1
pkgdesc="GNOME Scientific calculator"
url="https://wiki.gnome.org/Apps/Calculator"
arch=(x86_64)
license=(GPL)
depends=(gtk3 dconf gtksourceview3 mpfr libsoup libmpc)
makedepends=(intltool yelp-tools vala git appstream-glib meson)
groups=(gnome)
options=(!emptydirs)
_commit=d1d38fb5af568f73452de9271590fda74e172715  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/gnome-calculator#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
