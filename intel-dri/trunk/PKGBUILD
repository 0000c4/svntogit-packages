# $Id: $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=intel-dri
pkgver=7.2
pkgrel=2
pkgdesc="Mesa DRI drivers for Intel chipsets"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libdrm>=2.3.1' 'expat>=2.0.1' 'libgl>=7.2')
makedepends=('glproto>=1.4.9' 'pkgconfig' 'libxfixes' 'libxdamage' 'libxxf86vm' 'libxext')
options=('!libtool')
source=(http://downloads.sourceforge.net/mesa3d/MesaLib-${pkgver}.tar.bz2
	mesa-7.1-link-shared.patch
	intel-revert-vbl.patch
	intel-disable-ttm-warning.patch)
md5sums=('04d379292e023df0b0266825cb0dbde5'
         'f0baa948d9810f268413111ee439d24b'
         'a111f4dc82e894f8801bc3fa129af7af'
         'a393d374b5d187ff2a5bb90b0f347c9a')

build() {
  cd ${srcdir}/Mesa-${pkgver}
  patch -Np1 -i "${srcdir}/intel-revert-vbl.patch" || return 1
  patch -Np1 -i "${srcdir}/intel-disable-ttm-warning.patch" || return 1
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=i810,i915,i965 \
    --enable-glx-tls \
    --disable-ttm-api \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR=${pkgdir} install || return 1
  rm -rf ${pkgdir}/usr/lib/pkgconfig
  rm -rf ${pkgdir}/usr/include
  rm -f ${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so
}
