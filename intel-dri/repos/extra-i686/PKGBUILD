# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=intel-dri
pkgver=7.4
pkgrel=2
pkgdesc="Mesa DRI drivers for Intel chipsets"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('MIT')
depends=('libdrm>=2.4.5-3' 'expat>=2.0.1' 'libgl>=7.4')
makedepends=('glproto>=1.4.9' 'dri2proto>=1.1.93' 'pkgconfig')
options=('!libtool')
source=(http://downloads.sourceforge.net/mesa3d/MesaLib-${pkgver}.tar.bz2
	mesa-7.1-link-shared.patch
	intel-revert-vbl.patch
	LICENSE)
md5sums=('7ecddb341a2691e0dfdb02f697109834' 'f0baa948d9810f268413111ee439d24b'\
         'a111f4dc82e894f8801bc3fa129af7af' 'b58d7dffd436e79054e220ad1b4b4345')

build() {
  cd "${srcdir}/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  patch -Np1 -i "${srcdir}/intel-revert-vbl.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=i810,i915,i965 \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  install -D -m644 "${srcdir}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}
