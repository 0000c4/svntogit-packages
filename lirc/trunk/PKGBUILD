# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=lirc
pkgver=0.8.3
pkgrel=1
_kernver=2.6.26-ARCH
pkgdesc="Linux Infrared Remote Control kernel modules for stock arch kernel"
arch=(i686 x86_64)
url="http://www.lirc.org/"
license=('GPL')
depends=('lirc-utils=0.8.3' 'kernel26>=2.6.26-1' 'kernel26<2.6.27')
makedepends=('help2man')
replaces=('lirc+pctv')
options=('!makeflags')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2 \
	kernel-2.6.26.patch)
md5sums=('8e78eeded7b31e5ad02e328970437c0f' '1753acd774f50b638e6173d364de53fd')

build() {
	# configure
	cd $startdir/src/lirc-$pkgver || return 1
	patch -Np1 -i ../kernel-2.6.26.patch || return 1

	./configure --enable-sandboxed --prefix=/usr \
		--with-driver=all --with-kerneldir=/usr/src/linux-${_kernver}/ \
		--with-moduledir=/lib/modules/${_kernver}/kernel/drivers/misc \
	        --with-transmitter \
		|| return 1

	# disable parallel and bt829
        # because of incompatibility with smp systems
        sed -i -e "s:lirc_parallel::" -e "s:lirc_bt829::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile \
                || return 1
	# disable lirc_gpio due to brokeness of kernel 2.6.23
	sed -i -e "s:lirc_gpio::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile \
                || return 1

	# build
	cd drivers || return 1
	make || return 1
	make DESTDIR=$startdir/pkg install || return 1

	# set the kernel we've built for inside the install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
		$startdir/lirc.install || return 1
}
