# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=lirc
_pkgver=0.9.2
[[ $_pkgver =~ [a-z]$ ]] && pkgver="${_pkgver:0:-1}.${_pkgver: -1}" || pkgver="$_pkgver"
pkgrel=1
epoch=1
pkgdesc="Linux Infrared Remote Control utils"
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('alsa-lib' 'libx11' 'libftdi-compat' 'libirman')
makedepends=('help2man' 'alsa-lib' 'libx11' 'libftdi-compat' 'libirman' 'python')
optdepends=('python: pronto2lirc utility')
provides=('lirc-utils')
replaces=('lirc-utils')
backup=('etc/lirc/lirc_options.conf' 'etc/lirc/lircd.conf' 'etc/lirc/lircmd.conf')
install=lirc.install
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2"
        0001-tools-Make-make_rel_symlink.py-use-python3.patch
        lirc.logrotate
        irexec.service
        lirc.tmpfiles)
md5sums=('3afc84e79c0839823cc20e7a710dd06d'
         'bef13bd33bafcba828ec8175652474bc'
         '3deb02604b37811d41816e9b4385fcc3'
         '32df3b9bc859565d6acf5f0e5b747083'
         'febf25c154a7d36f01159e84f26c2d9a')

prepare() {
  cd "${srcdir}/lirc-${_pkgver}"
  patch -p1 -i 0001-tools-Make-make_rel_symlink.py-use-python3.patch
}

build() {
  cd "${srcdir}/lirc-${_pkgver}"

  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc \
	  --with-transmitter --enable-sandboxed 
  make
}

package() {
  cd "${srcdir}/lirc-${_pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/irexec.service "${pkgdir}"/usr/lib/systemd/system/irexec.service
  install -Dm644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
  install -Dm644 "${srcdir}/lirc.logrotate" "${pkgdir}/etc/logrotate.d/lirc"
}
