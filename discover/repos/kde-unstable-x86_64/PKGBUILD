# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.9.95
pkgrel=1
pkgdesc='KDE and Plasma resources management GUI'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/discover'
license=(LGPL)
depends=(knewstuff kdeclarative kitemmodels qt5-graphicaleffects appstream-qt archlinux-appstream-data
         hicolor-icon-theme kirigami2)
makedepends=(extra-cmake-modules python plasma-framework packagekit-qt5 flatpak)
optdepends=('packagekit-qt5: to install packages from Arch Linux repositories' 'flatpak: Flatpak packages support')
conflicts=(muon)
replaces=(muon)
groups=(plasma)
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig} discover-no-deb-rpm.patch)
md5sums=('db5c1ed9ad07b4a77c407b52bf53dcb2'
         'SKIP'
         '0154ac2c0a875a9113adac332bea6788')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Don't handle .rpm or .deb files
  patch -p1 -i ../discover-no-deb-rpm.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
