# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=bcel
pkgname=java-${_libname}
pkgver=5.2
pkgrel=2
pkgdesc="Java library to analyze, create, and manipulate (binary) Java class files"
arch=('any')
url="http://commons.apache.org/bcel/"
license=('APACHE')
depends=('java-runtime-headless')
makedepends=('apache-ant' 'junit')
source=(http://archive.apache.org/dist/jakarta/${_libname}/source/${_libname}-${pkgver}-src.tar.gz
        http://archive.apache.org/dist/jakarta/regexp/binaries/jakarta-regexp-1.5.tar.gz)
sha256sums=('68039d59a38379d7b65ea3fc72276c43ba234776460e14361af35771bcaab295'
            '79e80af8cbeb68ddad75a1aa6244d7acd62176bfd69bcdc0640d11177dcde97d')

build() {
  cd "${srcdir}/${_libname}-${pkgver}"
  # "-Dbuild.sysclasspath=only" prevents ant from downloading regexp
  export ANT_OPTS=-Dfile.encoding=iso-8859-1
  ant \
    -Dbuild.sysclasspath=only \
    -lib ${srcdir}/jakarta-regexp-1.5/jakarta-regexp-1.5.jar \
    jar
}

package() {
  cd "${srcdir}/${_libname}-${pkgver}/target"
  install -Dm644 ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}-${pkgver}.jar
  ln -s ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}.jar
}
