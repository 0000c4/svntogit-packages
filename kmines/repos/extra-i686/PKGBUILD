# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmines
pkgver=17.04.0
pkgrel=1
pkgdesc="The classic Minesweeper game"
url="https://kde.org/applications/games/kmines/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-kmines')
replaces=('kdegames-kmines')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kmines-${pkgver}.tar.xz"{,.sig})
sha256sums=('fbba3f9ee78a95e1af69617fe83b31ccd811aa9f48193be8d11c6129698b3580'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
