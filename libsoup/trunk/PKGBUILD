# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libsoup
pkgname=('libsoup' 'libsoup-gnome')
pkgver=2.39.91
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('glib2' 'libxml2' 'sqlite' 'libgnome-keyring' 'intltool' 'gobject-introspection' 'glib-networking' 'python2')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('bec3aeef96918ea9ed199d2be331f37a8ed819a7c3405bf4c33646cac8dc5ab5')

build() {
  cd "$pkgbase-$pkgver"
  sed -i -e 's/env python/python2/' libsoup/tld-parser.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package_libsoup() {
  pkgdesc="GNOME HTTP Library - base library"
  depends=('glib2' 'libxml2' 'glib-networking')

  cd "$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir"/usr/lib/libsoup-gnome-2.4.*
  rm -f "$pkgdir/usr/lib/pkgconfig/libsoup-gnome-2.4.pc"
  rm -rf "$pkgdir/usr/include/libsoup-gnome-2.4"
  rm -f "$pkgdir/usr/lib/girepository-1.0/SoupGNOME-2.4.typelib"
}

package_libsoup-gnome() {
  pkgdesc="GNOME HTTP Library - GNOME libraries"
  depends=("libsoup=$pkgver" 'libgnome-keyring' 'sqlite')

  cd "$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir"/usr/lib/libsoup-2.4.*
  rm -f "$pkgdir/usr/lib/pkgconfig/libsoup-2.4.pc"
  rm -rf "$pkgdir/usr/include/libsoup-2.4"
  rm -rf "$pkgdir/usr/share"
  rm -f "$pkgdir/usr/lib/girepository-1.0/Soup-2.4.typelib"
}
