# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup
pkgver=2.57.1+29+g5b6d632a
pkgrel=2
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 libxml2 glib-networking sqlite krb5)
makedepends=(intltool gobject-introspection python vala git gtk-doc)
_commit=5b6d632a34e1ce833e43f39fcdecff5388b26146  # master
source=("git+https://git.gnome.org/browse/libsoup#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
