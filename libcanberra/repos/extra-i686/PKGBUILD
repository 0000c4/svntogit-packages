# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libcanberra
pkgname=(libcanberra libcanberra-pulse libcanberra-gstreamer)
pkgver=0.26
pkgrel=5
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification"
arch=(i686 x86_64)
license=('LGPL')
depends=('libvorbis' 'libtool' 'alsa-lib' 'tdb')
makedepends=('gtk-doc' 'libpulse' 'gstreamer0.10' 'gtk2' 'gtk3')
options=(!emptydirs)
url=http://0pointer.de/lennart/projects/libcanberra
source=(http://0pointer.de/lennart/projects/${pkgbase}/${pkgbase}-${pkgver}.tar.gz
        libcanberra-gtk-module.sh libcanberra-quit-add.patch)
md5sums=('ee2c66ada7c851a4e7b6eb1682285a24'
         'a54799e624aac814b9343ab05f25c38b'
         'a147bce8a7addcd40b21faaaf028819b')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/libcanberra-quit-add.patch"
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static --with-builtin=dso --enable-null --disable-oss \
      --enable-alsa --enable-gstreamer --enable-pulse
  make
}

package_libcanberra() {
  install=libcanberra.install
  optdepends=("$pkgbase-pulse: PulseAudio driver"
              "$pkgbase-gstreamer: GStreamer driver")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}"/usr/lib/libcanberra-gtk*.la
  rm -f "${pkgdir}"/usr/lib/gtk-{2,3}.0/modules/*.la
  
  install -m755 -d "${pkgdir}/usr/share/gconf" 
  mv "${pkgdir}/etc/gconf/schemas" "${pkgdir}/usr/share/gconf/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/libcanberra-gtk-module.sh" "${pkgdir}/etc/X11/xinit/xinitrc.d/40-libcanberra-gtk-module"

  # Split plugins
  for _p in pulse gstreamer; do
    mkdir $_p-plugin
    mv "${pkgdir}"/usr/lib/${pkgbase}-${pkgver}/${pkgbase}-$_p.* $_p-plugin
  done
}

package_libcanberra-pulse() {
  pkgdesc="PulseAudio plugin for libcanberra"
  depends=("$pkgbase=$pkgver-$pkgrel" 'libpulse')
  groups=('pulseaudio-gnome')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib/${pkgbase}-${pkgver}"
  mv pulse-plugin/* "${pkgdir}/usr/lib/${pkgbase}-${pkgver}" 
}

package_libcanberra-gstreamer() {
  pkgdesc="GStreamer plugin for libcanberra"
  depends=("$pkgbase=$pkgver-$pkgrel" 'gstreamer0.10')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib/${pkgbase}-${pkgver}"
  mv gstreamer-plugin/* "${pkgdir}/usr/lib/${pkgbase}-${pkgver}" 
}
