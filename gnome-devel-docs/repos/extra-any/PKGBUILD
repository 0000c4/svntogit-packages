# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-devel-docs
pkgver=3.32.1+7+g67da0507
pkgrel=1
pkgdesc="Developer documentation for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-devel-docs"
arch=(any)
license=(FDL)
depends=(yelp)
makedepends=(yelp-tools git)
groups=(gnome-extra)
_commit=67da0507d7f7092cf927dba6ede682125b1cbaa4  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-devel-docs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
