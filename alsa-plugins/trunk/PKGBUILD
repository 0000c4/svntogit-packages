# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=alsa-plugins
pkgver=1.1.7
pkgrel=2
pkgdesc="Extra alsa plugins"
arch=(x86_64)
url="http://www.alsa-project.org"
license=(GPL)
depends=(alsa-lib)
makedepends=(libpulse jack ffmpeg)
optdepends=('libpulse: PulseAudio plugin'
            'jack: Jack plugin'
            'ffmpeg: libavcodec resampling plugin, a52 plugin'
            'libsamplerate: libsamplerate resampling plugin'
            'speex: libspeexdsp resampling plugin')
source=("ftp://ftp.alsa-project.org/pub/plugins/$pkgname-$pkgver.tar.bz2"
        ffmpeg.diff)
sha256sums=('a74b405ab6d9e346e6908a853d5e7631cc61038d9b265bc7f37fab16e827da47'
            'b98b760fe28bd33d18fa5f0c904d9f2735c07d42775e52afacd669459817a8c7')

prepare() {
  cd $pkgname-$pkgver

  # https://bugs.archlinux.org/task/60586
  patch -Np1 -i ../ffmpeg.diff

  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 doc/README* doc/*.txt "$pkgdir/usr/share/doc/$pkgname/"
}
