# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=3.9.92
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
arch=(i686 x86_64)
license=(GPL)
depends=(vte3 gsettings-desktop-schemas dconf)
makedepends=(gnome-doc-utils intltool itstool docbook-xsl desktop-file-utils gconf)
optdepends=('gconf: settings migration when upgrading from older version')
options=('!emptydirs' '!libtool')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('fe0917efcfa3825a14694bdf9d400d6eef7b8617ece7e851cce232b79a3c1ede')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
