# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=3.8.0.1
pkgrel=3
pkgdesc="The GNOME Terminal Emulator"
arch=(i686 x86_64)
license=(GPL)
depends=(vte3 gsettings-desktop-schemas libsm dconf)
makedepends=(gnome-doc-utils intltool itstool docbook-xsl desktop-file-utils python2 gconf)
optdepends=('gconf: settings migration when upgrading from older version')
options=('!emptydirs' '!libtool')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('b9218598ff4cf24ad317160d37d463f9823519295ac4de7417139c43958c565e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
