# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=3.36.0.1
pkgrel=2
pkgdesc="The GNOME Terminal Emulator"
url="https://wiki.gnome.org/Apps/Terminal"
arch=(x86_64)
license=(GPL)
depends=('vte3>=0.60.0' gsettings-desktop-schemas)
makedepends=(itstool docbook-xsl libnautilus-extension appstream-glib
             gnome-shell vala yelp-tools git)
groups=(gnome)
_commit=7d7b73d1005d8377e6f8303801e0da505f907611  # tags/3.36.0.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-terminal.git#commit=$_commit"
        0001-screen-Fix-non-preserved-working-dir.patch)
sha256sums=('SKIP'
            '32f26314ba3b33c95acb41510fa9e4f46d00bb575d3a32cc765b65303c30734a')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/65774
  git apply -3 ../0001-screen-Fix-non-preserved-working-dir.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --with-nautilus-extension
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
