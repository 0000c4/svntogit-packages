# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=3.8.0
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('vte3' 'gsettings-desktop-schemas' 'libsm' 'dconf')
makedepends=('gnome-doc-utils' 'intltool' 'gtk2' 'itstool' 'docbook-xsl' 'desktop-file-utils' 'libnautilus-extension' 'python2' 'gconf')
optdepends=('libnautilus-extension: Terminal extension for nautilus'
            'gconf: settings migration when upgrading from older version')
replaces=('nautilus-open-terminal')
conflicts=('nautilus-open-terminal')
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('93ef1e5bf1f59c2178cb9b9b754eddabd68094f04aeefdf22bb1c263d0126ea3')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-terminal
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
