# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=fvwm-devel
pkgver=2.5.28
pkgrel=3
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'readline>=6.0.00' 'librsvg')
makedepends=('pkgconfig')
conflicts=('fvwm')
provides=("fvwm=${pkgver}")
source=(ftp://ftp.fvwm.org/pub/fvwm/version-2/fvwm-${pkgver}.tar.bz2 fvwm-devel.desktop libpng-1.4.patch)
md5sums=('8e11fa4f752c568b392973d13af993df' '3f5bcb246e035ff29aa5bf54487a3db4'\
         '3fa46769c46fb8d05552c00ad37ccd2c')
sha1sums=('26b83b88656faa3d21ab55544269954e534abe08' '17db773870637b410ceb4f68743dbd517bc5c599'\
         'b07fd3f5dd0fcd9826a80a0a9cc838ad28ea3fd6')

build() {
  cd "${srcdir}/fvwm-${pkgver}"
  patch -p1 < ../libpng-1.4.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -d "${pkgdir}/etc/fvwm"
  install -D -m644 sample.fvwmrc/* "${pkgdir}/etc/fvwm" || return 1
  rm -f "${pkgdir}"/etc/fvwm/Makefile*
  install -D -m644 ../fvwm-devel.desktop "${pkgdir}/etc/X11/sessions/fvwm-devel.desktop" || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}
