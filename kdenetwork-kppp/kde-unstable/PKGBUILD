# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kppp
pkgver=17.07.80
pkgrel=1
pkgdesc='Internet Dial-Up Tool'
url='https://kde.org/applications/internet/kppp/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'ppp')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/kppp-${pkgver}.tar.xz"{,.sig})
sha256sums=('131e70185accca158c2ddc57660e521dd57c66dc3f4e5f0279b771608abf509e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../kppp-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
