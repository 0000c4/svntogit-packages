# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=3.28.1
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
url="https://wiki.gnome.org/Apps/Chess"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg)
makedepends=(gobject-introspection yelp-tools appstream-glib vala git meson) 
optdepends=('gnuchess: Play against computer')
groups=(gnome-extra)
_commit=aaeebd1c949c7826be8eb466bb3766da22cbdbed  # tags/3.28.1^0
source=("git+https://git.gnome.org/browse/gnome-chess#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
