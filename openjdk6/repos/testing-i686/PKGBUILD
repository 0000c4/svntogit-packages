# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=openjdk6
pkgver=1.2
_date=20080801
pkgrel=1
pkgdesc='Free Java environment based on OpenJDK 6.0 with IcedTea6 replacing binary plugs.'
url='http://icedtea.classpath.org'
arch=('i686' 'x86_64')
license=('GPL')
_javaver=1.6.0
_openjdk_version=b11
_openjdk_date=10_jul_2008
if [ "${CARCH}" = 'x86_64' ]; then
      _arch=amd64
 else _arch=i586
fi
depends=('gcc-libs')
makedepends=('alsa-lib' 'libxtst' 'giflib' 'gtk2' 'nspr' 'zlib' 'freetype2' 'libjpeg' 'libx11' 'libcups' 'patch'
             'xalan-java' 'xulrunner>=1.9.0.1' 'apache-ant>=1.7.0' 'autoconf' 'unzip' 'openjdk6')
conflicts=('jdk' 'j2sdk' 'java-environment' 'j2re' 'java-runtime' 'jre' 'gcc-gcj' 'java-gcj-compat')
provides=("jdk=6" "j2sdk" "java-environment" "jre=6" "j2re" "java-runtime")
source=(http://download.java.net/openjdk/jdk6/promoted/${_openjdk_version}/openjdk-6-src-${_openjdk_version}-${_openjdk_date}.tar.gz
	ftp://ftp.archlinux.org/other/openjdk6/icedtea6-${_date}.tar.bz2
	fix_jdk_cmds_path.diff
	fix_corba_cmds_path.diff
	gcjwebplugin_xulrunner.diff
	$pkgname.profile)
noextract=(openjdk-6-src-${_openjdk_version}-${_openjdk_date}.tar.gz)
md5sums=('8e60cdac02ec1b2d8ddb9d7369be69df'
         '0d4f3b981212e29621a0faf3edd8acf7'
         '5da3e39fa60985576c4f37d1491efbe2'
         'f7e7a212e50abb56a6ef1a2b1bd27405'
         'ed2e808df2d2fc2ea7dae5139d475192'
         '3ee3f2a7a1dfb33d58e39e01d6234519')

# for bootstrap build:
# makedepend on 'java-gcj-compat'
# remove 
#	--with-openjdk \
# add
# 	--with-gcj-home=/usr/lib/jvm/java-1.5.0-gcj-1.5.0.0 \
#	--with-openjdk-src-dir=${srcdir}/icedtea6-${pkgver}/openjdk \
#	--with-openjdk-src-zip=${srcdir} \
#	--with-javac \
                                        
build() {
  # for hg checkout install mercurial and
#  mkdir ${startdir}/src/icedtea6-${_date}
#  cd ${startdir}/src/icedtea6-${_date}
#  hg clone http://icedtea.classpath.org/hg/icedtea6 || return 1
#  tar -cvjf ../icedtea6-${_date}.tar.bz2 *
#  return 1

  unset JAVA_HOME
  unset CLASSPATH

  cd ${srcdir}/icedtea6
  ln ${srcdir}/openjdk-6-src-${_openjdk_version}-${_openjdk_date}.tar.gz .

  # fix xulrunner linking
  patch -Np0 -i ${srcdir}/gcjwebplugin_xulrunner.diff || return 1

  cp ${srcdir}/*.diff ${srcdir}/icedtea6/patches/
  export DISTRIBUTION_PATCHES="patches/fix_jdk_cmds_path.diff patches/fix_corba_cmds_path.diff"

  autoreconf # needed due to mercurial build

  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"
  unset MAKEFLAGS

  ./configure --with-parallel-jobs=${HOTSPOT_BUILD_JOBS} \
	--with-xalan2-jar=/usr/share/java/xalan.jar \
	--with-openjdk \
	--with-openjdk-home=/usr/lib/java-${_javaver}-openjdk \
	--with-rhino=no

  LD_PRELOAD="" make || return 1
  make || return 1
  
  mkdir -p ${pkgdir}/usr/lib/java-${_javaver}-openjdk/jre
  mkdir -p ${pkgdir}/usr/share/man

  pushd ${srcdir}/icedtea6/openjdk/control/build/linux-${_arch}/j2sdk-image
   # Install main files.
  cp -a bin include lib src.zip ${pkgdir}/usr/lib/java-${_javaver}-openjdk
  cp -a jre/bin jre/lib ${pkgdir}/usr/lib/java-${_javaver}-openjdk/jre
   # Install man pages.
  cp -a man/man1 ${pkgdir}/usr/share/man
   # Install demos and samples.
  cp -a demo ${pkgdir}/usr/lib/java-${_javaver}-openjdk
  mkdir -p sample/rmi
  mv bin/java-rmi.cgi sample/rmi
  cp -a sample ${pkgdir}/usr/lib/java-${_javaver}-openjdk
  popd

  # Install icons and menu entries.
  for s in 16 24 32 48 ; do
    install -D -p -m 644 \
    openjdk/jdk/src/solaris/classes/sun/awt/X11/java-icon${s}.png \
    ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/java.png
  done

  # Install desktop files.
  install -d -m 755 ${pkgdir}/usr/share/{applications,pixmaps}
  cp javaws.png ${pkgdir}/usr/share/pixmaps
  install  -m644 {javaws,jconsole,policytool}.desktop ${pkgdir}/usr/share/applications

  # link the mozilla-plugin
  mkdir -p ${pkgdir}/usr/lib/mozilla/plugins/
  pushd $pkgdir/usr/lib/mozilla/plugins/
  ln -v -s  /usr/lib/java-${_javaver}-openjdk/jre/lib/${_arch/i586/i386}/gcjwebplugin.so .
  popd

  # set some variables
  install -m755 -D ${startdir}/src/openjdk6.profile ${startdir}/pkg/etc/profile.d/openjdk6.sh
}
