# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=libass
pkgver=0.13.3
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
arch=('i686' 'x86_64')
url='https://github.com/libass/libass/'
license=('BSD')
depends=('fribidi' 'fontconfig')
makedepends=('pkg-config' 'yasm')
provides=('libass.so')
source=("https://github.com/libass/libass/releases/download/${pkgver}/libass-${pkgver}.tar.xz")
sha256sums=('a641b653d7c9f2f3b9d6a5e5a906a004ac3e110487ad485d9dd029e944bb3f6d')

build() {
  cd libass-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-harfbuzz \
    --enable-fontconfig
  make
}

package() {
  cd libass-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libass/
}

# vim: ts=2 sw=2 et:
