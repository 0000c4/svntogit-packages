# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-dictionary
pkgver=3.20.0+28+g7dce582
pkgrel=1
pkgdesc="Look up words in dictionary sources"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Dictionary"
license=('GPL2')
depends=('dconf' 'gtk3')
makedepends=('intltool' 'itstool' 'docbook-xsl' gtk-doc yelp-tools git gobject-introspection)
groups=('gnome')
_commit=7dce582599f483e6a2e541ce772e8e225341c280  # master
source=("git://git.gnome.org/gnome-dictionary#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
