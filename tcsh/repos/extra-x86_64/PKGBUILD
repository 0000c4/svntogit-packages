# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcsh
pkgver=6.16.00
pkgrel=3
pkgdesc="Enhanced version of the Berkeley C shell"
arch=('i686' 'x86_64')
url="http://www.tcsh.org/Welcome"
license=('BSD')
depends=('ncurses')
install=tcsh.install
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz csh.cshrc \
        tcsh-6.15.00-ls-colors-var.patch csh.login tcsh.config.skel usr_csh.sh usr_tcsh.sh)
md5sums=('55f1a41512250168ec4bf1db0e246db4' '299cd79eb3cbc2a3caeac1c3a0309556'\
         'e744f5c31fc8009e7977aa50bbd9d0ae' '4869b9da87c79854e2cc97241f125853'\
         '6bd69d3c815412ada4579ac8f8cc9c45' '2b672c67b5e08ab81fbb03d8588d25e3'\
         '8758b20326f00a1057bd95a300c28355')
sha1sums=('3d3536bbb9201abdf39b8b364b95e0a218970a5c'
          '8ebcee4e6c934481a46fe40c15d52a53ed5744bb'
          'e61fb09bd20d5af0cdb000de4972acf926521de8'
          'dbbcfacc078789104f75cbf5a99640ce7251c99e'
          '73a0ae17f2e3dcf2d3ae871982e8591379e1800e'
          '7668da494d053a00199f8e2c857bf3998c74f0be'
          '31da3a6aaa00cd8d66a6c1fba42e19fbc99f2c7c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../tcsh-6.15.00-ls-colors-var.patch || return 1
  ./configure --prefix=/ --sysconfdir=/etc --mandir=/usr/share/man || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install install.man || return 1

  install -D -m644 "${srcdir}/csh.cshrc" "${pkgdir}/etc/csh.cshrc" || return 1
  install -D -m644 "${srcdir}/csh.login" "${pkgdir}/etc/csh.login" || return 1
  install -D -m644 "${srcdir}/tcsh.config.skel" "${pkgdir}/etc/skel/.tcsh.config" || return 1
  ln -s tcsh "${pkgdir}/bin/csh" || return 1
  install -D -m 755 "${srcdir}/usr_tcsh.sh" "${pkgdir}/usr/bin/tcsh" || return 1
  install -D -m 755 "${srcdir}/usr_csh.sh" "${pkgdir}/usr/bin/csh" || return 1
  install -D -m644 Copyright "${pkgdir}/usr/share/licenses/${pkgname}/Copyright" || return 1
}
