# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgname=network-manager-applet
pkgver=0.8.0.997
pkgrel=1
pkgdesc="GNOME frontends to NetWorkmanager"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
makedepends=('pkgconfig' 'intltool' 'gnome-bluetooth')
depends=('networkmanager>=0.8.0.997' 'libglade>=2.6.4' 'libgnome-keyring>=2.30.1' 'polkit-gnome>=0.96' 'notification-daemon' 'libnotify>=0.4.5' 'hicolor-icon-theme' 'mobile-broadband-provider-info')
optdepends=('gnome-bluetooth: for PAN/DUN support')
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/network-manager-applet/0.8/network-manager-applet-${pkgver}.tar.bz2)
sha256sums=('1b5ccebbc2426bc1abf5137fd183c6a108704e57b8c16ea9b208d38042280a10')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nm-applet ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

}
