# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgname=network-manager-applet
pkgver=0.9.6.4
pkgrel=1
pkgdesc="GNOME frontends to NetWorkmanager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver::5}" 'libgnome-keyring' 'polkit-gnome' 'gtk3' 'libnotify'
         'gnome-icon-theme' 'mobile-broadband-provider-info' 'iso-codes')
makedepends=('intltool' 'gnome-bluetooth')
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool' '!emptydirs')
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.9/$pkgname-$pkgver.tar.xz)
sha256sums=('ae5667b165f0f83244ec76c42f17553ec2169f5250e144904994497137374141')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static \
      --disable-maintainer-mode \
      --disable-migration
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
