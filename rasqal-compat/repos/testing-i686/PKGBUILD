# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lawrence Lee <valheru@facticius.net>

pkgbase=rasqal-compat
pkgname=('rasqal-compat' 'rasqal-compat-devel')
_pkgname=rasqal
pkgver=0.9.21
pkgrel=2
url="http://librdf.org/rasqal"
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
options=('!libtool')
makedepends=('raptor1>=1.4.21' 'mpfr')
source=(http://download.librdf.org/source/${_pkgname}-${pkgver}.tar.gz)
md5sums=('55b67ec92a059ef8979d46486b00a032')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--disable-static \
	--enable-release
  make
}

package_rasqal-compat() {

 pkgdesc="a free C library that handles Resource Description Framework (RDF) query syntaxes, query construction and query execution returning result bindings; compatibility version providing an older API"
 depends=('raptor1>=1.4.21' 'mpfr')
 
  cd ${srcdir}/${_pkgname}-${pkgver}
  make -C src DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/librasqal.so
  rm -rf ${pkgdir}/usr/{bin,include,share}
}

package_rasqal-compat-devel() {

 pkgdesc="a free C library that handles Resource Description Framework (RDF) query syntaxes, query construction and query execution returning result bindings; compatibility version providing an older API - devel files"
 depends=('rasqal-compat')

  cd ${srcdir}/${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/librasqal.so.*
  rm -rf ${pkgdir}/usr/share
}
