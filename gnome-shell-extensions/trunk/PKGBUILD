# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=gnome-shell-extensions
pkgver=3.2.3
pkgrel=1
arch=(any)
url="http://live.gnome.org/GnomeShell/Extensions"
license=(GPL2)
makedepends=(gnome-shell intltool)
groups=(gnome-shell-extensions)
source=(ftp://ftp.archlinux.org/other/$pkgbase/$pkgbase-$pkgver-$pkgrel.tar.xz)
sha256sums=('a87b7bf17475cf473e565cfa149df30568524aca679bee2e9f9c21c38349a5de')

pkgname=(gnome-shell-extension-user-theme)

build() {
  _extensions=(user-theme)

  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr \
    --enable-extensions="${_extensions[*]}" \
    --disable-schemas-compile
  make
}

package_gnome-shell-extension-user-theme() {
  pkgdesc="User Theme extension for GNOME Shell"
  depends=(gnome-shell)
  conflicts=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
}
