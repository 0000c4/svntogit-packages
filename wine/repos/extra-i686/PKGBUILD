# $Id$
# Maintainer: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=wine
pkgver=1.1.29
pkgrel=1
pkgdesc="A compatibility layer for running Windows programs"
url="http://www.winehq.com"
arch=('i686')
license=('LGPL')
depends=('fontconfig' 'libldap' 'libxslt' 'lcms' 'libxxf86dga' \
         'libxcursor' 'libxrandr' 'libxdamage' 'mesa')
makedepends=('cups' 'sane' 'fontforge' 'flex' 'bison' 'libgphoto2' 
             'libxinerama' 'esound' 'giflib' 'ncurses' 'libxcomposite' 'samba' \
             'libxpm' 'libxmu' 'libxxf86vm' 'perl' 'libxml2')
optdepends=("cups" "sane" "libgphoto2" "alsa-lib" "giflib" "libpng")
source=(http://ibiblio.org/pub/linux/system/emulators/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        0001-Always-adjust-the-color_shifts-pointer-when-SelectBi.patch)
md5sums=('c3a9218840a80b29932cee49f2d45d9d'
         '63c4a6aee199039d35e6b57fa505e887')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/0001-Always-adjust-the-color_shifts-pointer-when-SelectBi.patch
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --with-x
  make depend || return 1
  make || return 1
  make prefix=${pkgdir}/usr install || return 1
  mkdir -p ${pkgdir}/etc/wine
}
