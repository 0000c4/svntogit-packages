# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-workspace-wallpapers
pkgver=5.7.2
pkgrel=1
pkgdesc='Additional wallpapers for the Plasma Workspace'
arch=('any')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
makedepends=('extra-cmake-modules' 'qt5-base')
conflicts=('kde-wallpapers')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('fd9f22cbbb300fb81d11f188cb431acc90a3d9720540abbea6df292b7accf0f3'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
