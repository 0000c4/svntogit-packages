# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=eric-plugins
pkgver=5.1.0
pkgrel=1
pkgdesc="Plug-ins for the Eric (5.x) IDE"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('eric')
conflicts=('eric5-plugins')
provides=('eric5-plugins')
replaces=('eric5-plugins')
source=("http://downloads.sourceforge.net/eric-ide/Plugin_Assistant_Eric-2.0.1.tar.gz"
        "http://downloads.sourceforge.net/eric-ide/stable/Plugin_Issue_Tracker-1.0.0.tar.gz")
md5sums=('4d07663fd2866f76b3e1c89de3c5cfe7'
         '6f098969ee43d7efb4de6694351290d1')

package() {
  cd "$srcdir"

  _pymodpath=$(python -c \
    "from distutils.sysconfig import get_python_lib; print(get_python_lib())")

  install -d "$pkgdir$_pymodpath/eric5plugins"

  bsdtar xf Plugin_Assistant_Eric-2.0.1/*.zip \
    -C "$pkgdir$_pymodpath/eric5plugins"
  bsdtar xf Plugin_Issue_Tracker-1.0.0/*.zip \
    -C "$pkgdir$_pymodpath/eric5plugins"

  rm -r "$pkgdir$_pymodpath"/eric5plugins/{VERSION,*/Documentation}
  find "$pkgdir" -type d -exec chmod 755 {} \;
  find "$pkgdir" -type f -exec chmod 644 {} \;
}

# vim:set ts=2 sw=2 et:
