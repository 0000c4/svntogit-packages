# $Id$
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xorg-xev
pkgver=1.1.0
pkgrel=3
pkgdesc='Print contents of X events'
url='http://xorg.freedesktop.org/'
arch=('i686' 'x86_64')
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros')
source=("http://xorg.freedesktop.org/archive/individual/app/xev-${pkgver}.tar.bz2")
sha1sums=('77dcbdaa2046db56f31936f921843bd05df2255f')

groups=('xorg-apps' 'xorg')

build() {
	cd "${srcdir}/xev-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/xev-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
