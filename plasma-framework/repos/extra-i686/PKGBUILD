# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.36.0
pkgrel=2
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-quickcontrols' 'kactivities' 'kdeclarative' 'kwayland')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools' 'python' 'doxygen')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-381130.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=a0e5bb3e")
sha256sums=('7060e5b61eb8b00a350967528e0c22e18edd9cdc2cb7cb398aae3874ac859f17'
            'SKIP'
            'e21619f3d23daccc6d8dac5a5a5685696ca7c0ff443c3ca831b3e056ccb8307b')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-381130.patch # Fix misplaced notifications https://bugs.kde.org/show_bug.cgi?id=381130
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
