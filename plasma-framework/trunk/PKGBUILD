# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.19.0
pkgrel=3
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/plasma-framework'
license=('LGPL')
depends=('qt5-quickcontrols' 'kactivities')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
	bug359388.patch::"https://quickgit.kde.org/?p=plasma-framework.git&a=commitdiff&h=525bf2d3&o=plain"
	bug357895.patch::"https://quickgit.kde.org/?p=plasma-framework.git&a=commitdiff&h=1e196fdf&o=plain")
md5sums=('dbb2096267291d4a36482575c00a3c58'
         'e721f2e101d2dc368c714dacb73dffc4'
         '100d14e455603711aae0ec42eb3cd729')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix missing sni-qt tray icons http://bugs.kde.org/show_bug.cgi?id=359388
  patch -p1 -i ../bug359388.patch
# Fix Plasma taskbar crash http://bugs.kde.org/show_bug.cgi?id=357895
  patch -p1 -i ../bug357895.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
