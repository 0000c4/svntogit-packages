# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-krfb
pkgver=4.14.1
pkgrel=1
pkgdesc='Desktop Sharing'
url='http://www.kde.org/applications/system/krfb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
# note on libxdamage:
#   not detected by namcap because libgl depends on it
#   but nvidia providing libgl does not depend on libxdamage
depends=('kdebase-runtime' 'libvncserver' 'libxdamage' 'telepathy-kde-common-internals')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/krfb-${pkgver}.tar.xz"
        'fix-crash-without-kwallet.patch')
sha1sums=('0131c93b17e8865d25ac950b173633ea2ae8ae38'
          'fe11f0d256de2e6520f93d5167d191800873cd61')

prepare() {
  mkdir build

  cd krfb-${pkgver}
  patch -p1 -i "${srcdir}"/fix-crash-without-kwallet.patch
}

build() {
  cd build
  cmake ../krfb-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
