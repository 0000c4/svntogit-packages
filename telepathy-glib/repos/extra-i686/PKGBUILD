# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>

pkgname=telepathy-glib
pkgver=0.16.3
pkgrel=1
pkgdesc="GLib bindings for the Telepathy D-Bus protocol"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
license=('LGPL2.1')
options=('!libtool' '!emptydirs')
depends=('dbus-glib')
makedepends=('libxslt' 'vala' 'gobject-introspection')
source=("http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('5c0cd5da2ce6257017751573e35c88ee')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
        --libexecdir=/usr/lib/telepathy \
        --enable-vala-bindings \
        --enable-static=no
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
