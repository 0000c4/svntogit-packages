# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=mythes-de
pkgver=20090802
pkgrel=1
pkgdesc="German thesaurus"
arch=(any)
url="http://www.openthesaurus.de/"
license=('LGPL')
groups=()
depends=()
makedepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
source=(http://www.openthesaurus.de/old/thes_de_DE_v2.zip)
md5sums=('a6ed5d637045ff7c91a5cd9084bda0a3')

build() {
  /bin/true
}

package() {
  cd "$srcdir"
  install -dm755 ${pkgdir}/usr/share/mythes
  cp -p th_de_DE_v2.* $pkgdir/usr/share/mythes

  pushd $pkgdir/usr/share/mythes/
  de_DE_aliases="de_AT de_BE de_CH de_LI de_LU"
  for lang in $de_DE_aliases; do
        ln -s th_de_DE_v2.idx "th_"$lang"_v2.idx"
        ln -s th_de_DE_v2.dat "th_"$lang"_v2.dat"
  done
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/mythes/*; do
      ln -sv /usr/share/mythes/$(basename $file) .
    done
  popd
  
  # docs
  install -dm755 ${pkgdir}/usr/share/doc/$pkgname
  cp -p README_th_de_DE_v2.txt $pkgdir/usr/share/doc/$pkgname
}
