# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org> 
# Contributor: kastor <kastor@fobos.org.ar>

pkgname=libupnp
pkgver=1.6.20
pkgrel=1
pkgdesc="Portable Open Source UPnP Development Kit"
arch=('i686' 'x86_64')
url="http://pupnp.sourceforge.net/"
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig')
source=("http://downloads.sourceforge.net/sourceforge/pupnp/${pkgname}-${pkgver}.tar.bz2"
        CVE-2016-6255.patch::"https://github.com/mjg59/pupnp-code/commit/be0a01bd.patch")
md5sums=('6839c376b5ec71d2d56f7b64e829c438'
         '101411c99ed0dd973e6e89848f0d2aaa')

prepare() {
  cd $pkgname-$pkgver
  # Fix CVE-2016-6255
  patch -p1 -i ../CVE-2016-6255.patch
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/custom/${pkgname}/LICENSE
}
