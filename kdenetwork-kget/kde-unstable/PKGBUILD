# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=16.07.90
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'libmms')
makedepends=('cmake' 'automoc4' 'boost' 'libktorrent4')
optdepends=('libktorrent4: torrent support')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kget-${pkgver}.tar.xz")
sha1sums=('97c3facdbb13c21a8a6737bcb76c5a0c77c99ecf')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_NepomukWidgets=OFF \
    -DWITH_KDE4Workspace=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
