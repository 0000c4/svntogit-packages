# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=kdiff3
pkgver=0.9.95
pkgrel=2
pkgdesc="A KDE file comparator/merge tool"
arch=('i686' 'x86_64')
url="http://kdiff3.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'kdebase-lib' 'docbook-xml')
optdepends=('kdebase-lib: for the plugin')
install=kdiff3.install
source=(http://downloads.sourceforge.net/sourceforge/kdiff3/${pkgname}-${pkgver}.tar.gz docbook.patch)
md5sums=('652a98bf79ba762a8a646d4a0fddb323' '7a91d1ab6f62c41dc65082ae99bdeff0')
sha1sums=('1fb27e8b42463ea23ad0169e20819352c1c476b7' '96a856fc7d9829f64bb59853a6dffba8a70d794e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../docbook.patch
  sed -i 's|kdiff3_part.rc DESTINATION ${DATA_INSTALL_DIR}/kdiff3|kdiff3_part.rc DESTINATION ${DATA_INSTALL_DIR}/kdiff3part|' src-QT4/CMakeLists.txt
  mkdir release
  cd release
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/release"
  make DESTDIR=${pkgdir} install
}
