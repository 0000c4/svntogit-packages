# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kdiff3
pkgver=0.9.96
pkgrel=1
pkgdesc="A KDE file comparator/merge tool"
arch=('i686' 'x86_64')
url="http://kdiff3.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'kdebase-lib' 'docbook-xml')
optdepends=('kdebase-lib: for the plugin')
install=kdiff3.install
source=(http://downloads.sourceforge.net/sourceforge/kdiff3/${pkgname}-${pkgver}.tar.gz)
md5sums=('46b96befddf3448a3cb673f018c6f6f9')
sha1sums=('d7efdacdfd7ea3c7ec9f7dee32aba5962c1709a5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir release
  cd release
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/release"
  make DESTDIR="${pkgdir}" install
}
