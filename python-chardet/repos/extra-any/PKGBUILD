# $Id$
# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-chardet
pkgname=('python-chardet' 'python2-chardet')
pkgver=3.0.3
pkgrel=1
arch=('any')
url="https://github.com/chardet/chardet"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/c/chardet/chardet-${pkgver}.tar.gz")
sha512sums=('cd9c24dc451908a91e814dd935641a53def46578341bde6a2635bf4fe3c1581340914a775c358e39a8b17259c5231e573a04e9e9c1be14331c6c765ccf35c723')

prepare() {
   cp -a chardet-$pkgver{,-py2}
}

package_python-chardet() {
   depends=('python-setuptools')
   pkgdesc="Python3 module for character encoding auto-detection"

   cd chardet-$pkgver
   python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-chardet() {
   depends=('python2-setuptools')
   pkgdesc="Python2 module for character encoding auto-detection"

   cd chardet-$pkgver-py2
   python2 setup.py install --root="$pkgdir" --optimize=1

   # To avoid file conflict with the python3 version
   mv "$pkgdir"/usr/bin/chardetect{,-py2}
}
