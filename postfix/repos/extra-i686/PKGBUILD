# $Id$
# Contributor: Jeff Brodnax <tullyarcher@bellsouth.net>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=postfix
pkgver=2.8.3
pkgrel=1
pkgdesc='Secure, fast, easy to administer drop in replacement for Sendmail (MTA)'
url='http://www.postfix.org/'
arch=('i686' 'x86_64')
license=('custom')
depends=('pcre' 'libsasl' 'libmysqlclient' 'postgresql-libs' 'sqlite3' 'libldap' 'db')
backup=('etc/postfix/'{access,aliases,canonical,generic,header_checks,main.cf,master.cf,relocated,transport,virtual})
source=("ftp://ftp.porcupine.org/mirrors/postfix-release/official/${pkgname}-${pkgver}.tar.gz" \
        'aliases.patch' \
        'rc.d')
sha1sums=('2604066f158f5327449960afd6334b996dc01799'
          '5fc3de6c7df1e5851a0a379e825148868808318b'
          '050e6afc5886049682c945ce147b3c47fe113939')

provides=('smtp-server' 'smtp-forwarder')
replaces=('postfix-mysql' 'postfix-pgsql')
conflicts=('postfix-mysql' 'postfix-pgsql' 'smtp-server' 'smtp-forwarder')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	make makefiles CCARGS=' \
		-DUSE_SASL_AUTH -I/usr/include/sasl \
		-DUSE_CYRUS_SASL \
		-DHAS_LDAP \
		-DUSE_TLS \
		-DHAS_MYSQL -I/usr/include/mysql \
		-DHAS_PGSQL -I/usr/include/postgresql \
		-DHAS_SQLITE \
	' AUXLIBS=' \
		-lsasl2 -lssl -lcrypto \
		-lldap -llber \
		-lmysqlclient -lz -lm \
		-lpq \
		-lsqlite3 -lpthread \
	'

	make OPT="${CFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sh postfix-install -non-interactive \
		install_root="${pkgdir}" \
		daemon_directory="/usr/lib/${pkgname}" \
		sample_directory="/etc/${pkgname}/sample" \
		manpage_directory="/usr/share/man"

	install -D -m755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	cd "${pkgdir}"
	patch -p0 < "${srcdir}"/aliases.patch
}
