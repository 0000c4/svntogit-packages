# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=xfce4-taskmanager
pkgver=1.4.0
pkgrel=2
pkgdesc="Easy to use task manager"
arch=('x86_64')
url="https://docs.xfce.org/apps/xfce4-taskmanager"
license=('GPL2')
depends=('libwnck3' 'libxmu')
makedepends=('intltool')
groups=('xfce4-goodies')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        ellipsize-memory-and-swap-labels.patch)
sha256sums=('655684ddfc15fc09071f78f9fce5439d20bbd2d80ecfc5ba0a08fb38313e7d43'
            '74abd16b1ade4fc9e939a0caa9d6630c6c479d19cee71a3615d1ccd6ef0a2bf7')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://gitlab.xfce.org/apps/xfce4-taskmanager/-/issues/32
  patch -Np1 -i ../ellipsize-memory-and-swap-labels.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
