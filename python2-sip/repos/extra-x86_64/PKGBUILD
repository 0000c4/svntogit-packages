# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname=python2-sip
pkgver=4.12
pkgrel=4
pkgdesc="A tool that makes it easy to create Python 2 bindings for C and C++ libraries"
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
depends=('python2')
replaces=('sip')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/sip-${pkgver}.tar.gz")
md5sums=('8a0998fbaf34bdab2c15af3d4fa3bc0e')

build() {
  cd "${srcdir}/sip-${pkgver}"

  python2 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package() {
  cd "${srcdir}/sip-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
