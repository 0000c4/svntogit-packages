# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=2.0.0
pkgrel=2
pkgdesc="A KDE program that browses samba shares"
arch=(i686 x86_64)
url="http://smb4k.sourceforge.net/"
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python plasma-framework)
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        CVE-2017-8849.patch::"https://cgit.kde.org/smb4k.git/patch/?id=a90289b0")
sha256sums=('8a9f9bbfc9876816393d9f269952ddfe896c540e2de26b634b8e709a13ceca95'
            'affa60422258166886370e242f0f2ed5c5a0cf01fb819afead2132202e68b308')

prepare() {
  mkdir -p build

# fix doc cmake conflicts
  sed -e '/kdoctools_install/d' -i $pkgname-$pkgver/CMakeLists.txt
# fix CVE-2017-8849
  cd $pkgname-$pkgver
  patch -p1 -i ../CVE-2017-8849.patch
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELEASE \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
