# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=audacious-plugins
pkgver=2.5.0
pkgrel=1
pkgdesc='Plugins for Audacious'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL')

makedepends=("audacious>=${pkgver}"
             'alsa-lib' 'pulseaudio' 'jack' 'lame' 'libvorbis' 'flac'
             'faad2' 'ffmpeg' 'fluidsynth' 'libcdio' 'libsidplay' 'wavpack'
             'libnotify' 'lirc-utils' 'curl' 'libmtp'
             'neon' 'libmms' 'libcue')

optdepends=('alsa-lib: Advanced Linux Sound Arch. output'
            'pulseaudio: PulseAudio output'
            'jack: Jack Audio Connection Kit output'
            'lame: FileWriter MP3 output'
            'libvorbis: Vorbis input, FileWriter Vorbis output'
            'flac: FLAC input, FileWriter FLAC output'

            'faad2: AAC input'
            'ffmpeg: ffaudio input'
            'fluidsynth: MIDI FluidSynth backend input'
            'libcdio: CD Digital Audio input'
            'libsidplay: Commodore 64 audio input'
            'wavpack: WavPack input'

            'libnotify: libnotify OSD'
            'lirc-utils: LIRC'
            'curl: AudioScrobbler Client'
            'libmtp: Upload to MTP device'

            'neon: neon-based http transport'
            'libmms: libmms-based mms transport'
            'libcue: CUE playlist format')

source=("http://distfiles.atheme.org/$pkgname-$pkgver.tgz")
sha1sums=('33d33f96f5de56f73398814f0534e88989b2c71f')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	sed 's/libavcore >= 0.12.0//g' -i configure # FS#23549

	[[ $CARCH == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
	./configure \
		--prefix=/usr \
		--enable-chardet \
		--enable-amidiplug \
		--disable-adplug \
		$extraopts \
		--disable-projectm # no support for projectm-2.0.1, cf. FS#21344
		                   # would require libvisual-projectm in make/optdeps
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}
