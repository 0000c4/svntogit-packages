# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins
pkgver=2.4.0
pkgrel=1
pkgdesc='Plugins for Audacious'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL')
depends=('libmad' 'libvorbis')
makedepends=('jack' 'lame' 'fluidsynth' 'libcdio' 'libsidplay' 'wavpack'
             'libmpcdec' 'taglib' 'lirc-utils' 'curl' 'libmtp' 'sdl' 'projectm' 'imlib2'
             'neon' 'libmms' 'gtkglext' "audacious>=${pkgver}" 'ffmpeg' 'libsamplerate')
optdepends=('jack: JACK output plugin'
	    'libsamplerate: for JACK output'
            'lame: FileWriter output plugin'
            'fluidsynth: FluidSynth backend for AMIDI-Plug input plugin'
            'libcdio: CD Audio input plugin'
            'libsidplay: SID input plugin'
            'wavpack: WavPack input plugin'
            'libmpcdec: Musepack input plugin'
            'taglib: Musepack input plugin'
            'lirc-utils: LIRC plugin'
            'curl: Audioscrobbler plugin'
            'libmtp: MTP upload plugin'
            'sdl: Paranormal and projectM visualization plugins'
            'projectm: projectM visualization plugin'
            'imlib2: RootVis visualization plugin'
            'neon: HTTP transport plugin'
            'libmms: MMS transport plugin'
	    'ffmpeg: for MPEG 4 Audio AAC')
source=("http://distfiles.atheme.org/$pkgname-$pkgver.tgz")
sha1sums=('bd59c31ffb439380fcfec028dcf10b49cb82ad5f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [[ $CARCH == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
  ./configure --prefix=/usr --disable-pulse --enable-chardet \
    --enable-amidiplug --disable-adplug $extraopts
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
