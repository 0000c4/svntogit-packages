# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=xawtv
pkgver=3.101
pkgrel=2
pkgdesc="A simple Xaw-based TV program which uses the bttv driver or video4linux"
arch=('i686' 'x86_64')
url="http://git.linuxtv.org/xawtv3.git"
license=('GPL')
depends=('aalib' 'v4l-utils' 'libxv' 'libxxf86dga' 'libxrandr' 'libgl' 'libdv' 'zvbi' 'libxinerama' 'lirc-utils' 'libxft' 'libxaw' 'xorg-fonts-misc' 'libxxf86vm')
makedepends=('mesa')
source=(http://linuxtv.org/downloads/xawtv/xawtv-${pkgver}.tar.bz2)
md5sums=('330ce1789595e370be34ad4cae3dc3fa')
                  
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoconf
  autoheader
  ./configure --prefix=/usr --disable-motif --disable-quicktime
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
