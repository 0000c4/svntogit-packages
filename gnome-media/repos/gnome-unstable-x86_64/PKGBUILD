# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-media
pkgver=3.4.0
pkgrel=1
pkgdesc="GNOME Media Tools"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnome-media-profiles' 'libcanberra' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
url="http://www.gnome.org"
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=gnome-media.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('a76fac286f24d3836137ddbaab66f05e19eb5fb83cca6e375dbef040765a1d1f')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-media \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper
  make
}

package() {
  cd $pkgname-$pkgver
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgbase}.schemas" --domain gnome-media-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  rm -rf "${pkgdir}/usr/share/sounds/"
}
