# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>

pkgname=libva-driver-intel
pkgver=1.0.18
pkgrel=1
pkgdesc="VA-API implementation for Intel G45 chipsets and Intel HD Graphics for Intel Core processor family."
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libva')
options=('!libtool')
source=(http://cgit.freedesktop.org/vaapi/intel-driver/snapshot/intel-driver-${pkgver}.tar.bz2)
md5sums=('d8c52f07148dfb2e0407549b90300bc1')

build() {
  cd intel-driver-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd intel-driver-$pkgver
  make DESTDIR="$pkgdir" install
  install -m644 -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
