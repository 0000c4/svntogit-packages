# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-mime
pkgver=20.11.90
pkgrel=1
pkgdesc="Libraries and daemons to implement basic email handling"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libakonadi kmime)
makedepends=(extra-cmake-modules kdoctools boost)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f5f00771ad0a9af6753a0a5cb9b39affdc8c2bdf01d85bb64663f6d1d865af3b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
