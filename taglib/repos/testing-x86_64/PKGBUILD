# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=taglib
pkgver=1.7.2
pkgrel=2
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats"
arch=('i686' 'x86_64')
url="http://developer.kde.org/~wheeler/taglib.html"
license=('LGPL' 'MPL')
depends=('sh' 'zlib' 'gcc-libs')
makedepends=('cmake' 'pkg-config')
options=('!libtool')
source=("http://developer.kde.org/~wheeler/files/src/$pkgname-$pkgver.tar.gz"
        'ape-idev2.patch')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/ape-idev2.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_MP4=ON \
    -DWITH_ASF=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
md5sums=('b0a9e797d3833fb933c7c3176de3d720'
         'c7c165d99acf01456351ba39ff7056cb')
