# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-weather
pkgver=3.32.1
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL)
depends=(gtk3 gjs libgweather geoclue2 gnome-desktop)
makedepends=(gobject-introspection appstream-glib git meson)
groups=(gnome)
_commit=9a35adeea3b8a9a734ca873c95f1222b23772959  # tags/3.32.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-weather.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
