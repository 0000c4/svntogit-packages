# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-terminal
pkgver=0.8.7.1
pkgrel=2
pkgdesc="A modern terminal emulator primarily for the Xfce desktop environment"
arch=('x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'vte3' 'hicolor-icon-theme')
makedepends=('intltool')
conflicts=('terminal')
replaces=('terminal')
source=(https://archive.xfce.org/src/apps/$pkgname/0.8/$pkgname-$pkgver.tar.bz2
        fix-handling-of-tabs-reordering.patch
        update-window-actions-and-active-tab-indication.patch)
sha256sums=('9ff21627e8d2a105e7133efc3e8eeeda376a2071fac737e37cf47a539a7b4351'
            '55d4204089e1da7f31e2ccf4180d4a002b35546f23311e2dfab89af93817c3a5'
            '061334ea4a02e53e58719b88b295930fa117291e0dd5c1c55be7c9b89130f6e5')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=14278
  patch -Np1 -i ../fix-handling-of-tabs-reordering.patch
  patch -Np1 -i ../update-window-actions-and-active-tab-indication.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
