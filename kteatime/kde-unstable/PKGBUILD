# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kteatime
pkgver=17.07.80
pkgrel=1
pkgdesc='Tea Cooker'
url='https://kde.org/applications/games/kteatime/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeutils')
depends=('knotifyconfig' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdetoys-kteatime')
replaces=('kdetoys-kteatime')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/kteatime-${pkgver}.tar.xz"{,.sig})
sha256sums=('ee4ef25380c3ed69c5911dced6b98689cec09638bb6937b3b6dc009aaea3d45e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../kteatime-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
