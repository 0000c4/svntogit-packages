# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=portsmf
pkgver=228
pkgrel=1
pkgdesc="Portable library for reading/writing Standard MIDI Files"
arch=('i686' 'x86_64')
url="http://sourceforge.net/p/portmedia/wiki/portsmf/"
license=('MIT')
depends=('gcc-libs')
makedepends=('subversion')
options=('!staticlibs')
source=(portsmf::svn+https://portmedia.svn.sourceforge.net/svnroot/portmedia/portsmf/trunk)
sha1sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s" "$(svnversion | tr -d 'A-z')"
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libdir=/usr/lib --includedir=/usr/include/portsmf
  make 
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" includedir=/usr/include/portsmf install
  install -D -m644 portSMF.pc "${pkgdir}/usr/lib/pkgconfig/portSMF.pc"
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
