# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=portsmf
pkgver=228
pkgrel=3
pkgdesc="Portable library for reading/writing Standard MIDI Files"
arch=('x86_64')
url="http://sourceforge.net/p/portmedia/wiki/portsmf/"
license=('MIT')
depends=('gcc-libs')
makedepends=('subversion')
options=('!staticlibs')
source=(portsmf::svn+https://svn.code.sf.net/p/portmedia/code/portsmf/trunk#revision=$pkgver
        portsmf-shared.patch)
sha512sums=('SKIP'
            'f2961c2da2e5b08e60cbbf7c3d47687ce75ad4f6d7ee1c4f4a5ef1212ef928fe1c61212dc11081155117e2adf51c9ac280e21f791f1da87f69a0dec00605426a')

pkgver() {
  cd $pkgname
  printf "%s" "$(svnversion | tr -d 'A-z')"
}

prepare() {
  cd $pkgname
  chmod 755 configure
  patch -p1 -i "${srcdir}/portsmf-shared.patch"
  autoreconf -f -i
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libdir=/usr/lib --includedir=/usr/include/portsmf
  make 
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" includedir=/usr/include/portsmf install
  install -D -m644 portSMF.pc "${pkgdir}/usr/lib/pkgconfig/portSMF.pc"
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
