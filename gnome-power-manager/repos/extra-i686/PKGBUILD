# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=3.22.2
pkgrel=1
pkgdesc="System power information and statistics"
url="https://git.gnome.org/browse/gnome-power-manager"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 upower dconf)
makedepends=(intltool gnome-common git)
options=(!emptydirs)
_commit=1b477ec670df6b9a8d6c55f458d554910e1fd0f8  # tags/GNOME_POWER_MANAGER_3_22_2^0
source=("git://git.gnome.org/gnome-power-manager#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_POWER_MANAGER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
