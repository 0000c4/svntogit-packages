# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=2.28.0
pkgrel=1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system."
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=('GPL')
depends=('libnotify>=0.4.5' 'libunique>=1.1.2' 'libcanberra>=0.14' 'hicolor-icon-theme' 'devicekit-power>=010' 'gnome-keyring>=2.27.92')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.17.5' 'gnome-panel>=2.27.92')
optdepends=('gnome-panel')
options=(!emptydirs)
install=gnome-power-manager.install
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-power-manager/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('6e4e142beed2db3d53890bc082d36d45b9b1ccaf239e7eba7f3c42fc666ee74e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --with-dpms-ext \
      --disable-scrollkeeper \
      --with-dbus-services=/usr/share/dbus-1/services \
      --enable-applets || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-power-manager ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
