#$Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2009.02.23
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=('gcc-libs')
source=(http://live555.com/liveMedia/public/live.$pkgver.tar.gz)
md5sums=('33d99d056ca3b2ca55b38e439be39c0d')

build()
{
  cd $srcdir/live
  sed -i 's/$(INCLUDES) -I. -O2 -DSOCKLEN_T/$(INCLUDES) -I. -O2 -I. -fPIC -DSOCKLEN_T/g' config.linux
  ./genMakefiles linux
  make || return 1

  for dir in BasicUsageEnvironment UsageEnvironment groupsock liveMedia; do
    mkdir -p $pkgdir/usr/lib/$pkgname/$dir
    cp -r $dir/*.a $dir/include $pkgdir/usr/lib/$pkgname/$dir
  done

  mkdir -p $pkgdir/usr/bin
  for testprog in `find testProgs -type f -perm 755`; do
    install $testprog $pkgdir/usr/bin
  done
}
