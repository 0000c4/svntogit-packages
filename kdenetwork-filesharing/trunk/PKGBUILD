# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-filesharing
pkgver=16.04.0
pkgrel=1
pkgdesc='Properties dialog plugin to share a directory with the local network'
url='https://projects.kde.org/projects/kde/kdenetwork/kdenetwork-filesharing'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdenetwork)
depends=(kio)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('samba: folder sharing')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('cd3599668f5b71511dc98c2a9ce85a94c09d19d2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
