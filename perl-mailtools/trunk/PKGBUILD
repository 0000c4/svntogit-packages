# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-mailtools
pkgver=2.09
pkgrel=1
pkgdesc='Various e-mail related modules'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-timedate)
url=https://metacpan.org/release/MailTools
source=("http://search.cpan.org/CPAN/authors/id/M/MA/MARKOV/MailTools-$pkgver.tar.gz")
md5sums=(2ab874c4202d647e0fc58dd7cffb59f6)
sha512sums=(2309a7b698afe7309d8c1cda99ed937fd2b3f8ac2e955cfb3fb933ba1e874a8aaf3b34d41f7ab4e3781610ea12d72c1eccd9d782fd03023dd25fc80ff7800f0c)
_dir="$srcdir/MailTools-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
