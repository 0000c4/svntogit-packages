# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=3.2.0
pkgrel=3
pkgdesc="Screen reader for individuals who are blind or visually impaired"
arch=(any)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
install=orca.install
options=('!libtool')
depends=('gtk3' 'python2-gobject' 'at-spi2-atk' 'dbus-python' 'pyatspi' 'pyxdg' 'speech-dispatcher' 'liblouis' 'brltty' 'xorg-xmodmap' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        fix-edit.patch::http://git.gnome.org/browse/orca/patch/?id=866b743e6b7d6dfa8f00bc810d7d7f5de4c20e30
        fix-hu.patch::http://git.gnome.org/browse/orca/patch/?id=81ded18fa56174730ad79f686c28952a0df48ef1)
sha256sums=('1bb6e5564ae2750cebbed4fe5283fef26e4c85ff22f99def8b334b41af3f2479'
            '0ef92b01fe927b8a0daa715c4f9d1a84c47d43af71b433b35ec05e9c28388744'
            '43110ec68df37681e6859cc223319f8b780fed9e96108a9328cbf4546f10d25e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-edit.patch"
  patch -Np1 -i "${srcdir}/fix-hu.patch"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
