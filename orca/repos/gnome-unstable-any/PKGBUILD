# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=2.91.91
pkgrel=1
pkgdesc="A scriptable screen reader and magnifier"
arch=(any)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
install=orca.install
options=('!libtool')
depends=('pygtk' 'python2-gconf' 'python2-bonobo' 'python2-libgnome'
         'python-wnck' 'dbus-python' 'at-spi' 'gnome-speech' 'gnome-mag'
         'brltty' 'hicolor-icon-theme' 'pyxdg')
makedepends=('intltool')
replaces=('gnopernicus')
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('07dbd12d6aeb42141223d1ca0388dc6c98569278e99f57dc3ee02a263e419c40')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
