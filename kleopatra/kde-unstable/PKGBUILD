# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kleopatra
pkgver=20.03.80
pkgrel=1
pkgdesc='Certificate Manager and Unified Crypto GUI'
arch=(x86_64)
url='https://www.kde.org/applications/utilities/kleopatra/'
license=(GPL LGPL FDL)
depends=(hicolor-icon-theme libkleo kcmutils kmime)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kleopatra-qt5.15.patch::"https://cgit.kde.org/kleopatra.git/patch/?id=66eab01b")
sha256sums=('95206597f6db80b37d0a0d62c7a16c3206301e6bbbac2066cca50e4cb412099a'
            'SKIP'
            '3956efb51ddf2b6cf2d2354f2105b5db628a9aa85afb53f027afd38597b19ad9')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -Rp1 -i ../kleopatra-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
