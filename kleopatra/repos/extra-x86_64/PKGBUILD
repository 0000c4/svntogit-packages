# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kleopatra
pkgver=19.04.2
pkgrel=1
pkgdesc='Certificate Manager and Unified Crypto GUI'
arch=(x86_64)
url='https://www.kde.org/applications/utilities/kleopatra/'
license=(GPL LGPL FDL)
depends=(hicolor-icon-theme libkleo kcmutils kmime)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8536ebdae2d0fe6fa8ac4bb39756e6aff1fc1b8459163b39efc2a32d25fa41c3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
