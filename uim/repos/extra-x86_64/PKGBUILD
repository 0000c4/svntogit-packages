# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=uim
pkgver=1.6.0
pkgrel=2
pkgdesc='Multilingual input method library'
arch=('i686' 'x86_64')
url='http://code.google.com/p/uim/'
license=('custom')
depends=('m17n-lib' 'ncurses' 'gtk2')
makedepends=('pkg-config' 'gettext' 'intltool' 'gnome-panel' 'anthy')
optdepends=('gnome-panel: for using the GNOME applet')
options=('!libtool')
install=uim.install
source=("http://uim.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2"
        'uim-home.patch')
sha1sums=('d27f2ca8136da0702c82f0522911d06b2b8f8ea7'
          'dbcf90f3ea246c5723d715e0935072baa9364cd2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # makechrootpkg runs build() as "nobody", which has HOME=/
  # However, UIM's make needs $HOME to be writable.
  patch -p0 < ../uim-home.patch
  export HOME="`pwd`"

  ./configure --prefix=/usr --libexecdir=/usr/lib/uim \
    --with-anthy-utf8 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
