# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=uim
pkgver=1.6.1
pkgrel=2
pkgdesc='Multilingual input method library'
arch=('i686' 'x86_64')
url='http://code.google.com/p/uim/'
license=('custom')
depends=('gtk2' 'libxft' 'libedit' 'anthy' 'm17n-lib')
makedepends=('intltool' 'gettext' 'gnome-panel')
optdepends=('gnome-panel: gnome applet indicator')
options=('!libtool')
source=("http://uim.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2"
        'home.patch')
sha1sums=('412bb2df6041185084e2f64fb73357389bf992b5'
          'dbcf90f3ea246c5723d715e0935072baa9364cd2')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# UIM's Makefile needs to write in $HOME
	patch -p0 < ../home.patch; export HOME="`pwd`"

	./configure --prefix=/usr --libexecdir=/usr/lib/uim \
		--with-anthy-utf8 
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
