# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=step
pkgver=17.04.3
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="https://kde.org/applications/education/step/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libqalculate' 'gsl' 'knewstuff' 'khtml' 'kdelibs4support' 'kplotting' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'eigen' 'mesa' 'kdesignerplugin' 'qt5-tools')
conflicts=('kdeedu-step')
replaces=('kdeedu-step')
source=("https://download.kde.org/stable/applications/${pkgver}/src/step-${pkgver}.tar.xz"{,.sig})
sha256sums=('7a2ef0548bae9dcf370d818fa527fba232ba84da84a6664cce114e0492c53784'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../step-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
