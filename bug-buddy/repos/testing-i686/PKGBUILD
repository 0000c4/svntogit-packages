# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=bug-buddy
pkgver=2.24.0
pkgrel=1
pkgdesc="A bug reporting tool for GNOME"
arch=(i686 x86_64)
install=bug-buddy.install
depends=('gnome-menus>=2.24.0' 'libgnomeui>=2.24.0' 'evolution-data-server>=2.24.0' 'libgtop>=2.24.0' 'libelf')
makedepends=('perlxml' 'pkgconfig')
groups=('gnome-extra')
license=(GPL)
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d0fa514747137ce56dc123392d066630')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
