# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=bug-buddy
pkgver=2.26.0
pkgrel=1
pkgdesc="A bug reporting tool for GNOME"
arch=(i686 x86_64)
install=bug-buddy.install
depends=('gnome-menus>=2.26.0' 'evolution-data-server>=2.26.0' 'libgtop>=2.26.0' 'libelf' 'gconf>=2.26.0-2')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.16.0')
groups=('gnome-extra')
license=(GPL)
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f92663a1a819f83e3eb3004f9e8a687a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain bug-buddy ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
