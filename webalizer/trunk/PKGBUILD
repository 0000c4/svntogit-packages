# $Id$
# Maintainer: Travis Willard <travisw@wmpub.ca>

pkgname=webalizer
pkgver=2.01
pkgrel=5
pkgdesc="A web statistics generator with geolizer built in."
url="http://www.mrunix.net/webalizer/"
license="GPL"
arch=('i686')
depends=('gd' 'geoip')
makedepends=()
source=(ftp://ftp.mrunix.net/pub/webalizer/$pkgname-$pkgver-10-src.tgz \
        http://sysd.org/stas/files/active/0/geolizer_$pkgver-10-patch.20070115.tar.gz)
md5sums=('9217595005aec46a505e1fb349052a8e'
         '457c836d25c363e94cfa8fa11de52146')

build() {
  cd $startdir/src/$pkgname-$pkgver-10
  patch -p1 < $startdir/src/geolizer_$pkgver-10-patch/geolizer.patch
  ./configure --prefix=/usr --enable-geoip
  make || return 1

  # Set up our directory structure
  install -d -m755 $startdir/pkg/usr/bin $startdir/pkg/usr/share/man/man1 \
                   $startdir/pkg/etc

  make prefix=$startdir/pkg/usr datarootdir=$startdir/pkg/usr/share \
       ETCDIR=$startdir/pkg/etc install

  cd $startdir/pkg/usr/bin
  rm -fr webazolver
  ln -s webalizer webazolver
}

