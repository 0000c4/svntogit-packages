# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkmahjongg
pkgver=17.07.80
pkgrel=1
pkgdesc="Common code, backgrounds and tile sets for games using Mahjongg tiles"
url="https://www.kde.org/applications/games/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfigwidgets' 'kcompletion' 'qt5-svg')
makedepends=('extra-cmake-modules' 'python')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('486cf0a1c5946709c31c1bb0025ab8eb634d3a565fd4d3ebaee83268ed902093'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
