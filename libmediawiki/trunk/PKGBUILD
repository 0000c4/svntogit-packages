# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libmediawiki
pkgver=5.0.0git20170401
pkgrel=1
_commit=700d8512caeb4d0116a4ad39b6b377adf2899007
pkgdesc="A KDE C++ interface for MediaWiki based web service as wikipedia.org"
arch=(i686 x86_64)
url='https://www.digikam.org/'
license=(GPL)
depends=(kcoreaddons)
makedepends=(extra-cmake-modules git)
source=("git+https://git.kde.org/libmediawiki#commit=$_commit")
sha1sums=('SKIP')

prepare() {
  mkdir -p build 
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
