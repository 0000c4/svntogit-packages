# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libmediawiki
pkgver=5.0.0git20160605
pkgrel=1
pkgdesc="A KDE C++ interface for MediaWiki based web service as wikipedia.org"
arch=(i686 x86_64)
url='https://www.digikam.org/'
license=(GPL)
depends=(kcoreaddons)
makedepends=(extra-cmake-modules)
source=("http://quickgit.kde.org/?p=libmediawiki.git&a=snapshot&h=3df0a45d&fmt=tgz")
sha1sums=('73b9afd1e255c1b2c9c2b01cb90856ae472a4a39')

prepare() {
  mkdir -p build 
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
