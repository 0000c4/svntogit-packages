# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.4.2
pkgrel=1
pkgdesc="A library to parse XMP metadata"
arch=(i686 x86_64)
url="https://libopenraw.freedesktop.org/wiki/Exempi"
license=('custom:BSD')
depends=('gcc-libs' 'expat' 'zlib')
makedepends=('boost')
source=(https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2{,.asc})
sha256sums=('5b0db25fa5ab11344adbc26e1d4a3ace2ad1166a7bea4f4d3fd1837e982bc6ec'
            'SKIP')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') #Hubert Figuiere

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$srcdir/$pkgname-$pkgver/COPYING" \
  	"$pkgdir/usr/share/licenses/$pkgname"
}

check() {
  cd $pkgname-$pkgver
  make check
}
