# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=appstream-glib
pkgver=0.7.2
pkgrel=1
pkgdesc="Objects and methods for reading and writing AppStream metadata"
url="https://people.freedesktop.org/~hughsient/appstream-glib/"
arch=(i686 x86_64)
license=(LGPL)
depends=(gtk3 libyaml pacman gcab libsoup libstemmer)
makedepends=(gobject-introspection gtk-doc autoconf-archive git gperf meson)
provides=(appdata-tools)
conflicts=(appdata-tools)
replaces=(appdata-tools)
_commit=6149f7eb067c4d267738fd5e7ea5c0cc1be7bc5b  # tags/appstream_glib_0_7_2^0
source=("git+https://github.com/hughsie/appstream-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^appstream_glib_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname \
    -Denable-alpm=true \
    -Denable-gtk-doc=true \
    -Denable-rpm=false
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
