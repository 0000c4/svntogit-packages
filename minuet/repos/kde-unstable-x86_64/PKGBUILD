# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=minuet
pkgver=16.11.80
pkgrel=1
pkgdesc='A KDE Software for Music Education'
url='https://minuet.kde.org'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(plasma-framework fluidsynth qt5-quickcontrols2 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('592222f8d058fcca7c7f115b39e0d6b1960302b5'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
