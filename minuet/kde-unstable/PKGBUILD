# $Id: PKGBUILD 261475 2016-03-15 10:26:40Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=minuet
pkgver=16.03.80
pkgrel=1
pkgdesc='A KDE Software for Music Education'
url='https://minuet.kde.org'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(kio drumstick)
makedepends=(extra-cmake-modules kdoctools)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('d019e1723ec63bf49b26b26fa36248bdd2c7a778')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
