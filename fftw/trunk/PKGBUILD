# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fftw
pkgver=3.1.3
pkgrel=1
pkgdesc="A C subroutine library for computing the discrete Fourier transform (DFT)"
arch=("i686" "x86_64")
license=('GPL')
url="http://www.fftw.org/"
depends=('glibc')
options=(!libtool)
source=(http://www.fftw.org/${pkgname}-${pkgver}.tar.gz)
md5sums=('a3dcba0d30880118310b86833023eea3')
install=fftw.install

# http://www.fftw.org/fftw2_doc/fftw_6.html#SEC69
# http://www.fftw.org/faq/section2.html#singleprec
# http://www.fftw.org/fftw3_doc/Precision.html#Precision

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # build + install double precision
  ./configure F77=gfortran \
              --prefix=/usr \
              --enable-type-prefix \
	      --enable-shared
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  make clean

  # build + install single precision
  ./configure F77=gfortran \
              --prefix=/usr \
              --enable-float \
	      --enable-type-prefix \
	      --enable-shared
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

# handle info files
  rm -f $pkgdir/usr/share/info/dir
  gzip $pkgdir/usr/share/info/*
}
