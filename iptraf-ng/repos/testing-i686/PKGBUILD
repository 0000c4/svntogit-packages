# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: sh0 <mee@sh0.org>

pkgname=iptraf-ng
pkgver=1.1.3.1
pkgrel=2
pkgdesc="A console-based network monitoring utility (a fork of original iptraf)"
url="https://fedorahosted.org/iptraf-ng/"
arch=('i686' 'x86_64')
depends=('ncurses')
license=('GPL2')
replaces=('iptraf')
options=('!libtool')
source=(https://fedorahosted.org/releases/i/p/$pkgname/$pkgname-$pkgver.tar.gz
        iptraf-ng-linux-3.5-compat.patch)
md5sums=('1a2c02944b0b012d6a3de96207610fa2'
         'd2ddee5f9ff485155235f7cb4671d8fb')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ${srcdir}/iptraf-ng-linux-3.5-compat.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --sbindir=/usr/bin
  make CFLAGS="$CFLAGS -std=gnu99"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
}
