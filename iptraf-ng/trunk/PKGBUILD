# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: sh0 <mee@sh0.org>

pkgname=iptraf-ng
pkgver=1.1.1
pkgrel=1
pkgdesc="A console-based network monitoring utility (a fork of original iptraf)"
url="https://fedorahosted.org/iptraf-ng/"
arch=('i686' 'x86_64')
depends=('ncurses')
license=('GPL2')
replaces=('iptraf')
options=('!libtool')
source=(https://fedorahosted.org/releases/i/p/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('a48c30c186aafc1e8921b611236b732b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed s/ncurses5-config/ncursesw5-config/ -i configure.ac
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make CFLAGS="$CFLAGS"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
  install -dm755 "$pkgdir/var/lib/iptraf"
}
