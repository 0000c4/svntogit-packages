# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
pkgname=haskell-regex-compat
pkgrel=1
pkgver=0.93.1
pkgdesc="Replaces and enhances Text.Regex"
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/regex-compat"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 sh 'haskell-regex-base' 'haskell-regex-posix')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/regex-compat/$pkgver/regex-compat-$pkgver.tar.gz)
install=haskell-regex-compat.install

build() {
    cd ${srcdir}/regex-compat-$pkgver
    runhaskell Setup configure -p --enable-split-objs --enable-shared --prefix=/usr
    runhaskell Setup build
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
    cd ${srcdir}/regex-compat-$pkgver
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('d6becec95ea8098cab663116fdce60a5')
