# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=esd
pkgver=0.2.39
pkgrel=1
pkgdesc="Enlightened Sound Daemon"
arch=(i686 x86_64)
license=('LGPL')
depends=('audiofile' 'tcp_wrappers' 'alsa-lib')
url="http://www.tux.org/~ricdude/EsounD.html"
backup=('etc/esd.conf')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/esound/0.2/esound-${pkgver}.tar.bz2
	esd-0.2.38-alsa-drain.patch
	esd)
md5sums=('d950b9cd0d7b5406cc4bb16bcdcea8f4'
         '3de93efcd1bc196a3585e6aef50eac48'
         'a4c76e7c7f75b201ea7ab6fb15b47472')

build() {
  cd ${srcdir}/esound-${pkgver}
  patch -Np1 -i ${srcdir}/esd-0.2.38-alsa-drain.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
              --with-audiofile --with-libwrap \
	      --enable-alsa --disable-artstest || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m755 -d ${pkgdir}/etc/rc.d
  install -m755 ${srcdir}/esd ${pkgdir}/etc/rc.d/esd || return 1
}
