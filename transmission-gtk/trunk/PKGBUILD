# $Id$
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=transmission-gtk
pkgver=1.22
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (GTK+ GUI)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
depends=('curl' 'libnotify' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('notification-daemon: Pop-up notifications')
replaces=('transmission')
install=transmission-gtk.install
source=(http://download.m0k.org/transmission/files/transmission-$pkgver.tar.bz2)
md5sums=('aa0a2db19b0f3dbfd75a0fb354d6c47d')

build() {
  cd "$srcdir/transmission-$pkgver"

  ./configure --prefix=/usr --disable-cli --disable-daemon || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  # Resolve conflict with transmission-cli
  rm -f "$pkgdir/usr/bin/benc2php"

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/transmission-gtk/LICENSE" || return 1
}
