# $Id$
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=transmission-gtk
pkgver=1.32
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (GTK+ GUI)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
depends=('curl' 'libnotify' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('notification-daemon: Desktop notification support')
replaces=('transmission')
install=transmission-gtk.install
source=(http://download.m0k.org/transmission/files/transmission-$pkgver.tar.bz2)
md5sums=('2525872f5c95886243f83ed513e3952b')

build() {
  cd "$srcdir/transmission-$pkgver"

  ./configure --prefix=/usr --disable-cli --disable-daemon || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  # Remove web interface
  rm -rf "$pkgdir/usr/share/transmission" || return 1

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/transmission-gtk/COPYING" || return 1
}
