# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ltrace
pkgver=0.7.1
pkgrel=1
pkgdesc="Tracks runtime library calls in dynamically linked programs"
arch=('i686' 'x86_64')
url="http://ltrace.alioth.debian.org/"
license=('GPL')
depends=('elfutils')
makedepends=('dejagnu')
backup=('etc/ltrace.conf')
source=(https://alioth.debian.org/frs/download.php/3844/$pkgname-$pkgver.tar.bz2)
sha256sums=('9010ab85444c44e2f1b340049b6ce877e41566996f0045af9519ad056ae9b83d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make -k check
}

package(){
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
