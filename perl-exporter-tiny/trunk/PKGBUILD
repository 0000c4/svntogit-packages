# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-exporter-tiny
pkgver=0.044
pkgrel=2
pkgdesc="an exporter with the features of Sub::Exporter but only core dependencies"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/Exporter-Tiny'
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TOBYINK/Exporter-Tiny-$pkgver.tar.gz")
sha512sums=('49f8df450c1e21b56df3ef0f15ef138f21429a2a4c3c0e8809b25c57dc42ae4d28e3db125676ca5d293517d36a81b41fcfe2b3f4a4d37530b5de5f92bd05d03a')
_distdir="Exporter-Tiny-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
