# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=0.4.0
pkgrel=5
pkgdesc="Notification daemon for the desktop notifications framework"
arch=(i686 x86_64)
license=('GPL')
url="http://www.galago-project.org/specs/notification/"
depends=('libsexy>=0.1.11' 'libwnck>=2.28.0' 'gconf>=2.28.0' 'gstreamer0.10-base>=0.10.25' 'libnotify>=0.7.1' 'libglade>=2.6.4' 'hicolor-icon-theme')
groups=('gnome')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
install=notification-daemon.install
source=(http://www.galago-project.org/files/releases/source/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        notification-daemon-0.4.0-libnotify-0.7.patch)
md5sums=('e61eff9782551d81045bb53e8a801d58'
         '1708e98353cab38a7c057b4385826c04')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/notification-daemon-0.4.0-libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/notification-daemon-1.0 \
      --localstatedir=/var --disable-static
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain notification-daemon ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
