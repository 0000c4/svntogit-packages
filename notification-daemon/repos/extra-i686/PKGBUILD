# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=0.7.2
pkgrel=1
pkgdesc="Notification daemon for the desktop notifications framework"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.galago-project.org/specs/notification/"
depends=('gtk3' 'libcanberra')
groups=('gnome')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
source=(http://download.gnome.org/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('74ccbedfe25c9559278ba35ef716125ada9ba0074fe7321a2271abfed5990661')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/notification-daemon-1.0 \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
