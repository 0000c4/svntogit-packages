# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cyrus-sasl
pkgver=2.1.23
pkgrel=5
pkgdesc="SASL authentication daemon"
arch=('i686' 'x86_64')
license=('custom')
url="http://asg.web.cmu.edu/cyrus/download/"
depends=('pam>=1.0.1-2' 'krb5' 'libldap' 'cyrus-sasl-plugins' 'db>=5.0')
replaces=(cyrus-sasl-mysql cyrus-sasl-pgsql)
conflicts=(cyrus-sasl-mysql cyrus-sasl-pgsql)
backup=(etc/conf.d/saslauthd)
source=(ftp://ftp.andrew.cmu.edu/pub/cyrus-mail/${pkgname}-${pkgver}.tar.gz
        saslauthd
        saslauthd.conf.d
        cyrus-sasl-2.1.23-gcc4.patch
        cyrus-sasl-2.1.23+db-5.0.patch)
md5sums=('2eb0e48106f0e9cd8001e654f267ecbc'
         '697dfb51206c398bc976ce9f4cffe72d'
         '96d8a2f6189501f8044838e04d5cae7f'
         '3a71688df7d5724cd55a8de17d74f34e'
         '35c189c8e93ad37e3ae3c49386fdeb2c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix building with db v5.x
  patch -Np1 -i ../cyrus-sasl-2.1.23+db-5.0.patch

  # Fix error: #elif with no expression
  patch -Np1 -i ../cyrus-sasl-2.1.23-gcc4.patch

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-ldap=/usr --with-saslauthd=/var/run/saslauthd \
    --disable-krb4 --with-gss_impl=mit --disable-otp
  cd saslauthd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/saslauthd"
  make DESTDIR="${pkgdir}" install
  make testsaslauthd
  install -m755 testsaslauthd "${pkgdir}/usr/sbin"

  install -dm766 "${pkgdir}/var/run/saslauthd"
  install -Dm755 "${srcdir}/saslauthd" "${pkgdir}/etc/rc.d/saslauthd"
  install -Dm644 "${srcdir}/saslauthd.conf.d" "${pkgdir}/etc/conf.d/saslauthd"

  install -Dm644 ../COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl/COPYING"
}
