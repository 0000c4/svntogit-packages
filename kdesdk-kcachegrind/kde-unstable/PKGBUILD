# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kcachegrind
pkgver=16.12.3
pkgrel=1
pkgdesc='Visualization of Performance Profiling Data'
url='https://kde.org/applications/development/kcachegrind/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
optdepends=('php: PHP support' 'graphviz: for plotting support')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kcachegrind-${pkgver}.tar.xz"{,.sig})
sha256sums=('f87452fdf64beb3a0601bcdcac0dca2c13329e6149de26a5fe3f9af42d493e81'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcachegrind-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/hotshot2calltree
}
