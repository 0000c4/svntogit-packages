# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>
# Contributor: sergeantspoon <sergeantspoon@archlinux.us>

pkgname=libssh
pkgver=0.4.8
pkgrel=1
pkgdesc="Library for accessing ssh client services through C libraries"
url="http://www.libssh.org/"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('openssl')
makedepends=('cmake' 'doxygen')
source=("http://www.libssh.org/files/0.4/${pkgname}-${pkgver}.tar.gz")
md5sums=('d97b3feea1abe047ca8cf86d06e4b789')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package(){
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
