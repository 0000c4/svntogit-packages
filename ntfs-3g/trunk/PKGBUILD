# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bächler <thomas.archlinux.org>

pkgname=ntfs-3g
pkgver=2009.11.14
pkgrel=1
pkgdesc="Stable read and write NTFS driver"
url="http://www.ntfs-3g.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse>=2.8.0')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://www.ntfs-3g.org/${pkgname}-${pkgver}.tgz
        25-ntfs-config-write-policy.fdi)
md5sums=('68a8d622bb6ef900b8bb64cdffad48f8'
         '9dca69d78310f8635877984167eb515f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr \
    --with-fuse=external --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs" || return 1
  install -m755 -d "${pkgdir}/usr/share/hal/fdi/policy/10osvendor"
  install -m644 "${srcdir}/25-ntfs-config-write-policy.fdi" "${pkgdir}/usr/share/hal/fdi/policy/10osvendor/" || return 1

}
