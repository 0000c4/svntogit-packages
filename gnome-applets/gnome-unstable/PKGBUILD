# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=2.27.4
pkgrel=1
pkgdesc="GNOME Applets"
arch=(i686 x86_64)
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'gnome-panel>=2.27.4' 'libgtop>=2.27.3' 'gucharmap>=2.27.0' 'libnotify>=0.4.5' 'cpufrequtils' 'libgnomekbd>=2.27.4')
makedepends=('gnome-doc-utils>=0.17.3' 'pkgconfig' 'gnome-settings-daemon>=2.27.91' 'intltool' 'gnome-icon-theme>=2.27.90' 'gnome-python-desktop>=2.27.2')
optdepends=('gnome-python-desktop: invest-applet')
options=(!emptydirs)
url="http://www.gnome.org"
groups=(gnome)
install=gnome-applets.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('bdde7391255b8a1771eba8cdd5398e6132c44fabcbe676e270810a054f37fe34')
	
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-applets \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --enable-mixer-applet \
      --disable-schemas-install || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  rm -f ${pkgdir}/usr/lib/*.la

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-applets-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
