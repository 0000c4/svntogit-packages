# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=2.26.1
pkgrel=1
pkgdesc="GNOME Applets"
arch=(i686 x86_64)
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'gnome-panel>=2.26.0' 'libgtop>=2.26.0' 'gucharmap>=2.26.1' 'libnotify>=0.4.5' 'cpufrequtils' 'libgnomekbd>=2.26.0')
makedepends=('gnome-doc-utils>=0.16.0' 'pkgconfig' 'gnome-settings-daemon>=2.26.0' 'intltool' 'gnome-icon-theme')
options=(!emptydirs)
url="http://www.gnome.org"
groups=(gnome)
install=gnome-applets.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ef6c44bba892d28b46cf19fcb2e71d61')
	
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/gnome-applets \
              --localstatedir=/var --disable-static \
	      --disable-scrollkeeper --enable-mixer-applet || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  rm -f ${pkgdir}/usr/lib/*.la

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-applets-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
