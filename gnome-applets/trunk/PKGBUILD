# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.1.1
pkgrel=1
pkgdesc="GNOME Applets"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-panel' 'libgtop' 'gucharmap' 'libnotify' 'cpufrequtils')
makedepends=('gnome-doc-utils' 'intltool' 'gnome-settings-daemon' 'networkmanager' 'gnome-common')
options=(!emptydirs)
url="http://www.gnome.org"
groups=(gnome)
install=gnome-applets.install
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}-20110506.tar.xz)
sha256sums=('eb19a412e804975d614255520f4c7c6477d927e8d5cb6abe217008ab62ab857b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc --libexecdir=/usr/lib/gnome-applets \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --disable-schemas-install \
      --without-hal
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-applets-3.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
