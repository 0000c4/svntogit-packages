# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=2.32.1.1
pkgrel=2
pkgdesc="GNOME Applets"
arch=(i686 x86_64)
license=('GPL')
depends=('gstreamer0.10-base-plugins>=0.10.30' 'gnome-panel>=2.31.92' 'libgtop>=2.28.1' 'gucharmap>=2.32.0' 'libnotify>=0.7.1' 'cpufrequtils')
makedepends=('gnome-doc-utils>=0.20.1' 'pkgconfig' 'gnome-settings-daemon>=2.31.92' 'intltool' 'gnome-icon-theme>=2.31.0' 'gnome-python-desktop>=2.31.1' 'networkmanager>=0.8')
optdepends=('python-gnomeapplet: invest-applet')
options=('!emptydirs' '!libtool')
url="http://www.gnome.org"
groups=(gnome)
install=gnome-applets.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        gnome-applets-2.32.1.1-libnotify-0.7.patch
        add-dbus-glib.patch)
sha256sums=('2b92fe4b3062dfbe264f45472b4db3a27d1e69e13260d37da9fb36b2cbd40327'
            '1e0b1f9545029bda0f4aadc28ef6eba34a5dbd1c3d1e5cec45627be7b0c32eda'
            '6c79b77aa731f82d9b7c9704e5c6d3cb06252eea3c2d21fa35245d2f02ca0346')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/add-dbus-glib.patch"
  patch -Np0 -i "${srcdir}/gnome-applets-2.32.1.1-libnotify-0.7.patch"
  autoreconf -fi
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-applets \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --enable-mixer-applet \
      --disable-schemas-install --without-hal
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    $pkgdir/usr/bin/invest-chart \
    $pkgdir/usr/lib/gnome-applets/invest-applet \
    $pkgdir/usr/lib/python2.7/site-packages/invest/chart.py

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-applets-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
