# $Id: PKGBUILD 290221 2017-03-09 16:12:45Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcachegrind
pkgver=17.04.1
pkgrel=1
pkgdesc='Visualization of Performance Profiling Data'
url='https://kde.org/applications/development/kcachegrind/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python qt5-tools)
optdepends=('php: PHP support' 'graphviz: for plotting support')
conflicts=(kdesdk-kcachegrind)
replaces=(kdesdk-kcachegrind)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('eea7557a38ec5d9d8015a9025450180ee80f3b834518706dde0e4b37cee91878'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "$pkgdir"/usr/bin/hotshot2calltree
}
