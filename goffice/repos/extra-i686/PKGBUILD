# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=goffice
pkgver=0.10.29
pkgrel=1
pkgdesc="A library of document-centric objects and utilities built on top of GLib and Gtk+"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'gobject-introspection')
depends=('librsvg' 'gtk3' 'libgsf' 'libspectre' 'libxslt' 'lasem')
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('b395dcf062f8575788aeab087b66ae21c2e22a7661309adc81d1825b507b7ff9')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-introspection

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
