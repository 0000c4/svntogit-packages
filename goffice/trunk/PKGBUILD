# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=goffice
pkgver=0.8.15
pkgrel=1
pkgdesc="A library of document-centric objects and utilities built on top of GLib and Gtk+"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
options=('!libtool')
depends=('gtk2' 'gconf' 'libgsf')
makedepends=('pkgconfig' 'intltool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('6a6ddf9821ff1547ef1977c276fa5a7c6842a1b4cb90c923d600283260aecf58')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
