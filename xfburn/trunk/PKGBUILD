# $Id$
# Maintainer: Tobias Kieslich <tobias funnychar archlinux.org>
# Contributor: Alois Nespor alois.nespor@gmail.com

pkgname=xfburn
pkgver=0.4.3
pkgrel=5
arch=('i686' 'x86_64')
pkgdesc="a simple CD/DVD burning tool based on libburnia libraries"
url="http://goodies.xfce.org/projects/applications/xfburn"
license=('GPL2')
groups=('xfce4-goodies')
install=${pkgname}.install
depends=('libburn>=1.0.6.pl00' 'libisofs>=1.0.6' 'libxfcegui4>=4.8.1' 'exo>=0.6.0' 'gstreamer0.10-base>=0.10.26'
         'hicolor-icon-theme' 'desktop-file-utils' 'librsvg')
makedepends=('intltool')
source=(http://www.xfce.org/archive/src/apps/xfburn/0.4/$pkgname-$pkgver.tar.bz2
	fix_empty_dir_segfault.diff)
md5sums=('147cdc2d909e751125be16103b8dc81f'
         '82a85be2442c42ab93de95e21c6c11b0')

build() { 
  cd ${srcdir}/$pkgname-$pkgver

  # fix https://bugs.archlinux.org/task/26121
  patch -Np0 -i ${srcdir}/fix_empty_dir_segfault.diff
  
  ./configure --prefix=/usr \
	--enable-gstreamer \
	--enable-dbus \
	--disable-hal
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
} 
