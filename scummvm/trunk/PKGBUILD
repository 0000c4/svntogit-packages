# $Id$
# Maintainer: juergen <juergen@hoetzel.info>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=1.3.1
pkgrel=2
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libmad' 'sdl' 'fluidsynth')
makedepends=('libmpeg2')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('87e5076e93bfa762f1c5d7f45b41d4e2')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --backend=sdl --disable-debug \
    --enable-mpeg2 --with-mpeg2-prefix=/usr \
    --with-flac-prefix=/usr \
    --with-fluidsynth-prefix=/usr \
    --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  install -Dm755 scummvm "${pkgdir}"/usr/bin/scummvm
  install -Dm644 dists/scummvm.6 "${pkgdir}"/usr/share/man/man6/scummvm.6
  # fixes FS#9118 
  install -Dm644 dists/${pkgname}.desktop \
	"${pkgdir}"/usr/share/applications/${pkgname}.desktop
  install -Dm644  icons/scummvm.xpm \
	"${pkgdir}"/usr/share/pixmaps/scummvm.xpm

  install -Dm644 gui/themes/scummmodern.zip "${pkgdir}"/usr/share/scummvm/scummmodern.zip
}
