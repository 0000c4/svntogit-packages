# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributer: Ben <contrasutra@myrealbox.com>

pkgname=sound-juicer
pkgver=2.24.0
pkgrel=1
pkgdesc="A cd ripper application"
arch=(i686 x86_64)
license=('GPL')
depends=('taglib>=1.5' 'libcdio>=0.80' 'musicbrainz>=2.1.5'
	 'gnome-media>=2.24.0' 'gstreamer0.10-base-plugins'
	 'nautilus-cd-burner>=2.24.0')
makedepends=('perlxml' 'gnome-doc-utils>=0.14.0' 'pkgconfig')
options=(!emptydirs)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
groups=('gnome-extra')
install=sound-juicer.install
url="http://www.burtonini.com/blog/computers/sound-juicer"
md5sums=('504979aadec8df1e59c1050bc968b2aa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --enable-hal \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
