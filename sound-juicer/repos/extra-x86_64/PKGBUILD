# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=sound-juicer
pkgver=3.4.0
pkgrel=1
pkgdesc="A cd ripper application"
arch=(i686 x86_64)
license=('GPL')
depends=('libmusicbrainz4' 'libdiscid' 'gtk3' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'brasero' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
options=(!emptydirs)
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
groups=('gnome-extra')
install=sound-juicer.install
sha256sums=('49805cc73617e745ee09b62fec46979387e5713d39e5f75320b91ee8f0c44f12')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain sound-juicer ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
