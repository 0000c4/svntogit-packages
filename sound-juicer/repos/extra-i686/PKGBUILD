# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=sound-juicer
pkgver=3.20.0
pkgrel=2
pkgdesc="A lean and friendly audio CD extractor for GNOME"
url="https://wiki.gnome.org/Apps/SoundJuicer"
arch=(i686 x86_64)
license=(GPL)
depends=(libmusicbrainz5 libdiscid gtk3 gst-plugins-base gst-plugins-good brasero iso-codes)
makedepends=(intltool itstool)
options=(!emptydirs)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('77e2b75f3f6fa82fc182ad782afe93c1be8d227979c80a7db9c5c54520d50c1a')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-compile-warnings=minimum
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
