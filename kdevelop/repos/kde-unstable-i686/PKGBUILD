# $Id: PKGBUILD 248915 2015-10-12 10:11:59Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=4.90.91
pkgrel=3
pkgdesc='A C/C++ development environment for KDE'
arch=(i686 x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
depends=(kdevplatform clang libksysguard)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt clang llvm qt5-tools plasma-framework krunner okteta python)
optdepends=('konsole: embedded terminal'
	'git: Git support'
	'subversion: SVN support'
	'cvs: CVS support'
	'gdb: GNU Debugger support'
	'qt5-doc: Qt documentation integration'
	'cmake: cmake integration'
	'qt5-tools: qthelp plugin'
 	'okteta: hex editor integration'
	'plasma-framework: for the plasma addons')
conflicts=(kdevelop-qmljs)
replaces=(kdevelop-qmljs)
source=("http://download.kde.org/unstable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('b56ba171d6ec42259664c088d80588a765d9c132')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
