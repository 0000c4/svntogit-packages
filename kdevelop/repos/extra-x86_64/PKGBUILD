# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=4.4.0
pkgrel=2
pkgdesc="A C/C++ development environment for KDE"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdebase-workspace' 'kdevplatform' 'kdebase-katepart')
makedepends=('cmake' 'automoc4' 'kdesdk-okteta')
optdepends=('kdesdk-okteta: hex editor'
            'kdebase-konsole: embedded terminal'
            'git: git support'
            'gdb: gdb support'
            'qt-doc: qt documentation integration')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('271a6ab3dd6063175e9ce18810bed7437d451ecf')

build() {
  mkdir build
  cd build 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
