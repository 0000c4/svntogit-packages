# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libmikmod
pkgver=3.3.9
pkgrel=1
pkgdesc="A portable sound library"
license=(GPL LGPL)
url="http://mikmod.sourceforge.net"
arch=(i686 x86_64)
depends=(libpulse sh)
makedepends=(alsa-lib)
source=(http://downloads.sourceforge.net/mikmod/$pkgname-$pkgver.tar.gz)
sha256sums=('3b65c1a5cae07a2771cbb82fc70ac75d99f4b3e35ac1d4cd2177865093df2893')

prepare() {
  mkdir build
}

build() {
  cd build
  ../$pkgname-$pkgver/configure \
    --prefix=/usr \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

