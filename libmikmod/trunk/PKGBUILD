# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libmikmod
pkgver=3.3.11.1
pkgrel=3
pkgdesc="A module player and library supporting many formats, including mod, s3m, it, and xm"
license=('GPL2' 'LGPL2.1')
url="http://mikmod.sourceforge.net"
arch=('x86_64')
depends=('alsa-lib' 'glibc' 'libpulse' 'sh')
makedepends=('cmake')
provides=('libmikmod.so')
source=("https://downloads.sourceforge.net/mikmod/$pkgname-$pkgver.tar.gz")
sha512sums=('f2439e2b691613847cd0787dd4e050116683ce7b05c215b8afecde5c6add819ea6c18e678e258c0a80786bef463f406072de15127f64368f694287a5e8e1a9de')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -B build \
        -S .
  make -C build VERBOSE=1
}

package() {
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir" install
  install -vDm 644 {NEWS,README,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
