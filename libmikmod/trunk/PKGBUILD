# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libmikmod
pkgver=3.3.2
pkgrel=1
pkgdesc="A portable sound library"
license=(GPL LGPL)
url="http://mikmod.shlomifish.org/"
arch=(i686 x86_64)
depends=(openal sh)
makedepends=(alsa-lib)
options=('!libtool')
install=$pkgname.install
source=($url/files/$pkgname-$pkgver.tar.gz)
sha256sums=('2311b209255bf24e95161907a16778cb054ac6d447fd8d05f1f0e41a555c7580')

prepare() {
  mkdir build
}

build() {
  cd build
  ../$pkgname-$pkgver/configure \
    --prefix=/usr \
    --enable-openal \
    --disable-static
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

