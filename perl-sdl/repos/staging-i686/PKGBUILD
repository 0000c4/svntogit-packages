# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=perl-sdl
pkgver=2.536
pkgrel=1
pkgdesc="Simple DirectMedia Layer for Perl"
arch=(i686 x86_64)
license=(LGPL)
url="http://sdl.perl.org"
depends=(perl-alien-sdl perl-tie-simple
         sdl_{net,ttf,image,mixer,gfx,pango} mesa smpeg)
#makedepends=(perl-yaml)
options=('!emptydirs')
replaces=(sdl_perl)
conflicts=(sdl_perl)
provides=("sdl_perl=$pkgver")
# http://search.cpan.org/dist/SDL/
_author=FROGGS
source=(http://search.cpan.org/CPAN/authors/id/${_author:0:1}/${_author:0:2}/$_author/SDL-$pkgver.tar.gz)
md5sums=('5c682e8a96cd655e959497b034b978ce')

build() {
  cd "$srcdir/SDL-$pkgver"
  # Perl selects the proper CFLAGS set for binary modules
  unset CFLAGS
  # install module in vendor directories
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/SDL-$pkgver"
  perl Build test || true
}

package() {
  cd "$srcdir/SDL-$pkgver"
  perl Build install destdir="$pkgdir"
}
