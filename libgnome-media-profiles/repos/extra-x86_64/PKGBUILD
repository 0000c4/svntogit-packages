# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=libgnome-media-profiles
pkgver=3.0.0
pkgrel=3
pkgdesc="Library for management of audio recording profiles"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
depends=('gconf' 'gtk3' 'gstreamer0.10-base-plugins')
makedepends=('intltool' 'gnome-doc-utils')
options=('!emptydirs' '!libtool')
install=libgnome-media-profiles.install
source=(http://download.gnome.org/sources/${pkgname}/3.0/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('ab1288e53a8fccca3e8fa59757ef586901e236e09a737a1178eeb8d74428c061')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-static -disable-scrollkeeper \
    --disable-schemas-install

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-audio-profiles "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
