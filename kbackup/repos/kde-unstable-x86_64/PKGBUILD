# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: BluePeril <blueperil (at) blueperil _dot_ de>

pkgname=kbackup
pkgver=20.11.90
pkgrel=1
pkgdesc="A program that lets you back up any directories or files"
arch=(x86_64)
url="https://www.kde.org/applications/utilities/kbackup/"
license=(GPL)
groups=(kde-applications kde-utilities)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2f5ed5c4b62c5b8c30a729ac1348d061bfc4be13ed8d41d2259f67cb28338aa3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>


build() {

  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
