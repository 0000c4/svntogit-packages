# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=0.8.6
pkgrel=2
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('mkinitcpio-busybox>=1.19.4-2' 'kmod>=3' 'util-linux>=2.21' 'libarchive' 'coreutils'
         'bash' 'findutils' 'sed' 'grep' 'filesystem>=2011.10-1' 'file' 'gzip')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('69863edd6cf27f2a29d2b283415c75fb')
sha256sums=('751a0319d6791612cb372f1b5a7c33e85ba98e94bb2ffe642645e081328b6ea7')

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # compat symlink
  install -dm755 "$pkgdir/sbin"
  ln -s /usr/bin/mkinitcpio "$pkgdir/sbin/mkinitcpio"

  # moved to pcmciautils:
  rm "$pkgdir/usr/lib/initcpio/install/pcmcia"
}

