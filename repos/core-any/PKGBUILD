# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.12.2
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
install=automake.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        automake-1.12.2-aclocal7-test-fix.diff)
md5sums=('56c3ab38a5ed8f2b825341b4e007372a'
         '09e754ad7198853dc6abe22cc5e6ba4b'
         '12280109fc55cfb6197feecfbe7ab513')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p2 -i $srcdir/automake-1.12.2-aclocal7-test-fix.diff
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
