# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=0.2.2
pkgrel=3
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('i686' 'x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('libgssglue')
backup=('etc/netconfig')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2
       libtirpc-0.2.1-fortify.patch
       libtirpc-0.2.3rc3.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # add various fixes from git
  patch -p1 -i ../libtirpc-0.2.3rc3.patch
  # avoid overflow on the other senses if hp_length is less than four
  # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-libs/libtirpc/files/libtirpc-0.2.1-fortify.patch?diff_format=c&view=log
  patch -p1 -i ../libtirpc-0.2.1-fortify.patch
  sh autogen.sh
  autoreconf -fisv
  ./configure --prefix=/usr --enable-gss --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 doc/etc_netconfig "${pkgdir}"/etc/netconfig
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('74c41c15c2909f7d11d9c7bfa7db6273'
         '2e5c067f1651469dfbbdc91d3c9c60e8'
         'f043df94804603c2c77ed40f09fad7bb')
