# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=0.2.1
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=(gnome-extra)
depends=('clutter-gtk' 'clutter-gst' 'gjs' 'libmusicbrainz3' 'libwebkit3' 'gtksourceview3' 'evince')
makedepends=('intltool' 'gobject-introspection')
options=(!libtool)
source=(http://download.gnome.org/sources/$pkgname/0.2/$pkgname-$pkgver.tar.xz)
sha256sums=('b18d166a52738bceeca178318212ef4bd6a5562122f105df40bcd436621cbdb1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --libexecdir=/usr/lib/sushi
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
