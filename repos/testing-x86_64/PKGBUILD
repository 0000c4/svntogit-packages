# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-dictionary
pkgver=3.26.0
pkgrel=1
pkgdesc="Look up words in dictionary sources"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Dictionary"
license=('GPL2')
depends=('dconf' 'gtk3')
makedepends=('itstool' 'docbook-xsl' gtk-doc yelp-tools git gobject-introspection meson appstream-glib)
groups=('gnome')
_commit=94bdad93b33e40815e6e63a3ed89d3ecd84ff53d  # tags/3.26.0^0
source=("git+https://git.gnome.org/browse/gnome-dictionary#commit=$_commit")
sha256sums=('SKIP')

check() {
  cd build
  meson test
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
