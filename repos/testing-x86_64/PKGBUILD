# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libphonenumber
pkgver=8.8.8
pkgrel=2
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64)
license=(Apache)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest git 'java-environment>=9' ninja)
_commit=50bb3905f6269b7fe640b065baea74e9b04f9a44  # tags/v8.8.8^0
source=("git+$url#commit=$_commit" libdir.diff)
sha256sums=('SKIP'
            '289949d0d24265aadb960b513543de21a29350342a40cab494a141dbec8ce39b')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  patch -Np1 -i ../libdir.diff
}

build() {
  cd build
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  CXXFLAGS+=" -Wno-error=unused-variable"  # this is nuts
  cmake ../$pkgname/cpp -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

check() {
  cd build
  ninja tests
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
