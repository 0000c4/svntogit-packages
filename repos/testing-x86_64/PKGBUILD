# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.26.0
pkgrel=1
pkgdesc='Advanced embeddable text editor'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kparts' 'libgit2')
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        'pkgbuild-syntax-highlight.patch')
md5sums=('3c44e3cdd5aeb599ebdcdf6d97c50489'
         '489275f1330fe43e4e24e9c5b3c842e1')

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}"/pkgbuild-syntax-highlight.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
