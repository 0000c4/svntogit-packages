# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.22.0+3+gab2f4c8
pkgrel=1
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
         json-glib libgl libsm libxtst systemd)
makedepends=(intltool mesa gtk-doc xtrans git gnome-common python)
options=('!emptydirs')
url="http://www.gnome.org"
groups=(gnome)
_commit=ab2f4c857763ca2cb610d381c327a1884d20b055
source=("git://git.gnome.org/gnome-session#commit=$_commit"
        timeout.patch)
sha256sums=('SKIP'
            '9eaf31857b41db417475c3b14adc11b10c8226ed76978cdf96dd648fa6e505fc')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch

  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-session \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
