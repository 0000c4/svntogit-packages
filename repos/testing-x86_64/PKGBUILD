# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=cryptsetup
pkgver=1.5.0
pkgrel=2
pkgdesc="Userspace setup tool for transparent encryption of block devices using dm-crypt"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'util-linux')
options=('!libtool' '!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2.asc
        encrypt_hook
        encrypt_install)

build() {
  cd "${srcdir}"/$pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static --enable-cryptsetup-reencrypt
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
}
md5sums=('0fa7ba8923b0ce8eed2aa65f2cb9950c'
         'efe1b74dfadf37726397fd27ad0df965'
         '4dab1b0e23d81907b79b49c2d8d719b5'
         '21c45f9cab3e0b5165f68358884fbd0f')
