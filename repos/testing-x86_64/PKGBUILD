# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=grep
pkgver=2.7
pkgrel=1
pkgdesc="A string search utility"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/grep/grep.html"
groups=('base')
depends=('glibc' 'pcre' 'sh')
makedepends=('texinfo')
install=${pkgname}.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('6dd9931a52501519d7779a27cf953326')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --bindir=/bin --without-included-regex
  make
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
