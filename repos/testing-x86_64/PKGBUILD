# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-photos
pkgver=3.22.1
pkgrel=1
pkgdesc="Access, organize, and share your photos on GNOME"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Photos"
license=(GPL)
depends=(exempi gtk3 libexif librsvg babl gegl gnome-desktop gnome-online-accounts lcms2 tracker
         grilo gfbgraph libgdata gnome-online-miners dleyna-server dleyna-renderer libgexiv2 libraw)
makedepends=(intltool itstool python yelp-tools autoconf-archive git)
groups=(gnome-extra)
_commit=ed68bbca704daed0fa52ae780067d4630997a54b  # tags/3.22.1^0
source=("git://git.gnome.org/gnome-photos#commit=$_commit"
        "git://git.gnome.org/libgd")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.libgd.url "$srcdir/libgd"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-compile-warnings=minimum
  make
}

check() {
  cd $pkgname
  # disabled: needs dogtail which we don't have
  #make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
