# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pciutils
pkgver=3.1.7
pkgrel=3
pkgdesc="PCI bus configuration space access library and tools"
arch=(i686 x86_64)
license=('GPL2')
groups=('base')
url="http://mj.ucw.cz/pciutils.html"
optdepends=('sh: required by update-pciids')
makedepends=('wget')
depends=('glibc')
source=(ftp://ftp.kernel.org/pub/software/utils/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('35222edb86b50a6641352c66fe829b2e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./update-pciids.sh
  make OPT="$CFLAGS -fPIC -DPIC" ZLIB=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=${pkgdir}/usr SHAREDIR=${pkgdir}/usr/share/hwdata MANDIR=${pkgdir}/usr/share/man install
  install -d ${pkgdir}/usr/lib
  install -m644 lib/libpci.a ${pkgdir}/usr/lib
  for i in config.h header.h pci.h types.h; do
    install -D -m 644 lib/${i} ${pkgdir}/usr/include/pci/${i}
  done
}
