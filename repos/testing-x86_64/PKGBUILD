# $Id$
# Maintainer:
# Contributor: François Charette <francois ατ archlinux δοτ org>
# Contributor: Jason Chu <jchu@xentac.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=perl-tk
_cpanname=Tk
pkgver=804.030
pkgrel=5
pkgdesc="A graphical user interface toolkit for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_cpanname}"
license=('PerlArtistic' 'GPL' 'custom')
depends=('libpng' 'libjpeg' 'tk' 'perl')
options=('!emptydirs')
replaces=('perltk')
provides=('perltk')
source=(http://www.cpan.org/authors/id/S/SR/SREZIC/${_cpanname}-${pkgver}.tar.gz
        0001-Fix-compilation-under-threaded-bleadperl.patch
        0001-fix-for-5.17.7-and-newer-SvUPGRADE-is-no-longer-an-e.patch)
md5sums=('13275e85f99ee467a86d9598a437abff'
         '361c34978746310e5c31b7a47c016b22'
         '46448cdac9d7159f15f2d4b2719d171b')

prepare() {
  cd "$srcdir/${_cpanname}-$pkgver"

  # https://rt.cpan.org/Public/Bug/Display.html?id=78824
  patch -Np1 -i "$srcdir/0001-Fix-compilation-under-threaded-bleadperl.patch"

  # https://rt.cpan.org/Public/Bug/Display.html?id=82677
  patch -Np1 -i "$srcdir/0001-fix-for-5.17.7-and-newer-SvUPGRADE-is-no-longer-an-e.patch"
}

build() {
  cd "$srcdir/${_cpanname}-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

#check() {
#  cd "$srcdir/${_cpanname}-$pkgver"
#  make test
#}

package() {
  cd "$srcdir/${_cpanname}-$pkgver"
  make install DESTDIR="$pkgdir"

  # license
  install -D -m644 pTk/license.terms \
                   "${pkgdir}/usr/share/licenses/${pkgname}/tk.license"
  install -D -m644 pTk/Tix.license \
                   "${pkgdir}/usr/share/licenses/${pkgname}/tix.license"
}
