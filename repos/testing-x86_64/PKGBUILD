# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-openconnect
pkgver=1.1.93
pkgrel=1
pkgdesc="NetworkManager VPN plugin for OpenConnect"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/projects/NetworkManager/"
depends=(libnm-gtk libsecret openconnect)
makedepends=(intltool python)
install=networkmanager-openconnect.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-openconnect/${pkgver::3}/NetworkManager-openconnect-${pkgver}.tar.xz)
sha256sums=('7ec8f5776c8cf7d5a8d4aa2607477629a3a92d2ffece47eebd078054bf570624')

build() {
  cd NetworkManager-openconnect-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --enable-more-warnings=yes \
    --disable-static
  make
}

package() {
  cd NetworkManager-openconnect-$pkgver
  make DESTDIR="$pkgdir" install
}
