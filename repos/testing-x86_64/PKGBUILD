# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=plasma-integration
pkgver=5.8.5
pkgrel=5
pkgdesc="Qt Platform Theme integration plugins for the Plasma workspaces"
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kio kwayland libxcursor noto-fonts ttf-hack)
makedepends=(extra-cmake-modules python breeze)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        fix-save-dialog.patch::"https://cgit.kde.org/plasma-integration.git/patch/?id=87b27476")
sha256sums=('cb4da56f940e87e1195a0ba19e07ea3384fed1dcd26ca91dee1d7dee8f00a7ea'
            'SKIP'
            'ff5ca909bafdabb6aeaa259cb4ab00b4ba5d08768dc4c57c304717aff2fbbc3e')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

# Fix open dialog showing instead of save dialog
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-save-dialog.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
