# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

pkgname=irqbalance
_realver=1.0.3
pkgver=1.0.3.20110809
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('i686' 'x86_64')
url="http://code.google.com/p/irqbalance"
license=('GPL')
depends=(glib2 numactl libcap-ng)
makedepends=(pkgconfig)
backup=(etc/conf.d/irqbalance)
source=(http://irqbalance.googlecode.com/files/irqbalance-$_realver.tar.gz
        irqbalance-2011-08-09.patch
        irqbalance.conf.d
        irqbalance.rc.d
        irqbalance.service)
md5sums=('6f246481d6295bcb9a79751c03207c96'
         '49a5669fc3eb452a5d24abec887f0a6a'
         '336c1ee99818f9ecda1687e34c69fd6b'
         'fb82fc5d267d39110baf720d81282a7c'
         '9e82dc471128117982a8dd0c4bd5f246')

build() {
  cd "$srcdir/$pkgname-$_realver"
  patch -Np1 < ../irqbalance-2011-08-09.patch
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$_realver"
  make install DESTDIR="$pkgdir"
  install -D -m644 ../irqbalance.conf.d "$pkgdir"/etc/conf.d/irqbalance
  install -D -m755 ../irqbalance.rc.d "$pkgdir"/etc/rc.d/irqbalance
  install -D -m644 ../irqbalance.service "$pkgdir"/usr/lib/systemd/system/irqbalance.service
}
