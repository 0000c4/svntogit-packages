# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=procps-ng
pkgver=3.3.3
pkgrel=2
pkgdesc="Utilities for monitoring your system and processes on your system"
arch=('i686' 'x86_64')
url="http://gitorious.org/procps"
license=('GPL' 'LGPL')
groups=('base')
depends=('ncurses')
conflicts=('procps')
provides=('procps')
replaces=('procps')
backup=('etc/sysctl.conf')
options=('!libtool')
source=(procps-ng-${pkgver}.tar.gz::http://gitorious.org/procps/procps/archive-tarball/v${pkgver}
        sysctl.conf)
sha1sums=('e78a098f1a3c06722155800cc5cfa0c865af03c0'
          '46215e54fbca366cecd81ac47bc1997f147be462')

build() {
  cd "${srcdir}/procps-procps"
  echo ${pkgver} > .tarball-version
  ./autogen.sh
  ./configure --exec-prefix=/ --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/procps-procps"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/sysctl.conf" "${pkgdir}/etc/sysctl.conf"
}
