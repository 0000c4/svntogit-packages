# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2018f
_tzcode=2018f
_tzdata=2018f
pkgrel=2
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64')
url="http://www.iana.org/time-zones"
license=('GPL')
options=('!emptydirs')
source=(https://www.iana.org/time-zones/repository/releases/tzcode${_tzcode}.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/${pkgname}${_tzdata}.tar.gz{,.asc})
# new signing key is not available via public keyservers
sha512sums=('d4270918fbd8eb786e6f5cec9f06df6a850bbf1ec278f60ce493b6b7e10e2ad1388383edcd869f07bc78b9d99a123d6f54e74105cdd3d5cb6c98e11d3f043727'
            'SKIP'
            'f876729419d45e2b861e564ec7d5940f34fab38c3fedc18852bb800010428c12506f71bde1f20feb23859065118ad4658d97efe89af6f4305cea2beafc515aeb'
            'SKIP')
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34') # Paul Eggert <eggert@cs.ucla.edu>

timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'pacificnew' 'etcetera' 'backward'
           'systemv' 'factory')

prepare() {
  sed -i "s:sbin:bin:g" Makefile
}

build() {
  make
}

package() {
  cd ${srcdir}
  # install tzcode stuff
  make DESTDIR="${pkgdir}" install

  # install tzdata stuff
  ./zic -d ${pkgdir}/usr/share/zoneinfo ${timezones[@]}
  ./zic -d ${pkgdir}/usr/share/zoneinfo/posix ${timezones[@]}
  ./zic -d ${pkgdir}/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}
  
  ./zic -d ${pkgdir}/usr/share/zoneinfo -p America/New_York
  install -m444 -t ${pkgdir}/usr/share/zoneinfo iso3166.tab zone1970.tab zone.tab # zone.tab is depricated and will go soon
  
  # cleanup
  rm "${pkgdir}/etc/localtime"
}
