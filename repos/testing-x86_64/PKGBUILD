# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=tomcat-native
pkgver=1.2.21
pkgrel=1
pkgdesc='Optional component for Tomcat to use certain native resources for performance, compatibility'
arch=('x86_64')
url='https://tomcat.apache.org/native-doc/'
license=('APACHE')
depends=('apr')
makedepends=('java-environment')
options=('!emptydirs')
source=(https://archive.apache.org/dist/tomcat/tomcat-connectors/native/${pkgver}/source/${pkgname}-${pkgver}-src.tar.gz{,.asc})
validpgpkeys=('A9C5DF4D22E99998D9875A5110C01C5A2F6059E7'
              'B3F49CD3B9BD2996DA90F817ED3873F5D3262722')
sha512sums=('7f43f815785e5bc3e43f5b2e9ad1c90576a3c1528d57230328b7175bf475eb5f4e1f91b66fae74ad32cd76f044d7705f98a79109f6f84e5af75dc32bc8830b29'
            'SKIP')

build() {
  cd "${srcdir}/tomcat-native-${pkgver}-src/native"

  ./configure \
    --prefix=/usr \
    --with-java-home=/usr/lib/jvm/default \
    --with-apr=/usr \
    --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src/native"

  make DESTDIR="${pkgdir}" install
}
