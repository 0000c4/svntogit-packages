# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
pkgname=(gtkmm3 gtkmm3-docs)
pkgdesc="C++ bindings for gtk3"
pkgver=3.22.0
pkgrel=1
arch=(i686 x86_64)
depends=(gtk3 pangomm atkmm)
makedepends=(glibmm-docs pangomm-docs atkmm-docs mm-common git)
license=(LGPL)
options=(!emptydirs)
url="http://www.gtkmm.org/"
_commit=0b6096676d878b9225b3897f3d359121cdebbe00
source=("git://git.gnome.org/gtkmm#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtkmm
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtkmm
  ./configure --prefix=/usr --enable-maintainer-mode
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_gtkmm3() {
  cd gtkmm
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_gtkmm3-docs() {
  pkgdesc="Developer documentation for gtkmm3"
  depends=()

  cd gtkmm
  make -C docs DESTDIR="$pkgdir" install
}
