# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.5.0
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.2.1-enable-valid.patch
        freetype-2.3.0-enable-spr.patch
        freetype-2.4.11-enable-sph.patch
        freetype-2.4.13-disable-old-cff-engine.patch)
md5sums=('07b7f54bc32d3a0f8a44ec4586c73d81'
         '214119610444c9b02766ccee5e220680'
         '816dc8619a6904a7385769433c0a8653'
         '2422f8902224fedb46ae9eec08c8ceac'
         '65f1c3e7a461e202bc10765b65f4f6d3')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch"
  #patch -Np1 -i "${srcdir}/freetype-2.4.11-enable-sph.patch" # Disabled for now due to resistance
  patch -Np1 -i "${srcdir}/freetype-2.4.13-disable-old-cff-engine.patch"
}

build() {
  cd "${srcdir}/freetype-${pkgver}"
  ./configure --prefix=/usr --disable-static \
    --without-png
  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
