# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>
# Maintainer: Travis Willard <travis@archlinux.org>
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng
pkgver=1.4.0
pkgrel=2
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${pkgver}-apng.patch.gz
	libpng14-enable-read-dither.patch)
md5sums=('fd73b764e92c711c219d766d03889b64' '1a1a1ac18b7c4a23aa9b1441f2c0f2e9'\
         'd4696db4af61b175b705f51aeb9c8d7e')
sha1sums=('886d651394024b5cab7e9cb5afac41b391f1ccd2' '9c47d62e4ea6e774e008abf4cf8df3461b39fe5d'\
         'ac26267be933abb0f5fbfe632bac22cc5ed4de49')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://hp.vector.co.jp/authors/VA013651/freeSoftware/apng.html
  patch -p1 -i "${srcdir}/libpng-${pkgver}-apng.patch" || return 1

  patch -p1 -i "${srcdir}/libpng14-enable-read-dither.patch" || return 1

  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr || return 1
  make ECHO=echo || return 1
  make ECHO=echo DESTDIR="${pkgdir}" install || return 1

  cd contrib/pngminus
  make ECHO=echo PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
  install -D -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
