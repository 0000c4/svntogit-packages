# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.4.0
pkgrel=1
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('libsystemd' 'consolekit' 'dconf' 'gconf' 'gsettings-desktop-schemas' 'gtk3'
         'hicolor-icon-theme' 'json-glib' 'libgl' 'libsm' 'libxtst' 'polkit-gnome'
         'startup-notification' 'upower')
makedepends=('intltool' 'mesa' 'xtrans')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('bf8ba2ba9d61ad680df9e66ca38f05d6e3bf00c65634bb0dc5e41690862608e1')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-session \
      --disable-schemas-compile --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
