# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2009c
pkgrel=1
_tzcode=2009b
_tzdata=2009c
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
groups=('base')
depends=()
makedepends=()
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch)
md5sums=('5a6aa9cbf477334f31e427cb59adf72f'
         'ec8fa00a07f3e1d598d5d722cc0ab697'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz  || return 1
  tar -xf ${pkgname}${_tzdata}.tar.gz || return 1

  patch -Np1 -i ../Makefile.patch || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm ${pkgdir}/usr/share/zoneinfo/localtime
}
