# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=tomcat-native
pkgver=1.2.17
pkgrel=1
pkgdesc='Optional component for Tomcat to use certain native resources for performance, compatibility'
arch=('x86_64')
url='https://tomcat.apache.org/native-doc/'
license=('APACHE')
depends=('apr')
makedepends=('java-environment')
options=('!emptydirs')
source=(https://archive.apache.org/dist/tomcat/tomcat-connectors/native/${pkgver}/source/${pkgname}-${pkgver}-src.tar.gz{,.asc})
validpgpkeys=('A9C5DF4D22E99998D9875A5110C01C5A2F6059E7'
              'B3F49CD3B9BD2996DA90F817ED3873F5D3262722')
sha512sums=('8fa946855fd14525ec0abe7b09975bbd34d6127352e90730a8afb77e16cd91715417e812a40017fee65939a9ce95faf39a9193222f441cda0ad2eb7f690e77b9'
            'SKIP')

build() {
  cd "${srcdir}/tomcat-native-${pkgver}-src/native"

  ./configure \
    --prefix=/usr \
    --with-java-home=/usr/lib/jvm/default \
    --with-apr=/usr \
    --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src/native"

  make DESTDIR="${pkgdir}" install
}
