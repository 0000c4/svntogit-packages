# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=htop
pkgver=3.0.0
pkgrel=2
pkgdesc='Interactive process viewer'
arch=('x86_64')
url='https://htop.dev/'
license=('GPL')
depends=('ncurses' 'libncursesw.so' 'libnl')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
options=('!emptydirs')
source=("https://github.com/htop-dev/htop/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '0001-Increae-the-size-of-sysfs-power-supply-path-buffers.patch')
sha256sums=('1c0661f0ae5f4e2874da250b60cd515e4ac4c041583221adfe95f10e18d1a4e6'
            'e31d8ab3fc41048b9dce0016cbb8facb6d0e62cf4a91c295b99f268cf120f1fc')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 < ../0001-Increae-the-size-of-sysfs-power-supply-path-buffers.patch

  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-cgroup \
      --enable-delayacct \
      --enable-openvz \
      --enable-unicode \
      --enable-vserver

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
