# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=eog-plugins
pkgver=3.8.0
pkgrel=2
pkgdesc="Plugins for Eye of Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/"
license=('GPL2')
depends=('eog' 'libchamplain' 'libgdata')
makedepends=('intltool')
install=eog-plugins.install
options=('!libtool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('a7e4822c46654f67ef25cbc82e4c955ee84ecae92cf356e15a136032cff35e78')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
