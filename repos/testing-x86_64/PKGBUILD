# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
pkgname=libva
pkgver=1.0.13
pkgrel=1
pkgdesc="Video Acceleration (VA) API for Linux"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libgl' 'libdrm>=2.4.23' 'libxfixes')
makedepends=('mesa')
optdepends=('vdpau-video: VDPAU backend for VA API')
options=('!libtool')
source=(http://cgit.freedesktop.org/libva/snapshot/${pkgname}-${pkgver}.tar.bz2
        libva-dont-install-tests.patch)
md5sums=('221207c386129e02fc05827147af7aa6'
         'd13d1868f2db99bedec1057bbfbd9c61')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/libva-dont-install-tests.patch"
  ./autogen.sh
  ./configure --prefix=/usr  \
    --enable-i965-driver \
    --enable-glx
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
