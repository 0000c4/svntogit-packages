# $Id: PKGBUILD 142367 2011-11-08 21:52:38Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd
pkgver=5.2.12
pkgrel=4
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/dhcpcd/"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
depends=('glibc' 'sh')
backup=('etc/conf.d/dhcpcd' 'etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.bz2" \
        'dhcpcd.conf.d' '0001-Set-hostname-via-proc-if-file-is-available.patch' \
	'0001-set-MTU-via-sysfs-if-file-is-available.patch')
sha1sums=('27378a251705c7888332e6d60eea7805d1f8aeb5'
          'b67b9ce6a2faaca75fea356966a16be2283b7db0'
          'ee85adac972a8172a9d9b3a1bdd46e0430301582'
          'afbed18dc5544f7d0b81e2266b322ca12becf9a4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # remove net-tools dep
  patch -Np1 -i ${srcdir}/0001-Set-hostname-via-proc-if-file-is-available.patch
  patch -Np1 -i ${srcdir}/0001-set-MTU-via-sysfs-if-file-is-available.patch

  # configure variables
  ./configure --libexecdir=/usr/lib/dhcpcd --dbdir=/var/lib/dhcpcd

  # Build
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Create Binary Symlink
  install -d ${pkgdir}/usr/sbin
  ln -sf /sbin/dhcpcd ${pkgdir}/usr/sbin/dhcpcd

  # Install Configuration File used in /etc/rc.d/network
  install -D -m644 ../dhcpcd.conf.d $pkgdir/etc/conf.d/$pkgname

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname
  awk '{if(FNR<27)print $0}' ${srcdir}/${pkgname}-${pkgver}/configure.h \
        >> ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf # Disable ip4vall
}
