# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pstoedit
pkgver=3.61
pkgrel=1
pkgdesc="Translates PostScript and PDF graphics into other vector formats"
arch=('i686' 'x86_64')
url="http://www.pstoedit.net/"
license=('GPL')
depends=('gcc-libs' 'plotutils' 'gd' 'imagemagick')
makedepends=('ghostscript')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/sourceforge/pstoedit/pstoedit-${pkgver}.tar.gz")
sha1sums=('426f3746ecb441caa0db401d5880e1ac04a399d5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's/-pedantic//' configure

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
