# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.34.1+11+gbf485934b
pkgrel=3
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils ctags
         libpeas vte3 vala python-jedi autoconf-archive sysprof flatpak-builder gspell libdazzle
         template-glib jsonrpc-glib python-sphinx webkit2gtk glade python-lxml meson)
makedepends=(llvm gobject-introspection gtk-doc yelp-tools appstream-glib git mm-common
             python-sphinx_rtd_theme)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-code-assistance: Legacy assistance services')
groups=(gnome-extra)
_commit=bf485934b3762c6915419458623b850b4a63c2a8  # gnome-builder-3-34
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  # meson 0.52
  git cherry-pick -n c8b862b491cfbbb4f79b24d7cd90e4fb1f37cb9f
  git cherry-pick -n da26dfbf78468f5ed724e022b300a07862a95833

  # libgit2 0.99
  git cherry-pick -n 582bf0281358437cc35d96d363f0af81a1b21068

  # vala 0.48
  git cherry-pick -n fc4607dec2f3dca5dbfee7a56662856dc5a60254
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D help=true
  ninja -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
