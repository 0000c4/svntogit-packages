# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=procps-ng
pkgver=3.3.2
pkgrel=1
pkgdesc="Utilities for monitoring your system and processes on your system"
arch=('i686' 'x86_64')
url="http://gitorious.org/procps"
license=('GPL' 'LGPL')
groups=('base')
depends=('ncurses')
conflicts=('procps')
provides=('procps')
replaces=('procps')
backup=('etc/sysctl.conf')
options=('!libtool')
source=(http://gitorious.org/procps/procps/archive-tarball/v${pkgver}
        sysctl_man.patch includes_restrict.patch gnu-kbsd-version.patch
	complain_unmounted_proc.patch sig_printf_literal.patch
	makefile_watch_ncurses.patch)
sha1sums=('889692f891e790ea035315ec321cce92d00a8db2'
          '39d6562b83389ec4893de88ec88222f7169fa732'
          'd588825497fef0b057491377421fd8f68730d181'
          '9eb2bb32a13656b5d671f9cffed3094399a26048'
          'bf57f5a1f54a0f4a6d9e99d66b8e447225c3371d'
          '25be10b456d56b8bdf6260276c60738a7d76a34d'
          '9fc24e48f902c19315e2557e4deea5592bbfad7e')

build() {
  cd "${srcdir}/procps-procps"
  ./autogen.sh
  patch -p1 < ../sysctl_man.patch
  patch -p1 < ../includes_restrict.patch
  patch -p1 < ../gnu-kbsd-version.patch
  patch -p1 < ../complain_unmounted_proc.patch
  patch -p1 < ../sig_printf_literal.patch
  patch -p1 < ../makefile_watch_ncurses.patch
  ./configure --exec-prefix=/ --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/procps-procps"
  make DESTDIR="${pkgdir}" install
}
