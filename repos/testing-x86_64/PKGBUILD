# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.3_20181022
pkgrel=1
epoch=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('x86_64')
url="https://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('sh' 'ncurses')
provides=('libdialog.so')
source=(https://invisible-mirror.net/archives/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('d5f9282e701c52e1ae3784ebb33a1ba57202436832688c511414136fcb0dcdb3')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
    --prefix=/usr \
    --enable-nls \
    --with-libtool \
    --with-ncursesw
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}

# vim:set ts=2 sw=2 et:
