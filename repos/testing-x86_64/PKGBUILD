# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2012b
pkgrel=2
_tzcode=2012b
_tzdata=2012b
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.iana.org/time-zones"
license=('GPL')
optdepends=('bash: required by tzselect')
options=('!emptydirs')
source=(#http://www.iana.org/time-zones/repository/releases/${pkgname}${_tzdata}.tar.gz
        ftp://munnari.oz.au/pub/${pkgname}${_tzdata}.tar.gz)
md5sums=('0615fd29def380a917e528433c820368')

timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'pacificnew' 'etcetera' 'backward'
           'systemv' 'factory'
           'solar87' 'solar88' 'solar89')

package() {
  cd ${srcdir}
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo ${timezones[@]}
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/posix ${timezones[@]}
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo -p America/New_York
  install -m444 -t ${pkgdir}/usr/share/zoneinfo iso3166.tab zone.tab
}
