# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libetpan
pkgver=1.6
pkgrel=4
pkgdesc="A portable middleware for email access"
arch=('i686' 'x86_64')
url="http://www.etpan.org/libetpan.html"
license=("custom:etpan")
depends=('libsasl' 'curl' 'expat')
source=($pkgname-$pkgver.tar.gz::https://github.com/dinhviethoa/${pkgname}/archive/${pkgver}.tar.gz
        fix_STARTTLS_negotiation.diff)
md5sums=('1158c4ce291876c2b7bb003cd4b3566e'
         '63c65052f6cd82a23ba9e15b14c85e24')

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#48531 - https://github.com/dinhviethoa/libetpan/commit/7f1f97f4d59d5724af97f4d32424c2841715561c
  patch -Np1 -i ${srcdir}/fix_STARTTLS_negotiation.diff
}

build() {
  cd ${pkgname}-${pkgver}
  
  # generate automake files that are not included in the tarball
#  libtoolize --force --copy
#  aclocal -I m4
#  autoheader
#  autoconf
#  automake --add-missing --foreign --force --copy
  #autoreconf -vfi

  #./configure --prefix=/usr \
  
  ./autogen.sh --prefix=/usr \
    --disable-static \
    --disable-db
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
  cd tests
  make imap-sample
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/$pkgname/license.txt
}
