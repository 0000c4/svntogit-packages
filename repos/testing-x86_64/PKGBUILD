# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgbase=calligra
pkgname=('calligra-devtools'
         'calligra-extras'
         'calligra-filters'
         'karbon'
         'calligra-libs'
         'calligra-plan'
         'calligra-plugins'
         'calligra-sheets'
         'calligra-stage'
         'calligra-words'
         'calligra-l10n')
pkgver=3.0.0.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.calligra-suite.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('extra-cmake-modules' 'kcmutils' 'kdoctools' 'python' 'kdelibs4support' 'knotifyconfig'
             'kdesignerplugin' 'kross' 'ktexteditor' 'threadweaver' 'kactivities' 'khtml' 'boost'
             'libwpg' 'okular' 'kdiagram' 'kreport' 'eigen' 'marble' 'openjpeg' 'pstoedit' 'libspnav' 'cauchy'
             'libvisio' 'libetonyek' 'libodfgen' 'libwps' 'gsl' 'openexr' 'akonadi-contacts' 'kcalcore' 'vc' 'qca-qt5')
groups=('calligra')
source=("http://download.kde.org/stable/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.xz"{,.sig})
md5sums=('c4cffca0a8008b5efd285414eeeddcff'
         'SKIP')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD) # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_calligra-filters() {
  pkgdesc="Filters for the Calligra office suite"
  depends=('calligra-sheets' 'calligra-words' 'karbon' 'libodfgen')
  optdepends=('libwpg: Corel WordPerfect Graphics image importer'
              'libwps: Microsoft Works file word processor format import'
              'libvisio: Microsoft Visio import filter'
              'libetonyek: Apple Keynote import filter'
              'pstoedit: EPS to SVG filter'
              'poppler: PDF to SVG filter'
              'khtml: HTML to ODS filter'
              'openjpeg: PDF to SVG filer')
  cd build/filters
  make DESTDIR="$pkgdir" install
}

package_calligra-libs() {
  pkgdesc="Libraries for the Calligra office suite"
  depends=('akonadi-contacts' 'ilmbase' 'kactivities' 'qca-qt5' 'kross')

  for _d in interfaces libs pics servicetypes; do
    cd "$srcdir"/build/$_d
    make DESTDIR="$pkgdir" install
  done
}

package_calligra-plugins() {
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-filters' 'libspnav' 'kdiagram')
  optdepends=('cauchy: formula shape')

  cd build/plugins
  make DESTDIR="$pkgdir" install
}

package_karbon() {
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs' 'hicolor-icon-theme')
  optdepends=('calligra-filters: import/export filters'
              'calligra-extras: extra calligra tools')
  conflicts=('calligra-karbon')
  replaces=('calligra-karbon')

  cd build/karbon
  make DESTDIR="$pkgdir" install
}

package_calligra-plan() {
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words' 'kreport' 'kcalcore' 'khtml' 'kdiagram')

  cd build/plan
  make DESTDIR="$pkgdir" install
}

package_calligra-stage() {
  pkgdesc="Write presentation documents"
  depends=('calligra-libs' 'qt5-webkit' 'hicolor-icon-theme')

  cd build/stage
  make DESTDIR="$pkgdir" install
}

package_calligra-sheets() {
  pkgdesc="Write spreadsheet documents"
  depends=('calligra-libs' 'gsl' 'kdelibs4support' 'kcmutils' 'knotifyconfig' 'hicolor-icon-theme')
  optdepends=('calligra-filters: import/export filters'
              'calligra-plugins: extra calligra plugins'
              'calligra-extras: extra calligra tools')

  cd build/sheets
  make DESTDIR="$pkgdir" install
}

package_calligra-words() {
  pkgdesc="Word Processor"
  depends=('calligra-libs' 'hicolor-icon-theme')
  optdepends=('calligra-extras: extra calligra tools')

  cd build/words
  make DESTDIR="$pkgdir" install
}

package_calligra-extras() {
  pkgdesc="Calligra suite extras"
  depends=('calligra-libs')

  cd build/extras
  make DESTDIR="$pkgdir" install

  cd ../data
  make DESTDIR="$pkgdir" install
}

package_calligra-devtools() {
  pkgdesc="Calligra developer tools"
  depends=('calligra-sheets' 'calligra-words' 'poppler-qt5')

  cd build/devtools
  make DESTDIR="$pkgdir" install
}

package_calligra-l10n() {
  pkgdesc="Localization for Calligra"
  for _lang in bs ca ca@valencia cs da de el en_gb es et fi fr gl hu it ja kk nb nl \
               pl pt pt_br ru sk sv uk zh_cn zh_tw; do
    conflicts+=(calligra-l10n-$_lang)
    replaces+=(calligra-l10n-$_lang)
  done

  cd build
  mkdir -p install_l10n
  make DESTDIR=install_l10n install
  mkdir -p "$pkgdir"/usr/share
  mv install_l10n/usr/share/locale "$pkgdir"/usr/share/
}
