# $Id$
# Maintainer: tobias <tobias@archlinux.net>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=jack
pkgver=0.118.0
pkgrel=1
pkgdesc="A low-latency audio server"
arch=('i686' 'x86_64')
license=('GPL2' 'custom:LGPL2')
depends=('libsamplerate' 'bash')
makedepends=('doxygen')
url="http://jackaudio.org/"
backup=(etc/conf.d/jack)
options=('!libtool')
provides=("jack-audio-connection-kit=${pkgver}")
replaces=('jack-audio-connection-kit')
install=jack.install
source=(http://jackaudio.org/downloads/jack-audio-connection-kit-${pkgver}.tar.gz
        rc.${pkgname} ${pkgname}.conf)

build() {
	cd ${srcdir}/jack-audio-connection-kit-${pkgver}
	./configure --prefix=/usr --libdir=/usr/lib
	make || return 1
}

package() {
	cd ${srcdir}/jack-audio-connection-kit-${pkgver}
	make DESTDIR=${pkgdir} install
	install -Dm755 ../rc.${pkgname} ${pkgdir}/etc/rc.d/${pkgname}
	install -Dm644 ../${pkgname}.conf ${pkgdir}/etc/conf.d/${pkgname}
	install -Dm644 COPYING.LGPL ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.LGPL
}
md5sums=('d58e29a55f285d54e75134cec8e02a10'
         '7d4422087233b8dfca00474596851fdb'
         'f238e9fb8f9e53c1e59ec977d9af1ad2')
