# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy
pkgver=3.12.12+35+g8311b76
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework"
url="https://wiki.gnome.org/Apps/Empathy"
arch=(i686 x86_64)
license=(GPL2)
depends=(clutter-gst clutter-gtk folks gcr iso-codes libcanberra libpulse webkitgtk
         telepathy-farstream telepathy-glib telepathy-logger telepathy-mission-control geoclue2
         geocode-glib cheese libchamplain libgudev)
makedepends=(intltool itstool docbook-xsl python python2 gnome-common git)
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support')
options=(!makeflags)
groups=(gnome)
_commit=8311b765895d092c178ba81a36dd3956503ef351  # master
source=("git://git.gnome.org/empathy#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EMPATHY_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
