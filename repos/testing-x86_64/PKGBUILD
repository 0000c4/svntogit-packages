# $Id$
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=banshee
pkgver=2.0.0
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=('libxxf86vm' 'gnome-desktop>=2.28' 'mono-addins>=0.3.1' 'notify-sharp-svn' 'boo>=0.8.1' 'libwebkit>=1.3.13' 'gdata-sharp>=1.4' 'gtk-sharp-beans' 'gudev-sharp' 'gkeyfile-sharp' 'taglib-sharp>=2.0.3.7' 'gnome-sharp' 'libmtp>=0.2.0' 'libgpod>=0.7.95' 'mono-zeroconf>=0.8.0' 'gstreamer0.10-base-plugins>=0.10.25.2' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0' 'gnome-doc-utils>=0.17.3')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: CD burning')
options=('!libtool')
install=$pkgname.install
source=(http://download.banshee.fm/$pkgname/stable/$pkgver/$pkgname-$pkgver.tar.bz2)
md5sums=('ecd129a3232bda507b98c90e6f9272cf')
sha1sums=('2b67fda2a15aa7eb5bc2a1e88f7013f3896913a4')

build() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-scrollkeeper \
              --disable-ipod --disable-hal \
              --with-vendor-build-id=ArchLinux
  make
}

package() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
