# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.org>

pkgname=nfs-utils
pkgver=1.1.6
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('i686' 'x86_64')
url='http://nfs.sourceforge.net'
license=('GPL')
backup=('etc/exports' 'etc/conf.d/nfs')
depends=('glibc' 'tcp_wrappers' 'e2fsprogs' 'portmap' 'nfsidmap' 'libevent>=1.3e')
source=(http://downloads.sourceforge.net/nfs/${pkgname}-${pkgver}.tar.bz2
	nfsd
	nfslock
	exports
	nfs.conf.d
	start-statd.patch)
install=nfs-utils.install
md5sums=('b0d1b8ec5c8c081a340cfc77bb8670cd'
         '5a9436e492e68efaf801b9c496684541'
         '5d0d31f27f2319747d4499df11485ae7'
         '1358d29a549dfc37b522c35e823c6813'
         'b4aae6963ec25d226eb2d97214f5bb18'
         '9c8aeff40ca25312328afe3b7fad293f')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  
  # arch specific patch
  patch -Np0 -i $srcdir/start-statd.patch || return 1
  
  ./configure --prefix=/usr --enable-nfsv3 --disable-gss \
    --with-statedir=/var/lib/nfs --sysconfdir=/etc
  
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -D -m 755 $srcdir/nfsd $pkgdir/etc/rc.d/nfsd
  install -D -m 755 $srcdir/nfslock $pkgdir/etc/rc.d/nfslock
  install -D -m 644 $srcdir/exports $pkgdir/etc/exports
  install -D -m 644 $srcdir/nfs.conf.d $pkgdir/etc/conf.d/nfs
  
  install -d $pkgdir/var/lib/nfs/v4recovery
}

