# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
pkgname=libarchive
pkgver=3.0.3
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat')
source=("http://libarchive.googlecode.com/files/libarchive-${pkgver}.tar.gz")
md5sums=('ca4090f0099432a9ac5a8b6618dc3892')
sha256sums=('c5fc7620f74a54b1717e4aed38aee85dc27a988ad1db7640f28eb63a82ea62d7')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  # currently fails on i686 in a minor test involving atime updates (#60)
  make check || :
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
