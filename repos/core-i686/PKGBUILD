# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Thomas Bächler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.2.29
pkgrel=2
pkgdesc='Library for applications dealing with netlink sockets'
arch=(i686 x86_64)
url='http://www.infradead.org/~tgr/libnl/'
license=('GPL')
depends=('glibc')
backup=('etc/libnl/classid' 'etc/libnl/pktloc')
source=(https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz{,.sig}
        fs52778.patch)
sha256sums=('0beb593dc6abfffa18a5c787b27884979c1b7e7f1fd468c801e3cc938a685922'
            'SKIP'
            '63be64e57c830240d851f776f102a437e3762829eb628537a586c839fa9a2306')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728') # Thomas Haller

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/fs52778.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
