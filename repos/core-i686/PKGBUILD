# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pinentry
pkgver=0.8.1
pkgrel=4
pkgdesc="a collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnupg.org/aegypten2"
depends=('ncurses' 'libcap>=2.16')
makedepends=('gtk2' 'qt')
optdepends=('gtk2: for gtk2 backend'
            'qt: for qt4 backend')  
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.gz
        qt4-pinentry-window.patch)
install=pinentry.install

build() {
  cd $srcdir/$pkgname-$pkgver
  #fix: https://bugs.archlinux.org/task/29998
  patch -Np1 -i ../qt4-pinentry-window.patch
  for file in qt4/*.moc; do
       /usr/bin/moc ${file/.moc/.h} > ${file}
   done
  ./configure --prefix=/usr \
	--enable-pinentry-curses \
        --disable-pinentry-gtk \
        --disable-pinentry-qt \
	--enable-pinentry-gtk2 \
        --enable-pinentry-qt4 \
	--enable-fallback-curses
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install 
}
md5sums=('81f99904daee5331eb6738408bb024b6'
         '450b8713fe42f1bd93311ef84767c3d8')
