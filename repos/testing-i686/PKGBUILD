# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgname=ruby
pkgver=1.9.1_p129
_pkgver=1.9.1-p129
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.ruby-lang.org/en/"
depends=('gdbm>=1.8.3' 'db>=4.7.25' 'openssl>=0.9.8k' 'zlib>=1.2.3.3' 'readline>=5.2.013')
makedepends=('tk>=8.5')
optdepends=('tk: for Ruby/TK')
provides=('rubygems' 'rake')
conflicts=('rubygems' 'rake')
source=(ftp://ftp.ruby-lang.org/pub/ruby/1.9/${pkgname}-${_pkgver}.tar.bz2) 
options=('!emptydirs')
md5sums=('6fa62b20f72da471195830dec4eb2013')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr --enable-shared --enable-pthread || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  
  #generate the ri docs - to be split: FS#11638
  #make -j1 DESTDIR="${pkgdir}" install-doc || return 1
  
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}
