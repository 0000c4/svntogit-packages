# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=print-manager
pkgver=17.08.0
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url='https://www.kde.org/applications/utilities/'
license=('GPL' 'LGPL' 'FDL')
depends=('libcups' 'kcmutils')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'plasma-framework')
optdepends=('system-config-printer: auto-detect the printer driver')
replaces=('kdeutils-print-manager')
conflicts=('kdeutils-print-manager')
groups=('kde-applications' 'kdeutils')
source=("https://download.kde.org/stable/applications/${pkgver}/src/print-manager-${pkgver}.tar.xz"{,.sig})
sha256sums=('9d2d75d1153f38fc0d2a5429cd3f2dea57822466fac5cb66f65df741bf8c4832'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
