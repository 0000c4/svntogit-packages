# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=aisleriot
pkgver=3.2.2
pkgrel=1
pkgdesc="GNOME Solitaire card games"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
groups=('gnome-extra')
depends=(libsm gconf guile libcanberra librsvg hicolor-icon-theme dconf)
makedepends=(gnome-doc-utils intltool itstool)
options=('!emptydirs' '!libtool')
install=aisleriot.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('818e16767f30238a98332fe07e8b905376a71de05feb3de19815652a48fdfcca')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain aisleriot ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
