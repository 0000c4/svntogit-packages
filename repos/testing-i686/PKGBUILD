# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgepub
pkgver=0.4+1+gba0bde3
pkgrel=1
pkgdesc="GObject based library for handling and rendering epub
documents"
arch=('i686' 'x86_64')
license=('LGPL')
url="https://www.gnome.org/"
depends=('webkit2gtk' 'libarchive')
makedepends=('git' 'gnome-common' 'gobject-introspection')
_commit=ba0bde3d36b459a070e6bcc599b271d6a20d5b27
source=("git://git.gnome.org/libgepub#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
