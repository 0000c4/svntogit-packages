# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.2_20130523
pkgrel=2
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="http://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('ncurses')
source=(ftp://invisible-island.net/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('c8d114b7698022bcbd6c88f3c0b2296b0e846c60c5ed6bd28f86dd72b94fd36d')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-ncursesw \
    --enable-nls
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}

# vim:set ts=2 sw=2 et:
