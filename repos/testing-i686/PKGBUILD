# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kservice
pkgver=5.14.2
pkgrel=2
pkgdesc='Advanced plugin and service introspection'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kservice'
license=('LGPL')
depends=('ki18n' 'kconfig' 'kcrash' 'kdbusaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz" timestamp.patch)
md5sums=('eca764e18afbcef5496183c96c613c91'
         'b77b056fb6bb5adba72702fddf0feff8')

prepare() {
  mkdir -p build

# Fix kbuildsycoca infinite loop https://bugs.kde.org/show_bug.cgi?id=352854
  cd $pkgname-$pkgver
  patch -p1 -i ../timestamp.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
