# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=cryptsetup
pkgver=1.5.1
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using dm-crypt"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'util-linux')
options=('!libtool' '!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2.asc
        encrypt_hook
        encrypt_install)
md5sums=('87cc018c0c65f36043f38ceb8ffd4d81'
         '5f711687f453f4d5a38596f60df5247a'
         'c970831d733ca42e20415005967e7843'
         '21c45f9cab3e0b5165f68358884fbd0f')

build() {
  cd "${srcdir}"/$pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static --enable-cryptsetup-reencrypt
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
}
