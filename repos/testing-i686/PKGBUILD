# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-tetravex
pkgver=3.22.0
pkgrel=1
pkgdesc="Complete the puzzle by matching numbered tiles"
url="https://wiki.gnome.org/Apps/Tetravex"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3)
makedepends=(intltool itstool gobject-introspection vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=a0c099009d4a22c4745d173b7a13e202da81f4b8
source=("git://git.gnome.org/gnome-tetravex#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
