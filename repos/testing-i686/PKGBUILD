# $Id$
# Maintainer: Andrea Scarpino <bash@archlinux.it>
# Contributor: damir <damir@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org> 
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=kdevelop
pkgver=3.5.4
pkgrel=1
pkgdesc="A C/C++ development environment for KDE - stable snapshot build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdelibs3>=3.5.9' 'flex>=2.5.4' 'gcc' 'perl' 'make' 
         'automake>=1.6' 'autoconf>=2.52' 'gettext' 'db>=4.7' 'cvs' 'subversion')
replaces=('kdevelop3-cvs') # compatibility reasons
conflicts=('kdevelop-devel')
options=('!makeflags')
source=(http://download.kde.org/stable/3.5.10/src/$pkgname-$pkgver.tar.bz2)
md5sums=('06aa12a89b2c09968a92e1d8b575819c')

build() {
   # Source the QT and KDE profile
   [ "$QTDIR" = "" ] && source /etc/profile.d/qt3.sh 
   [ "$KDEDIR" = "" ] && source /etc/profile.d/kde3.sh
   # start building
   cd ${startdir}/src/${pkgname}-${pkgver}
   ./configure --prefix=/opt/kde --enable-gcc-hidden-visibility --without-arts || return 1
   make || return 1
   make DESTDIR=$startdir/pkg install || return 1
   rm -f ${startdir}/pkg/opt/kde/bin/extractrc
}
