# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=0.9.2.0
pkgrel=1
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver}" 'pptpclient' 'gtk3' 'libgnome-keyring')
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
options=('!libtool')
install=networkmanager-pptp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/0.9/NetworkManager-pptp-${pkgver}.tar.xz)
sha256sums=('2b340db7f63d39aa781099b888d4e3352e8eaa14ec4d46154bf504262fda42ba')

build() {
  cd "${srcdir}/NetworkManager-pptp-${pkgver}"
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
	--disable-static
  make
}

package() {
  cd "${srcdir}/NetworkManager-pptp-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
