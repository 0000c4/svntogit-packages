# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.26.0+65+g2cfefd154
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(i686 x86_64)
license=(GPL3)
depends=(gtksourceview3 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils
         ctags libpeas vte3 vala python-jedi autoconf-archive sysprof flatpak gspell libdazzle)
makedepends=(intltool llvm gobject-introspection gtk-doc yelp-tools appstream-glib vala git
             mm-common meson)
optdepends=('gnome-code-assistance: Legacy assistance services')
groups=(gnome-extra)
_commit=2cfefd154bcedf36720cd830473fe74ba252f702  # gnome-builder-3-26
source=("git+https://git.gnome.org/browse/gnome-builder#commit=$_commit"
        "git+https://git.gnome.org/browse/libgd"
        "git+https://git.gnome.org/browse/jsonrpc-glib"
        "git+https://git.gnome.org/browse/template-glib"
        "git+https://git.gnome.org/browse/libdazzle")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  git submodule init
  git config --local submodule.subprojects/libgd.url "$srcdir/libgd"
  git config --local submodule.subprojects/jsonrpc-glib.url "$srcdir/jsonrpc-glib"
  git config --local submodule.subprojects/template-glib.url "$srcdir/template-glib"
  git config --local submodule.subprojects/libdazzle.url "$srcdir/libdazzle"
  git submodule update
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
