# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=3.0.1
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtksourceview3' 'gsettings-desktop-schemas' 'libpeas' 'enchant'
         'iso-codes' 'libsm' 'desktop-file-utils' 'pygobject')
makedepends=('gnome-doc-utils' 'intltool' 'pygobject-devel')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('5a362f05325f4d47e31e5e9d3689a37d4a597e218965be9a39a193c847beaa87')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-scrollkeeper \
      --disable-updater --disable-schemas-compile \
      --enable-python
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
