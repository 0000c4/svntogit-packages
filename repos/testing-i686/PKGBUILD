# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.26.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('solid' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet' 'desktop-file-utils' 'kinit')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        kio-konsole.patch::"https://quickgit.kde.org/?p=kio.git&a=commitdiff&h=c784a879&o=plain")
md5sums=('c525ec050558e2d5c372616b8848aef8'
         'eade65cf460457d7fd34085478f27b6d')

prepare() {
  mkdir -p build

# Fix launching terminal applications
  cd $pkgname-$pkgver
  patch -p1 -i ../kio-konsole.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
