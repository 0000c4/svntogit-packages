# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgbase=lvm2
pkgname=('lvm2' 'device-mapper')
pkgver=2.02.60
_pkgverdm=1.02.40
_pkgverlvm=${pkgver}
pkgrel=1
arch=('i686' 'x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL')
groups=('base')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_pkgverlvm}.tgz
        lvm2_install
        lvm2_hook)
sha256sums=('5b9fc9daa3befb715b6930d5b788909bff881269ebbafce828a686565064abc7'
            '4767758e63aca392d762ac6899fcf662758ea3232cf1a9a0ecb7129184cf96a1'
            'b7682e4dd2512fad8fd57fc1f3a1c72144f401ea122af924629e72cda05fd5af')
md5sums=('33e0e0f4b4582d495ff619696c634b2a'
         'a28ae1f1bcb78347da4c43c669601229'
         'd6f3ca901747c386aa0d842e9ea64413')
                                          
build() {
  cd "${srcdir}/LVM2.${_pkgverlvm}"
  ./configure --prefix= --sysconfdir=/etc --localstatedir=/var --enable-readline \
    --mandir=/usr/share/man --includedir=/usr/include --with-usrlibdir=/usr/lib \
    --enable-pkgconfig --enable-static_link --enable-dmeventd --enable-cmdlib \
    --with-udevdir=/lib/udev/rules.d/ || return 1
  make || return 1
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"
  depends=('glibc' 'readline')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_device-mapper || return 1
  # install the static libraries in /usr/lib
  install -d -m755 "${pkgdir}/usr/lib"
  mv "${pkgdir}"/lib/libdevmapper.a* "${pkgdir}"/usr/lib/
  ln -sf ../usr/lib/libdevmapper.a "${pkgdir}"/lib/libdevmapper.a
  mv "${pkgdir}"/lib/libdevmapper-event.a* "${pkgdir}"/usr/lib/
  ln -sf ../usr/lib/libdevmapper-event.a "${pkgdir}"/lib/libdevmapper-event.a
  # install udev rules
  cd udev
  make DESTDIR="${pkgdir}" install_device-mapper || return 1
}

package_lvm2() {
  pkgdesc="Logical Volume Manager 2 utilities"
  depends=('bash' "device-mapper>=${_pkgverdm}")
  conflicts=('lvm')
  backup=('etc/lvm/lvm.conf')
  options=('!makeflags')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_lvm2 || return 1
  # install the static libraries in /usr/lib
  install -d -m755 "${pkgdir}/usr/lib"
  mv "${pkgdir}"/lib/liblvm2cmd.a* "${pkgdir}"/usr/lib/
  ln -sf ../usr/lib/liblvm2cmd.a "${pkgdir}"/lib/liblvm2cmd.a
  # /etc directories
  install -d "${pkgdir}"/etc/lvm/{archive,backup} || return 1
  # install udev rules
  cd udev
  make DESTDIR="${pkgdir}" install_lvm2 || return 1
  cd ..
  # mkinitcpio hook
  install -D -m644 "${srcdir}/lvm2_hook" "${pkgdir}/lib/initcpio/hooks/lvm2" || return 1
  install -D -m644 "${srcdir}/lvm2_install" "${pkgdir}/lib/initcpio/install/lvm2" || return 1
}
