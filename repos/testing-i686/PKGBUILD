# $Id$
# Maintainer: juergen <juergen@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=metalog
pkgver=1.0
pkgrel=3
pkgdesc="Metalog is a modern replacement for syslogd and klogd"
url="http://metalog.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
depends=('pcre')
provides=('logger')
backup=('etc/metalog.conf' 'etc/conf.d/metalog')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-1.tar.lzma"
        'metalog'
        'metalog.confd')
md5sums=('df4ec89f6a0df24a43f9592ee80ab6be'
         'cdd1d34eedbfbc2d9e659bf0a0f58f56'
         '86bccd5a02996921e29eff8a0394d11f')

build() {
  cd $srcdir/$pkgname-1
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-1
  make DESTDIR=$pkgdir install
  install -D -m755 $srcdir/metalog $pkgdir/etc/rc.d/metalog
  install -D -m644 $srcdir/metalog.confd $pkgdir/etc/conf.d/metalog
  install -D -m644 metalog.conf $pkgdir/etc/metalog.conf
}
