# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=dmraid
pkgver=1.0.0.rc16.3
pkgrel=4
pkgdesc="Device mapper RAID interface"
url="http://people.redhat.com/~heinzm/sw/dmraid/"
conflicts=('mkinitcpio<0.7')
depends=('device-mapper>=2.0.54')
arch=('i686' 'x86_64')
license=('GPL')
source=(#ftp://ftp.archlinux.org/other/dmraid/$pkgname-$pkgver.tar.bz2
        http://people.redhat.com/~heinzm/sw/dmraid/src/$pkgname-1.0.0.rc16-3.tar.bz2
        dmraid_install
        dmraid_hook)
install=dmraid.install
md5sums=('819338fcef98e8e25819f0516722beeb'
         '832ed57a0f7ca6ba72adebcba2b17f87'
         'faec669dc85f87187b45b5d3968efe2c')

build() {
  cd "$pkgname/1.0.0.rc16-3/$pkgname"
  ./configure --enable-led --enable-intel_led
  make
}

package() {
  cd "$pkgname/1.0.0.rc16-3/$pkgname"
  make DESTDIR="$pkgdir" prefix=/usr libdir=/usr/lib mandir=/usr/share/man includedir=/usr/include install
  mkdir -p "$pkgdir"/var/lock/dmraid
  chmod 1777 "$pkgdir"/var/lock/
  install -D -m644 "$srcdir"/dmraid_install "$pkgdir"/usr/lib/initcpio/install/dmraid
  install -D -m644 "$srcdir"/dmraid_hook "$pkgdir"/usr/lib/initcpio/hooks/dmraid
  # fix permissions
  chmod 644 "$pkgdir"/usr/lib/libdmraid.a
  chmod 644 "$pkgdir"/usr/include/dmraid/*
}
