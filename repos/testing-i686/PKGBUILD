# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=swt
pkgver=4.2
pkgrel=2
_date=201206081400
pkgdesc="An open source widget toolkit for Java"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/swt/"
license=('EPL')
depends=('java-runtime>=6' 'gtk2>=2.20.1' 'libxtst')
optdepends=('libgnomeui' 'mesa' 'glu' 'libwebkit')
makedepends=('java-environment' 'libxtst' 'mesa' 'glu' 'libgnomeui' 'unzip' 'pkgconfig' 'libwebkit' 'apache-ant')
if [ "${CARCH}" = "i686" ]; then
  _carch=x86
  md5sums=('3efe0404f6129183abae46f7620fe14f'
           '2f556ab534fc2488c6e8c0ee6c02825c')
fi
if [ "${CARCH}" = "x86_64" ]; then
  _carch=x86_64
  md5sums=('9e06b576116ff409f395571603582827'
           '2f556ab534fc2488c6e8c0ee6c02825c')
fi
source=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-${_carch}.zip
        build-swt.xml)
# To test this pkg:
# http://www.eclipse.org/swt/examples.php#standaloneOutsideEclipse

build() {
  cd ${srcdir}
  unzip -oq src.zip -d src

  . /etc/profile.d/jdk.sh
  . /etc/profile.d/apache-ant.sh

  # Shared objects
  cd src
  ./build.sh

  # SWT jar
  ant -f ../build-swt.xml compile
}

package() {
  cd ${srcdir}/src

  # Shared objects
  export OUTPUT_DIR=${pkgdir}/usr/lib
  install -dm755 ${OUTPUT_DIR} 
  make -f make_linux.mak install

  # SWT jar
  ant -f ../build-swt.xml jar
  install -Dm755 ../swt.jar ${pkgdir}/usr/share/java/swt-${pkgver}.jar
  ln -s swt-${pkgver}.jar ${pkgdir}/usr/share/java/swt.jar
}
