# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=ghex
pkgver=3.4.0
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Ghex"
depends=('gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
options=('!libtool' '!emptydirs')
install=ghex.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('59e409cc629fddeb06552c505b60be1d20274cea75eb22f1d65d7ca0aaf1e64d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
