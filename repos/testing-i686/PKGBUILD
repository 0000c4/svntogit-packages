# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
 
pkgname=rhythmbox
pkgver=0.11.6
pkgrel=2
pkgdesc="An iTunes-like music player/libary"
arch=(i686 x86_64)
license=('GPL')
url="http://www.rhythmbox.org"
depends=( 'libgpod>=0.6.0' 'libsoup>=2.24.0' 'gnome-media>=2.24.0'
          'totem-plparser>=2.24.1' 'musicbrainz>=2.1.5'
	  'libsexy>=0.1.11' 'nautilus-cd-burner>=2.24.0' 'libmtp>=0.2.4'
	  'lirc-utils' 'desktop-file-utils' 'gnome-python>=2.22.3'
	  'gstreamer0.10-python>=0.10.13' 'gstreamer0.10-base-plugins'
	  'gstreamer0.10-good-plugins')
makedepends=('perlxml' 'pkgconfig' 'gnome-doc-utils>=0.14.0' 'xulrunner>=1.9.0.3') 
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
	    'gstreamer0.10-ffmpeg: Extra media codecs')
options=('!libtool' '!emptydirs')
install=rhythmbox.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.11/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b827e047d13954ba956c3c4ea940935e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/rhythmbox \
              --localstatedir=/var --disable-static \
	      --with-cd-burner \
	      --with-playback=gstreamer-0-10 --enable-daap \
              --with-mdns=avahi --disable-scrollkeeper \
              --enable-python || return 1
  
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
