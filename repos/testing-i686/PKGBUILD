# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-vmmouse
pkgver=12.7.0
pkgrel=4
pkgdesc="X.org VMWare Mouse input driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'sh')
makedepends=('xorg-server-devel>=1.11.99.902')
conflicts=('xorg-server<1.11.99.902')
groups=('xorg-drivers' 'xorg')
backup=('etc/X11/xorg.conf.d/50-vmmouse.conf')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        0001-Deal-with-opaque-InputOption-types-in-ABI-14.patch)
sha1sums=('6618f84a037c8f4817e1a2822d6a5a795814e544'
          '71283328a23e4b33e73164b40252fcd593b54e07')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/0001-Deal-with-opaque-InputOption-types-in-ABI-14.patch
  ./configure --prefix=/usr --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rf ${pkgdir}/usr/{lib,share}/hal
}
