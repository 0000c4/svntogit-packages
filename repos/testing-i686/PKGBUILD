# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-vpnc
pkgver=1.2.0
pkgrel=1
pkgdesc="NetworkManager VPN plugin for VPNC"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/projects/NetworkManager/"
depends=(libnm-gtk libsecret vpnc)
makedepends=(intltool python)
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-vpnc/${pkgver::3}/NetworkManager-vpnc-${pkgver}.tar.xz)
sha256sums=('fa8a8cc3b9f744b2807ae96a86a6e1f761d918701aa69bfe538da595c4e815fc')

build() {
  cd NetworkManager-vpnc-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --enable-more-warnings=yes \
    --disable-static
  make
}

package() {
  cd NetworkManager-vpnc-$pkgver
  make DESTDIR="$pkgdir" install
}
