# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-nettool
pkgver=2.26.0
pkgrel=1
pkgdesc="A Collection of GNOME2 Networking Tools."
arch=(i686 x86_64)
license=('GPL')
depends=('gconf>=2.26.0-2' 'libglade>=2.6.4' 'libgtop>=2.26.0' 'dnsutils' 'netkit-bsd-finger' 'whois')
makedepends=('gnome-doc-utils>=0.16.0' 'pkgconfig' 'intltool')
conflicts=('gnome-network')
replaces=('gnome-netinfo')
groups=('gnome-extra')
url="http://www.gnome.org"
install=gnome-nettool.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('34bc06209bb72bd85d45829eecc078cb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
