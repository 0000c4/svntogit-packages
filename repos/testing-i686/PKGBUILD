# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=3.0.1
pkgrel=1
pkgdesc="The Control Center for GNOME"
arch=('i686' 'x86_64')
depends=('gtk3' 'gsettings-desktop-schemas' 'gconf' 'gnome-menus' 'gnome-desktop' 'gnome-settings-daemon' 'upower' 'libgtop' 'cups-pk-helper' 'accountsservice')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'apg: adds password generation for user accounts')
makedepends=('gnome-doc-utils' 'intltool' 'networkmanager')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/3.0/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('90dde4b3db8b7a9c973f71e15601fd176ad6746cfef63c818fbdfd2254a8e99f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --disable-update-mimedb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-control-center-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

