# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libva
pkgver=1.7.3
pkgrel=2
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('i686' 'x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('mesa')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
source=(https://freedesktop.org/software/vaapi/releases/$pkgname/$pkgname-$pkgver.tar.bz2
        not-finding-wl-drm-interface-with-libglvnd.patch)
sha1sums=('18e46f3d5a0e971eb0ec9fe14bc021e69eb13fb8'
          'b6f28238cc3c93f727d44198d9b303379b74fab1')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../not-finding-wl-drm-interface-with-libglvnd.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
