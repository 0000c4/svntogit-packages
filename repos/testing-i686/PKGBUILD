# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kipi-plugins
pkgver=1.9.0
pkgrel=4
pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.kipi-plugins.org"
makedepends=('pkgconfig' 'cmake' 'automoc4' 'libgpod')
depends=('kdebase-runtime' 'kdepimlibs' 'libkexiv2' 'libkdcraw' 'libkipi'
         'libksane' 'opencv' 'qjson')
source=("http://downloads.sourceforge.net/kipi/$pkgname-$pkgver.tar.bz2")
optdepends=('imagemagick: for picture manipulation'
            'libgpod: send images to ipod'
            'hugin: for expoblending plugin')
install=${pkgname}.install

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
md5sums=('a3200b0ce90251bc631fa90ff0818dc0')
