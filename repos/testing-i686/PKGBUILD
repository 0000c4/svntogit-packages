# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.22.2
pkgrel=2
pkgdesc="Music player and management application"
url="https://wiki.gnome.org/Apps/Music"
arch=(i686 x86_64)
license=(GPL)
depends=(grilo grilo-plugins tracker libdmapsharing python-dbus dbus-glib
         gnome-themes-standard gtk3 gvfs python-gobject python-cairo
         gst-plugins-base python-requests)
makedepends=(intltool itstool gobject-introspection gnome-common git)
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
groups=(gnome-extra)
_commit=f5a63a2b7747fd2d684c83468c7311ab6f3b1b8c  # tags/3.22.2^0
source=("git://git.gnome.org/gnome-music#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
