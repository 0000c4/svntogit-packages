# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=0.2.2
pkgrel=1
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('i686' 'x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('libgssglue')
backup=('etc/netconfig')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2
       libtirpc-0.2.1-fortify.patch)
md5sums=('74c41c15c2909f7d11d9c7bfa7db6273'
         '2e5c067f1651469dfbbdc91d3c9c60e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix http://bugs.gentoo.org/293593
  # https://bugs.archlinux.org/task/20082
  patch -Np1 -i ../libtirpc-0.2.1-fortify.patch
  ./configure --prefix=/usr --enable-gss
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 doc/etc_netconfig "${pkgdir}"/etc/netconfig
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
