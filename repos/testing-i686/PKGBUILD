# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xaw3d
pkgver=1.6
pkgrel=1
pkgdesc="Three-D Athena widgets"
arch=('i686' 'x86_64')
url="http://directory.fsf.org/project/xaw3d/"
license=('MIT')
depends=('libxmu')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(http://xorg.freedesktop.org/archive/individual/lib/libXaw3d-${pkgver}.tar.bz2)
md5sums=('db88f0c5afc5f285e046d84e15ad30de')

build() {
  cd "${srcdir}/libXaw3d-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/libXaw3d-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
