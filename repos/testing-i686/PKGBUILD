# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=zenity
pkgver=3.22.0+1+gcd1647c
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
url="https://git.gnome.org/browse/zenity"
arch=(i686 x86_64)
license=(LGPL)
depends=(webkit2gtk libnotify)
makedepends=(itstool yelp-tools gettext gnome-common git)
_commit=cd1647cb2dfbc0e83ee372bee49028acbaf9a781  # master
source=("git://git.gnome.org/zenity#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^ZENITY_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
