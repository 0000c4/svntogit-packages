# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-2048
pkgver=3.22.0+1+g41f89c7
pkgrel=1
pkgdesc="Obtain the 2048 tile"
url="https://wiki.gnome.org/Apps/2048"
arch=(x86_64 i686)
license=(GPL3)
depends=(clutter-gtk libgee libgnome-games-support)
makedepends=(intltool vala itstool yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=41f89c7b2f29a46ec842e13110fda3d229af1cba  # master
source=("git://git.gnome.org/gnome-2048#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
