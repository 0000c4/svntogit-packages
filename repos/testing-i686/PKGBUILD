# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gvfs
pkgver=0.2.5
pkgrel=3
pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio"
arch=(i686 x86_64)
license=('LGPL')
depends=('libsoup>=2.4.1' 'libcdio>=0.80-2' 'fuse>=2.7.3' 'bluez>=4.1' 'smbclient>=3.0.31' 'libgphoto2>=2.4.2' 'gnome-keyring>=2.22.3' 'avahi>=0.6.23' 'libarchive>=2.5.5')
makedepends=('perlxml' 'pkgconfig')
url="http://www.gnome.org"
install=gvfs.install
options=(!libtool)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.2/${pkgname}-${pkgver}.tar.bz2
	bug536614.patch)
md5sums=('01676053d9481022ade9a5274576541b'
         '80e6b702f3a28dc3fc3b94b06f4442fd')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np2 -i ${srcdir}/bug536614.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --libexecdir=/usr/lib/gvfs || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
}
