# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libphonenumber
pkgver=8.4.1
pkgrel=4
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(i686 x86_64)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest git java-environment)
license=("Apache")
_commit=02dbc0921cf503c548278b7aa8818ea5aa0b8ed3  # tags/v8.4.1^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/-/+/g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  cmake ../$pkgname/cpp \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd build
  make tests
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
