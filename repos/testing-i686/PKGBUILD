# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=jemalloc
epoch=1
pkgver=5.0.0
pkgrel=3
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('i686' 'x86_64')
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('glibc')
provides=('libjemalloc.so')
optdepends=('perl: for jeprof')
source=("https://github.com/jemalloc/jemalloc/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        '0001-only-abort-on-dlsym-when-necessary.patch'
        '0002-clear-tcache_ql-after-fork-in-child.patch'
        '0003-Add-minimal-initialized-tsd.patch')
sha256sums=('9e4a9efba7dc4a7696f247c90c3fe89696de5f910f7deacf7e22ec521b1fa810'
            'ef8b3afd9f7e8ee871bf6b228b0f9288881f6cc0243478bab727ba02eb2776e0'
            'd627e0cf2b540bdeea3a4cd15c28b949faa30064eb7f53da11aa9a428732bb63'
            'bb36a2802e5f78fc0a1f3c0ddf334d01bb04116ba712f1fd05eb47231af094c6')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < "$srcdir"/0001-only-abort-on-dlsym-when-necessary.patch
  patch -Np1 < "$srcdir"/0002-clear-tcache_ql-after-fork-in-child.patch
  patch -Np1 < "$srcdir"/0003-Add-minimal-initialized-tsd.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name \*.a -type f -exec chmod 644 '{}' \;
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
