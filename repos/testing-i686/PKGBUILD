# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=dhcpcd
pkgver=4.0.5
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/dhcpcd/"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
depends=('glibc')
backup=('etc/conf.d/dhcpcd' 'etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.bz2" \
        'dhcpcd.conf.d')
md5sums=('4897c11180e0c4880810673a3acb85d6' '372d33485556982b64a97f301e17c5dd')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Disable DUID Usage
  #echo "#undef ENABLE_DUID" >> config.h

  # Fix Installation Locations
  export PREFIX=/usr
  sed -i 's/${PREFIX}\/etc/\/etc/' Makefile || return 1
  sed -i 's/\/db/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' Makefile || return 1
  sed -i 's/\/libexec/\/lib\/dhcpcd/' dhcpcd-hooks/Makefile || return 1
  sed -i 's/${PREFIX}\/sbin/\/sbin/' Makefile || return 1

  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -d $pkgdir/usr/sbin || return 1
  ln -sf /sbin/dhcpcd $pkgdir/usr/sbin/dhcpcd || return 1

  # Install Configuration File used in /etc/rc.d/network
  install -D -m644 ../dhcpcd.conf.d $pkgdir/etc/conf.d/$pkgname || return 1

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname || return 1
  awk '{if(FNR<27)print $0}' $srcdir/$pkgname-$pkgver/config.h \
	>> $pkgdir/usr/share/licenses/$pkgname/license || return 1

  # Make Man Pages FHS Compliant
  mv -f $pkgdir/usr/man $pkgdir/usr/share/man || return 1

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> $pkgdir/etc/dhcpcd.conf || return 1 # Disable ip4vall
}
