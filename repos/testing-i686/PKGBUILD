# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgname=network-manager-applet
pkgver=0.8.1
pkgrel=2
pkgdesc="GNOME frontends to NetWorkmanager"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager>=0.8.1' 'libglade>=2.6.4' 'libgnome-keyring>=2.30.1' 'polkit-gnome>=0.96' 'notification-daemon' 'libnotify>=0.4.5' 'hicolor-icon-theme' 'mobile-broadband-provider-info')
makedepends=('pkg-config' 'intltool' 'gnome-bluetooth')
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool')
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/network-manager-applet/0.8/network-manager-applet-${pkgver}.tar.bz2
        new-glib.patch)
sha256sums=('4a84b9762c6fa13004f66a5e208236d2c4b0a15fcbf47da19b6b44b441aec366'
            'c0fc125b9b4479609897cf87cfd6b1af7bb7f1647ff38f1e863455a6798830a8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/new-glib.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-maintainer-mode
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nm-applet ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
