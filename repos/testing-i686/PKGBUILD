# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Committer: dorphell <dorphell@gmx.net>

pkgname=screen
pkgver=4.5.0
pkgrel=1
pkgdesc='Full-screen window manager that multiplexes a physical terminal'
url='https://www.gnu.org/software/screen/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('ncurses' 'pam')
validpgpkeys=('2EE59A5D0C50167B5535BBF1B708A383C53EF3A4'
              '71AA09D9E8870FDB0AA7B61E21F968DEF747ABD7')
source=("https://ftp.gnu.org/gnu/screen/screen-${pkgver}.tar.gz"{,.sig}
        'tmpfiles.d'
        'pam.d')
sha1sums=('b329f538e7265405a5c14e13520e95ced9d2cb17' 'SKIP'
          '76b9c70b77940eb1214fe65739f9f932dc57fb66'
          '1ab4b512d2ac840d16db6986d7c98d7ce2f6383f')

backup=('etc/screenrc' 'etc/pam.d/screen')
options=('!makeflags')

_ptygroup=5 #the UID of our PTY/TTY group

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		--enable-colors256 \
		--enable-pam \
		--enable-rxvt_osc \
		--enable-telnet \
		--with-pty-group=$_ptygroup \
		--with-socket-dir=/run/screens \
		--with-sys-screenrc=/etc/screenrc \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../pam.d "${pkgdir}"/etc/pam.d/screen
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/screen.conf

	install -Dm644 etc/completer.zsh "${pkgdir}"/usr/share/zsh/site-functions/_screen
	install -Dm644 etc/etcscreenrc "${pkgdir}"/etc/screenrc
	install -Dm644 etc/screenrc "${pkgdir}"/etc/skel/.screenrc
}
