# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=links
pkgver=2.3pre1
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx"
arch=('i686' 'x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'zlib' 'openssl' 'gpm')
makedepends=('libtiff' 'libpng' 'libxt')
optdepends=('libx11: for using xlinks' 'libtiff: for using xlinks' 'libpng: for using xlinks')
provides=('links-g')
conflicts=('links-g')
replaces=('links-g')
source=(http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2 links.desktop)
md5sums=('76cff0c716cd2a710abb078f859cfe9b' '7c47b15aaef51f1de129ff3a2afb7eac')
sha1sums=('6d420a5c4514b45ee245fd3933d2a8cfa6eae76d' 'f600e27c2a71184444f7dd07a10230aa44463a02')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  (cd intl; ./gen-intl; ./synclang)
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-javascript \
    --enable-graphics --with-x --with-fb
  make
  mv links xlinks

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-javascript \
    --disable-graphics --without-x  --without-fb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 xlinks "${pkgdir}/usr/bin/xlinks"
  ln -s links.1.gz "${pkgdir}/usr/share/man/man1/xlinks.1.gz"

  install -D -m644 "${srcdir}/links.desktop" "${pkgdir}/usr/share/applications/links.desktop"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -m644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm "${pkgdir}/usr/share/pixmaps/"

  install -d "${pkgdir}/usr/share/doc/links/calibration"
  install -m644 doc/links_cal/* "${pkgdir}/usr/share/doc/links/calibration/"
}
