# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapptemplate
pkgver=15.12.3
pkgrel=2
pkgdesc='KDE Template Generator'
url='http://kde.org/applications/development/kapptemplate/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kio' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdesdk-kapptemplate')
conflicts=('kdesdk-kapptemplate')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kapptemplate-${pkgver}.tar.xz")
sha1sums=('046141d54158d840b31d71fdf7c846303d7f16ac')

build() {
  mkdir -p build
  cd build
  cmake ../kapptemplate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Fix conflict with plasma-framework, kdevelop and krunner
  rm "$pkgdir"/usr/share/kdevappwizard/templates/{qmake_qt4guiapp,qml-plasmoid,runner}.tar.bz2
}
