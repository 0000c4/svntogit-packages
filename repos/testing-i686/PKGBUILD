# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.22.1
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server 'gsettings-desktop-schemas>=3.21.2')
makedepends=(intltool python gnome-common appstream-glib git)
groups=(gnome-extra)
_commit=b0654cd29b2621566df6cd0de14aac4a34912f4a  # tags/3.22.1^0
source=("git://git.gnome.org/gnome-calendar#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
