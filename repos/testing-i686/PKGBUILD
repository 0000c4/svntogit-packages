# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=17.04.0
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://community.kde.org/KDE_PIM'
license=('GPL' 'LGPL' 'FDL')
depends=(libkgapi libkolab kdav akonadi-calendar knotifyconfig kalarmcal kmbox kimap hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost python kdesignerplugin)
conflicts=(kio-pim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdepim-runtime-gmail-migration.patch::"https://cgit.kde.org/kdepim-runtime.git/patch/?id=c9ae1636")
sha256sums=('b46d3db769de31b758c49a881b8e0b1869ada82f39ac282c28280980b1a59201'
            'SKIP'
            '4e497f9f551d411daf1c04b5daa33e8eb2a0e37b9f8d562575cfa3eddf43070f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

# Fix migration of Gmail accounts to XOAUTH authentication
  cd $pkgname-$pkgver
  patch -p1 -i ../kdepim-runtime-gmail-migration.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
