# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.org>
# Contributor: abelstr <abel@pinklf.eu>
# Contributor: Marco Lima <cipparello gmail com>

pkgname=nfs-utils
pkgver=1.2.5
pkgrel=3
pkgdesc="Support programs for Network File Systems"
arch=('i686' 'x86_64')
url='http://nfs.sourceforge.net'
license=('GPL')
backup=(etc/{exports,idmapd.conf,nfsmount.conf} etc/conf.d/{nfs-common.conf,nfs-server.conf})
depends=('glibc' 'e2fsprogs' 'rpcbind' 'libtirpc>=0.2.1' 'librpcsecgss>=0.19-2' 'nfsidmap' 'libevent>=2.0.10' 'libgssglue' 'device-mapper')
makedepends=('pkgconfig' 'autoconf' 'automake')
source=(http://downloads.sourceforge.net/project/nfs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
	nfs-common
	nfs-common.conf
	nfs-server
	nfs-server.conf
	exports
	idmapd.conf
	start-statd.patch
        nfs
	nfs-server.service
        nfs-server.postconfig
        nfs-server.preconfig
        nfs-blkmap.service
        nfs-secure-server.service
        nfs-secure-server.preconfig
        nfs-lock.service
        nfs-lock.preconfig
        nfs-secure.service
	proc-fs-nfsd.mount
	var-lib-nfs-rpc_pipefs.mount
	nfs-utils-1.1.4-mtab-sym.patch
	nfs-utils-1.1.4-no-exec.patch)
install=nfs-utils.install


build() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -Np1 -i ../nfs-utils-1.1.4-mtab-sym.patch
  #patch -Np1 -i ../nfs-utils-1.1.4-no-exec.patch
  # arch specific patch
  patch -Np0 -i $srcdir/start-statd.patch

  ./configure --prefix=/usr --enable-nfsv4 --enable-nfsv41 --enable-gss \
              --without-tcp-wrappers --with-statedir=/var/lib/nfs \
              --enable-ipv6 --sysconfdir=/etc --enable-libmount-mount \
              --enable-mountconfig

  make 
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install

  # support python2 (FS#25120)
  sed -i '1s/python$/python2/' "$pkgdir"/usr/sbin/{nfsiostat,mountstats}

  # NFS & NFSv4 init scripts
  install -D -m 755 ../nfs-common "$pkgdir/"etc/rc.d/nfs-common
  install -D -m 755 ../nfs-server "$pkgdir/"etc/rc.d/nfs-server
  # Configuration
  install -D -m 644 ../exports "$pkgdir/"etc/exports
  install -D -m 644 ../idmapd.conf "$pkgdir/"etc/idmapd.conf
  install -D -m 644 ../nfs-common.conf "$pkgdir/"etc/conf.d/nfs-common.conf
  install -D -m 644 ../nfs-server.conf "$pkgdir/"etc/conf.d/nfs-server.conf
  install -D -m 644 ../nfs "$pkgdir/"etc/conf.d/nfs
  install -D -m 644 utils/mount/nfsmount.conf "$pkgdir/"etc/nfsmount.conf
  # systemd files
  for i in ${srcdir}/*.{service,mount}; do
    install -D -m 644 $i "$pkgdir/"usr/lib/systemd/system/$(basename $i)
  done
  install -D -m 755 ../nfs-server.preconfig "$pkgdir/"usr/lib/nfs-utils/scripts/nfs-server.preconfig
  install -D -m 755 ../nfs-server.postconfig "$pkgdir/"usr/lib/nfs-utils/scripts/nfs-server.postconfig
  install -D -m 755 ../nfs-lock.preconfig "$pkgdir/"usr/lib/nfs-utils/scripts/nfs-lock.preconfig
  install -D -m 755 ../nfs-secure-server.preconfig "$pkgdir/"usr/lib/nfs-utils/scripts/nfs-secure-server.preconfig
  # directories
  mkdir "$pkgdir/"etc/exports.d
  mkdir "$pkgdir/"var/lib/nfs/rpc_pipefs
  mkdir "$pkgdir/"var/lib/nfs/v4recovery
}
md5sums=('8395ac770720b83c5c469f88306d7765'
         'dd0d65fc6e8f422fa12520813098264b'
         'f73f197a16b02c3e248488ec35c4cf43'
         'e619f18354ff958ed624d05d08853d8f'
         '9cef69bc686cc5dcac23fbb51450747d'
         'ff585faf410a62c4333a027c50b56bae'
         'eb4f4027fab6fc1201f1ca04f5954c76'
         'e9144277a89a620d9bc80413158a7d27'
         'f5e7bba09a46c5c5d7007cac6eff9df5'
         '630a8ae10036bd1ffc6b6780fcd43403'
         '956d5b8a9476cedc9fec322b9635a169'
         '44c6bc21fca29b37de5cc689c3166ad1'
         '23551447a1a4505ebc4e4919c08f142a'
         'ff803f282aa2532c016ed149f9579d2f'
         'c2995ec2e6e0158f15b01f9e20ce706b'
         '218837aa129989146de8cbbf7c95ee5c'
         '6a6e8befc9700d9f93949b7e12f38fa4'
         '864178f2e13e077c76eec1003b4f662a'
         '8f1b5282795895c9b8ce8430d20cdda6'
         '4cb9d4277577c9e93e347e5855501e33'
         '7674106eaaa4c149bccd4f05fe3604e9'
         '4f4827dfc93008dfadd0a530ad0872b2')
