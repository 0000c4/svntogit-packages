# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-dict
pkgver=0.6.0
pkgrel=3
pkgdesc="A dictionary plugin for the Xfce panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/applications/xfce4-dict"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4' 'hicolor-icon-theme' 'librsvg')
makedepends=('intltool')
provides=('xfce4-dict-plugin')
conflicts=('xfce4-dict-plugin')
replaces=('xfce4-dict-plugin')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/apps/xfce4-dict/0.6/xfce4-dict-${pkgver}.tar.bz2)
md5sums=('c371c5c0bbe45a2bfac336cfe01dfe01')

build() {
  cd ${srcdir}/xfce4-dict-${pkgver}

  # Work around DSO linking issue
  LDFLAGS+=' -Wl,--copy-dt-needed-entries'

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/xfce4-dict-${pkgver}
  make DESTDIR=${pkgdir} install
}
