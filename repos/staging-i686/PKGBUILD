# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.7.5
pkgrel=1
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('libxfce4ui' 'thunar>=1.1.4' 'hicolor-icon-theme' 'libwnck>=2.30.0')
makedepends=("xfce4-panel>=4.7.4" 'pkgconfig' 'intltool')
optdepends=('xfce4-panel: panel plugin')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
options=('!libtool')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.7/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ae6c2b1d0f43a294bf409ec0332352a4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # git fixes

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-gio-unix \
	--enable-thunarx \
	--enable-exo \
	--enable-notifications \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
