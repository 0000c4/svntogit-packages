# $Id$
# Maintainer: François Charette <francois ατ archlinux δοτ org>
# Contributor: Jason Chu <jchu@xentac.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=perl-tk
_cpanname=Tk
pkgver=804.029
pkgrel=2
pkgdesc="A graphical user interface toolkit for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_cpanname}"
license=('PerlArtistic' 'GPL' 'custom')
depends=('libx11' 'libpng' 'libjpeg' 'tk' 'perl')
options=('!emptydirs')
replaces=('perltk')
provides=('perltk')
source=(http://www.cpan.org/authors/id/S/SR/SREZIC/${_cpanname}-${pkgver}.tar.gz) 
md5sums=('f4aebe6fcdb309a8480514f2ccf8117c')

build() {
  cd $srcdir/${_cpanname}-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  /bin/chmod -R u+w *
  # The Xlib header is wrong...
  sed -i 's/XKeycodeToKeysym\(.*\)unsigned int/XKeycodeToKeysym\1KeyCode/' pTk/Xlib.h
  make
}

package() {
  cd $srcdir/${_cpanname}-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete

  # license
  install -D -m644 pTk/license.terms \
                   ${startdir}/pkg/usr/share/licenses/${pkgname}/tk.license
  install -D -m644 pTk/Tix.license \
                   ${startdir}/pkg/usr/share/licenses/${pkgname}/tix.license
}
