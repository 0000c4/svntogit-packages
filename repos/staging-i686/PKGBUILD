# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# This package spans multiple repositories.
# Always build from cyrus-sasl/trunk and merge changes to libsasl/trunk.

#pkgbase=('cyrus-sasl')
#pkgname=('cyrus-sasl' 'cyrus-sasl-gssapi' 'cyrus-sasl-ldap' 'cyrus-sasl-sql')
pkgname=libsasl
pkgver=2.1.26
pkgrel=2
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i686' 'x86_64')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
options=('!makeflags' '!libtool')
makedepends=('postgresql-libs' 'libmariadbclient' 'libldap' 'krb5' 'openssl')
source=(ftp://ftp.cyrusimap.org/cyrus-sasl/cyrus-sasl-${pkgver}.tar.gz
        cyrus-sasl-2.1.22-qa.patch
        cyrus-sasl-2.1.26-size_t.patch
        0010_maintainer_mode.patch
        0011_saslauthd_ac_prog_libtool.patch
        0025_ld_as_needed.patch
        0026_drop_krb5support_dependency.patch
        0030-dont_use_la_files_for_opening_plugins.patch
        saslauthd.conf.d
        saslauthd)
md5sums=('a7f4e5e559a0e37b3ffc438c9456e425'
         '79b8a5e8689989e2afd4b7bda595a7b1'
         'f45aa8c42b32e0569ab3d14a83485b37'
         'f45d8b60e8f74dd7f7c2ec1665fa602a'
         '9d93880514cb5ff5da969f1ceb64a661'
         '62bf892fe4d1df41ff748e91a1afaf67'
         'b7848957357e7c02d6490102be496bf9'
         '8e7106f32e495e9ade69014fd1b3352a'
         '49219af5641150edec288a3fdb65e7c1'
         '75542f613185d5a90520ad0d7d926a20')

build() {
  export CFLAGS="$CFLAGS -fPIC"
 
  cd "${srcdir}/cyrus-sasl-${pkgver}"
  patch -Np1 -i "${srcdir}/cyrus-sasl-2.1.22-qa.patch"
  patch -Np1 -i "${srcdir}/cyrus-sasl-2.1.26-size_t.patch"
  patch -Np1 -i "${srcdir}/0010_maintainer_mode.patch"
  patch -Np1 -i "${srcdir}/0011_saslauthd_ac_prog_libtool.patch"
  patch -Np1 -i "${srcdir}/0025_ld_as_needed.patch"
  patch -Np1 -i "${srcdir}/0026_drop_krb5support_dependency.patch"
  patch -Np1 -i "${srcdir}/0030-dont_use_la_files_for_opening_plugins.patch"

  sed 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.in
  rm -f config/config.guess config/config.sub 
  rm -f config/ltconfig config/ltmain.sh config/libtool.m4
  rm -fr autom4te.cache
  libtoolize -c
  aclocal -I config -I cmulocal
  automake -a -c
  autoheader
  autoconf

  pushd saslauthd
  rm -f config/config.guess config/config.sub 
  rm -f config/ltconfig config/ltmain.sh config/libtool.m4
  rm -fr autom4te.cache
  libtoolize -c
  aclocal -I config -I ../cmulocal -I ../config
  automake -a -c
  autoheader
  autoconf
  popd

  ./configure --prefix=/usr \
      --mandir=/usr/share/man \
      --infodir=/usr/share/info \
      --disable-static \
      --enable-shared \
      --enable-alwaystrue \
      --enable-checkapop \
      --enable-cram \
      --enable-digest \
      --disable-otp \
      --disable-srp \
      --disable-srp-setpass \
      --disable-krb4 \
      --enable-gssapi \
      --enable-auth-sasldb \
      --enable-plain \
      --enable-anon \
      --enable-login \
      --enable-ntlm \
      --disable-passdss \
      --enable-sql \
      --enable-ldapdb \
      --disable-macos-framework \
      --with-pam \
      --with-saslauthd=/var/run/saslauthd \
      --with-ldap \
      --with-configdir=/etc/sasl2:/etc/sasl:/usr/lib/sasl2 \
      --sysconfdir=/etc \
      --with-devrandom=/dev/urandom
  make
}

package_libsasl() {
  pkgdesc="Cyrus Simple Authentication Service Layer (SASL) Library"
  depends=('openssl' 'krb5')
  conflicts=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}"
  for dir in include lib sasldb plugins utils; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done
  rm -f "${pkgdir}"/usr/lib/sasl2/libsql.so*
  rm -f "${pkgdir}"/usr/lib/sasl2/libgssapiv2.so*
  rm -f "${pkgdir}"/usr/lib/sasl2/libldapdb.so*
  install -m755 -d "${pkgdir}/usr/share/licenses/libsasl"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/libsasl/"
}

package_cyrus-sasl() {
  depends=("libsasl=${pkgver}-${pkgrel}")
  pkgdesc="Cyrus saslauthd SASL authentication daemon"
  backup=('etc/conf.d/saslauthd')

  cd "${srcdir}/cyrus-sasl-${pkgver}/saslauthd"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 -d "${pkgdir}/etc/conf.d"
  install -m755 "${srcdir}/saslauthd" "${pkgdir}/etc/rc.d/"
  install -m644 "${srcdir}/saslauthd.conf.d" "${pkgdir}/etc/conf.d/saslauthd"

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl/"
}

package_cyrus-sasl-gssapi() {
  pkgdesc="GSSAPI authentication mechanism for Cyrus SASL"
  depends=("libsasl=${pkgver}-${pkgrel}" 'krb5')
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/lib/sasl2"
  cp -a .libs/libgssapiv2.so* "${pkgdir}/usr/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl-gssapi"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl-gssapi/"
}

package_cyrus-sasl-ldap() {
  pkgdesc="ldapdb auxprop module for Cyrus SASL"
  depends=("libsasl=${pkgver}-${pkgrel}" 'libldap')
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/lib/sasl2"
  cp -a .libs/libldapdb.so* "${pkgdir}/usr/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl-ldap"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl-ldap/"
}

package_cyrus-sasl-sql() {
  pkgdesc="SQL auxprop module for Cyrus SASL"
  depends=("libsasl=${pkgver}-${pkgrel}" 'postgresql-libs' 'libmariadbclient')
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/lib/sasl2"
  cp -a .libs/libsql.so* "${pkgdir}/usr/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl-sql"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl-sql/"
}
