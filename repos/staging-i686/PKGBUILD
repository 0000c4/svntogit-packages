# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.26.3
pkgrel=2
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk2>=2.20.0' 'libcroco>=0.6.2' 'libgsf>=1.14.16')
makedepends=('intltool' 'pkg-config' 'gtk-doc')
optdepends=('python2: rsvg commandline utility')
options=('!libtool')
url="http://librsvg.sourceforge.net/"
install=librsvg.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c0005f9471b4bd3ba4873d165d8e481ee6b1d124dda4efc6bdc24b7da024b7e5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' rsvg.in
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} \
              --with-croco --disable-static \
	      --with-svgz
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
