# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfloppy
pkgver=16.11.80
pkgrel=1
pkgdesc='Floppy Formatter'
url='http://kde.org/applications/utilities/kfloppy/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin)
conflicts=(kdeutils-kfloppy)
replaces=(kdeutils-kfloppy)
groups=(kde-applications kdeutils)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('80b1ddb1ca4aaee95e6a66dd085b05aaffd6d39a'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
