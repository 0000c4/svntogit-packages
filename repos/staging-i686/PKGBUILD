# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgbase=unrar
pkgname=('unrar' 'libunrar')
pkgver=5.5.5
pkgrel=2
epoch=1
arch=('i686' 'x86_64')
url="http://www.rarlab.com/rar_add.htm"
license=('custom')
makedepends=('hardening-wrapper')
source=("http://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz")
sha256sums=('a4553839cb2f025d0d9c5633816a83a723e3938209f17620c8c15da06ed061ef')

build() {
  cp -a unrar libunrar
  make -C libunrar lib CXXFLAGS="-fPIC ${CXXFLAGS}"
  make -C unrar CXXFLAGS="-fPIC ${CXXFLAGS}" STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=(gcc-libs)
  cd unrar
  install -Dm755 unrar "$pkgdir"/usr/bin/unrar
  # install license
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=(gcc-libs)
  cd libunrar
  install -Dm755 libunrar.so "$pkgdir"/usr/lib/libunrar.so
  install -Dm644 dll.hpp "$pkgdir"/usr/include/unrar/dll.hpp
  # install license
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
