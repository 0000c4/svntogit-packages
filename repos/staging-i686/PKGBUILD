# $Id: PKGBUILD 230776 2015-02-04 23:00:34Z bluewind $
# Maintainer:
# Contributor: eric <eric@archlinux.org>
# Contributor: Markus Meissner <markus@meissna.de>

pkgname=tidy
pkgver=5.0.0
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=(i686 x86_64)
url="http://www.html-tidy.org/"
license=(custom)
depends=(glibc)
makedepends=(cmake libxslt)
conflicts=(tidyhtml)
provides=(tidyhtml)
replaces=(tidyhtml)
source=("https://github.com/htacg/tidy-html5/archive/$pkgver.tar.gz")
md5sums=('b0e95bda662a06e7652c8475cf8e9b1a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../tidy-html5-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-html5-$pkgver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
