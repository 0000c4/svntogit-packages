# $Id: PKGBUILD 250288 2015-11-09 21:13:39Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kamera
pkgver=16.03.80
pkgrel=1
pkgdesc="KDE integration for gphoto2 cameras"
url="http://kde.org/applications/graphics/kamera/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kio libgphoto2)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegraphics-kamera)
replaces=(kdegraphics-kamera)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('5030be817c9661d8fd07a0218fb74389af566267')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
