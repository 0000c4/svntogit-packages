# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-common-internals
_pkgname=ktp-common-internals
pkgver=0.4.1
pkgrel=1
pkgdesc="Common components for KDE-Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('kdelibs' 'telepathy-qt' 'telepathy-mission-control')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
install=${pkgname}.install
source=("http://download.kde.org/unstable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('9acb65efa371ba4f23063bc83ea7a488ac73e4b7')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
