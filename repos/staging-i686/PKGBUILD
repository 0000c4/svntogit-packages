# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=ekiga
pkgver=3.2.7
pkgrel=4
pkgdesc="VOIP/Videoconferencing app with full SIP and H.323 support (GnomeMeeting expanded and renamed)"
url="http://www.ekiga.org"
license=(GPL)
arch=(i686 x86_64)
depends=('opal=3.6.8' 'evolution-data-server>=2.32.0' 'libsigc++>=2.2.8' 'libxv' 'libnotify>=0.4.5' 'hicolor-icon-theme' 'avahi>=0.6.27')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils>=0.20.0')
provides=('gnomemeeting')
replaces=('gnomemeeting')
options=(!emptydirs)
groups=('gnome-extra')
install=ekiga.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/3.2/${pkgname}-${pkgver}.tar.bz2
        ekiga-3.2.7-libnotify-0.7.patch)
sha256sums=('f25a1309b68eafe69f0d7aed461bc9a8196939060543d09bd2f19e0bb1e3fd8f'
            'ad850d4e81a958081220f450a49ba9c10209d03f066f6afee99477c815c23daf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/ekiga-3.2.7-libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-install \
      --disable-scrollkeeper --enable-dbus
  make
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain ekiga ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
