# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

pkgname=tumbler
pkgver=0.1.23
pkgrel=2
pkgdesc="D-Bus service for applications to request thumbnails"
arch=('i686' 'x86_64')
url="http://git.xfce.org/xfce/tumbler/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('poppler-glib' 'dbus-glib' 'gdk-pixbuf2')
makedepends=('intltool' 'ffmpegthumbnailer' 'gstreamer0.10')
optdepends=('ffmpegthumbnailer: for video thumbnails'
            'gstreamer0.10: for video thumbnails')
options=('!libtool')
source=(http://archive.xfce.org/src/apps/$pkgname/0.1/$pkgname-$pkgver.tar.bz2)
sha256sums=('b3066d17b7e1fee5135eb876a7c2c50a1221bd0d447c3dd7c36efe6d7f3874cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
