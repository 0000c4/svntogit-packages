# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-systemload-plugin
pkgver=1.1.0
pkgrel=2
pkgdesc="A system load plugin for the Xfce4 panel"
arch=('i686' 'x86_64')
license=('custom')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-systemload-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel>=4.7.4')
makedepends=('intltool')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('130ad7befddd8705ca09a98e9cee5c3e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
