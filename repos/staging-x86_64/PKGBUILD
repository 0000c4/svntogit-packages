# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=nmap
pkgver=6.01
pkgrel=3
pkgdesc='Utility for network discovery and security auditing'
url='http://nmap.org/'
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('pygtk')
optdepends=('pygtk: zenmap')
depends=('pcre' 'openssl' 'libpcap' 'lua51')
source=("http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'kdesu.patch')
sha1sums=('e397e453893930d14e9bb33a847d15b94b7ee83a'
          '159bc1243564d3a9dee820291bf07168870cbaf6')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Show icon only if runnable; see FS#12045
	echo 'TryExec=/usr/bin/pygtk-demo' >> zenmap/install_scripts/unix/zenmap.desktop 
	echo 'TryExec=/usr/bin/pygtk-demo' >> zenmap/install_scripts/unix/zenmap-root.desktop 

	patch -p1 -i ../kdesu.patch # FS#31158

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--mandir=/usr/share/man \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/uninstall_zenmap
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
