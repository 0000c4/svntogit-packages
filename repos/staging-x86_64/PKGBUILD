# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-text-ui
_pkgname=ktp-text-ui
pkgver=16.03.80
pkgrel=1
pkgdesc='Telepathy handler for Text Chats'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-kde-common-internals' 'kdewebkit' 'kemoticons')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz"
	ktp-text-ui-qt5.6.patch::"https://git.reviewboard.kde.org/r/127493/diff/raw/")
md5sums=('0659426d5131bf64b4f7cbe4e9a44958'
         '4832cb3f97a6311149a8974ad907f585')

prepare() {
  mkdir -p build

# Fix displaying chats with Qt 5.6
  cd $_pkgname-$pkgver
  patch -p1 -i ../ktp-text-ui-qt5.6.patch
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
