# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-session
pkgver=4.7.3
pkgrel=1
pkgdesc="A session manager for Xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('xfce4-panel' 'libwnck' 'xorg-iceauth' 'hicolor-icon-theme' ) # keep xorg-server-utils for https://bugs.archlinux.org/task/21096  'xorg-server-utils'
makedepends=('pkgconfig' 'intltool')
optdepends=('fortune-mod: for xfce4-tips')
replaces=('xfce4-toys')
backup=(etc/polkit-1/localauthority/50-local.d/org.freedesktop.consolekit.pkla
	etc/polkit-1/localauthority/50-local.d/org.freedesktop.upower.pkla)
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.7/${pkgname}-${pkgver}.tar.bz2
	org.freedesktop.consolekit.pkla
	org.freedesktop.upower.pkla)
md5sums=('ae6c98b4419cc4b651a7e68c4afdf432'
         '73e8925488386bbf38129ba3b960fce4'
         '75ccbdb1271cb7ff21f8a65e548f409f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # git fixes

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var \
	--disable-static \
	--disable-hal \
	--disable-gnome \
	--disable-libgnome-keyring \
	--disable-legacy-sm \
	--enable-session-screenshots \
	--enable-upower \
	--enable-consolekit \
	--enable-polkit \
	--enable-panel-plugin \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # install polkit/consolekit permission file needed for usage without a display manager
  install -dm700 ${pkgdir}/etc/polkit-1/localauthority
  install -dm755 ${pkgdir}/etc/polkit-1/localauthority/50-local.d
  install -m644 ${srcdir}/org.freedesktop.consolekit.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
  install -m644 ${srcdir}/org.freedesktop.upower.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
}

