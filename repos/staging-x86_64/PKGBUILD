# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pim-sieve-editor
pkgver=16.11.80
pkgrel=1
pkgdesc="Mail sieve editor"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(libksieve messagelib)
makedepends=(extra-cmake-modules kdoctools boost python)
conflicts=('kdepim<16.11')
groups=(kde-applications kdepim)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('b7becd850dba16fa368ee71ded7377d9'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
