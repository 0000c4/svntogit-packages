# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kshisen
pkgver=16.11.80
pkgrel=1
pkgdesc="A solitaire-like game played using the standard set of Mahjong tiles"
url="http://kde.org/applications/games/kshisen/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'libkmahjongg' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-kshisen')
replaces=('kdegames-kshisen')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kshisen-${pkgver}.tar.xz"{,.sig})
sha1sums=('51c82c150bbd41bd9e12b189e9c568eafcfb9669'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../kshisen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
