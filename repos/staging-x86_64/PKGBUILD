# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: XazZ <xazz.xazz [AT] googlemail.com>

pkgname=liblastfm
pkgver=1.0.6
pkgrel=2
pkgdesc="A Qt4 C++ library for the Last.fm webservices"
arch=('i686' 'x86_64')
url='https://github.com/lastfm/liblastfm/'
license=('GPL')
depends=('qt4' 'fftw' 'libsamplerate')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/lastfm/${pkgname}/tarball/${pkgver}")
sha1sums=('cd84cf61d315c9739b3b65e31c2854a6b1a38630')

build() {
  . /etc/profile.d/qt4.sh

  mkdir build
  cd build
  cmake ../lastfm-${pkgname}-5826fe6 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_TESTS=OFF
  make
}

package(){
  cd build
  make DESTDIR="${pkgdir}" install
}
