# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-weather
pkgver=3.8.0
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(gtk3 gjs libgweather)
makedepends=(intltool gobject-introspection)
options=(!libtool)
groups=(gnome-extra)
install=gnome-weather.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('74eedc262305cca6592a059545ac82709219374f7c5cf14c46c82d30aebd1dbd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
