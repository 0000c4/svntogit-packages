# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-diskperf-plugin
pkgver=2.6.1
pkgrel=2
pkgdesc="Plugin for the Xfce4 panel displaying instant disk/partition performance"
arch=('x86_64')
license=('BSD')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-diskperf-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool' 'xfce4-dev-tools')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        glibc-2.25.patch)
sha256sums=('212ddc742be3eecb6ad5554e1b1df03d5685cb71f48a558d5f895de37c57e4fa'
            'e856cf7e77edff1db6046745e9a81e5c27ae40cadbdf57b377c49687527a3c7f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=13940
  patch -Np1 -i ../glibc-2.25.patch

  NOCONFIGURE=1 xdt-autogen
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
