# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-server
pkgver=0.6.0
pkgrel=2
pkgdesc="Library to discover, browse and manipulate Digital Media Servers"
arch=(x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git)
_commit=776950d5d96ac9dbf5c5c47bde8ac06f50a3cf46  # tags/0.6.0
source=("git+https://github.com/01org/dleyna-server#commit=$_commit"
        gupnp-1.2.diff)
sha256sums=('SKIP'
            '9954e8f8c46ad9ee42359b53332264821b27470dac6617e30f44e8961e1599d0')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../gupnp-1.2.diff
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  CFLAGS+=" -Wno-deprecated-declarations"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
