# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcontacts
pkgver=16.07.90
pkgrel=1
pkgdesc="Address book API for KDE"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcoreaddons kconfig ki18n kcodecs)
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('15a6369906fd8a8306054b4c734172f3')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
