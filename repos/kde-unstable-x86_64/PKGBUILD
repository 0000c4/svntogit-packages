# $Id: PKGBUILD 320127 2018-03-24 09:48:28Z arojas $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=digikam
_pkgver=6.0.0-beta2
pkgver=${_pkgver//-/_} # for beta versions
pkgrel=3
pkgdesc="An advanced digital photo management application"
arch=(x86_64)
license=(GPL)
url="http://www.digikam.org/"
depends=(liblqr lensfun opencv akonadi-contacts knotifyconfig libksane kfilemetadata qtav marble-common threadweaver kcalcore
         qt5-webengine libkvkontakte libmediawiki)
makedepends=(extra-cmake-modules doxygen eigen boost kdoctools)
optdepends=('hugin: panorama tool' 'qt5-imageformats: support for additional image formats (WEBP, TIFF)')
source=("http://download.kde.org/unstable/${pkgname}/${pkgname}-${_pkgver}.tar.xz"{,.sig}
        digikam-opencv4.patch)
sha256sums=('73ae57e2a5af538f335da312b5e9039268c81f124864af047ac3ebe6a86e7330'
            'SKIP'
            'd86033a92eb4fbe337636b1fb943d9234181efef9848ff7544e8117645347542')
validpgpkeys=(D1CF2444A7858C5F2FB095B74A77747BC2386E50) # digiKam.org (digiKam project) <digikamdeveloper@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$_pkgver
  patch -p1 -i ../digikam-opencv4.patch # Fix build with opencv 4
}

build() {
  cd build

  cmake ../$pkgname-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_APPSTYLES=ON \
    -DENABLE_QWEBENGINE=ON \
    -DOpenGL_GL_PREFERENCE=GLVND
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
