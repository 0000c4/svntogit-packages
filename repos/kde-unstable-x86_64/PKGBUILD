# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kimagemapeditor
pkgver=20.03.90
pkgrel=1
arch=(x86_64)
pkgdesc="HTML Image Map Editor"
url='https://www.kde.org'
license=(GPL LGPL FDL)
groups=(kde-applications kdewebdev)
depends=(qt5-webengine kparts)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1bee163e3bb6c8b33fbfd557b8768f94cd09e7730daeba366ffb6acf73c0636d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
