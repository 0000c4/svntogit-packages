# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konquest
pkgver=20.11.80
pkgrel=1
pkgdesc="The KDE version of Gnu-Lactic"
url="https://kde.org/applications/games/konquest/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d5a541d8e104d506659b27b368b1275023e9b7e4603f6daef6943fe8553034f9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
