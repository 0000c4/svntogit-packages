# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcalc
pkgver=17.03.80
pkgrel=1
pkgdesc='Scientific Calculator'
url='https://kde.org/applications/utilities/kcalc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kinit')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdeutils-kcalc')
replaces=('kdeutils-kcalc')
groups=('kde-applications' 'kdeutils')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kcalc-${pkgver}.tar.xz"{,.sig})
sha256sums=('13a06fdd06d2bd760217718b21b3732130ab5304b2b40d5e0ec782f282b4905d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
