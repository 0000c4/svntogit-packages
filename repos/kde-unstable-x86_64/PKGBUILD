# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-addons
pkgver=19.11.80
pkgrel=1
pkgdesc='Addons for KDE PIM applications'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(incidenceeditor ktnef libksieve akonadi-notes akonadi-import-wizard poppler-qt5 kitinerary kontactinterface discount)
makedepends=(extra-cmake-modules kdoctools boost)
optdepends=('grammalecte: French grammar plugin')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4191b6c24bf44bfa965e676bba26bb150b4ec19674906cc7bbb018b7b5ecb9a2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
