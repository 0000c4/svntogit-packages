# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-addons
pkgver=17.11.90
pkgrel=2
pkgdesc='Addons for KDE PIM applications'
arch=(x86_64)
url='https://community.kde.org/KDE_PIM'
license=(GPL LGPL FDL)
depends=(incidenceeditor libktnef libksieve akonadi-notes akonadi-import-wizard kparts)
makedepends=(extra-cmake-modules kdoctools boost python)
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdepim-addons-qt5.10.patch::"https://cgit.kde.org/kdepim-addons.git/patch/?id=9d4c218b")
sha256sums=('76807fdb1dea4cf37f2299b133ebd74f14820da2f4e8b7f450e42be6f99ce926'
            'SKIP'
            'a089a77bd16dda2d38fa422c2562bf3112ba7dbdacbddbc95e617146477100bd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdepim-addons-qt5.10.patch # Qt 5.10 rc2 API changes
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
