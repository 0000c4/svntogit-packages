# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmousetool
pkgver=19.11.80
pkgrel=1
arch=(x86_64)
pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
url='https://kde.org/applications/utilities/kmousetool/'
license=(GPL FDL)
groups=(kde-applications kdeaccessibility)
depends=(knotifications kxmlgui phonon-qt5 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools libxt)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kmousetool-qt-5.14.patch::"https://phabricator.kde.org/file/data/zfoj33sv5tfzejyigb6i/PHID-FILE-wvp5ew6fji5w6zxp6q5n/D25095.diff")
sha256sums=('ee118a93785ae31d0c681c679eb5ac9a957f3250969a0ab5ccd25706d8928657'
            'SKIP'
            '749a32d1c55c9b8148fe0ceffe15f508fb6bf4ac6f27979a7f47165f569f92b9')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kmousetool-qt-5.14.patch # Fix build with Qt 5.14
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
