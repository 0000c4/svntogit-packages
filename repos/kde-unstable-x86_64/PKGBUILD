# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcalc
pkgver=16.07.90
pkgrel=1
pkgdesc='Scientific Calculator'
url='http://kde.org/applications/utilities/kcalc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kinit')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdeutils-kcalc')
replaces=('kdeutils-kcalc')
groups=('kde-applications' 'kdeutils')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kcalc-${pkgver}.tar.xz")
sha1sums=('23e8e040b9d72bc3bee05ae45e2978cbc169a518')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
