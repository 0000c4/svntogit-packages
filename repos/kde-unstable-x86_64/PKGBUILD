# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libksieve
pkgver=20.03.90
pkgrel=2
pkgdesc="KDE PIM library for managing sieves"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kidentitymanagement pimcommon kmailtransport)
makedepends=(extra-cmake-modules boost kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         libksieve-revert-qt5.15-port.patch::"https://cgit.kde.org/libksieve.git/patch/?id=8e93abf5")
sha256sums=('3206b92fafdad17061910088a55eeb4d198f72b506cfa82c51830ff33058d39b'
            'SKIP'
            '253cb2437ca0f47401b48c1309aa4de79173122cb2bd345208a0711d6972888f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../libksieve-revert-qt5.15-port.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
