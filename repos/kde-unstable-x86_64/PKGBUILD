# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-desktop-applets
_pkgname=ktp-desktop-applets
pkgver=20.11.90
pkgrel=1
pkgdesc='The KDE-Telepathy Plasma desktop applets'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kde-network telepathy-kde)
depends=(telepathy-kde-common-internals telepathy-kde-accounts-kcm plasma-workspace)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('da5455e7f3d54dff8b66012da36a0f0b704c66b5c2d76a1499840cc75a707e42'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
