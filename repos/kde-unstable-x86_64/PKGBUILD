# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpimtextedit
pkgver=16.11.90
pkgrel=1
pkgdesc="A textedit with PIM-specific features"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kio kemoticons grantlee syntax-highlighting)
makedepends=(extra-cmake-modules python kdoctools kdesignerplugin qt5-tools)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('21ba7510886c79b129e1f610f05c1371'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
