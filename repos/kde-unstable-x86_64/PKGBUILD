# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=korganizer
pkgver=17.11.90
pkgrel=2
pkgdesc="Calendar and scheduling Program"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kdepim-runtime kontactinterface incidenceeditor)
makedepends=(extra-cmake-modules kdoctools python qt5-tools boost)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        korganizer-qt5.10.patch::"https://cgit.kde.org/korganizer.git/patch/?id=a383758d")
sha256sums=('7b70d39ec547572f8992af18e37f8750bab3726a460226a24a728793c592b3e6'
            'SKIP'
            '5c2f3362b4d07bb8ad88194c20d1ce8546fd58851bb337971aedb55ceda30793')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../korganizer-qt5.10.patch # Qt 5.10 rc2 API changes
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
