# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkleo
pkgver=16.07.80
pkgrel=1
pkgdesc="KDE PIM cryptographic library"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kpimtextedit)
makedepends=(extra-cmake-modules python boost)
conflicts=('libkdepim<16')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('bc86347b2a109cd1fe51ef764f0648f2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
