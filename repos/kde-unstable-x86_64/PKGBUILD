# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkleo
pkgver=16.03.80
pkgrel=1
pkgdesc="KDE PIM cryptographic library"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kpimtextedit)
makedepends=(extra-cmake-modules)
conflicts=('libkdepim<16')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('d35d55f3ef1a6c09e8269682a5033587')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
