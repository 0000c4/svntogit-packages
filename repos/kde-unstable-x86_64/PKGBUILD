# $Id: PKGBUILD 319871 2018-03-23 19:45:34Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: said
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=kamoso
pkgver=18.03.90
pkgrel=1
pkgdesc="A webcam recorder from KDE community"
url="https://userbase.kde.org/Kamoso"
arch=(x86_64)
license=(GPL)
groups=(kde-applications kdemultimedia)
depends=(purpose gst-plugins-bad qt5-quickcontrols kirigami2)
makedepends=(extra-cmake-modules kdoctools boost python)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f4d5da900629076b59c784868c90a946db98a95c992d394127b750e25fb423b0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

