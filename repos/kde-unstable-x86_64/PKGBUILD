# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=step
pkgver=18.03.90
pkgrel=2
pkgdesc="Interactive Physical Simulator"
url="https://kde.org/applications/education/step/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(libqalculate gsl knewstuff khtml kdelibs4support kplotting hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools eigen mesa kdesignerplugin qt5-tools)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('346e263065c2283d30b3e3cc7068c28f28992a48dff031fe75d7d0ab1b5d6464'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  sed -e '/Qml/d' -e '/Quick/d' -i $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
