# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=17.03.90
pkgrel=1
pkgdesc="KDE File Manager & Web Browser"
arch=(i686 x86_64)
url="https://konqueror.org/"
license=(LGPL)
depends=(dolphin keditbookmarks qt5-webengine)
makedepends=(extra-cmake-modules kdoctools python tidy kdesignerplugin kdesu khtml)
optdepends=('tidy: tidy HTML plugin' 'kdesu: shell command plugin' 'khtml: alternative web engine')
conflicts=(kdebase-konqueror kdebase-konq-plugins)
replaces=(kdebase-konqueror kdebase-konq-plugins)
groups=(kde-applications kdebase)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a4f9201c04f1a4578aa24297bc578c17e49e2f83b612c36bfd1c49ce008476b6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
