# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kldap
pkgver=16.11.80
pkgrel=1
pkgdesc="LDAP access API for KDE"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcompletion kmbox)
optdepends=('kio: LDAP kioslave')
makedepends=(extra-cmake-modules python kio kdoctools)
conflicts=(kio-pim)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('acf1cbd7d8fc159943ab1ab109572ff4'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
