# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=filelight
pkgver=20.03.80
pkgrel=1
pkgdesc='View disk usage information'
url='https://kde.org/applications/utilities/filelight'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         filelight-qt5.15.patch::"https://cgit.kde.org/filelight.git/patch/?id=50e8681a")
sha256sums=('0c1bf7933938c7ee56d13ad7c50e30c56f7cf90539298506e348a143aba2a526'
            'SKIP'
            '50dd287d363e8985bf46b65ef697f73e509434255ba68e4d61076694a2093e05')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -Rp1 -i ../filelight-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
