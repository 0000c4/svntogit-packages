# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-common-internals
_pkgname=ktp-common-internals
pkgver=17.03.90
pkgrel=1
pkgdesc='Common components for KDE-Telepathy'
arch=(i686 x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=(knotifyconfig ktexteditor kpeople telepathy-logger-qt kaccounts-integration libotr hicolor-icon-theme telepathy-accounts-signon)
makedepends=(extra-cmake-modules kdoctools doxygen python)
source=("https://download.kde.org/stable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('288fe7415bdf3e7727f3d59e3c450a8f333f59fb9fd9eec0919a12710c009c1b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
