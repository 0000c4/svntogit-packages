# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-data
pkgver=17.07.80
pkgrel=1
pkgdesc="Common data for KDE Edu applications"
url="https://kde.org/applications/education"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
depends=('hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'qt5-base')
conflicts=('libkdeedu<14')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('5aa7725224584a8678499018a8d47f3872304794b18ead872c326b31284fc6d5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
