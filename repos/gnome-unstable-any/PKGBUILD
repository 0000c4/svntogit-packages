# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=pyatspi
pkgver=2.1.90
pkgrel=1
pkgdesc="Python bindings for at-spi"
arch=(any)
url="http://www.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=('GPL2')
depends=('pygobject' 'at-spi2-core')
source=(http://download.gnome.org/sources/${pkgname}/2.1/${pkgname}-${pkgver}.tar.xz)
sha256sums=('163bc777026d86be539ec623e4990496dcfd5318b76c634d4ddca5955e844bf6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
