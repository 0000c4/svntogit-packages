# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-backgrounds
pkgver=3.20+49+gd740714
pkgrel=1
pkgdesc="Background images and data for GNOME"
arch=(any)
makedepends=('intltool' 'git' 'glib2' 'gnome-common')
url="http://www.gnome.org"
license=('GPL')
groups=('gnome')
_commit=d7407141e7ec1b5941482bceddb006054c590b09
source=("git://git.gnome.org/gnome-backgrounds#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
