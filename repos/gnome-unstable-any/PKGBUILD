# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
pkgname=cantarell-fonts
pkgver=0.0.1
pkgrel=1
pkgdesc="Cantarell font family for the GNOME desktop"
arch=('any')
url="http://abattis.org/cantarell/"
license=('GPL2')
depends=('fontconfig')
install=cantarell-fonts.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.bz2)
sha256sums=('471ae75968c0a8f25f63f7265a63250441d124528a193fe12d5722a501c176b9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/etc/fonts"
  mv "$pkgdir/usr/share/fontconfig/conf.avail" "$pkgdir/etc/fonts/conf.avail"
  rmdir "$pkgdir/usr/share/fontconfig"
}

# vim:set ts=2 sw=2 et:
