# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=pyatspi
pkgver=2.3.91
pkgrel=1
pkgdesc="Python bindings for at-spi"
arch=(any)
url="http://www.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=('GPL2')
depends=('python2-gobject' 'at-spi2-core')
source=(http://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('9aa4a8c1085453da67cc1f4d99ff6dea39e9f873a6a3db6de1d582717dac28be')

build() {
  cd $pkgname-$pkgver
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
