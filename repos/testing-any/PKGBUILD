# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=noto-fonts
pkgname=(noto-fonts ttf-croscore)
_pkgver=2017-01-12
_commit=721e63c04fa8fb8b4d7c1c908e68e721317d8b53  # master
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto TTF fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(custom:SIL)
provides=(ttf-font)
#source=("$pkgbase-$_pkgver.tar.gz::https://github.com/googlei18n/noto-fonts/archive/v$_pkgver-license-adobe.tar.gz")
source=("$pkgbase-$_commit.tar.gz::https://github.com/googlei18n/noto-fonts/archive/$_commit.tar.gz")
sha256sums=('a36a76566381d6f2b72cf4831c3d052804beb63ad94fa3ff4a6cae3412a50202')

package_noto-fonts() {
  optdepends=('noto-fonts-cjk: CJK characters' 'noto-fonts-emoji: Emoji characters')

  cd $pkgbase-*/
  mkdir -p "$pkgdir"/usr/share/fonts/noto
  install -m644 unhinted/Noto*.tt[fc] "$pkgdir"/usr/share/fonts/noto
  install -m644 hinted/Noto*.tt[fc] "$pkgdir"/usr/share/fonts/noto
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/noto-fonts/LICENSE

  # Already in NotoSans-*.ttc
  rm "$pkgdir"/usr/share/fonts/noto/NotoSans{,UI}-*.ttf
}

package_ttf-croscore() {
  pkgdesc='Chrome OS core fonts'

  cd $pkgbase-*/
  mkdir -p "$pkgdir"/usr/share/fonts/TTF
  install -m644 hinted/{Arimo,Cousine,Tinos}*.ttf "$pkgdir"/usr/share/fonts/TTF
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/ttf-croscore/LICENSE
}
