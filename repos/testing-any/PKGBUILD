# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-file-copy-recursive
pkgver=0.44
pkgrel=2
pkgdesc="Perl extension for recursively copying files and directories"
arch=('any')
url="https://metacpan.org/release/File-Copy-Recursive"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-path-tiny' 'perl-test-deep' 'perl-test-fatal' 'perl-test-file'
              'perl-test-warnings')
options=('!emptydirs')
source=("http://cpan.metacpan.org/authors/id/D/DM/DMUEY/File-Copy-Recursive-${pkgver}.tar.gz")
sha512sums=('7599c48cee0b9848d5e275a1de00845cb2d4820eda9092d550063d4791974870129ce8d3d9337a8f7ea413ed4c21e533c2eb3134c2fcb5cc412dbbfddd2500e4')

build() {
  cd File-Copy-Recursive-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check () {
  cd File-Copy-Recursive-$pkgver
  make test
}

package () {
  cd File-Copy-Recursive-$pkgver
  make DESTDIR="${pkgdir}" install
}

