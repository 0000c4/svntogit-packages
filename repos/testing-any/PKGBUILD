# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=meld
pkgver=1.3.3
pkgrel=2
pkgdesc='Visual diff and merge tool for GNOME'
arch=('any')
url='http://meld.sourceforge.net/'
license=('GPL')
depends=('pygtk>=2.16.0')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'gnome-python: gnome integration')
install=meld.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.3/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('bfdf9a5e33a7ebc5b976fbd2925033e2bbd46e8f632437959b88aac35676d08a')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PYTHON=python2 prefix=/usr
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PYTHON=python2 prefix=/usr DESTDIR="${pkgdir}" install
    sed -i -e 's#env python#env python2#' "${pkgdir}"/usr/bin/meld
}
