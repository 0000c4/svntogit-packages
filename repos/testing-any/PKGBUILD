# Maintainer:

pkgname=perl-file-listing
pkgver=6.07
pkgrel=1
pkgdesc="parse directory listing"
arch=('any')
url="https://search.cpan.org/dist/File-Listing"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-http-date')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/P/PL/PLICEASE/File-Listing-${pkgver}.tar.gz")
sha512sums=('1d525058e64ed3f3115721df63527752c8f3de2e0b000efea93b48054695e6b8143c613cdc1c652db30178f517f5bc106dfa176d30629553150234e7cd50682f')

build() {
  cd File-Listing-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-Listing-${pkgver}
  make test
}

package() {
  cd File-Listing-${pkgver}
  make DESTDIR="$pkgdir" install
}
