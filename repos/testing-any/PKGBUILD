# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.0.2
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('822171fb08a598976019c4754f0318b2ecef07c080ec01ff03a86e140d0802311aa2523277bcad4966ca5803fb0a12b529dbf531d9642872d8b0b89cd885d3cf')

build() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/astroid-astroid-$pkgver
  pytest
}

package() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
