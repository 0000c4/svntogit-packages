# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=eigen
pkgver=3.0.0
pkgrel=1
pkgdesc="A C++ template library for linear algebra: matrices, vectors and related algorithms"
arch=('any')
url='http://eigen.tuxfamily.org/index.php?title=Main_Page'
license=('GPL' 'LGPL3')
makedepends=('cmake' 'pkg-config' 'fftw' 'gsl')
source=("http://bitbucket.org/${pkgname}/${pkgname}/get/${pkgver}.tar.bz2")
md5sums=('046baf7072f008653361f8321560a26f')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-eigen-65ee2328342f \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
