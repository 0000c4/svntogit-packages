# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-file-copy-recursive
pkgver=0.41
pkgrel=1
pkgdesc="Perl extension for recursively copying files and directories"
arch=('any')
url="https://metacpan.org/release/File-Copy-Recursive"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-path-tiny' 'perl-test-deep' 'perl-test-exception' 'perl-test-file'
              'perl-test-warn' 'perl-file-find-rule')
options=('!emptydirs')
source=("http://cpan.metacpan.org/authors/id/D/DM/DMUEY/File-Copy-Recursive-${pkgver}.tar.gz")
sha512sums=('8950b1810d86cde5533778efccd2b8ec2be4c9764a49e03bbd0a0d927984cda74f9972226c73f6967ccbacf42791358643c3b20d3129fd322497d6b8f195c3ea')

build() {
  cd File-Copy-Recursive-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check () {
  cd File-Copy-Recursive-$pkgver
  make test
}

package () {
  cd File-Copy-Recursive-$pkgver
  make DESTDIR="${pkgdir}" install
}

