# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

#
# Create a new tarball from the linux-firmware git tree with the following command:
# git clone git://git.kernel.org/pub/scm/linux/kernel/git/dwmw2/linux-firmware.git
# git archive --format=tar --prefix linux-firmware-$(date +%Y%m%d)/ master | xz -9 > linux-firmware-$(date +%Y%m%d).tar.xz
#

pkgname=linux-firmware
pkgver=20111101
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/dwmw2/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
provides=('kernel26-firmware=2.6.34.99')
conflicts=('linux-firmware-git'
           'kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw')
replaces=('kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw')
options=(!strip)
source=(ftp://ftp.archlinux.org/other/linux-firmware/$pkgname-$pkgver.tar.xz
        http://www.kernel.org/pub/linux/kernel/people/mcgrof/firmware/ar9170/ar9170.fw
        http://intellinuxwireless.org/iwlwifi/downloads/iwlwifi-5000-ucode-8.83.5.1-1.tgz
        http://intellinuxwireless.org/iwlwifi/downloads/iwlwifi-1000-ucode-39.31.5.1.tgz
        http://intellinuxwireless.org/iwlwifi/downloads/iwlwifi-6000-ucode-9.221.4.1.tgz
        http://intellinuxwireless.org/iwlwifi/downloads/iwlwifi-6000g2a-ucode-17.168.5.3.tgz
        http://intellinuxwireless.org/iwlwifi/downloads/iwlwifi-6000g2b-ucode-17.168.5.2.tgz
)
md5sums=('660c30aea4c8bdf4a1441dcf8530a9ed'
         '34feec4ec0eae3bb92c7c1ea2dfb4530'
         'cec71b615f3ca6d7823c032da0be1b61'
         '8098503cb2abcdeffffb3ddd2d8d6f60'
         'c132a4c1946a9dbc0c36b41696e5c793'
         'fe99ef72d4c016c9b04506fa474c1acd'
         'd87411296b4eeda0c91322228e9f8437')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d -m755 "${pkgdir}/lib/firmware"
  cp -a * "${pkgdir}/lib/firmware/"
  install -d -m755 "${pkgdir}/usr/share/licenses/linux-firmware/"
  rm -f "${pkgdir}/lib/firmware/GPL*"
  mv "${pkgdir}/lib/firmware"/{LICEN*,WHENCE} "${pkgdir}/usr/share/licenses/linux-firmware/"

  # Symlinks for Broadcom firmware
  #ln -s bcm43xx-0-610-809-0.fw "${pkgdir}/lib/firmware/brcm/bcm43xx-0.fw"
  #ln -s bcm43xx_hdr-0-610-809-0.fw "${pkgdir}/lib/firmware/brcm/bcm43xx_hdr-0.fw"

  # https://bugs.archlinux.org/task/20064
  install -m644 "${srcdir}/ar9170.fw" "${pkgdir}/lib/firmware/"
  # fix intel firmware files
  install -m 644 ${srcdir}/iwlwifi-1000-ucode-39.31.5.1/iwlwifi-1000-5.ucode "${pkgdir}/lib/firmware/"
  install -m 644 ${srcdir}/iwlwifi-5000-ucode-8.83.5.1/iwlwifi-5000-5.ucode "${pkgdir}/lib/firmware/"
  install -m 644 ${srcdir}/iwlwifi-6000-ucode-9.221.4.1/iwlwifi-6000-4.ucode "${pkgdir}/lib/firmware/"
  install -m 644 ${srcdir}/iwlwifi-6000g2a-ucode-17.168.5.3/iwlwifi-6000g2a-5.ucode "${pkgdir}/lib/firmware/"
  install -m 644 ${srcdir}/iwlwifi-6000g2b-ucode-17.168.5.2/iwlwifi-6000g2b-5.ucode "${pkgdir}/lib/firmware/"
}
# vim:set ts=2 sw=2 et:
