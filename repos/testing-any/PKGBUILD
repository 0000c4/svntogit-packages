# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=0.8.0
pkgrel=2
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('mkinitcpio-busybox>=1.16.1-2' 'module-init-tools' 'util-linux>=2.19' 'libarchive' 'coreutils'
         'bash' 'findutils' 'sed' 'grep' 'filesystem>=2011.10-1' 'udev>=174-1' 'file' 'gzip')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
replaces=('mkinitrd' 'mkinitramfs' 'klibc' 'klibc-extras' 'klibc-kbd'
          'klibc-module-init-tools' 'klibc-udev')
backup=(etc/mkinitcpio.conf)
source=(ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('097faaf23aaa6d442e7cc6add7238698abfea4185e7d125dcffd74d0ea159246')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # add compat symlink until people stop hardcoding paths !$%(&*!#$
  mkdir "$pkgdir/sbin"
  ln -s /usr/bin/mkinitcpio "$pkgdir/sbin/mkinitcpio"
}
