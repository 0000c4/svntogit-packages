# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2017a
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('any')
url="http://www.iana.org/time-zones"
license=('GPL')
options=('!emptydirs')
# new signing key is not available via public keyservers
source=(https://www.iana.org/time-zones/repository/releases/${pkgname}${pkgver}.tar.gz{,.asc})
sha512sums=('fe57930711998a0755749a2bf1b32fdbc9e6ee249762017beea52eae7e521236396300fe2d79298fd81eb6d73fb7567884c35995ed248be06188329d937e3873'
            'SKIP')
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34') # Paul Eggert <eggert@cs.ucla.edu>

timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'pacificnew' 'etcetera' 'backward'
           'systemv' 'factory')

package() {
  cd ${srcdir}
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo ${timezones[@]}
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/posix ${timezones[@]}
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}
  
  zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo -p America/New_York
  install -m444 -t ${pkgdir}/usr/share/zoneinfo iso3166.tab zone1970.tab zone.tab # zone.tab is depricated and will go soon
}
