# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-config-simple
pkgver=4.59
pkgrel=4
pkgdesc="Simple configuration file class to create and read config files"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
url="http://search.cpan.org/~sherzodr/Config-Simple"
source=("http://search.cpan.org/CPAN/authors/id/S/SH/SHERZODR/Config-Simple-${pkgver}.tar.gz")
md5sums=('96513b61c7db591339ce2577878a3b32')
_distdir="${srcdir}/Config-Simple-${pkgver}"

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$_distdir"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  cd "$_distdir"
  make test
}

package() {
  cd "$_distdir"
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

