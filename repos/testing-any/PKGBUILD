# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=jsch
pkgname=java-${_libname}
pkgver=0.1.54
pkgrel=1
pkgdesc="A pure Java implementation of SSH2"
arch=('any')
url='http://www.jcraft.com/jsch/'
license=('BSD')
depends=('java-runtime-headless=8')
makedepends=('apache-ant' 'java-environment=8')
source=(http://downloads.sourceforge.net/project/${_libname}/${_libname}/${pkgver}/${_libname}-${pkgver}.zip)
sha512sums=('2ed29f07495fbc28d6eb7b4fe414e8a9a96b911b2feea5b1513cc48a9439adba12341bceb23a3ea9ab307e32d48bfd4356c7bc877c56e77f6f55d69214db3a9e')

build() {
  cd "${srcdir}/${_libname}-${pkgver}"
  ant dist
}

package() {
  install -Dm644 "${srcdir}/${_libname}-${pkgver}/dist/lib/${_libname}-${pkgver}.jar" \
                 "${pkgdir}/usr/share/java/${_libname}-${pkgver}.jar"
  ln -s ${_libname}-${pkgver}.jar "${pkgdir}/usr/share/java/${_libname}.jar"
  install -Dm644 "${srcdir}/${_libname}-${pkgver}/LICENSE.txt" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
