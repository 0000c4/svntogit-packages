# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.26.0
pkgrel=2
pkgdesc="Configuration database system"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(LGPL2.1)
depends=(glib2)
makedepends=(vala intltool docbook-xsl python)
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('8683292eb31a3fae31e561f0a4220d8569b0f6d882e9958b68373f9043d658c9')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install \
    completiondir=/usr/share/bash-completion/completions
}

# vim:set ts=2 sw=2 et:
