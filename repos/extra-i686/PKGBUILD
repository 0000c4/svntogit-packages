# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Alexander Rødseth
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
pkgname=clucene
pkgver=2.3.3.4
pkgrel=4
pkgdesc="C++ port of the high-performance text search engine Lucene"
arch=('x86_64' 'i686')
url="http://clucene.sourceforge.net/"
license=('APACHE' 'LGPL')
depends=('gcc-libs' 'zlib' 'boost-libs')
makedepends=('cmake' 'boost')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-core-$pkgver.tar.gz
        clucene-core-2.3.3.4-install_contribs_lib.patch
        clucene-core-2.3.3.4-pkgconfig.patch
        fix_zlib_detections.diff)
sha256sums=('ddfdc433dd8ad31b5c5819cc4404a8d2127472a3b720d3e744e8c51d79732eab'
            '3d3f73685f75b4ceacf1941e50b6108941bded3ca558ac1343c35b1b7d0e78dc'
            '10c808ce483d997d7ff349cc3ec97b8785c365f956d6eef45458e9caf4e5e88d'
            'e1a6a58dc344d8d5e3218137a5e7a689900eb6ea4cce1dc426e861706ab78889')

build() {
  cd $srcdir/$pkgname-core-$pkgver

  # add missing contrib-libs needed by LibO 3.6, patch by FC
  patch -Np1 -i ${srcdir}/clucene-core-2.3.3.4-install_contribs_lib.patch
  # pkgconfig file is missing clucene-shared (upstream ID: 3461512), patch by FC
  patch -Np1 -i ${srcdir}/clucene-core-2.3.3.4-pkgconfig.patch
  # one upstream postrelease commit for proper zlib detection
  patch -Np1 -i ${srcdir}/fix_zlib_detections.diff
  sed -i 's:core:core -lclucene-shared:' src/core/libclucene-core.pc.cmake

  mkdir build
  pushd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_ASCII_MODE=OFF \
    -DENABLE_PACKAGING=OFF \
    -DBUILD_CONTRIBS_LIB=BOOL:ON \
    -DDISABLE_MULTITHREADING=OFF
  popd
  make -C build
}

#check() {
#  cd $srcdir/$pkgname-core-$pkgver
#  make cl_test -C build
#  make test -C build || /bin/true # currently fails the tests as expected (see FC comment)
#}

package() {
  cd $srcdir/$pkgname-core-$pkgver
  make DESTDIR="$pkgdir" install -C build
  rm -rf $pkgdir/usr/lib/CLuceneConfig.cmake
}
