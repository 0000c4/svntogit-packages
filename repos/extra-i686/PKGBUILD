# $Id: PKGBUILD 174870 2013-01-08 10:06:34Z jgc $
# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+0.90
pkgrel=1
pkgdesc="CD paranoia libraries from libcdio"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/libcdio/"
depends=('libcdio')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.bz2{,.sig}
        header-fix.patch)
md5sums=('432e9f1c5ceb645c4bca9db3f9297437'
         '39766404d473ecda66c2f3a932d1c57c'
         '1ebb33a1cfe00181de31138ef001e01b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ../header-fix.patch
  sed -i -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
  aclocal
  autoconf
  automake --add-missing
  ./configure --prefix=/usr --enable-cpp-progs --disable-static --disable-example-progs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  
  #sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
  #  $pkgdir/usr/include/cdio/cdio_config.h
}
