# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xdm
pkgver=1.1.8
pkgrel=1
pkgdesc="X Display Manager"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('pam' 'libxaw' 'libxinerama' 'xorg-server-utils' 'libxft')
makedepends=('pkgconfig')
backup=(etc/X11/xdm/Xaccess etc/X11/xdm/Xresources etc/X11/xdm/Xservers etc/X11/xdm/xdm-config etc/pam.d/xdm etc/X11/xdm/Xsetup_0 etc/X11/xdm/Xsession)
options=('!libtool')
source=(${url}/releases/individual/app/xdm-${pkgver}.tar.bz2
	Xsession-loginshell.patch
	xdm.pam)
md5sums=('cc1816bc62a3722ad509373b0b7f30fe'
	 '234ae568e7b22cbc09433bb396ff3d80'
         '419d6289ba6f851135f5c70c0e3cbec4')

build() {
  cd ${startdir}/src/xdm-${pkgver}
  patch -Np0 -i ${srcdir}/Xsession-loginshell.patch || return 1
  ./configure --prefix=/usr \
              --disable-static \
	      --disable-xprint \
	      --with-xdmconfigdir=/etc/X11/xdm \
	      --with-xdmscriptdir=/etc/X11/xdm \
	      --with-pixmapdir=/usr/share/xdm/pixmaps || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
  install -m755 -d ${startdir}/pkg/etc/pam.d
  install -m644 ${startdir}/src/xdm.pam ${startdir}/pkg/etc/pam.d/xdm || return 1

  #Get rid of /usr/X11R6
  cd ${startdir}/pkg/etc/X11/xdm
  sed -i -e 's/\/X11R6//g' *
}
