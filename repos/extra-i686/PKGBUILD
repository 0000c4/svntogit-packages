# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=0.2.3
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback"
arch=('i686' 'x86_64')
url="http://www.videolan.org/developers/libbluray.html"
license=('LGPL2.1')
depends=('libxml2')
makedepends=('apache-ant' 'jdk7-openjdk')
optdepends=('java-environment: Blu-ray Disc Java support library')
options=(!libtool)
source=(ftp://ftp.videolan.org/pub/videolan/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('b84fd172523facb207134bb1fee09c7f65914d22f6120fbfd6f312f0435229f3b110e8cc8ea121159929cda3760cfac4b52f84de39d0fb976f8c88d1f7e6d278')

build() {
  cd "$pkgname-$pkgver"

  . /etc/profile.d/jdk.sh
  ./configure --prefix=/usr \
    --enable-bdjava \
    --with-jdk=${JAVA_HOME} \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  #install bdj
  install -Dm644 src/.libs/libbluray.jar "$pkgdir/usr/share/java/libbluray.jar"
}

# vim:set ts=2 sw=2 et:
