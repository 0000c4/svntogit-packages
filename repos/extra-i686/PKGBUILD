# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=cfitsio
pkgver=3.400
pkgrel=1
pkgdesc="A library of C and Fortran subroutines for reading and writing data files in FITS (Flexible Image Transport System) data format"
arch=(i686 x86_64)
url="http://heasarc.gsfc.nasa.gov/fitsio/"
license=(custom)
depends=(glibc)
source=("ftp://heasarc.gsfc.nasa.gov/software/fitsio/c/${pkgname}${pkgver/./}.tar.gz" cfitsio-3.400-fix-build.patch)
sha512sums=('e54efb5c7a5a19ae354b202644cf2fcac946997bc0d14a3dc0b609af8725050604df167b4211586868a90b1d5098c1efdf167d6ce842c79b494e154544f3441a'
            '931579603417c55922d02cdcb9aec9deb79e3aca2c36e15e697e6c371749291aec52873f7bdc188f3a4aabe849655be4172b3edbcba1c529d5bfed265f26fc38')

prepare() {
  cd $pkgname
  patch -p1 -i ../cfitsio-3.400-fix-build.patch
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --enable-reentrant
  make shared
  make utils
}

check() {
  cd $pkgname
  LD_LIBRARY_PATH=. ./testprog > testprog.lis
  [[ -z $(diff testprog.lis testprog.out) ]] || return 1
  [[ -z $(cmp testprog.fit testprog.std) ]] || return 1
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -D -m644 License.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

# Fix conflicts with ccfits and smem
  rm "$pkgdir"/usr/bin/{cookbook,smem,testprog}
}
