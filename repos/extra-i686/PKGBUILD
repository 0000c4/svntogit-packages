# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.22.1+5+g5d281a8
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(i686 x86_64)
license=(GPL)
depends=(libsoup libnotify gsettings-desktop-schemas webkit2gtk nss iso-codes
         dconf gcr gnome-desktop libwnck3 gnome-themes-standard)
makedepends=(intltool itstool docbook-xml startup-notification
             gobject-introspection yelp-tools autoconf-archive appstream-glib git)
groups=(gnome)
_commit=5d281a891b956ad714ce91d37c64438eadeca7bc  # gnome-3-22
source=("git://git.gnome.org/epiphany#commit=$_commit"
        "git://git.gnome.org/libgd"
        pluginsdir.diff)
sha256sums=('SKIP'
            'SKIP'
            '42a7b820fd659fee8508a9b9a57e385c658b3b312c7a8f9456078223e4d78f22')

pkgver() {
  cd $pkgname

  git submodule init
  git config --local submodule.libgd.url "$srcdir/libgd"
  git submodule update

  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../pluginsdir.diff
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/$pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
