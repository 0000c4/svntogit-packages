# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxi
pkgver=1.7.7
pkgrel=2
pkgdesc="X11 Input extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
depends=('libxext' 'inputproto')
makedepends=('pkgconfig' 'xorg-util-macros' 'libxfixes' 'automake')
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2{,.sig}
        fix-gtk2-segfault.patch)
sha256sums=('996f834fa57b9b33ba36690f6f5c6a29320bc8213022943912462d8015b1e030'
            'SKIP'
            '34ac1854b6bb14cbb048ddbd20cce7a4b2ad1e8ffa6b116aa20b0dfc56655c4b')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer
validpgpkeys+=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>

prepare() {
  cd libXi-${pkgver}
  #fix FS#51298
  patch -Np1 -i ../fix-gtk2-segfault.patch
}

build() {
  cd libXi-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXi-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
