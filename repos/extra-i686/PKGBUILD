#$Id$
#Maintainer: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring
pkgver=2.26.0
pkgrel=1
pkgdesc="GNOME Password Management daemon"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gconf>=2.26.0-2' 'hal>=0.5.11')
makedepends=('intltool' 'pkgconfig')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gnome-keyring.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2
	gnome-keyring.service)
md5sums=('0395fcf79b990465030a2795dcddacb9' '16062d82eb8062201fb24f3e0ceb49a6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --libexecdir=/usr/lib/gnome-keyring \
	      --with-pam-dir=/lib/security || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/dbus-1/services"
  install -m644 "${srcdir}/gnome-keyring.service" "${pkgdir}/usr/share/dbus-1/services/" || return 1
     
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-keyring ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
