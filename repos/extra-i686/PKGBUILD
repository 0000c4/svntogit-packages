# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=3.18.3
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=(GPL)
depends=(gtksourceview3 gsettings-desktop-schemas libpeas enchant iso-codes desktop-file-utils python-gobject dconf)
makedepends=(yelp-tools intltool vala gobject-introspection)
groups=(gnome-extra)
options=('!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6762ac0d793b0f754a2da5f88739d04fa39daa7491c5c46401d24bcef76c32e7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-updater --disable-schemas-compile \
      --enable-python

  # Hack to remove unneeded library linking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
