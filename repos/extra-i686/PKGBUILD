# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=5.10.0
pkgrel=2
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('GPL2')
depends=('plasma-workspace' 'modemmanager-qt' 'networkmanager-qt' 'qca-qt5' 'mobile-broadband-provider-info')
makedepends=('extra-cmake-modules' 'kdoctools' 'openconnect' 'python' 'kdesignerplugin')
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
groups=('plasma')
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-380299.patch::"https://cgit.kde.org/plasma-nm.git/patch/?id=0ebb1756")
sha256sums=('1c9c55066ac35e5d7b60fd601816418bfd68673072ac9918ee8f6c8018ffe649'
            'SKIP'
            'bd1d8e948e8d0c0803584192933d4af859cc75a90decab8a6e81bea7e1f422f2')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix openconnect support https://bugs.kde.org/show_bug.cgi?id=380299
  patch -p1 -i ../kdebug-380299.patch
} 

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
