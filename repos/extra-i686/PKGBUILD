# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=purpose
pkgver=1.0
pkgrel=3
pkgdesc="Framework for providing abstractions to get the developer's purposes fulfilled"
arch=(i686 x86_64)
url='https://projects.kde.org/purpose'
license=(LGPL)
depends=(kaccounts-integration hicolor-icon-theme)
makedepends=(extra-cmake-modules intltool python)
source=("http://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz")

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# remove icons provided by kdevplatform
  rm "$pkgdir"/usr/share/icons/hicolor/*/apps/reviewboard.png
}
md5sums=('8f27161d5bc0193ad67db48cb6782d84')
