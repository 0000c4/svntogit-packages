# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=blinken
pkgver=17.04.3
pkgrel=1
pkgdesc="Memory Enhancement Game"
url="https://kde.org/applications/education/blinken/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui' 'phonon-qt5' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-blinken')
conflicts=('kdeedu-blinken')
source=("https://download.kde.org/stable/applications/${pkgver}/src/blinken-${pkgver}.tar.xz"{,.sig})
sha256sums=('401b06f744f85ff5d3dd106600534f47f70d9b27601fec2e83502dd24841a08e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
