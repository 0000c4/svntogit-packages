# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Samuel Mendes <heka.lok@gmail.com>

pkgname=telepathy-idle
pkgver=0.1.13
pkgrel=1
pkgdesc="An IRC connection manager for Telepathy"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL')
depends=('telepathy-glib' 'openssl')
makedepends=('libxslt' 'python2')
install=telepathy-idle.install
options=(!makeflags)
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('a82a6f15bf12e21285e6a36256c2bde0')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr \
    	--libexecdir=/usr/lib/telepathy
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
