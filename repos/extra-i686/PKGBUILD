# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-contacts
pkgver=16.08.1
pkgrel=1
pkgdesc="Libraries and daemons to implement Contact Management in Akonadi"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(akonadi grantlee kcontacts kmime qt5-webengine prison)
makedepends=(extra-cmake-modules kdoctools python boost akonadi-mime kcalcore)
conflicts=(akonadi-contact)
replaces=(akonadi-contact)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('fd7f41da52d1e2ef56a7e32e08659a89')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
