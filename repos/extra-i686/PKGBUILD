# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview3
_pkgbasename=gtksourceview
pkgver=3.0.5
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libxml2')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
options=('!libtool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-${pkgver}.tar.xz)
sha256sums=('3502fb5da443f687fdba228e3c015429874b4286e19670a87055866800f985c5')

build() {
  cd "${srcdir}/$_pkgbasename-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/$_pkgbasename-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
