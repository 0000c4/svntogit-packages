# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=16.04.0
pkgrel=3
pkgdesc="KDE screenshot capture utility"
arch=(i686 x86_64)
url='https://www.kde.org/applications/graphics/'
license=(GPL)
depends=(xcb-util-cursor libkscreen libkipi purpose)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdegraphics-ksnapshot kscreengenie)
replaces=(kdegraphics-ksnapshot kscreengenie)
groups=(kde-applications kdegraphics)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
        spectacle-hidpi.patch::"https://quickgit.kde.org/?p=spectacle.git&a=commitdiff&h=3d4dcacc&o=plain"
        kdebug-362241.patch)
md5sums=('231a565255ff420b2a8b0974530b1c28'
         '53e7af8dd42eb968d642d110f0a69be1'
         '4cc380edb046432832fc6a3aeb926d82')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix high DPI
  patch -p1 -i ../spectacle-hidpi.patch
# Unbreak rectangular image grabs for nvidia users http://bugs.kde.org/show_bug.cgi?id=362241
  patch -p1 -i ../kdebug-362241.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

