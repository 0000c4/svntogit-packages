# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libtiff
pkgver=4.0.7
pkgrel=1
pkgdesc="Library for manipulation of TIFF images"
arch=('i686' 'x86_64')
url="http://www.simplesystems.org/libtiff/"
license=('custom')
depends=('gcc-libs' 'libjpeg' 'zlib' 'xz')
makedepends=('freeglut' 'glu' 'mesa' 'jbigkit')
optdepends=('freeglut: for using tiffgt')
source=(http://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz)
sha1sums=('2c1b64478e88f93522a42dd5271214a0e5eae648')

build() {
  cd tiff-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd tiff-${pkgver}
  make check
}

package() {
  cd tiff-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
