# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.20.2
pkgrel=1
pkgdesc="Remove colored balls from the board by forming lines"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
url="https://wiki.gnome.org/Apps/Five%20or%20more"
groups=('gnome-extra')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('1e129c705eb09638d1515c2d8fe1925db7d9bcb8db4a01df591a9b98de2a73d1')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
