# $Id$
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xorg-xlsfonts
pkgver=1.0.3
pkgrel=2
pkgdesc='List available X fonts'
url='http://xorg.freedesktop.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('libx11')
makedepends=('xorg-util-macros')
source=("http://xorg.freedesktop.org/archive/individual/app/xlsfonts-${pkgver}.tar.bz2"
        'copyright.patch')
sha1sums=('9d4e0adbcb83362dd9234e90507c0528f60e309b'
          'abedbde85cc391021901c069d086882c143ac621')

build() {
	cd "${srcdir}/xlsfonts-${pkgver}"
	patch -p1 -i ../copyright.patch
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/xlsfonts-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
