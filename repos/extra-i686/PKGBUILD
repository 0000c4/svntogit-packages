# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=1.2.0
pkgrel=1
pkgdesc="NetworkManager VPN plugin for PPTP"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.7
depends=(libnm-gtk libsecret "ppp=$_pppver" pptpclient)
makedepends=(intltool python)
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/${pkgver::3}/NetworkManager-pptp-${pkgver}.tar.xz)
sha256sums=('28de8b13e80020dfbe9b81243a0ec01d482adc2054afa0b6ac22aa9f74748ac5')

build() {
  cd NetworkManager-pptp-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --enable-more-warnings=yes \
    --disable-static
  make
}

package() {
  cd NetworkManager-pptp-$pkgver
  make DESTDIR="$pkgdir" install
}
