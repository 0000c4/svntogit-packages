# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>

pkgname=hefur
pkgver=0.4
pkgrel=12
pkgdesc='A standalone and lightweight BitTorrent tracker'
arch=('i686' 'x86_64')
url='http://github.com/abique/hefur'
license=('MIT')
makedepends=('cmake')
depends=('gnutls' 'xz' 'protobuf')
source=("https://github.com/downloads/abique/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'hefurd.service' 'tmpfiles.conf' 'sysusers.conf')
md5sums=('f0461363f38105140a1f119a3b97d818'
         '3107be29769b8afde3574dc6e31d5b6a'
         'b884385d6c87c350172427da4ba2688f'
         '7971708a1fe25c1b51e0a05e812f17c9')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	mkdir build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}/build
	DESTDIR=${pkgdir} make install
	install -D -m644 ${srcdir}/hefurd.service ${pkgdir}/usr/lib/systemd/system/hefurd.service
	install -D -m644 ${srcdir}/tmpfiles.conf ${pkgdir}/usr/lib/tmpfiles.d/hefurd.conf
	install -D -m644 ${srcdir}/sysusers.conf ${pkgdir}/usr/lib/sysusers.d/hefurd.conf
	install -D -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/hefur/LICENSE
}
