# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libmediaart
pkgver=1.9.2
pkgrel=1
pkgdesc="Library tasked with managing, extracting and handling media art caches"
url="https://git.gnome.org/browse/libmediaart"
arch=(i686 x86_64)
license=(LGPL)
depends=(gdk-pixbuf2)
makedepends=(vala gobject-introspection git gtk-doc meson)
_commit=c73faaa1bac76a34fa9fcda0f6f1bf0ca9a62f50  # tags/1.9.2^0
source=("git+https://git.gnome.org/browse/libmediaart#commit=$_commit"
        0001-meson-Fix-library-versioning.patch)
sha256sums=('SKIP'
            '796493227d381c6db579133ef1bd6ef06652cd5d922a2f604e5c26bea7cfbaeb')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  patch -Np1 -i ../0001-meson-Fix-library-versioning.patch
}

build() {
  cd build
  meson --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

check() {
  cd build
  mesontest
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
