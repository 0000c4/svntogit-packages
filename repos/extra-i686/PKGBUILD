# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

pkgname=irqbalance
pkgver=1.0.6
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('i686' 'x86_64')
url="http://code.google.com/p/irqbalance"
license=('GPL')
depends=(glib2 numactl libcap-ng)
makedepends=(pkgconfig)
source=(http://irqbalance.googlecode.com/files/irqbalance-$pkgver.tar.gz
        irqbalance.service)
md5sums=('b73b1a5a9e1c3c428ae39024c711e41e'
         'a97ad17dc7df98b81acb500b27f931e5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  install -D -m644 ../irqbalance.service "$pkgdir"/usr/lib/systemd/system/irqbalance.service
}
