# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
_pkgbasename=libwnck
pkgver=3.20.0
pkgrel=1
pkgdesc="Library to manage X windows and workspaces (via pagers, tasklists, etc.)"
url="https://git.gnome.org/browse/libwnck"
arch=(i686 x86_64)
license=(LGPL)
depends=(gtk3 startup-notification libxres)
makedepends=(intltool gobject-introspection python2 gtk-doc)
source=(https://download.gnome.org/sources/libwnck/${pkgver%.*}/libwnck-$pkgver.tar.xz)
sha256sums=('0b2f3ffbbacc6b7baeeace4799011d4c0c938cc24187a20c1a9b32b607ea56d2')

build() {
    cd libwnck-$pkgver
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-static --enable-gtk-doc
    make
}

check() {
    cd libwnck-$pkgver
    make -k check
}

package() {
    cd libwnck-$pkgver
    make DESTDIR="$pkgdir" install
}
