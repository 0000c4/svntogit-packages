# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgbase=anjuta
pkgname=('libanjuta' 'anjuta')
pkgver=3.8.2
pkgrel=1
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=(i686 x86_64)
license=(GPL)
makedepends=(gdl vte3 autogen devhelp glade libgda subversion gnome-icon-theme vala dconf
             gtk-doc yelp-tools gnome-common intltool gobject-introspection itstool gjs
             python)
url="http://www.anjuta.org/"
options=('!libtool' '!emptydirs')
source=(ftp://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz
        fix-compile-error.patch)
sha256sums=('096203a294e0a7d8d7048fa74810495d590d32f96b7a7f014f34e532faa1c76e'
            'd072af4c1c52ce65f84026b83baa642a9bcfa06b10913b356b5cd99d98931b1d')

build() {
  cd "$pkgbase-$pkgver"

  patch -Np1 -i ../fix-compile-error.patch

  # Bump vala version
  sed -i '/vala/s/-0\.18/-0.20/' configure.ac

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-glade-catalog --enable-compile-warnings=minimum
  make
}

package_libanjuta(){
  pkgdesc="Anjuta runtime library"
  depends=(gdl)
  conflicts=('anjuta<3.4.4')

  cd "$pkgbase-$pkgver/libanjuta"

  # j1: Race during linking
  make -j1 DESTDIR="$pkgdir" install
}

package_anjuta(){
  pkgdesc="GNOME Integrated Development Environment (IDE)"
  depends=(libanjuta vte3 autogen devhelp glade libgda subversion gnome-icon-theme vala dconf)
  groups=('gnome-extra')

  install=anjuta.install

  cd "$pkgbase-$pkgver"
  make -j1 DESTDIR="$pkgdir" install

  cd libanjuta
  make -j1 DESTDIR="$pkgdir" uninstall
}
