 $Id: PKGBUILD,v 1.10 2008/05/13 19:03:39 ronald Exp $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima
pkgver=16.12.2
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('i686' 'x86_64')
url="http://wxmaxima.sourceforge.net/"
license=('GPL2')
depends=('maxima' 'libxml2' 'wxgtk')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha1sums=('c38634fe84f867cf586f3e47b06b9824f29ad09e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export WX_CONFIG_PATH=/usr/bin/wx-config
  export CXXFLAGS+=" -fno-delete-null-pointer-checks" # FS#49969
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install 

  # Fix category in .desktop file
  sed -i '/Categories=/c\Categories=Science;Math;' data/wxMaxima.desktop
}
