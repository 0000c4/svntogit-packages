# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libvdpau
pkgver=0.5
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=(i686 x86_64)
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
depends=('gcc-libs')
conflicts=('nvidia-utils<190.42-2')
options=('!libtool')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${pkgname}-${pkgver}.tar.gz)
makedepends=('libx11')
sha1sums=('1b18b8f75eea52740af84881313d860ad5c27d0b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
