# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=5.8.5
pkgrel=2
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('GPL2')
depends=('plasma-workspace' 'modemmanager-qt' 'networkmanager-qt' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'openconnect' 'mobile-broadband-provider-info'
             'python' 'kdelibs4support' 'kdesignerplugin')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers'
            'openconnect: Cisco AnyConnect VPN plugin')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
        plasma-nm-openvpn-2.4.patch::"https://cgit.kde.org/plasma-nm.git/patch/?id=f7db059a")
sha256sums=('6c2fee26ed36ebc7929f8d8da42d29ed475fe8dd6ab3e8987f1a0a8fd8f73ebc'
            'SKIP'
            'e8ec8c12fe8f4e7d92fbe773126ef6819f884bda30c3ce13037eab8d021deea7')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

# Add openvpn 2.4 compatibility
  cd $pkgname-$pkgver
  patch -p1 -i ../plasma-nm-openvpn-2.4.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
