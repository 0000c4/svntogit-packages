# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=3.2.1
pkgrel=1
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('vte3' 'gdl' 'autogen' 'devhelp' 'glade' 'libgda' 'subversion' 'gnome-icon-theme' 'hicolor-icon-theme' 'vala' 'python2' 'shared-mime-info' 'desktop-file-utils')
makedepends=('gnome-doc-utils' 'intltool' 'gobject-introspection')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('1e59aed63312e293c55f2934c4766411e9908fa512dd86e1422e3d906fe75613')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package(){
  cd "${srcdir}/anjuta-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed -i "1s|#!/usr/bin/python$|&2|" \
    $pkgdir/usr/share/anjuta/project/{pygtk,python}/src/main.py
}
