# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-integration-module
pkgver=0.2.0
pkgrel=1
pkgdesc="Provide several plugins for the Telepathy KDE suite"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdelibs' 'telepathy-qt4')
makedepends=('cmake' 'automoc4')
groups=('telepathy-kde')
source=("http://download.kde.org/unstable/telepathy-kde/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('5ad3e5786707b624a0f785c6891ed6de4a5d79c7')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
