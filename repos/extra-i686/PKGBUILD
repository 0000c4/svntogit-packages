# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=2.2
pkgrel=2
pkgdesc="Media player based on BMP"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libsamplerate' 'libxml2' 'desktop-file-utils')
optdepends=('unzip: Zipped skins support')
provides=('audacious-player')
replaces=('audacious-player')
install=audacious.install
source=(http://distfiles.atheme.org/$pkgname-$pkgver.tgz)
md5sums=('ad0f7484692c529acfa91ce784170478')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr \
      --enable-samplerate \
      --disable-sse2 || return 1
  elif [[ $CARCH == "x86_64" ]]; then
    ./configure --prefix=/usr \
      --enable-samplerate || return 1
  fi

  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}
