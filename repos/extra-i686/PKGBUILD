# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=mkvtoolnix
pkgver=5.0.1
pkgrel=2
pkgdesc="Set of tools to create, edit and inspect Matroska files"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('libmatroska' 'flac' 'libvorbis' 'file' 'wxgtk' 'boost-libs' 'lzo2' 'xdg-utils')
makedepends=('boost' 'ruby')
install=mkvtoolnix.install
source=("http://www.bunkus.org/videotools/${pkgname}/sources/${pkgname}-${pkgver}.tar.bz2")
md5sums=('93fbbe946de6013eca699c0c2a93a4e9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Disable automagic curl dep used for online update checking
  sed -i -e '/curl/d' configure.in
  export CURL_CFLAGS="" CURL_LIBS=""

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  autoreconf

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib
  rake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  rake DESTDIR="${pkgdir}" install
}
