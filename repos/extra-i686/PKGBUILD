# $Id$
# Maintainer:
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-smartbookmark-plugin
pkgver=0.4.4
pkgrel=3
pkgdesc="Plugin for the Xfce4 panel that lets you quicksearch from selected websites"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-smartbookmark-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4')
makedepends=('intltool' 'libxt')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/0.4/$pkgname-$pkgver.tar.bz2
        xfce4-smartbookmark-plugin-archlinux.patch
        use-exo-open-instead-of-xfbrowser4.patch)
sha256sums=('6c77e8fee0ec4dcee7aa34d94377e068a522b1ea650823422f2f5cca8126f2ed'
            'a126b086811b60a8f3cd1a2b818cb6b9a9c6e959e00db1499b114fa8803325e6'
            '436f7cda2b2bdfee61ac3d887d17dc91094526fec8713ab84fda249850525aac')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/xfce4-smartbookmark-plugin-archlinux.patch"
  patch -Np1 -i "$srcdir/use-exo-open-instead-of-xfbrowser4.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
