# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ltrace
pkgver=0.7.2
pkgrel=1
pkgdesc="Tracks runtime library calls in dynamically linked programs"
arch=('i686' 'x86_64')
url="http://ltrace.alioth.debian.org/"
license=('GPL')
depends=('elfutils')
makedepends=('dejagnu')
backup=('etc/ltrace.conf')
source=(https://alioth.debian.org/frs/download.php/3848/$pkgname-$pkgver.tar.bz2)
sha256sums=('bd9e7bd94ba6728fd3ec57f4bcfef8240260bea8f4fcebbc9c454d07ccc11894')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make -k check
}

package(){
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
