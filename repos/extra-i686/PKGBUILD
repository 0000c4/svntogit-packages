# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-desktop-applets
_pkgname=ktp-desktop-applets
pkgver=0.6.1
pkgrel=1
pkgdesc="The KDE-Telepathy Plasma desktop applets"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals' 'kdebase-workspace')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
conflicts=('telepathy-kde-contact-applet' 'telepathy-kde-presence-applet')
replaces=('telepathy-kde-contact-applet' 'telepathy-kde-presence-applet')
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('78cf1e37fbaf2af2b3c14df81dde7417a5bd7c07')

build() {
  mkdir build
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
