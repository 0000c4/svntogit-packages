# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify
pkgver=0.7.3
pkgrel=1
pkgdesc="Desktop notification library"
arch=('i686' 'x86_64')
url="http://library.gnome.org/devel/notification-spec/"
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('gtk3' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('322541fdf276eb803686df81763a5186af041b2ee3e9cc5fa1dc86c550388c88')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
