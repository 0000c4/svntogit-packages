# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

pkgname=slim
pkgver=1.3.2
pkgrel=7
pkgdesc='Desktop-independent graphical login manager for X11'
arch=('i686' 'x86_64')
url='http://slim.berlios.de/'
license=('GPL2')
depends=('pam' 'libxmu' 'libpng' 'libjpeg' 'libxft')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim')
source=("http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'rc.d'
        'pam.d'
        'logrotate'
        'ptr_pam.patch'
        'no-host.patch'
        'restart.patch'
        'sigterm.patch'
        'session-name.patch'
        'tty-slowness.patch'
        'slim-1.3.2-libpng15.patch')
sha1sums=('e421d5487732c8317f8f591906661e014b036358'
          '6fe0ba83509af634bce47be34e30995965bffc79'
          'a0e991ef0ac5120465a3be014a26e70ba073b6ae'
          'b969cc902c1d9915a5609141a652c77b2732407b'
          '640668c984a13593a1bfba8d3b503c005d5f401e'
          'b86eddd083fb9f6259e46c735f55ebe76c655bd3'
          '2d526bc0c498bf307ee50e2d22b4f53ffa0c4435'
          '0b35048723c527fb824c5e0f9b9064f751871785'
          'fdd35562ce010babaeb793f92f9906fdcdaf3f9f'
          '213fefe8533c845ea8c40585b6a8097820d5e5d2'
          '3cd674e548f4e0aadbbd32a67b4efd1ed263afef')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i -e 's/png12/png15/g' Makefile
	patch -p1 -i ../ptr_pam.patch # FS#23995: pointer mishandling confuses PAM
	patch -p1 -i ../no-host.patch # cf patch: do not set PAM host
	patch -p1 -i ../restart.patch # cf patch: restart X server if killed
	patch -p1 -i ../sigterm.patch # FS#23984: do not wait for input when SIGTERM'd
	patch -p1 -i ../session-name.patch # FS#26693: fix default session name
	patch -p1 -i ../tty-slowness.patch # FS#18313: fix sluggish TTY after slim start
	patch -p0 -i ../slim-1.3.2-libpng15.patch

	make USE_PAM=1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" MANDIR=/usr/share/man install

	install -D -m755 ../rc.d "${pkgdir}"/etc/rc.d/slim
	install -D -m644 ../pam.d "${pkgdir}"/etc/pam.d/slim
	install -D -m644 ../logrotate "${pkgdir}"/etc/logrotate.d/slim

	# Provide sane defaults
	sed -i 's|#xserver_arguments.*|xserver_arguments -nolisten tcp vt07|' "${pkgdir}"/etc/slim.conf
	sed -i 's|/var/run/slim.lock|/var/lock/slim.lock|' "${pkgdir}"/etc/slim.conf
}
