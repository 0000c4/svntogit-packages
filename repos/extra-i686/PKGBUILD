# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=artikulate
pkgver=17.08.2
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://edu.kde.org/applications/s/artikulate"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(hicolor-icon-theme kdeclarative knewstuff)
makedepends=(extra-cmake-modules kdoctools boost python)
conflicts=(kdeedu-artikulate)
replaces=(kdeedu-artikulate)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('81f77b2149653c2b5c6d40e4db2455aa2a97a086491e3074d61c75fb64038143'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
