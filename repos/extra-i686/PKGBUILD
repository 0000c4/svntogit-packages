# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-thumbnailers
pkgver=4.8.1
pkgrel=1
pkgdesc="Thumbnailers for various graphics file formats"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkexiv2' 'libkdcraw')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/${pkgver}/src/xz/${pkgname}-${pkgver}.tar.xz")
sha1sums=('a714f485f68202c8b6e2716cf426abd6d82c0dc7')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
