# $Id$
# Maintainer : Allan McRae <allan@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=fakechroot
pkgver=2.11
pkgrel=1
pkgdesc="Gives a fake chroot environment"
arch=('i686' 'x86_64')
url="http://fakechroot.alioth.debian.org"
license=('LGPL')
options=('!libtool')
install=fakechroot.install
source=(http://ftp.debian.org/debian/pool/main/f/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz)
md5sums=('5a3db123a70cbfd83ad97000bfc32948')

build()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot
  make
  make check
}
  
package()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > ${pkgdir}/etc/ld.so.conf.d/fakechroot.conf
}
