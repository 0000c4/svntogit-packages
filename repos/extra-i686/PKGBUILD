# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: mgushee

pkgname=djvulibre
pkgver=3.5.23
pkgrel=1
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
arch=("i686" "x86_64")
license=('GPL')
url="http://djvu.sourceforge.net/"
depends=('gcc-libs' 'libtiff' 'bash' 'hicolor-icon-theme')
makedepends=('pkg-config')
provides=("libdjvu=${pkgver}")
replaces=('libdjvu')
conflicts=('libdjvu')
optdepends=('djview4: Viewer application')
options=('!libtool')
install=djvulibre.install
source=(http://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz) 
md5sums=('0053b9908b9e3d57d0d89b3d065168e9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-desktopfiles
  make
  make DESTDIR="${pkgdir}" install

  #Install icons for mime-types.
  install -m755 -d ${pkgdir}/usr/share/icons/hicolor/{22x22,32x32,48x48,64x64}/mimetypes
  for sz in 22 32 48 64; do
    install -m644 desktopfiles/prebuilt-hi${sz}-djvu.png "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png"
  done
}
