# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=0.9.6.0
pkgrel=2
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver}" 'pptpclient' 'gtk3' 'libgnome-keyring')
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
options=('!libtool')
install=networkmanager-pptp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/0.9/NetworkManager-pptp-${pkgver}.tar.xz
        fix-path-to-auth-dialog.patch
        gtk_table_to_gtk_grid.patch)
sha256sums=('a84cbbf24827229e3dd3611bbde191398275c3b7ecd03913047197644f27a2b4'
            '35f9c2cb4d046c8f215617e1a0bfd8551965bd0655af0cd25f89c17ea5e9081a'
            '17c194c605bca780bf657711e9cff910c63ab27eae70c11cb6c53e9c75ad3215')

build() {
  cd NetworkManager-pptp-${pkgver}
  patch -Np1 -i ../fix-path-to-auth-dialog.patch
  patch -Np1 -i ../gtk_table_to_gtk_grid.patch
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
	--disable-static
  make
}

package() {
  cd NetworkManager-pptp-${pkgver}
  make DESTDIR="${pkgdir}" install
}
