# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: jlvsimoes
# Contributor: damir <damir@archlinux.org>

pkgname=k3b
pkgver=2.0.1
pkgrel=3
depends=('kdebase-runtime' 'kdemultimedia-kioslave' 'libsamplerate' 'libmad'
         'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread' 'cdrdao' 'cdrkit'
         'shared-mime-info' 'libxft')
makedepends=('cmake' 'pkgconfig' 'automoc4' )
pkgdesc="Feature-rich and easy to handle CD burning application"
url="http://k3b.sourceforge.net/"
arch=("i686" "x86_64")
license=('GPL')
install="${pkgname}.install"
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager:    for vcd burning support'
            'transcode:    for advanced mpeg conversion support'
            'emovix:       for bootable multimedia cd/dvd support')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
	'fix-crash-with-qt-4.7.0.patch')
md5sums=('4e5b6665d91d4984cbfa814b797257cb'
	'd4f0b447d8054eaa927f2bc65026cbf5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ${srcdir}/fix-crash-with-qt-4.7.0.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
