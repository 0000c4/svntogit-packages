# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=glib-perl
pkgver=1.280
pkgrel=3
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${pkgver}.tar.gz
        0001-Make-Glib-Object-subclassing-more-robust.patch)
md5sums=('1d81a8aec5f7f1182a96cfaaf119d866'
         'ebe39d50eff182a1ad4390cbbef46e41')

prepare() {
  cd "${srcdir}/Glib-${pkgver}"

  # Needed for gtk2-perl to compile against Perl 5.18
  patch -Np1 -i "${srcdir}/0001-Make-Glib-Object-subclassing-more-robust.patch"
}

build() {
  cd "${srcdir}/Glib-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Glib-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/Glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
