# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=0.9.3.r84.05c9fdec
pkgrel=1
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/libbluray.html'
license=('LGPL2.1')
depends=('fontconfig' 'glibc' 'libfreetype.so' 'libxml2')
makedepends=('apache-ant' 'git')
optdepends=('java-environment: BD-J library')
provides=('libbluray.so')
_commit_libbluray='05c9fdececbff43561f8d8ca704e7e2203bdd5a8'
_commit_libudfread='64ac239e7aa741ad3e2e2d48eafd6e26fb202ee7'
source=("git+https://git.videolan.org/git/libbluray.git#commit=${_commit_libbluray}"
        "git+https://git.videolan.org/git/libudfread.git#commit=${_commit_libudfread}")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd libbluray

  git describe | sed 's/-/.r/; s/-g/./'
}

prepare() {
  cd libbluray

  git submodule init contrib/libudfread
  git config submodule.contrib/udfread.url ../libudfread
  git submodule update contrib/libudfread

  ./bootstrap
}

build() {
  cd libbluray

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd libbluray

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
