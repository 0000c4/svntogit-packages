# $Id$
# Maintainer: Andreas Radke <andyrtr at archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=raptor
pkgver=2.0.3
pkgrel=3
pkgdesc="A C library that parses RDF/XML/N-Triples into RDF triples"
arch=('i686' 'x86_64')
url="http://librdf.org/raptor"
depends=('libxml2>=2.7.8' 'curl>=7.21.7' 'zlib>=1.2.5' 'libxslt>=1.1.26')
license=('LGPL')
options=('!libtool')
source=("http://librdf.org/dist/source/raptor2-$pkgver.tar.gz"
        'Allow-instead-of-in-TRiG.patch')
md5sums=('46eff4b20f8752d1146a3e0c8b2168dd'
         '73cfb0c1098ded4eff8c7f64bcaa5471')

build() {
  cd ${srcdir}/raptor2-${pkgver}

  # see http://bugs.librdf.org/mantis/view.php?id=451
  patch -p1 -i "${srcdir}"/Allow-instead-of-in-TRiG.patch

  # fix build with curl >= 7.21.2
  sed -i '/#include <curl\/types.h>/d' src/raptor_internal.h

  ./configure --prefix=/usr \
    --disable-static \
    --with-yajl=no
  make
}

package() {
  cd ${srcdir}/raptor2-${pkgver}
  make prefix=${pkgdir}/usr install
}
