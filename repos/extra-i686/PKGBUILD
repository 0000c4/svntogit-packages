# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tomboy
pkgver=0.12.1
pkgrel=1
pkgdesc="Desktop note-taking application for Linux and Unix"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org/projects/tomboy/"
depends=('libgnomeprintui>=2.18.2' 'gtkspell' 'gmime>=2.2.18' 'ndesk-dbus-glib>=0.4.1' 'gnome-sharp>=2.24.0' 'mono-addins>=0.3.1' 'libsm')
makedepends=('perlxml' 'pkgconfig' 'gnome-panel>=2.24.0' 'gnome-doc-utils>=0.14.0')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=tomboy.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.12/${pkgname}-${pkgver}.tar.bz2
	gedit-print.patch)
md5sums=('934d1258f855f04eff7d0fe8a976f6ad' 'f8f522688a8fa3e1524f42b53906027d')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
 
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/gedit-print.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
