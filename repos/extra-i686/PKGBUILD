# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=akonadi
pkgver=1.6.0
pkgrel=3
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url='http://pim.kde.org/akonadi'
license=('LGPL')
depends=('shared-mime-info' 'boost-libs' 'mysql' 'soprano')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/src/${pkgname}-${pkgver}.tar.bz2"
        'fix-slow-kde-logout.patch')
md5sums=('16bc40e022d06f287f18c71faaeb2e42'
         'cdf0c3bd5741303e4f98ea435c39f09d')

build() {
  # Already fixed upstream
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/fix-slow-kde-logout.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_QSQLITE_IN_QT_PREFIX=TRUE
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
