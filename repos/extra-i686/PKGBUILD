# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=gthumb
pkgver=3.1.2
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/gthumb"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'dconf' 'librsvg' 'clutter-gtk'
         'gstreamer0.10-base' 'gsettings-desktop-schemas' 'libwebp')
makedepends=('intltool' 'gnome-doc-utils' 'libchamplain' 'libopenraw' 'exiv2' 'libsoup-gnome'
             'brasero' 'librsvg' 'libopenraw' 'liboauth')
optdepends=('libopenraw: read RAW files'
            'exiv2: metadata support'
            'libchamplain: map viewer'
            'brasero: burn discs'
            'libsoup-gnome: web albums'
            'liboauth: web albums')
options=('!libtool' '!emptydirs')
install=gthumb.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5e7f80b3ffa8a6e06b9f1bf0cfa869f772af4163b842a7f457c9dc68ff7e6f8f')

build() {
  cd $pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-libchamplain --enable-libopenraw
  
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
