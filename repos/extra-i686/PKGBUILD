# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.22.0
pkgrel=3
pkgdesc='Secure and unified container for user passwords'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('knotifications' 'kiconthemes' 'kservice' 'gpgmepp')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'boost')
optdepends=('kwalletmanager: Configuration GUI')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        kdebug-356944.patch::"https://quickgit.kde.org/?p=kwallet.git&a=commitdiff&h=58a80367&o=plain")
md5sums=('786c2c39d19c5eb3afc7fa7fd1102292'
         '0510c98e6bb0ff8c4f9d126981c2d849')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix migration https://bugs.kde.org/show_bug.cgi?id=356944
  patch -p1 -i ../kdebug-356944.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
