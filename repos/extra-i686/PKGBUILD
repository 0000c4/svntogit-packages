# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=geocode-glib
pkgver=3.23.90
pkgrel=1
pkgdesc="Helper library for geocoding services"
url="https://git.gnome.org/browse/geocode-glib/"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 json-glib libsoup)
makedepends=(intltool gobject-introspection git gtk-doc autoconf-archive)
options=(!emptydirs)
_commit=537eb30d0479588053cde1336e1a93c1d66b7493  # tags/3.23.90^0
source=("git+https://git.gnome.org/browse/geocode-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i 's/gnome/Adwaita/g' icons/Makefile.am
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc --disable-Werror
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

