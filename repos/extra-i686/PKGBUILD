# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Jani Talikka <jani.talikka@gmail.com>
# Contributor: Ralf Barth <archlinux.org@haggy.org>

pkgname=smartmontools
pkgver=5.43
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives"
url="http://smartmontools.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libcap-ng' 'bash')
backup=('etc/smartd.conf'
        'etc/conf.d/smartd')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'smartd.conf'
        'smartd.rc')
md5sums=('a1cb2c3d8cc2de377037fe9e7cef40a9'
         '28c1b80c41dac52d433961dcfb4f70e0'
         'fb30e07f87f2c1d99532e018291ad3e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-drivedb \
              --with-libcap-ng=yes \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e "s:sysconfig/smartmontools:conf.d/smartd:g" smartd.service
  sed -i -e "s:smartd_opts:SMARTD_ARGS:g" smartd.service

  make DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/etc/rc.d
  install -Dm755 ${srcdir}/smartd.rc "${pkgdir}/etc/rc.d/smartd"
  install -Dm644 ${srcdir}/smartd.conf "${pkgdir}/etc/conf.d/smartd"
}
