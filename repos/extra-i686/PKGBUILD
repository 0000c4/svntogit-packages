# $Id$
# Contributor: Camille Moncelier <pix@devlife.org>

pkgname=m17n-lib
pkgver=1.6.2
pkgrel=2
pkgdesc='A multilingual text processing library (runtimes)'
arch=('i686' 'x86_64')
url='http://www.m17n.org/m17n-lib/'
license=('GPL')
depends=('libxft' 'm17n-db' 'fribidi' 'libxml2' 'gd' 'libotf>=0.9.11')
optdepends=('anthy: to use the anthy input method')
options=('!libtool' '!makeflags')
source=("http://www.m17n.org/m17n-lib-download/$pkgname-$pkgver.tar.gz"
        'locale.patch')
sha1sums=('9b9ada79357dd56c90c64cd36845c5537c363d79'
          '55a09b9d8e6ea590d5c8f06d1397bfa0b00c1697')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../locale.patch # cf. FS#20482
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
