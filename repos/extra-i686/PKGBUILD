# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fluxbox
pkgver=1.3.3
pkgrel=2
pkgdesc="A lightweight and highly-configurable window manager"
arch=('i686' 'x86_64')
url="http://www.fluxbox.org"
license=('MIT')
depends=('libxft' 'libxpm' 'libxinerama' 'libxrandr' 'gcc-libs' 'imlib2' 'fribidi')
optdepends=('xorg-xmessage: for using the fbsetbg and fluxbox-generate_menu utilities')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.lzma \
        fluxbox.desktop fluxbox-fix_timer.diff)
sha1sums=('6892259dd495424291194fde242230cca95abce3'
          'f3f83b8ce84d79c2f8670ef687e0dd89ab0552b8'
          'bc10368d7a157f83428897e2b7aced7ce620dec3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/fluxbox-fix_timer.diff"
  ./configure --prefix=/usr \
    --enable-xft --enable-xinerama \
    --enable-imlib2 --enable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../fluxbox.desktop "${pkgdir}/usr/share/xsessions/fluxbox.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
