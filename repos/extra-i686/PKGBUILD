# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>

pkgname=libva
pkgver=1.1.0
pkgrel=1
pkgdesc="Video Acceleration (VA) API for Linux"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libegl' 'libgl' 'libdrm' 'libxfixes')
makedepends=('mesa')
optdepends=('vdpau-video: vdpau back-end for nvidia'
            'libva-driver-intel: back-end for intel cards')
options=('!libtool')
install=libva.install
source=(http://cgit.freedesktop.org/libva/snapshot/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b22179bd44137e91d5c77a43a0cb8063')

build() {
  cd "$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m644 -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
