# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil
pkgver=1.0
pkgrel=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil' 'obex-data-server' 'obexd-client')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://media.ereslibre.es/2010/11/${pkgname}-v${pkgver}.tar.bz2")
md5sums=('b20d9c234adfe3c498f5577a88cd2f03')

build(){
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-v${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
