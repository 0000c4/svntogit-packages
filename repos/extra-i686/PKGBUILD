# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gssdp
pkgver=0.12.2.1
pkgrel=1
pkgdesc="A GObject-based API for handling resource discovery and announcement over SSDP"
arch=('i686' 'x86_64')
url="http://gupnp.org/"
license=('LGPL')
depends=('libsoup')
makedepends=('gobject-introspection' 'gtk2' 'gtk-doc')
optdepends=('gtk2: gssdp-device-sniffer')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('928aa257815d044fdbfc740f5799530d41897947b5d7854173f636b36d728414')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
