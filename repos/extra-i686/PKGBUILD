# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=ghex
pkgver=3.18.0
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Ghex"
depends=('gtk3')
makedepends=('intltool' 'itstool' 'docbook-xsl')
options=('!emptydirs')
install=ghex.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('c5b1eb50a8dd1334880b37617871498b778ea137f79bb43894ec68e4f63dc925')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
