# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: arjan <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=imlib2
pkgver=1.4.1
pkgrel=2
pkgdesc="Imlib2 is the successor to Imlib. It is NOT a newer version -- Imlib 2 can be installed alongside Imlib 1.x"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/enlightenment"
license=('BSD')
depends=('libtiff' 'libungif' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/enlightenment/$pkgname-$pkgver.tar.gz \
	bufferoverflow.patch)
md5sums=('16a3d885e523303be794282c0ed90841'
         '76def1fe0ac2ac1686ff24d29979606f')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Enable x86 MMX optimizations for i686 (32-bit assembler code not compatible with x86_64)
  [ "${CARCH}" = "i686" ] && EXTRAOPTS="--disable-mmx"
  # Disable AMD64 optimizations due to bug in assembler code
  [ "${CARCH}" = "x86_64" ] && EXTRAOPTS="--disable-amd64"

  # Security fix (buffer overflow), Debian bug #505714
  patch -Np0 -i $srcdir/bufferoverflow.patch || return 1

  # build & install
  ./configure --prefix=/usr --sysconfdir=/etc/imlib2 --x-libraries=/usr/lib $EXTRAOPTS
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

  # install BSD license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
