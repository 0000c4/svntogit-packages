# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.12
pkgrel=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/mtools/"
license=('GPL')
depends=('sh')
makedepends=('texinfo' 'libx11' 'libsm')
optdepends=('libx11: required by floppyd'
	    'libsm:  required by floppyd')
backup=(etc/mtools.conf)
source=(http://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0ee77a14e5f113ad8136a867f8ed4c3a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '/^SAMPLE FILE$/s:^:# :' mtools.conf
  sed -i 's:/usr/local/etc:/etc:g' mtools.5 mtools.texi
  ./configure --prefix=/usr \
	      --sysconfdir=/etc
  make || return 1
  make -j 1 prefix=${pkgdir}/usr install
  install -Dm644 mtools.conf "${pkgdir}/etc/mtools.conf" || return 1
}
