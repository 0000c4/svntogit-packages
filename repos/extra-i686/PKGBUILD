# $Id$
# Maintainer: damir <damir@archlinux.org>
# Contributor: Jason Taylor <jftaylor21@gmail.com>

pkgname=lapack
pkgver=3.1.1
pkgrel=4
pkgdesc="Linear Algebra PACKage"
url="http://www.netlib.org/lapack"
arch=('i686' 'x86_64')
license=("custom")
depends=('blas>=20070405')
source=(http://www.netlib.org/${pkgname}/${pkgname}-${pkgver}.tgz
	make.inc.archlinux
	blas-link.patch)
md5sums=('00b21551a899bcfbaa7b8443e1faeef9'
         'cc368c407e396227c473db5bac92cf02'
         'c223181737ed2eb68422b139233cc019')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/blas-link.patch" || return 1
  install -m755 -d "${pkgdir}/usr/lib" || return 1
  install -m755 -d "${pkgdir}/usr/share/man/man1" || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}" || return 1

  cp "${srcdir}/make.inc.archlinux" make.inc || return 1
  if [ "${CARCH}" = "x86_64" ]; then
    CFLAGS="${CFLAGS} -fPIC"
  fi
  make clean
  make lib OPTS="${CFLAGS}" NOOPT="${CFLAGS/-O?/-O0}" || return 1
  install -m755 *.so* "${startdir}/pkg/usr/lib/" || return 1
  ln -sf liblapack.so.3 "${pkgdir}/usr/lib/liblapack.so"
  ln -sf libtmglib.so.3 "${pkgdir}/usr/lib/libtmglib.so"

# manpages
  for _file in manpages/man/manl/* manpages/blas/man/manl/*; do
    install -m644 "${_file}" "${pkgdir}/usr/share/man/man1/$(basename ${_file} .l).1" || return 1
  done

  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
