# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.24.2
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
url="https://wiki.gnome.org/Apps/Evolution/EWS"
arch=(i686 x86_64)
license=(LGPL2.1)
depends=(libmspack evolution)
makedepends=(intltool gnome-common git cmake)
options=(!emptydirs)
_commit=1a1ecee37c5577078e8e9f7340bd6daa7ab2bba2  # tags/EVOLUTION_EWS_3_24_2^0
source=("git+https://git.gnome.org/browse/evolution-ews#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_EWS_//;s/_/./g;s/-/+/g'
}

build() {
  cd $pkgname
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DLIBEXEC_INSTALL_DIR=/usr/lib/evolution -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
