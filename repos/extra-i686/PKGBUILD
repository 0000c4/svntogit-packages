# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.8.3
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://www.gnome.org"
depends=('evolution-data-server' 'gtkhtml4' 'gnome-desktop')
makedepends=('intltool' 'evolution')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('948dbee5e31b364155719003a0a8b28dfa270aae5f364b5614faa0b262a7ff3a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/evolution --disable-static --with-krb5
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
