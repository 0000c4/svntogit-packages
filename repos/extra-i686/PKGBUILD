# $Id$
# Maintainer:
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libkvkontakte
pkgver=5.0.0git20160305
pkgrel=1
pkgdesc="C++ library for asynchronous interaction with VK social network via its web API"
arch=(i686 x86_64)
url='https://www.digikam.org'
license=(GPL)
depends=(kdewebkit)
makedepends=(extra-cmake-modules python)
source=("https://quickgit.kde.org/?p=libkvkontakte.git&a=snapshot&h=b5d347c1&fmt=tgz")
sha1sums=('d6880a4f88952ec9f7447c1f4c4ebc828d90a38e')

prepare() {
  mkdir -p build 
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
