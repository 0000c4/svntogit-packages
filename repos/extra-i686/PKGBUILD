# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=2.24.3
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf>=2.24.0' 'pygtk>=2.13.0' 'pygtksourceview2>=2.4.0' 'enchant>=1.4.2' 'desktop-file-utils' 'iso-codes' 'gtksourceview2>=2.4.2')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.14.2' 'intltool')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('48ed6c5c156791cffc219a7d7390da57')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --libexecdir=/usr/lib --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
