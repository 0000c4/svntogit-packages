# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.9.0.1
pkgrel=1
pkgdesc="A service which logs the users's activities and events and makes relevant information available to other applications"
arch=('i686' 'x86_64')
url="http://zeitgeist-project.com/"
license=('GPL')
depends=('python2-dbus' 'python2-gobject2' 'pyxdg' 'xapian-core')
makedepends=('intltool' 'raptor' 'python2-rdflib')
checkdepends=('vala')
source=("https://launchpad.net/$pkgname/${pkgver%.*.*}/$pkgver/+download/$pkgname-$pkgver.tar.bz2"{,.asc})
md5sums=('08f2eb384824e8458f18e10db7654965'
         '64486b3e6bec080c447c94f3ac2ff8e8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/python -/$PYTHON -/' configure
  PYTHON=/usr/bin/python2 LIBS+="-lgmodule-2.0" ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/zeitgeist --localstatedir=/var
  make
}

#check() {
#  cd "$srcdir/$pkgname-$pkgver"
#  make check
#}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" zeitgeistdocdir=/usr/share/doc/zeitgeist install
  install -D -m644 data/zeitgeist-daemon.bash_completion "$pkgdir/usr/share/bash-completion/completions/zeitgeist-daemon"
}
