# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>
pkgname=libwnck
pkgver=2.30.6
pkgrel=2
pkgdesc="Window Navigator Construction Kit"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2' 'startup-notification' 'libxres')
makedepends=('libxt' 'intltool' 'gobject-introspection')
options=('!libtool')
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4d25984ed5cfe215eb650f960de634300a6cafc9d98bbc7caea8043b7cd96cc5')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    #The binaries are shipped in libwnck3
    rm -f ${pkgdir}/usr/bin/wnckprop
    rm -f ${pkgdir}/usr/bin/wnck-urgency-monitor
}
