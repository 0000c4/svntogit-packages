# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
_pkgbasename=libwnck
pkgver=3.4.4
pkgrel=1
pkgdesc="Window Navigator Construction Kit (GTK+3)"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'startup-notification' 'libxres')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('a545a23ea7681fafae033b4f68b69ef022d446a9325286291bb8882b9016a130')

build() {
    cd "$_pkgbasename-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "$_pkgbasename-$pkgver"
    make DESTDIR="$pkgdir" install
}
