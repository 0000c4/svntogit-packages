# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwordquiz
pkgver=16.04.0
pkgrel=1
pkgdesc="Flash Card Trainer"
url="http://kde.org/applications/education/kwordquiz/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('knotifyconfig' 'libkeduvocdocument' 'knewstuff' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdeedu-kwordquiz')
replaces=('kdeedu-kwordquiz')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kwordquiz-${pkgver}.tar.xz")
sha1sums=('4b49be1da07916ad3f57ea612f65bd145bdfee81')

build() {
  mkdir -p build
  cd build
  cmake ../kwordquiz-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
