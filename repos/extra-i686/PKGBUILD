# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.26.2
pkgrel=2
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(i686 x86_64)
depends=(gnome-online-accounts nss krb5 libgweather libical db libgdata libphonenumber)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost git cmake ninja)
license=(GPL)
_commit=1ef35fef0ed50d52cd2da6e8776429522708e3a5  # tags/EVOLUTION_DATA_SERVER_3_26_2^0
source=("git+https://git.gnome.org/browse/evolution-data-server#commit=$_commit"
        0001-Don-t-free-instance_recur_id-when-not-initialized.patch)
sha256sums=('SKIP'
            '9cadbaf8a38c59ca28494ae608466690d6cb72b4f82df4f10b35cbe57daf9e8a')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_DATA_SERVER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../0001-Don-t-free-instance_recur_id-when-not-initialized.patch
}

build() {
  cd $pkgname

  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/$pkgname \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DENABLE_INTROSPECTION=ON \
    -DENABLE_VALA_BINDINGS=ON \
    -DENABLE_UOA=OFF \
    -DENABLE_GOOGLE_AUTH=ON \
    -DENABLE_GTK_DOC=ON \
    -DWITH_PHONENUMBER=ON
  ninja
}

check() {
  cd $pkgname
  # libedata-book tests fail because they try to
  # mkdir /usr/lib/evolution-data-server/camel-providers
  ninja test || :
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
