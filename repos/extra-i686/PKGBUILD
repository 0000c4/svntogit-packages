# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-sdk
pkgver=5.9.3
pkgrel=1
pkgdesc="Applications useful for Plasma development"
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-framework ktexteditor)
makedepends=(extra-cmake-modules qt5-webkit kitemmodels knewstuff python2 kdevplatform kdelibs4support
             kdesignerplugin kdoctools kirigami)
optdepends=('kdevplatform: for plasmate' 'kirigami2: for Look & Feel explorer' )
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        lfexplorer-kirigami2.patch::"https://cgit.kde.org/plasma-sdk.git/patch/?id=db668992")
sha256sums=('2bff3ede9fcf7c6e96b7a8d845487cc28a02d096778dfbe9b35836c896879980'
            'SKIP'
            '2a8315eaf765d4f94b5a1b5465b527073d0deeb3ccce7b2b33f4a08b9c9cfb3a')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

# Port look and feel explorer to kirigami2
  cd $pkgname-$pkgver
  patch -p1 -i ../lfexplorer-kirigami2.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
