# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mahjongg
pkgver=3.20.0
pkgrel=2
pkgdesc="Disassemble a pile of tiles by removing matching pairs"
url="https://wiki.gnome.org/Apps/Mahjongg"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
groups=('gnome-extra')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('d09bb1923636dfd05b6f3a5af8d6fe7b6daa1434e2c06188dd27c96a61cea00a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
