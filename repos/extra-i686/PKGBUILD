# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=liborcus
pkgver=0.11.1
pkgrel=1
pkgdesc="File import filter library for spreadsheet documents."
arch=('i686' 'x86_64')
url="https://gitlab.com/orcus/orcus/blob/master/README.md"
license=('MPL')
depends=('libixion')
makedepends=('boost' 'mdds')
source=(http://kohei.us/files/orcus/src/${pkgname}-${pkgver}.tar.xz)
sha256sums=('02a6251a9b732c2c555c1be083411a2d0383dfd2ebc79f60ac282875b1ada49b')

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=$pkgdir install
}
