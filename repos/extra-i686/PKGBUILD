# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: pumbur

pkgname=opus
pkgver=1.1.4
pkgrel=1
pkgdesc="Totally open, royalty-free, highly versatile audio codec"
arch=(i686 x86_64)
url="https://www.opus-codec.org/"
license=(BSD)
depends=(glibc)
makedepends=(doxygen)
source=(http://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('9122b6b380081dd2665189f97bfd777f04f92dc3ab6698eea1dbb27ad59d8692')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-custom-modes --disable-static \
    --enable-intrinsics --enable-rtcd --enable-ambisonics
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
