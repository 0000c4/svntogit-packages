# $Id$
# Contributor: John Proctor <jproctor@prium.net>
# Maintainer: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
pkgver=3.2b_2
pkgrel=1
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
depends=('openssl' 'sqlite3' 'gmp' 'texinfo')
makedepends=('java-environment')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/mimosa/fp/Bigloo/${pkgname}${pkgver%_*}-${pkgver##*_}.tar.gz)
md5sums=('48d8853e108436469ee858116b3da00d')
sha1sums=('0ec07a7be937f1546941f7a890a277e0376a56a7')

build() {
  cd "${srcdir}/${pkgname}${pkgver%_*}"
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo --dotnet=no --javaprefix=/opt/java/bin || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  make -C manuals DESTDIR="${pkgdir}" install-man-bee || return 1
  chmod 644 "${pkgdir}"/usr/lib/bigloo/${pkgver%_*}/*.a || return 1
}
