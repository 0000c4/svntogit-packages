# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman-mirrorlist
pkgver=20091206
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
backup=(etc/pacman.d/mirrorlist)
source=(mirrorlist)

# NOTE on building this package:
# * Go to the trunk/ directory
# * Run bash -c ". PKGBUILD; updatelist"
# * Update the checksums, update pkgver
# * Build the package

updatelist() {
  wget -O mirrorlist http://www.archlinux.org/mirrorlist/i686/all/
  sed -i 's/i686/@carch@/' mirrorlist
}

build() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/mirrorlist $pkgdir/etc/pacman.d/
  # customize mirrorlist to architecture
  sed -i -e "s/@carch@/${CARCH}/g" $pkgdir/etc/pacman.d/mirrorlist
}

md5sums=('7a40d7734b7d9e01a59fd3a24d73cef2')
sha256sums=('c0db9289d1a4742f43121a8c2f5b24c181e533e4fb4726c6ddf6003306c05778')
