# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>

pkgname=libssh2
pkgver=1.3.0
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="http://www.libssh2.org/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('openssl')
makedepends=('zlib')
options=('!libtool')
source=("http://www.libssh2.org/download/$pkgname-$pkgver.tar.gz"{,.asc})
md5sums=('6425331899ccf1015f1ed79448cb4709'
         '6ddcc9f2b37941f8291d7494ee8f89b7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
