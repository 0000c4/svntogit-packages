# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gmp
pkgver=5.0.2
pkgrel=3
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3')
options=(!libtool)
install=gmp.install
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2
        538dfce27f41.patch)
md5sums=('0bbaedc82fb30315b06b1588b9077cd3'
         'a769be9c41618ca9c35d83375e7097d0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/538dfce27f41.patch
  
  [ "${CARCH}" = "i686" ] && export ABI="32"
  ./configure --build=${CHOST} \
    --prefix=/usr --infodir=/usr/share/info \
    --enable-cxx
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
