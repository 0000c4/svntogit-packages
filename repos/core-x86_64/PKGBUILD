# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2008f
pkgrel=1
_tzcode=2008e
_tzdata=2008f
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
groups=('base')
depends=()
makedepends=()
options=()
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch)
md5sums=('12ec2d85324ddcba39db1471a03f486b'
         'aa3b077a7b786d46c131303b1d75deee'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}/

  tar -xf tzcode${_tzcode}.tar.gz  || return 1
  tar -xf ${pkgname}${_tzdata}.tar.gz || return 1

  patch -Np1 -i ../Makefile.patch || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install

  rm ${pkgdir}/usr/share/zoneinfo/localtime
}
