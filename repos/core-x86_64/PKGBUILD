# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=libarchive
pkgver=3.2.2
pkgrel=4
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.org/"
license=('BSD')
depends=('acl' 'attr' 'bzip2' 'expat' 'lz4' 'lzo' 'openssl' 'xz' 'zlib')
options=('strip' 'debug' 'libtool')
provides=('libarchive.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz"
        '0001-issue-822-try-harder-to-detect-directories-in-zip-archives.patch'
        '0002-fixes-a-heap-buffer-overflow.patch')
sha256sums=('edfc2ee7d42dd03228d0fa3bb9cbaade454557b326b2608b2e32c27aae62bdd4'
            '79bd6b3889131ab36501af2c9460ccb940ba95d568a72578163fb5d212a7a7e5'
            'e6177bd052090a2111d62c7c68157df71cebf4ad359aad02ce89d5585c9e64a4')

prepare() {
  cd "$pkgname-$pkgver"

  # Issue #822: Try harder to detect directories in zip archives
  patch -Np1 < "$srcdir"/0001-issue-822-try-harder-to-detect-directories-in-zip-archives.patch

  # Fail with negative lha->compsize in lha_read_file_header_1()
  # Fixes a heap buffer overflow reported in Secunia SA74169
  patch -Np1 < "$srcdir"/0002-fixes-a-heap-buffer-overflow.patch
}

build() {
  cd "$pkgname-$pkgver"

  autoreconf -fi
  ./configure \
      --prefix=/usr \
      --without-xml2 \
      --without-nettle

  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
