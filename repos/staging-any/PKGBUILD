# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Massimiliano Torromeo < massimiliano DOT torromeo AT gmail DOT com >

pkgname=('python-beaker' 'python2-beaker')
pkgver=1.6.2
pkgrel=1
arch=('any')
license=('custom')
pkgdesc="Caching and sessions WSGI middleware for use with web applications and stand-alone Python scripts and applications"
url="http://beaker.groovie.org/"
makedepends=('python-distribute' 'python2-distribute')
source=("http://cheeseshop.python.org/packages/source/B/Beaker/Beaker-${pkgver}.tar.gz")
md5sums=('455a264cb481ab07446f020c001dcdc5')

build() {
    cp -r Beaker-${pkgver} python2-Beaker-${pkgver}

    cd ${srcdir}/Beaker-${pkgver}
    python setup.py build

    cd ${srcdir}/python2-Beaker-${pkgver}
    sed -i "s#/usr/bin/python#/usr/bin/python2#" beaker/crypto/pbkdf2.py
    python2 setup.py build
}

package_python-beaker() {
    depends=('python')

    cd ${srcdir}/Beaker-${pkgver}
    python setup.py install --root=${pkgdir} --optimize=1
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/python-beaker/LICENSE
}

package_python2-beaker() {
    depends=('python2')

    cd ${srcdir}/python2-Beaker-${pkgver}
    python2 setup.py install --root=${pkgdir} --optimize=1
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/python2-beaker/LICENSE
}
