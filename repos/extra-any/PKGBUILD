# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kewl <xrjy@nygb.rh.bet(rot13)>
# Contributor: Justin Settle <jus10 at partlycloudy dot org>

pkgname=ttf-caladea
pkgver=20200113
pkgrel=1
_commit=bea9e4996451fb18e0bdb5cbd177c8a263c0343d
pkgdesc="Google's Caladea font"
arch=(any)
license=(Apache)
url='https://code.google.com/p/chromium/issues/detail?id=168879'
source=(https://github.com/huertatipografica/Caladea/archive/$_commit.tar.gz
        62-google-crosextra-caladea.conf)
sha256sums=('95a01f4a85bc77d52241ad831ef049661e99636fcc83cd1316eef898e82498ce'
            'cd61569289d7a188636df05addd3a744808de38b3d492b3e87c7a060bce4a2e6')

package() {
  cd Caladea-$_commit

  install -Dt "$pkgdir/usr/share/fonts/${pkgname#ttf-}" -m644 fonts/ttf/*.ttf

  install -Dt "$pkgdir/etc/fonts/conf.avail" -m644 ../*.conf

  mkdir "$pkgdir/etc/fonts/conf.d"
  ln -srt "$pkgdir/etc/fonts/conf.d" "$pkgdir"/etc/fonts/conf.avail/*
}
