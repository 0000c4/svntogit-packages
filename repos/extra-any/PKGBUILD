# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=eclipse-ecj
pkgver=4.2.1
pkgrel=1
_date=201209141800
pkgdesc='Eclipse java bytecode compiler'
arch=('any')
license=('EPL')
url='http://www.eclipse.org/'
depends=('java-runtime')
makedepends=('unzip' 'apache-ant' 'java-environment')
source=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/ecjsrc-${pkgver}.jar
        ecj)
sha256sums=('d261b2158f598640f1923805d2e9bf47eb21d8333f4e1b37f59f847ad00d48f4'
            '63aff3d126243d303ddc4305cfa77827df72e87ccf85bd8a22a2f832357e396c')

build() {
  cd ${srcdir}
  mkdir src
  unzip ecjsrc-${pkgver}.jar -d src
  cd src
  ant build
}

package() {
  install -Dm644 ${srcdir}/src/ecj.jar $pkgdir/usr/share/java/eclipse-ecj-${pkgver}.jar
  ln -s eclipse-ecj-${pkgver}.jar $pkgdir/usr/share/java/ecj.jar
  ln -s eclipse-ecj-${pkgver}.jar $pkgdir/usr/share/java/eclipse-ecj.jar
  install -Dm755 ecj $pkgdir/usr/bin/ecj
}
