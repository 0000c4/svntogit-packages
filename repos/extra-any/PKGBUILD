# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=3.8.1
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
arch=(any)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
install=orca.install
options=('!libtool')
depends=(gtk3 at-spi2-atk python-atspi python-dbus python-xdg speech-dispatcher liblouis brltty
         xorg-xmodmap hicolor-icon-theme gsettings-desktop-schemas)
makedepends=(itstool intltool gnome-doc-utils)
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('acca5393d33c2e21a167dd1584bfc7c3038fba1feddf2e5edf4c41e07d19ad03')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
