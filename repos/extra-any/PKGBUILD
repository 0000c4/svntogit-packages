# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.22.0+3+g2f9d966
pkgrel=1
pkgdesc="Customize advanced GNOME 3 options"
arch=(any)
url="https://wiki.gnome.org/Apps/GnomeTweakTool"
license=(GPL)
depends=(gnome-settings-daemon python2-gobject)
makedepends=(intltool gnome-common git)
groups=(gnome-extra)
_commit=2f9d96652bb3232de5fd65840a0a390d63af9081  # master
source=("git://git.gnome.org/gnome-tweak-tool#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i '1s|python$|&2|' gnome-tweak-tool
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
