# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.110
pkgrel=2
epoch=1
pkgdesc="Humanist sans serif font"
url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
arch=(any)
license=(custom:SIL)
makedepends=(meson appstream-glib git)
_commit=801f581de296a1d2d0ddca3db6565d4bca5044d3  # tags/v0.110^0
source=("git+https://gitlab.gnome.org/GNOME/cantarell-fonts.git#commit=$_commit"
        weights.diff)
sha256sums=('SKIP'
            '93a3f89efa12895a33ce0aaea717767a34fde0584c7969c0334ea38573c753cb')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  python -m venv venv
  venv/bin/pip install fontmake psautohint
  cd $pkgname
  patch -Np1 -i ../weights.diff
}

build() {
  PATH="$srcdir/venv/bin:$PATH" arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
