# Maintainer: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-mozilla-ca
pkgver=20110914
pkgrel=1
pkgdesc="Mozilla's CA cert bundle in PEM format"
arch=(any)
license=(GPL2 LGPL2.1)
options=(!emptydirs)
depends=('perl>=5.006')
url=http://search.cpan.org/dist/Mozilla-CA
source=("http://search.cpan.org/CPAN/authors/id/A/AB/ABH/Mozilla-CA-${pkgver}.tar.gz")
md5sums=(10774d7598c8bb2c242669648ff663e3)
sha512sums=(669fca6229142517408eb503a2b53dead7345b35ab8864a8c0651e0977ff2ed21908eb8825e3d338539d03ceed3b983bf8eedda9d688f121c153dcf1c21150aa)
_distdir="${srcdir}/Mozilla-CA-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
