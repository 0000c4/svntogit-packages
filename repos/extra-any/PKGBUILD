# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-ipc-system-simple
pkgver=1.28
pkgrel=1
pkgdesc="Run commands simply, with detailed diagnostics"
arch=('any')
url="https://search.cpan.org/dist/IPC-System-Simple"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/J/JK/JKEENAN/IPC-System-Simple-$pkgver.tar.gz")
sha512sums=('93f7145365100c01f039d65cadfaf6c057d6081e4ee3bc23d1c679fc589a9d9a489b99aa653842d50e5c9eb0c0854ed3ed7ca1baf35e6059bbb2b88be3815927')

build() {
  cd IPC-System-Simple-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IPC-System-Simple-$pkgver
  make test
}

package() {
  cd IPC-System-Simple-$pkgver
  make DESTDIR="$pkgdir" install
}
