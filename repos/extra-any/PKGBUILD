# $Id$
# Maintainer: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-latexextra
pkgver=2016.43768
_revnr=${pkgver#2016.}
pkgrel=1
pkgdesc="TeX Live - Large collection of add-on packages for LaTeX"
license=('GPL')
arch=(any)
depends=('texlive-core'
         'perl-file-which' # for pdfannotextractor
        )
optdepends=(
  'texlive-genericextra: to use the calctab package'
  'texlive-pictures: to use the package overpic'
  'java-environment: to use pdfannotextractor'
)
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip"
        "$pkgname.maps"
        "komacv-spurious-changes.patch")
options=('!emptydirs')
sha256sums=('80f5886d4ae9864d672fad8e11e8aa6178305f4ddd7ae232703120768d411d3c'
            'd53750a03a52472a1c7c5ba5142959ba382a5d69215471691c0b12c3663950ce'
            'bca65abf9b833f379702753697d5f57074367fc359c6b16ed48b097752998d72')

build() {
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
   # Patch for spurious komacv \changes{...} lines
   patch -Np1 -i "${srcdir}/komacv-spurious-changes.patch"
}

package() {
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps "$pkgdir"/var/lib/texmf/arch/installedpkgs/
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi

    #add symlinks that were in texlive-bin:
    _linked_scripts="
authorindex/authorindex
exceltex/exceltex
glossaries/makeglossaries
glossaries/makeglossaries-lite.lua
makedtx/makedtx.pl
pax/pdfannotextractor.pl
perltex/perltex.pl
pst-pdf/ps4pdf
splitindex/splitindex.pl
svn-multi/svn-multi.pl
vpe/vpe.pl
yplan/yplan
"
    install -m755 -d "$pkgdir"/usr/bin
    for _script in ${_linked_scripts}; do
        _scriptbase=$(basename $_script)
        _scriptbase=${_scriptbase%.*}
        ln -s /usr/share/texmf-dist/scripts/${_script} ${pkgdir}/usr/bin/${_scriptbase}
    done

    rm "$pkgdir"/usr/share/texmf-dist/scripts/glossaries/makeglossaries.bat
    rm "$pkgdir"/usr/share/texmf-dist/scripts/pst-pdf/ps4pdf.bat*
    rm "$pkgdir"/usr/share/texmf-dist/scripts/shipunov/biokey2html.bat
}
