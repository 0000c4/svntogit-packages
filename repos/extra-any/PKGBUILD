# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=breeze-icons
pkgver=5.26.0
pkgrel=2
pkgdesc='Breeze icon themes'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
groups=('kf5')
makedepends=('extra-cmake-modules' 'qt5-base')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
md5sums=('b3a784fe7be04be0e1bf79573fc7a1be')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBINARY_ICONS_RESOURCE=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
