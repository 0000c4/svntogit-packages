# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Giulio Bai <giulio@hewle.com>

pkgname=accerciser
pkgver=3.32.2
pkgrel=1
pkgdesc="Interactive Python accessibility explorer for the GNOME desktop"
arch=(any)
url="https://wiki.gnome.org/Apps/Accerciser"
license=(BSD)
depends=(python gtk3 python-atspi ipython libwnck3 python-cairo)
makedepends=(appstream-glib yelp-tools git)
groups=(gnome-extra)
_commit=062a0fa753caf5daaf460739f40fffd320f9a21b  # tags/3.32.2^0
source=("git+https://gitlab.gnome.org/GNOME/accerciser.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}
