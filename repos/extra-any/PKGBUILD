# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-getting-started-docs
pkgver=3.28.1
pkgrel=1
pkgdesc="Help a new user get started in GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-getting-started-docs"
license=('CCPL:cc-by-sa-3.0')
arch=(any)
# gst plugins required to decode help videos
depends=(yelp gst-plugins-base gst-plugins-good)
makedepends=(yelp-tools git)
groups=(gnome)
_commit=ce7fda900f24c012075527aa1bd8d20c1c62336d  # tags/3.28.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-getting-started-docs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
