# $Id$
# Maintainer:
# Contributor: Simo Leone <simo@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=aspell-de
pkgver=20100727
pkgrel=1
pkgdesc="German dictionary for aspell"
arch=('any')
url="http://j3e.de/ispell/igerman98/index.html"
license=('GPL2')
depends=('aspell')
source=(http://j3e.de/ispell/igerman98/dict/igerman98-${pkgver}.tar.bz2)
md5sums=('53a66eccaf15fa92d4e5085e7938ee69')

build() {
  cd ${srcdir}/igerman98-${pkgver}

  LANGS="de_DE de_AT de_CH"
  for l in ${LANGS}; do
    make aspell/${l}.rws
  done
}

package() {
  cd ${srcdir}/igerman98-${pkgver}/aspell

  install -d ${pkgdir}/usr/lib/aspell-0.60

  LANGS="de_DE de_AT de_CH"
  for l in ${LANGS}; do
    install -m644 ${l}.alias ${l}.dat ${l}_affix.dat ${l}.multi ${l}.rws \
      ${pkgdir}/usr/lib/aspell-0.60/
  done
}
