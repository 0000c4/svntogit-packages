# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.3.90
pkgrel=2
pkgdesc="grml's zsh setup"
arch=('any')
url='http://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh')
makedepends=('txt2tags')
source=("http://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz"
        'umask.patch')
md5sums=('84f7f2ca292c42c6d0c1b9a4bf538bc5'
         '0c17dc2e288a25cf9dc38391594c21fb')

build() {
	cd ${srcdir}/grml-etc-core-${pkgver}
	# do not override our global umask setting in /etc/profile
	# especially because users don't have their own group by default
	# this is an upstream patch
	patch -p1 -i ${srcdir}/umask.patch
	cd doc
	make
}

package() {
	cd ${srcdir}/grml-etc-core-${pkgver}
	install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
	install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
	install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

	install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
	ln -sf grmlzshrc.5.gz ${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz
}
