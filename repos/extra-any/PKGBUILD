# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-try-tiny
pkgver=0.29
pkgrel=1
pkgdesc="Minimal try/catch with proper localization of \$@"
arch=('any')
url="http://search.cpan.org/dist/Try-Tiny"
license=('MIT')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Try-Tiny-${pkgver}.tar.gz")
sha512sums=('7c7323edf5c93c635f9efd49b9167ad111947326d1685eaa2d612758f7b2547f8ca23255542243ff78668d5f42b30d64b537fef9278afc553406b268fe7d7cea')

build() {
  cd Try-Tiny-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Try-Tiny-${pkgver}
  make test
}

package() {
  cd Try-Tiny-${pkgver}
  make DESTDIR="$pkgdir" install
  # upstream typo :/
  install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
