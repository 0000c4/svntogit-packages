# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20170719
pkgrel=2
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}.xml.gz
        https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}-icons-64x64.tar.gz
        https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
sha256sums=('6141dfee0cc6137f287bf3fcf81dd2a147c2386c452ee0db0369186eef7e2123'
            'adaaa89b636b088baf607eddaac091d31c46cb869fc8aeb39c566fed147a18e1'
            'cbdec4137336a5ff0c4c522f77f44cc5b1940e885f01e6bc2a7c775d2e91805d'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '8900020b6aefcbf4e6d380d567efac425bb6bc75fc61fce3838c7d67450f1084'
            'd479b52dfff7b9369c837125280c8b1427cbf75d4438bfb752724ba49d80507d'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '16148aa2472f641018e658d023244eb499570e06e8f54113b846a324319ce328'
            '2c23b369f4181088d41e2d4b63275afc4b7818e21a455990158333a821c2ba81')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
