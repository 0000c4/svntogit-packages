# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-date-manip
pkgver=6.80
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/Date-Manip/"
depends=('perl')
makedepends=('perl-file-find-rule' 'perl-test-inter' 'perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-$pkgver.tar.gz")
sha512sums=('9d6684acc198d8a6b542c563860c081d160367c4de127eb6729cc313914e2933cf645e2b3df4b23dd73628e7789f684658c77ce9572081f0464fc397641cea92')

build() {
  cd Date-Manip-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Date-Manip-$pkgver
  make test
}

package() {
  cd Date-Manip-$pkgver
  make DESTDIR="$pkgdir" install
}
