# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=perl-date-manip
_realname=Date-Manip
pkgver=6.23
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-yaml-syck')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/S/SB/SBECK/${_realname}-${pkgver}.tar.gz)
md5sums=('e00c4c5758ac3692db99422e7c389675')

build() {
  cd ${srcdir}/${_realname}-${pkgver}

  # install module in vendor directories.
  perl Build.PL installdirs=vendor destdir=${pkgdir}
  perl Build
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  perl Build install

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
