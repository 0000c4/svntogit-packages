# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-workspace-wallpapers
pkgver=5.6.4
pkgrel=1
pkgdesc='Additional wallpapers for the Plasma Workspace'
arch=('any')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
makedepends=('extra-cmake-modules')
conflicts=('kde-wallpapers')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('6290853bcc8e4d21fd16a714b0f01cbe')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
