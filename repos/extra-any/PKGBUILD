# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hunspell-es
pkgname=(hunspell-es_any hyphen-es mythes-es)
pkgver=2.4
pkgrel=1
epoch=1
arch=(any)
url="https://github.com/sbosio/rla-es"
license=(LGPL3)
_countries=(
  'AR "Argentina"'
  'BO "Bolivia"'
  'CL "Chile"'
  'CO "Colombia"'
  'CR "Costa Rica"'
  'CU "Cuba"'
  'DO "Dominican Republic"'
  'EC "Ecuador"'
  'ES "Spain"'
  'GT "Guatemala"'
  'HN "Honduras"'
  'MX "Mexico"'
  'NI "Nicaragua"'
  'PA "Paraguay"'
  'PE "Peru"'
  'PR "Puerto Rico"'
  'PY "Paraguay"'
  'SV "El Salvador"'
  'UY "Uruguay"'
  'VE "Venezuela"'
)

source=(https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_ANY.oxt)
noextract=(es_ANY.oxt)

for _co in "${_countries[@]}"; do
  _code=${_co%% *}
  noextract+=(es_$_code.oxt)
  source+=("https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_$_code.oxt")
  _pkgname=hunspell-es_${_code,,}
  pkgname+=($_pkgname)
  eval "package_$_pkgname() {
    _package $_co
  }"
done

sha256sums=('36507ebabce67d64056c51ee071b4bce2f2c2ebb150278561761480937a27f9a'
            '1744460e6ba4420583abeed848b542808886b223cc893e3c63a416ae6298085c'
            'dddbc2aba787b44044ec47b9ef9eea16a3b50c28fed67d8d9a3abb764a7ada19'
            '8831dbc59385890555d147e4597c8399c386fc78a2b378e6702f152d6b0c4418'
            'e58670a36b02db69f7123eeb414c721252923f875ed702da9de14e62e90ef169'
            '88c648dd97eec3e3fbeb9753e152b027d63d79191d6ea2031539c4f5dfa37d2c'
            '90c39c7e64f77920102a8ba7ccf85a97c7db88bbd75759fceb40fc8ed6995a18'
            '63ba5d5d7475207f31d221cb98843401932018b06b9f7f77b6f0f74b4268849a'
            'aff1eec6657e184242771be82094aece25bb9f07051f076662c0465e76e89006'
            'c1b47643e694b1bb1002d964684b8669a9a4976ca3db48d4a20f5bc3f9d8d40e'
            '57011e91855f2345db1d15064ca4690a7a7be546d0cfd45104822807e6e24fc9'
            '9acb3106a5625ca37418f0a56a0bd4da1a2e6b9fb352685cffb138b82c80b2a8'
            '737d15890134d8117830491f8115c3d5bbe7dd2cd5e1781371625bcefdd5e6fb'
            '7e64b2ee7ab56811ff599fc659c2f737e960cd2835f1616111089032bac4dff5'
            'f42b128fd6b1c5e0adf6427b29fcee60f094b3c938499dccdf2582bf37e805a2'
            'b7ad2ff019d6b0e310bc0eef0e9d812fa8dbc0bf44af1bff8d71e4c617f562dc'
            'd7309671abd23b1af4efd0af8603090cfefdf3164750fa8b3423f458329adef8'
            'dd4ce4225560d2b168f816bb3d7b05000f850a084e833d8f27b93ce8a5d16edb'
            '6027bbd08f73b38be789ad823d0e1779d56a9a8e737b12d965d4ab90579f3161'
            '1a39be8e7da4b3662993ef83ae012084e666f290a2bf4e6190426f3fc27f0d2b'
            '0a75f88c656de0bca7d819975816612ae4b88343b986b23b12893cc3bc876396')

_package() {
  pkgdesc="Spanish ($2) hunspell dictionary"
  depends=(hunspell)
  conflicts=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es_$1.oxt

  install -dm755 "${pkgdir}/usr/share/hunspell"
  install -Dm644 es_$1.dic es_$1.aff "$pkgdir/usr/share/hunspell"

  install -d "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hunspell-es_any() {
  pkgdesc="Spanish (generic) hunspell dictionary"
  depends=(hunspell)
  conflicts=(hunspell-es)
  replaces=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt

  install -Dm644 es_ANY.dic es_ANY.aff -t "$pkgdir"/usr/share/hunspell

  install -d "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
  
  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hyphen-es() {
  pkgdesc="Spanish hyphenation rules"
  depends=(hyphen)

  install -Dm644 hyph_es_ANY.dic -t "$pkgdir"/usr/share/hyphen/

  pushd "$pkgdir"/usr/share/hyphen/
  for _co in "${_countries[@]}"; do
    ln -s hyph_es_ANY.dic hyph_es_${_co%% *}.dic
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
  
  install -Dm644 README_hyph_es_ANY.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_mythes-es() {
  pkgdesc="Spanish thesaurus"
  depends=(libmythes)

  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt
  install -Dm644 th_es_ES_v2.* -t "$pkgdir"/usr/share/mythes

  pushd "$pkgdir"/usr/share/mythes/
  for _co in "${_countries[@]}"; do
    ln -s th_es_ES_v2.idx "th_es_"${_co%% *}"_v2.idx" || true
    ln -s th_es_ES_v2.dat "th_es_"${_co%% *}"_v2.dat" || true
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
  for file in "$pkgdir"/usr/share/mythes/*; do
    ln -sv /usr/share/mythes/$(basename $file) .
  done
  popd
  
  install -Dm644 README_th_es_ES.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}
