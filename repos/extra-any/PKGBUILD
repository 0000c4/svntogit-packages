# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Francois Charette <francois.archlinux.org>

pkgname=perl-io-socket-ssl
pkgver=1.88
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/IO-Socket-SSL/"
depends=('perl-net-ssleay' 'perl' 'perl-uri')
checkdepends=('perl-io-socket-inet6')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/S/SU/SULLR/IO-Socket-SSL-${pkgver}.tar.gz)
sha1sums=('25db598891cc1ed02738a6956e398c1af6cb1f74')

build() {
  cd IO-Socket-SSL-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Socket-SSL-${pkgver}
  make test
}

package() {
  cd IO-Socket-SSL-${pkgver}
  make install DESTDIR="${pkgdir}"
}
