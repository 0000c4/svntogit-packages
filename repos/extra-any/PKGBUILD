# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sound-recorder
pkgver=3.34.0+10+gc4bdfa0
pkgrel=1
pkgdesc="A utility to make simple audio recording from your GNOME desktop"
url="https://wiki.gnome.org/Apps/SoundRecorder"
arch=(any)
license=(GPL2)
depends=(gtk3 gjs gst-plugins-good gst-plugins-base)
makedepends=(gobject-introspection git meson)
groups=(gnome-extra)
_commit=c4bdfa0843b402cfc5155e25115b280a611d7a4e  # gnome-3-36
source=("git+https://gitlab.gnome.org/GNOME/gnome-sound-recorder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
