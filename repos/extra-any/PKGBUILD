# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Evan LeCompte <evanlec@gmail.com>

pkgname=('munin' 'munin-node')
pkgbase=munin
pkgver=1.4.5.stable
_realver=1.4.5
pkgrel=2
pkgdesc="A distributed monitoring/graphing tool"
arch=('any')
url="http://munin-monitoring.org/"
license=("GPL")
depends=('rrdtool' 'perl' 'perl-log-log4perl' 'perl-html-template' 'perl-date-manip')
source=(http://downloads.sourceforge.net/sourceforge/munin/munin-$_realver.tar.gz
        Makefile.config
        munin-1.4-stable.patch
        keep-defaults.patch
        munin-lock-location.patch
        munin-cron-entry
        munin-node.init
        logrotate.munin
        logrotate.munin-node
        08-munin-font-dir.conf
        iostat_partition)

build() { 
	cd $srcdir/munin-$_realver
	patch -Np0 < $srcdir/munin-1.4-stable.patch
	# This build is beyond fucked, also need to report this upstream
	patch -Np1 < $srcdir/keep-defaults.patch
	patch -Np1 < $srcdir/munin-lock-location.patch

	cp $srcdir/Makefile.config $srcdir/$pkgname-$_realver
	# multithreading wrecks havoc on the build, should probably report this
	make -j1 PREFIX=''
}

package_munin() {
	depends=('perl' 'rrdtool' 'perl-html-template' 'perl-date-manip' 'perl-log-log4perl' 'munin-node')
	backup=(etc/munin/munin.conf etc/logrotate.d/munin)
	install=munin.install

	cd $srcdir/munin-$_realver
	make DESTDIR=$pkgdir install-master-prime
	install -D -m644 $srcdir/munin-cron-entry $pkgdir/etc/munin/munin-cron-entry
	install -D -m644 $srcdir/logrotate.munin $pkgdir/etc/logrotate.d/munin
	install -D -m644 $srcdir/08-munin-font-dir.conf $pkgdir/etc/fonts/conf.d/08-munin-font-dir.conf
	rm -rf $pkgdir/var/run/
}

package_munin-node() {
	depends=('perl' 'perl-net-server')
	optdepends=('perl-net-snmp: for SNMP plugins'
	            'perl-net-ssleay: for SSL/TLS support')
	backup=(etc/munin/munin-node.conf etc/logrotate.d/munin-node)
	install=munin-node.install

	cd $srcdir/munin-$_realver
	make DESTDIR=$pkgdir install-common-prime install-node-prime install-plugins-prime
	install -m755 $srcdir/iostat_partition $pkgdir/usr/lib/munin/plugins/
	install -D -m755 $srcdir/munin-node.init $pkgdir/etc/rc.d/munin-node
	install -D -m644 $srcdir/logrotate.munin-node $pkgdir/etc/logrotate.d/munin-node
	rm -rf $pkgdir/var/run/
}

md5sums=('4ae84b9a27b686c3819d8d7b51d8cb4c'
         'df9b86e3057b0f563149fe06e7f7b50a'
         '45dc3cc416ba259363f55a2a684794f9'
         '7d318c55b33680b14f236c5c06a0a64b'
         '97d787260f075c5edef898e6919ce871'
         'dc9c83aa2a278466fb475364462f4119'
         '683627bd0f0c0d1e146dde7d246b6b3c'
         'db77b53150a906256a71a9f539c7fac2'
         'cdf139f2b6ae36852113f3411caa6e99'
         'e33a45c3b80a83eecabbe5a9920c1eb6'
         '12289ab5d062449eaa14376f927f5d26')
