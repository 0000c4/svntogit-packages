# $Id$
# Maintainer: Firmicus <francois.archlinux.org> 
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-html-template
_cpanname=HTML-Template
pkgver=2.94
pkgrel=1
pkgdesc="A simple HTML templating system"
arch=('any')
url="http://search.cpan.org/dist/HTML-Template"
license=("GPL" "PerlArtistic")
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/W/WO/WONKO/${_cpanname}-${pkgver}.tar.gz")
md5sums=('7b7683c3672d55fb922734ea1e9ba7e8')

build() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
