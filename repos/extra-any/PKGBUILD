# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simo Leone <neotuli@gmail.com>

# Note: python2 version must be built first as packaging python3 version "destroys" the source
pkgname=('python2-feedparser' 'python-feedparser')
pkgbase=python-feedparser
pkgver=5.0.1
pkgrel=1
pkgdesc="Parse RSS and Atom feeds in Python"
arch=('any')
url="http://feedparser.sf.net"
license=('custom')
makedepends=('python' 'python2' 'libxml2' )
source=(http://feedparser.googlecode.com/files/feedparser-${pkgver}.tar.bz2)
md5sums=('702835de74bd4a578524f311e62c2877')


package_python2-feedparser() {
  depends=('python' 'libxml2' )
  pkgdesc="Parse RSS and Atom feeds in Python2"

  cd ${srcdir}/feedparser-$pkgver
  python2 setup.py install --root=${pkgdir}
  
  sed -i 's#env python$#env python2#' \
    $pkgdir/usr/lib/python2.7/site-packages/feedparser.py
  
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/license
}

package_python-feedparser() {
  depends=('python' 'libxml2' )
  
  cd ${srcdir}/feedparser-$pkgver
  . convert_to_py3.sh
  PYTHONPATH=feedparser python setup.py install --root=${pkgdir}  
  
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/license
}
