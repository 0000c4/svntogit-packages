# $Id$
# Maintainer: Jan de Groot
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-mime-types
_realname=MIME-Types
pkgver=1.32
pkgrel=1
pkgdesc="Perl/CPAN Module MIME::Types : Information and processing MIME types"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/M/MA/MARKOV/${_realname}-${pkgver}.tar.gz)
md5sums=('d9dd51a22491688425c2b3cf9cf2736d')
sha1sums=('92ed0b5caf4743b30240a44f0a45458c2cd1a21f')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
