# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=(python-prometheus_client python2-prometheus_client)
pkgver=0.6.0
pkgrel=1

pkgdesc="Prometheus instrumentation library for Python applications"
url="https://github.com/prometheus/client_python"
arch=('any')
license=('APACHE')

makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
checkdepends=('python-twisted' 'python-pytest'
              'python2-twisted' 'python2-pytest' 'python2-futures')

source=(client_python-$pkgver.tar.gz::https://github.com/prometheus/client_python/archive/v${pkgver}.tar.gz)
sha512sums=('a2b92683f939961c567c266a54699de6f37088cd4e61c9d9a5f4451228b4c3bfa4c517e4a5187867f5463d726a8dc6e7fdae30973c1e9aa9aa1a03247b2e99cd')

prepare() {
  cp -r client_python-$pkgver client_python-$pkgver-py2
}

build() {
  cd client_python-$pkgver
  python setup.py build

  cd "$srcdir"/client_python-$pkgver-py2
  python2 setup.py build
}

check() {
  cd client_python-$pkgver
  python setup.py test

  cd "$srcdir"/client_python-$pkgver-py2
  python2 setup.py test
}

package_python-prometheus_client() {
  depends=('python')

  cd client_python-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}

package_python2-prometheus_client() {
  depends=('python2')

  cd client_python-$pkgver
  python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
}
