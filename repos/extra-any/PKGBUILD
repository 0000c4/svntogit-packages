# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ttf-dejavu
pkgver=2.32
pkgrel=2
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters"
arch=('any')
url="http://dejavu.sourceforge.net/"
license=('custom')
depends=('fontconfig' 'xorg-fonts-encodings')
install=ttf-dejavu.install
source=(http://downloads.sourceforge.net/project/dejavu/dejavu/${pkgver}/dejavu-{,lgc-}fonts-ttf-${pkgver}.tar.bz2)
md5sums=('63b1bf644cd54188a2ec43aff1846104'
         '2813b852ded0c1c89264627ce1bfcadc')

package() {
  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/usr/share/fonts/TTF
  
  for i in dejavu-{,lgc-}fonts-ttf; do
    cd "${srcdir}"/${i}-${pkgver}
    install -m644 ttf/*.ttf "${pkgdir}"/usr/share/fonts/TTF/
    install -m644 fontconfig/*.conf "${pkgdir}"/etc/fonts/conf.avail/
  done

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
