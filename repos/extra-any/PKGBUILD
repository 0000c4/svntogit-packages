# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=('geoip-database' 'geoip-database-extra')
pkgver=20200507
pkgrel=2
arch=('any')
url="https://mailfud.org/geoip-legacy/"
license=('CCPL:by-sa')
makedepends=('util-linux')
checkdepends=('geoip')
source=(GeoIP-$pkgver.dat.gz::https://mailfud.org/geoip-legacy/GeoIP.dat.gz
        GeoIPv6-$pkgver.dat.gz::https://mailfud.org/geoip-legacy/GeoIPv6.dat.gz
        GeoIPCity-$pkgver.dat.gz::https://mailfud.org/geoip-legacy/GeoIPCity.dat.gz
        GeoIPCityv6-$pkgver.dat.gz::https://mailfud.org/geoip-legacy/GeoIPCityv6.dat.gz
        GeoIPASNum-$pkgver.dat.gz::https://mailfud.org/geoip-legacy/GeoIPASNum.dat.gz
        GeoIPASNumv6-$pkgver.dat.gz::https://mailfud.org/geoip-legacy/GeoIPASNumv6.dat.gz)
sha256sums=('5ede20bf8d2ed9b5b1d9b6c1f5629485f2c5cbaaa7d4b5f5e109bf4988c4ac1a'
            'e08315f7d6ebef446edce332c7c10850aa3b13318b0dca2b41a667f2264e4017'
            'a66cfc0d48007c1c1b905419662785d7dd919286db9fbb2e824fa4545c2a0a31'
            'ce1bdf1c1dc0cce41de6bac173b7338f5f9873c0e531a660168752cd18937c1d'
            'c53dbc43af4186ca8a1515ffc16c82d70c54b4a12e135addc659f7e1bbf8eb96'
            'b2bfe48257a778d2fd8cf2327d8782dbe5b03a56cb42ae8e6d42a2dc932c1066')

prepare() {
  cd "$srcdir"
  rename -v -- "-$pkgver" '' *.dat
}

check() {
  cd "$srcdir"

  if [[ $(geoiplookup -f GeoIP.dat 8.8.8.8) != *'US, United States' ]]; then
    error 'Unable to resolve IPv4 address to country.'
    return 1
  fi

  if [[ $(geoiplookup6 -f GeoIPv6.dat 2001:4860:4860::8888) != *'US, United States' ]]; then
    error 'Unable to resolve IPv6 address to country.'
    return 1
  fi

  if [[ $(geoiplookup -f GeoIPCity.dat 8.8.8.8) != *'US, 00, N/A, N/A'* ]]; then
    error 'Unable to resolve IPv4 address to city.'
    return 1
  fi

  if [[ $(geoiplookup6 -f GeoIPCityv6.dat 2001:4860:4860::8888) != *'US, 00, N/A, N/A'* ]]; then
    error 'Unable to resolve IPv6 address to city.'
    return 1
  fi

  if [[ $(geoiplookup -f GeoIPASNum.dat 8.8.8.8) != *'AS15169 GOOGLE' ]]; then
    error 'Unable to resolve IPv4 address to ASN.'
    return 1
  fi

  if [[ $(geoiplookup6 -f GeoIPASNumv6.dat 2001:4860:4860::8888) != *'AS15169 GOOGLE' ]]; then
    error 'Unable to resolve IPv6 address to ASN.'
    return 1
  fi
}

package_geoip-database() {
  pkgdesc="GeoIP legacy country database (based on GeoLite2 data created by MaxMind)"

  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoIP{,v6}.dat
}

package_geoip-database-extra() {
  pkgdesc="GeoIP legacy city/ASN databases (based on GeoLite2 data created by MaxMind)"

  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoIPCity{,v6}.dat GeoIPASNum{,v6}.dat
}

# vim:set ts=2 sw=2 et:
