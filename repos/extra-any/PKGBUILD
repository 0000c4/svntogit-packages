# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=adobe-source-sans-pro-fonts
pkgver=2.040ro+1.090it
_relver=2.040R-ro/1.090R-it
pkgrel=1
pkgdesc="Sans serif font family for user interface environments"
url="https://adobe-fonts.github.io/source-sans-pro/"
arch=(any)
license=(custom)
_tarname=source-sans-pro-${_relver//\//-}
source=("$_tarname.tar.gz::https://github.com/adobe-fonts/source-sans-pro/archive/$_relver.tar.gz")
sha256sums=('6053d4d26f9df1482d0898e52bef7a9a92bf478635bc8ce01d45df164583ebf2')

package() {
  cd $_tarname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VAR/*.otf
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE.txt
}

# vim:set ts=2 sw=2 et:
