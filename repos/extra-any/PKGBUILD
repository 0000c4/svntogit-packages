# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname=perl-file-copy-recursive
pkgver=0.38
pkgrel=2
pkgdesc="extension for recursively copying files and directories"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/File-Copy-Recursive'
source=("http://search.cpan.org/CPAN/authors/id/D/DM/DMUEY/File-Copy-Recursive-$pkgver.tar.gz")
md5sums=('e76dc75ab456510d67c6c3a95183f72f')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/File-Copy-Recursive-$pkgver"
  perl Makefile.PL
  make
}

check() {
  cd "$srcdir/File-Copy-Recursive-$pkgver"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "$srcdir/File-Copy-Recursive-$pkgver"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
