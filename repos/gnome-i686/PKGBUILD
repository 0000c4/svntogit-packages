# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=2.23.91
pkgrel=1
pkgdesc="Evolution Data Server provides a central location for addressbook and calendar in the GNOME Desktop"
arch=(i686 x86_64)
depends=('libsoup>=2.23.91' 'nss>=3.12' 'libgnome>=2.23.5' 'libglade>=2.6.3' 'gnome-keyring>=2.23.91' 'db4.1' 'heimdal>=1.2')
makedepends=('intltool' 'pkgconfig')
options=('!libtool')
url="http://www.gnome.org"
license=('GPL')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.23/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c5c7dc31421f33f6665e66661599cae0')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  # Look for correct DB, we want 4.1
  sed -e 's|DB_CFLAGS="-I$withval/include"|DB_CFLAGS="-I$withval/include/db4.1"|' \
      -e 's|DB_LIBS="-L$withval/lib -ldb"|DB_LIBS="-L$withval/lib -ldb-4.1"|' \
      -i configure || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var  --with-openldap=yes \
    --libexecdir=/usr/lib/evolution-data-server-1.2 \
    --with-krb5=/usr --enable-gnome-keyring \
    --with-libdb=/usr || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
}
