# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=tracker
pkgver=0.12.0
pkgrel=2
pkgdesc="Powerful object database, tag/metadata database, search tool and indexer"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgee' 'libgnome-keyring' 'upower' 'libexif' 'exempi' 'poppler-glib'
         'libgsf' 'icu' 'enca' 'xdg-utils' 'networkmanager')
makedepends=('gobject-introspection' 'intltool' 'giflib' 'gstreamer0.10-base'
             'totem-plparser' 'evolution' 'taglib' 'nautilus' 'gnome-panel'
             'firefox' 'thunderbird' 'libvorbis' 'flac' 'vala')
optdepends=('giflib: extractor for GIF data'
            'gstreamer0.10-base: video extractor'
            'totem-plparser: playlist support'
            'evolution: Evolution email data miner'
            'taglib: writeback for audio files'
            'nautilus: nautilus-extension'
            'libvorbis: Vorbis metadata extractor'
            'flac: FLAC metadata extractor'
            'firefox: Firefox data miner extension'
            'thunderbird: Thunderbird data miner extension'
            'gnome-panel: tracker-search-bar')
provides=('libtracker')
url="http://www.gnome.org"
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=tracker.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('6819b5fbbcaa14ac004759e4b25962f8ef36f2c4b37908a32fcf6d7a22e5b261')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  _ffdir=/usr/lib/firefox-6.0.2/extensions
  _tbdir=/usr/lib/thunderbird-6.0.2/extensions

  # Force vala regeneration
  find . -name '*.stamp' -delete

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/tracker \
    --disable-unit-tests \
    --enable-libflac \
    --enable-libvorbis \
    --with-firefox-plugin-dir=$_ffdir \
    --with-thunderbird-plugin-dir=$_tbdir

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
