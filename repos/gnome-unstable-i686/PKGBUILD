# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=2.91.6
pkgrel=1
pkgdesc="Central location for addressbook and calendar storage in the GNOME Desktop"
arch=('i686' 'x86_64')
depends=('libsoup' 'nss' 'libgnome-keyring' 'heimdal' 'libgweather' 'libical' 'db' 'libgdata')
makedepends=('intltool' 'gperf' 'gobject-introspection')
options=('!libtool')
url="http://www.gnome.org"
license=('GPL')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('2db7f454ec1666a6c856ed3cbc807d32fc65089770440b44c7525b9c219a76ea')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-openldap=yes \
      --libexecdir=/usr/lib/evolution-data-server-1.2 \
      --with-krb5=/usr --with-libdb=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
