# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=3.3.92
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf' 'libnotify')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus-sendto' 'gobject-introspection' 'gtk-doc' 'gnome-common')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        61-gnome-bluetooth-rfkill.rules
        fix-build::http://git.gnome.org/browse/gnome-bluetooth/patch/?id=94a23c3a5194d527aae993edbc68a50695a3f93e)
sha256sums=('081834f33a45829a108dab33e7d6bdeef9efdde9741bb55208327b38f425343c'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474'
            '357f4abfc62da3a7e87bc815daf698f258135707bcf302e46f5b189fbdbeaf95')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/fix-build"
  gnome-autogen.sh
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -m644 -D "$srcdir/61-gnome-bluetooth-rfkill.rules" \
                     "$pkgdir/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
