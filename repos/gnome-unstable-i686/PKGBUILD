# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdazzle
pkgver=3.26.0
pkgrel=1
pkgdesc="Feature-filled library for GTK+ and GObject"
arch=('i686' 'x86_64')
license=('LGPL')
depends=(gtk3)
makedepends=('git' 'meson' 'vala' 'gobject-introspection' 'gtk-doc')
_commit=c772276812a7be5d8b4fb262a3ee2569601d1c7a  # tags/3.26.0^0
source=("git+https://git.gnome.org/browse/libdazzle#commit=$_commit")
url="https://www.gnome.org/"
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release -Denable_gtk_doc=true ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
