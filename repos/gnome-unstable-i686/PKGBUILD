# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=geocode-glib
pkgver=3.20.1+1+ga646012
pkgrel=1
pkgdesc="Helper library for geocoding services"
url="https://git.gnome.org/browse/geocode-glib/"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 json-glib libsoup)
makedepends=(intltool gobject-introspection gnome-common git gtk-doc)
options=(!emptydirs)
_commit=a64601202c6cbdb2578aa054f6c3a1249cf3596c
source=("git://git.gnome.org/geocode-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i 's/gnome/Adwaita/g' icons/Makefile.am
  NOCONFIGURE=1 gnome-autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

