# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-user-share
pkgver=2.91.6
pkgrel=1
pkgdesc="Easy to use user-level file sharing for GNOME."
arch=('i686' 'x86_64')
url="http://ftp.gnome.org/pub/GNOME/sources/gnome-user-share/"
license=('GPL')
depends=('mod_dnssd' 'gconf' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'gnome-bluetooth' 'libcanberra')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus')
options=('!emptydirs' '!libtool')
install=gnome-user-share.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('d23d750da50b99eb16f770829ea12bfeae3dc141daccbf0222d13cff81d72120')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-user-share \
      --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain gnome-user-share ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
