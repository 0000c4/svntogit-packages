# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=2.33.90
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gconf' 'vte3' 'gsettings-desktop-schemas' 'libsm')
makedepends=('gnome-doc-utils' 'gtk2' 'intltool')
#gtk2 is needed for gtk-builder-convert
#https://bugzilla.gnome.org/show_bug.cgi?id=588945
#https://bugzilla.gnome.org/show_bug.cgi?id=588732
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.33/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('d0c16be584a150f98788e1380df07280864c4351109dfa421a03a718e0052acb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --with-gtk=3.0
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-terminal ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
