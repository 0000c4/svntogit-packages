# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgtop
pkgver=2.36.0
pkgrel=1
pkgdesc="A library that reads information about processes and the running system"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2' 'libxau')
makedepends=('gobject-introspection' gnome-common git gtk-doc)
_commit=88ecae1d00463909fc1d4e9fe46be64ea47b030f  # tags/2.36.0^0
source=("git+https://git.gnome.org/browse/libgtop#commit=$_commit")
url="http://www.gnome.org/"
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/$pkgname"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-gtk-doc
  make
}

package() {
  cd "${srcdir}/$pkgname"
  make DESTDIR="${pkgdir}" install
}
