# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gupnp
pkgver=0.18.4
pkgrel=1
pkgdesc="An object-oriented UPNP framework"
arch=('i686' 'x86_64')
url="http://gupnp.org/"
license=('LGPL')
depends=('gssdp' 'util-linux')
makedepends=('gobject-introspection' 'gtk-doc')
optdepends=('python2: gupnp-binding-tool')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('6b437d5711b212292c6080626b97480268310da636a6cf937ad6119291af78a1')

build() {
  cd $pkgname-$pkgver
  sed -i '1s|^#!.*python$|&2|' tools/gupnp-binding-tool
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-context-manager=linux
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
