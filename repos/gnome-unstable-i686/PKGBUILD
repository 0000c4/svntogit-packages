# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=liferea
pkgver=1.7.4
pkgrel=1
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf' 'libwebkit' 'libxslt' 'sqlite3' 'hicolor-icon-theme'
         'libunique' 'libnotify')
makedepends=('pkg-config' 'intltool')
options=('!libtool' '!emptydirs')
install=liferea.install
changelog=ChangeLog
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz
        libnotify-0.7.patch)
md5sums=('69ece60acc93a58cffb66e0e4f753704'
         'c6bb9248bdf99afe96d7fdb944d8d808')
sha1sums=('2607aad81b2ada9a932eccd326ad8bbcf1c16986'
          'cfc0191161215d6181d936edcebf1142213b7074')

build() {
  cd "${srcdir}/liferea-${pkgver}"
  patch -Np1 -i "$srcdir/libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var\
    --disable-schemas-install --disable-nm
  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
