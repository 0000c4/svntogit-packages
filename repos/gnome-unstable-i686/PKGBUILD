# $Id$
# Maintainer: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=3.1.92
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('gconf' 'hicolor-icon-theme')
makedepends=('gtk-doc' 'intltool' 'gobject-introspection')
options=('!libtool' '!emptydirs')
install=gucharmap.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('370179135acbe87c22854932964a1f5ad469c811abe63d4c10e2394cbccfbd47')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper  --enable-introspection
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gucharmap ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
