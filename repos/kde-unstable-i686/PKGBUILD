# $Id: PKGBUILD 145371 2011-12-22 07:58:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkipi
pkgver=4.10.95
pkgrel=2
pkgdesc="An interface to use kipi-plugins from a KDE application"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkipi"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('0208efa282f63a8e51f28a132112a8cb919cb5c5')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
