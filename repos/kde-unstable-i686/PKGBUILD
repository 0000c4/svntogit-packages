# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=4.9.80
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs' 'libkgapi')
makedepends=('cmake' 'automoc4' 'boost')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'qjson080.patch')
sha1sums=('d5d47f830786c627e6fb3a336b5506ba525e7e17'
          '9160ac49b0b79e2c58d03c434a6d8c080a7d75e2')

build() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i "${srcdir}"/qjson080.patch
    cd ../

	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
