# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcron
pkgver=17.08.0
pkgrel=1
pkgdesc='Configure and schedule tasks'
url='https://userbase.kde.org/KCron'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kio' 'cron')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdeadmin-kcron')
replaces=('kdeadmin-kcron')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kcron-${pkgver}.tar.xz"{,.sig})
sha256sums=('08d4c3ac76cee0f6a7a65102299d1b4f1e933bfd9ef77a66200efa0fe68cbf3a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
