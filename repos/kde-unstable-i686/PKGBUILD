# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.5.95
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
source=("http://download.kde.org/unstable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
depends=('powerdevil' 'polkit-kde-agent' 'libcanberra' 'libxkbfile' 'kmenuedit'
         'systemsettings' 'ksysguard' 'kpeople' 'baloo' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'xf86-input-evdev' 'xf86-input-synaptics' 'xorg-server-devel' 'libibus' 'scim')
optdepends=('plasma-nm: Network manager applet'
            'ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support')
conflicts=('kdebase-workspace' 'kcm-touchpad-frameworks' 'kdeplasma-addons<5.5.95' 'kactivities<5.19.0-3')
replaces=('kcm-touchpad-frameworks')
install="${pkgname}.install"
groups=('plasma')
md5sums=('9e042a438b308f2aeccd51a5f388aaff')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# Conflicts with kactivitymanagerd
  rm -r "$pkgdir"/usr/share/locale/*/LC_MESSAGES/kactivities5.mo
}
