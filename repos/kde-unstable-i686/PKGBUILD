# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khelpcenter
pkgver=16.03.90
pkgrel=1
pkgdesc="Application to show KDE Applications' documentation"
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/khelpcenter'
license=(LGPL)
groups=(kde-applications kdebase)
depends=(khtml kinit kdoctools xapian-core grantlee-qt5)
makedepends=(extra-cmake-modules)
conflicts=(khelpcenter4 kdebase-runtime-doc)
replaces=(khelpcenter4 kdebase-runtime-doc)
source=("http://download.kde.org/unstable/applications/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('8cfbcc53f17b96d55f4ed63d22c66b1b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
