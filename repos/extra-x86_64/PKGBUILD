# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kalgebra
pkgver=16.12.2
pkgrel=2
pkgdesc="Graph Calculator"
url="https://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('analitza' 'kio' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'glu')
optdepends=('kirigami2: for KAlgebra Mobile')
replaces=('kdeedu-kalgebra')
conflicts=('kdeedu-kalgebra' 'kdeedu-analitza')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kalgebra-${pkgver}.tar.xz"{,.sig}
        kalgebra-kirigami2.patch::"https://cgit.kde.org/kalgebra.git/patch/?id=f151065a")
sha1sums=('067a17bbd6c44f885db13634bba5db71bfd54f13'
          'SKIP'
          'cfbb47daf23d0655cce0da6b5ac08cff0929a45f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Port to kirigami2
  patch -p1 -i ../kalgebra-kirigami2.patch
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
