# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmix
pkgver=18.08.3
pkgrel=2
pkgdesc="KDE volume control program"
url='https://kde.org/applications/multimedia/kmix/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kinit hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
conflicts=(kdemultimedia-kmix)
replaces=(kdemultimedia-kmix)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kmix-qt5.12.patch)
sha256sums=('2d27ad00533188d55f01c4d98b1ca91a22e0cdcd1f3f77bae9005a2a357e1214'
            'SKIP'
            '9980bc24c289e86067c6f05b12d1b761918237e758329d72d8284fe7bee7eea5')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kmix-qt5.12.patch # Fix PulseAudio support with Qt 5.12
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
