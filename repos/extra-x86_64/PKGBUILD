# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libappimage
pkgver=1.0.0
pkgrel=1
pkgdesc="Reference implementation of the AppImage specification"
arch=(x86_64)
url="https://appimage.org/"
license=(GPL)
depends=(libarchive squashfuse boost-libs)
makedepends=(cmake wget vim desktop-file-utils boost xdg-utils-cxx cairo)
source=($pkgname-$pkgver.tar.gz::"https://github.com/AppImage/libappimage/archive/v$pkgver.tar.gz")
sha256sums=('605774879e2eb02a9a73355a2db77e307c42b25d21025be19819500fe569c81b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_SYSTEM_XZ=ON \
    -DUSE_SYSTEM_SQUASHFUSE=ON \
    -DUSE_SYSTEM_LIBARCHIVE=ON \
    -DUSE_SYSTEM_BOOST=ON \
    -DUSE_SYSTEM_XDGUTILS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
