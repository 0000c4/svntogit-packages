# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.14.0
pkgrel=2
pkgdesc="Settings manager of the Xfce desktop"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'xfconf' 'libnotify' 'colord'
         'libxklavier' 'adwaita-icon-theme' 'gnome-themes-extra')
makedepends=('intltool' 'xf86-input-libinput')
optdepends=('libcanberra: for sound control')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        0001-display-Assure-correct-gchar-Bug-15816.patch
        0002-display-Fix-profile-matching-in-xfsettingsd.patch
        default-xsettings-xml.patch)
sha256sums=('e03040d623abda0222ad04f226582277a1936507ddfa927ec8e87927debc113c'
            '48ef9711f7f84370d5e39792ffed818452358f5ccacf11ff06ed1abc02e775e6'
            '667c914ce8fe058df5217bab15e06d0034b049e025870ae72135e5e3c415eb63'
            '8e9a6c70ab0ceb5d91b637dc290768f8a47edb5d7b6e2eebc4459dbc4ee040d7')
prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=15816
  patch -Np1 -i ../0001-display-Assure-correct-gchar-Bug-15816.patch
  patch -Np1 -i ../0002-display-Fix-profile-matching-in-xfsettingsd.patch

  # Enable Adwaita theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-upower-glib \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
