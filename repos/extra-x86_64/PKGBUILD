# $Id$
# Maintainer: kevin <kevin@archlinux.org>
# Contributor:  Matt Thompson <mattt@defunct.ca>

pkgname=perl-dbi
_realname=DBI
pkgver=1.623
pkgrel=1
pkgdesc="Database independent interface for Perl"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TI/TIMB/${_realname}-${pkgver}.tar.gz)
md5sums=('b45654dca3b495f3d496c359f0029d96')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# vim: ts=2 sw=2 et ft=sh
