# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=16.08.1
pkgrel=2
pkgdesc='Archiving Tool'
arch=(i686 x86_64)
url='http://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kparts kpty libarchive hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('p7zip: 7Z format support' 'zip: ZIP compression support' 'unzip: ZIP format support' 'unrar: RAR format support')
conflicts=(kdeutils-ark)
replaces=(kdeutils-ark)
groups=(kde-applications kdeutils)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
        kdebug-368786.patch::"https://quickgit.kde.org/?p=ark.git&a=commitdiff&h=9a9a8510&o=plain")
md5sums=('4c8d6a236436e1fc8b5bfe629243e223'
         '40ae44a105c9bf0c235d5f55afeda34c')

prepare() {
  mkdir -p build

# Fix opnening RAR files with shared-mime-info 1.7 http://bugs.kde.org/show_bug.cgi?id=368786
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-368786.patch
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

