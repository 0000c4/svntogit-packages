# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-logs
pkgver=3.28.0+1+gea3e78c
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Logs"
license=(GPL)
depends=(systemd gtk3 gsettings-desktop-schemas)
makedepends=(appstream-glib git meson itstool)
groups=(gnome)
_commit=ea3e78c89697deeb2b404166dccd62e6baf8f70d  # gnome-3-28
source=("git+https://git.gnome.org/browse/gnome-logs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-logs-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f gnome-logs-3.28.0 2939d8f578d46d95edc3aacd0538f7f0ab860dd8  # missing tag
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
