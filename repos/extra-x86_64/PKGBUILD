# $Id$
# Maintainer:
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=easytag
pkgver=2.1.7
pkgrel=3
pkgdesc="Utility for viewing, editing and writing ID3 tags of your MP3 files"
arch=('i686' 'x86_64')
license=('GPL')
url='http://easytag.sourceforge.net/'
depends=('id3lib' 'libid3tag' 'gtk2' 'libvorbis' 'flac' 'speex' 'wavpack' 'libmp4v2' 'desktop-file-utils')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2" easytag-2.1.7-new_libmp4v2.patch)
md5sums=('9df3e800d80e754670642f2ba5e03539'
         '0d01887de463585d944777b638a28c85')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/easytag-2.1.7-new_libmp4v2.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
