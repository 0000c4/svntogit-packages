# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fvwm
pkgver=2.6.1
pkgrel=2
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'librsvg' 'libxinerama' 'libxcursor')
makedepends=('libxslt')
conflicts=('fvwm-devel')
options=('!emptydirs')
source=(ftp://ftp.fvwm.org/pub/fvwm/version-2/${pkgname}-${pkgver}.tar.bz2 fvwm2.desktop)
md5sums=('fbbbed1bb41fa0878b17ee209947602d'
         'e52a6ef25a936dcf3ad0e58f2e9ed0ea')
sha1sums=('cf7165d40180b45409e604736066cf9fcc792e36'
          'e0924164329e7eb14e0d6137d8ff18cf6230191c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke-library
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/fvwm"
  install -D -m644 sample.fvwmrc/* "${pkgdir}/etc/fvwm"
  rm -f "${pkgdir}"/etc/fvwm/Makefile*
  install -D -m644 ../fvwm2.desktop "${pkgdir}/usr/share/xsessions/fvwm2.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
