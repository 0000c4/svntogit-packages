# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.28.0
pkgrel=2
pkgdesc="The GNOME Session Handler"
url="https://git.gnome.org/browse/gnome-session"
arch=(x86_64)
license=(GPL LGPL)
depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
         json-glib libgl libsm libxtst systemd)
makedepends=(mesa gtk-doc xtrans git python xmlto docbook-xsl meson)
groups=(gnome)
_commit=7b33ab3db3ac6a1402232b547611a0dd86a4b8ec  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/gnome-session#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # XTRANS support
  git cherry-pick -n 1ece66e683258a0bfa044d074becfe207d04a748
}
  
build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
