# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=16.04.0
pkgrel=2
pkgdesc="Offers a selection of solitaire card games"
url="http://kde.org/applications/games/kpatience/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-kpatience')
replaces=('kdegames-kpatience')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kpat-${pkgver}.tar.xz")
sha1sums=('7e010eedfaa51db950c04d43c7361537a266d367')

build() {
  mkdir -p build
  cd build
  cmake ../kpat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
