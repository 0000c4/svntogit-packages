# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kross-interpreters
pkgver=18.04.1
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=(x86_64)
url='https://www.kde.org/applications/development/'
license=(GPL)
depends=(kross)
makedepends=(extra-cmake-modules kdoctools python2 ruby)
optdepends=('python2: kross-python plugin' 'ruby: kross ruby plugin')
groups=(kde-applications kdebindings)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6a2325359a8c3ab34cd07591dfb7e311903b037433aa7e345655091cdb71cbbf'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

