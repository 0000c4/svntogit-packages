# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-tetravex
pkgver=3.34.1
pkgrel=1
pkgdesc="Complete the puzzle by matching numbered tiles"
url="https://wiki.gnome.org/Apps/Tetravex"
arch=(x86_64)
license=(GPL)
depends=(gtk3)
makedepends=(meson gobject-introspection vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=d8707cac579709016104530816ba9f8b62f1dc21  # tags/3.34.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-tetravex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
