# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgbase=('cdrdao')
pkgname=('cdrdao' 'gcdmaster')
pkgver=1.2.3
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
url="http://cdrdao.sourceforge.net/"
makedepends=('gcc-libs' 'lame' 'libmad' 'libvorbis' 'libao' \
             'libsigc++2.0' 'gtkmm' 'libgnomeuimm')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	cdrdao-1.2.3-autoconf-update.patch
        cdrdao-1.2.3-k3b.patch)
md5sums=('8d15ba6280bb7ba2f4d6be31d28b3c0c'
         '8e53dfc174f7c0882194caa05e68b85e'
         '696f6ca01e1eeb9b6a5be88e535d9398')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  
  patch -Np1 -i "${srcdir}/cdrdao-1.2.3-autoconf-update.patch" || return 1
  patch -Np1 -i "${srcdir}/cdrdao-1.2.3-k3b.patch" || return 1
  
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-xdao --with-lame \
    --with-ogg-support --with-mp3-support || return 1
  make || return 1
}

package_cdrdao() {
  pkgdesc="Records audio/data CD-Rs in disk-at-once (DAO) mode"
  depends=('gcc-libs' 'lame' 'libmad' 'libvorbis' 'libao')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}

package_gcdmaster() {
  pkgdesc="GUI frontend for creating audio CDs using cdrdao"  
  depends=('gcc-libs' 'lame' 'libmad' 'libvorbis' 'libao' \
	   'libsigc++2.0' 'gtkmm' 'libgnomeuimm')
  provides=('cdrdao')
  conflicts=('cdrdao')
 
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1  
}


