# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=18.08.0
pkgrel=2
pkgdesc="Offers a selection of solitaire card games"
url="https://kde.org/applications/games/kpatience/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames freecell-solver hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/applications/$pkgver/src/kpat-$pkgver.tar.xz"{,.sig}
        kdebug-397770.patch::"https://cgit.kde.org/kpat.git/patch/?id=fac28f11")
sha256sums=('b4ba00d0d90ccb60a8b9328d22f0ce31413abe8a4ead775d712b8a5b2355a936'
            'SKIP'
            'd65b06cd6abf13f4009cef4eec405d5191bd9aee229e64428271f2c67129873a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd kpat-$pkgver
  patch -p1 -i ../kdebug-397770.patch # Fix cards don't autoplay to the foundations
}

build() {
  cd build
  cmake ../kpat-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
