# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcolorchooser
pkgver=17.04.0
pkgrel=1
pkgdesc="Color Chooser"
url="https://kde.org/applications/graphics/kcolorchooser/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
conflicts=(kdegraphics-kcolorchooser)
replaces=(kdegraphics-kcolorchooser)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('40718354ce27719cab1c3baafdbc080f89fa03a4f64048959d7ca2c4621e84c9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
