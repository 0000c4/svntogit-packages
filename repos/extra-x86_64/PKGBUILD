# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kparts
pkgver=5.18.0
pkgrel=2
pkgdesc='Document centric plugin system'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kparts'
license=('LGPL')
depends=('kio')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
	bug355711.patch::"https://quickgit.kde.org/?p=kparts.git&a=commitdiff&h=5ac5df&o=plain")
md5sums=('4d465788cbc34707a64bdebd9aa978bf'
         '13ed942a61ad7a4198e61fa290e3fd81')

prepare() {
  mkdir -p build

# Fix crash in systemsettings http://bugs.kde.org/show_bug.cgi?id=355711
  cd $pkgname-$pkgver
  patch -p1 -i ../bug355711.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
