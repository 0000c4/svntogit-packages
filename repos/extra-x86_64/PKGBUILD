# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgbase=sip
pkgname=('sip' 'python-sip' 'python2-sip')
pkgver=4.15.1.908d49322dcf
_pkgver=4.15.2-snapshot-908d49322dcf
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
makedepends=('python' 'python2')
#source=("http://downloads.sourceforge.net/pyqt/${pkgbase}-${pkgver}.tar.gz")
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/${pkgname}-${_pkgver}.tar.gz")
md5sums=('a58484947c324dad2c99b5fd770fefea')

build() {
  cp -r ${pkgbase}-${_pkgver} python2-${pkgbase}-${_pkgver}

  cd ${pkgbase}-${_pkgver}
  python configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
  
  ### Python2 version ###
  cd ../python2-${pkgbase}-${_pkgver}
  python2 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  depends=('glibc')

  cd ${pkgbase}-${_pkgver}
  make DESTDIR="${pkgdir}" install -C sipgen

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-sip() {
  pkgdesc="Python 3.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python')

  cd ${pkgbase}-${_pkgver}
  make DESTDIR="${pkgdir}" install -C siplib
  
  install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python3.3/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python3.3/site-packages/sipdistutils.py
  
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-sip() {
  pkgdesc="Python 2.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python2')

  cd python2-${pkgbase}-${_pkgver}
  make DESTDIR="${pkgdir}" install -C siplib
  
  install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipdistutils.py
  
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
