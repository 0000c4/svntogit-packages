# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail
pkgver=19.04.2
pkgrel=2
pkgdesc="KDE mail client"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kmail-account-wizard akonadi-import-wizard mbox-importer
         pim-data-exporter pim-sieve-editor kontactinterface ktnef)
makedepends=(extra-cmake-modules kdoctools boost)
optdepends=('kleopatra: mail encryption support' 'kdepim-addons: additional plugins')
conflicts=('ktnef<18')
replaces=('ktnef<18')
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-404881.patch::https://cgit.kde.org/kmail.git/patch/?id=97e165dc)
sha256sums=('177a9d6b904ce4b3b6195a86863141c81d101b32a2bc0c8bab763fba18c67426'
            'SKIP'
            'b0701d886f4b52153346379b9d020d6f7eced98d7de1b1e0df6f051195547f02')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-404881.patch # Fix crash on logout
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
