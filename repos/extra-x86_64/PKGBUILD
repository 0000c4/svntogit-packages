# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=tracker-miners
pkgver=2.3.3
pkgrel=2
pkgdesc="Collection of data extractors for Tracker/Nepomuk"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(tracker gst-plugins-base-libs exempi flac libexif libcue libgrss libgsf libgxps libiptcdata
         libosinfo libvorbis poppler-glib totem-plparser giflib libgexiv2 gupnp-dlna upower
         libseccomp)
makedepends=(intltool git meson vala)
checkdepends=(python-gobject gst-plugins-good gst-plugins-base gst-libav)
groups=(gnome)
_commit=9a507396dd980a6023f7bc15b18aaeedbd5f2df8  # tags/2.3.3^0
source=("git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package() {
  depends+=(libtracker-{miner,sparql}-2.0.so)
  DESTDIR="$pkgdir" meson install -C build
}
