# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-volman
pkgver=0.6.0
pkgrel=3
pkgdesc="Automatic management of removeable devices in Thunar"
arch=('i686' 'x86_64')
url="http://foo-projects.org/~benny/projects/thunar-volman"
license=('GPL2')
groups=('xfce4-goodies')
depends=('thunar' 'libxfce4ui' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/0.6/$pkgname-$pkgver.tar.bz2)
sha256sums=('b1afd2b3b285cfcf07c11caa3d37fa5ff1fc06f7dc68fcca07f9a0d70daa694f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
