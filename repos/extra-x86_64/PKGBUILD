# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-photos
pkgver=3.8.2
pkgrel=1
pkgdesc="Access, organize, and share your photos on GNOME"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(exempi gtk3 libexif librsvg babl gegl gnome-desktop gnome-online-accounts lcms2 tracker hicolor-icon-theme)
makedepends=(intltool)
options=(!libtool)
groups=(gnome-extra)
install=gnome-photos.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('c6e777cdc6a4b0ee24b4dbd1b971fe239198926a3057c27c08eeda0a53ba6d58')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
