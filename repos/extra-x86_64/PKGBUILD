# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.28.4+5+ge02a9ae0
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 exempi libpeas librsvg dconf)
makedepends=(intltool itstool gobject-introspection gtk-doc gnome-common git meson)
optdepends=('eog-plugins: Additional features')
groups=(gnome)
options=(!emptydirs)
_commit=e02a9ae0c7120538a2711929ff4e16d41a7d67aa  # gnome-3-28
source=("git+https://gitlab.gnome.org/GNOME/eog.git#commit=$_commit"
        0001-EogScrollView-Set-scale-1-for-the-image-surface.patch)
sha256sums=('SKIP'
            '5d2c0dea7f1775fd54a0568984bcd72854b29a414d14201dadf15d4f92b42cb9')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../0001-EogScrollView-Set-scale-1-for-the-image-surface.patch
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
