# $Id$
# Maintainer:
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=cvsps
pkgver=2.2b1
pkgrel=1
pkgdesc="Generating 'patchset' information from a CVS repository"
url="http://www.cobite.com/cvsps/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('zlib')
source=("http://www.cobite.com/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        call-cvs-q-option.patch
        cvsps-2.2b1-bufferoverflow.patch
        cvsps-2.2b1-dynamic-logbuf.patch
        cvsps-2.2b1-man.patch
        seperate-CPPFLAGS.patch
        use-INADDR_NONE-define.patch
        use-__linux__-define.patch
        use-offsetof-macro.patch)
md5sums=('997580e8e283034995b9209076858c68'
         '06324c5e3c321126b76dc97fc6c00b34'
         '2910012a11a24d5e4192cf9eae7dd259'
         '970566460e72cb159426965d6e5763b3'
         'f9d2ee40e52ecd5096fa9a885418eee1'
         '6baafbd7512aa52e1b83ed0bead32a6e'
         'a3a81481c07dcc676f5a82ae07e8ee89'
         '9e2763315cbda02665c47827d38df459'
         '8f7e6f09f42f76deeefe24bbfa509150')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  for file in ${source[@]}; do
      if [[ $file == *.patch ]]; then
          patch -Np1 < ../$file
      fi
  done
  make prefix=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir/usr" install
}
