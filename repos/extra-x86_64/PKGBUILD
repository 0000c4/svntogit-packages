# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=3.4.1
pkgrel=2
pkgdesc="GNOME file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gvfs' 'desktop-file-utils' 'gnome-icon-theme' 'dconf' 'libtracker-sparql' 'libnotify')
makedepends=('intltool' 'gobject-introspection')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        samba-crash.patch)
sha256sums=('02581c9475e9c4250960555b248378a6a1c63a5c029728ec48347c78ca77392d'
            'e437ced7bbebe49d2454b4daecd10cff39d1c427e9631fe0fe5325072ed6aefd')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/samba-crash.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
