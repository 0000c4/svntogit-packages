# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
pkgver=0.1.24
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('i686' 'x86_64')
url="http://gegl.org/babl/"
license=('LGPL3')
depends=('glibc')
makedepends=('gobject-introspection' 'vala')
options=('!makeflags')
source=(https://ftp.gimp.org/pub/babl/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('472bf1acdde5bf076e6d86f3004eea4e9b007b1377ab305ebddec99994f29d0b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
