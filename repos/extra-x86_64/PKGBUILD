# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mihai Bi≈üog <mihai.bisog@gmail.com>
pkgname=fmt
pkgver=5.2.1
pkgrel=3
pkgdesc='Open-source formatting library for C++'
arch=(x86_64)
url='http://fmtlib.net'
license=(BSD)
makedepends=(
  cmake
  git
  ninja
)
provides=(libfmt.so)

source=(git+https://github.com/fmtlib/fmt.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd fmt

  # backport the pc file
  git cherry-pick -n 9d0c9c4bb145a286f725cd38c90331eee7addc7f
  git cherry-pick -n 0835f1ba3b538f4e7603976419d5be8b17394cb5
}

build() {
  cd build

  cmake ../fmt \
    -G Ninja \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DBUILD_SHARED_LIBS='1'
  cmake --build .
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm 644 fmt/LICENSE.rst -t "${pkgdir}"/usr/share/licenses/fmt
}

# vim: ts=2 sw=2 et:
