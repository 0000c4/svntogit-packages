# $Id$
# Maintainer: eric <eric@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=a2ps
pkgver=4.13c
pkgrel=1
pkgdesc="a2ps is an Any to PostScript filter"
arch=(i686 x86_64)
license=('GPL')
url="http://www.inf.enst.fr/~demaille/a2ps/"
depends=('ghostscript' 'psutils' 'imagemagick' 'perl')
options=('!libtool')
replaces=('a2ps-lprng')
provides=('a2ps-lprng')
source=(http://www.archlinux.org/~jgc/${pkgname}/${pkgname}-${pkgver}.tar.gz
        http://www.archlinux.org/~jgc/${pkgname}/${pkgname}-4.13c-ja_nls.patch.gz
	4.13c-gcc34.patch
	a2ps-4.13-fixcachecheck.patch
	a2ps-4.13-manpage-chmod.patch
	a2ps-4.13-select-freebsd.patch
	a2ps-4.13c-fixps.patch
	a2ps-4.13c-psmandup.diff
	a2ps-4.13c-psset.patch
	a2ps-4.13c-stdarg.patch)
md5sums=('97164e84ba911e4604155507e0893669' 'e315466f75d99c0c1ca563d82f2ace2e'\
         '449b2445dc0949a990a2b29cab89dc59' '6432b1da449b96b531b0843259839236'\
         '09cc5ed4d3d8ff1b2103e519191e8286' 'b62538dbd828d995bdc3ce36188b0768'\
         'df6bc895cc6fb0fd47f22fd1c36d1ed3' 'd68d0bfab547ef1ce70576c7a5570f13'\
         'b8ef4708f4a65e9b373f9f42df11b92d' '2e24d355a977969b83fce03510ebd7b8')

build() {
  cd ${startdir}/src/${pkgname}-4.13
  patch -Np0 -i ${startdir}/src/a2ps-4.13-select-freebsd.patch || return 1
  patch -Np1 -i ${startdir}/src/a2ps-4.13c-stdarg.patch || return 1
  patch -Np1 -i ${startdir}/src/4.13c-gcc34.patch || return 1
  patch -Np1 -i ${startdir}/src/a2ps-4.13c-ja_nls.patch || return 1
  patch -Np0 -i ${startdir}/src/a2ps-4.13c-fixps.patch || return 1
  patch -Np0 -i ${startdir}/src/a2ps-4.13c-psmandup.diff || return 1
  patch -Np1 -i ${startdir}/src/a2ps-4.13c-psset.patch || return 1
  patch -Np1 -i ${startdir}/src/a2ps-4.13-fixcachecheck.patch || return 1
  patch -Np1 -i ${startdir}/src/a2ps-4.13-manpage-chmod.patch || return 1
  libtoolize --force --copy || return 1
  autoreconf -I m4 || return 1
  ./configure --prefix=/usr --sysconfdir=/etc/a2ps \
              --includedir=/usr/include --enable-nls
  export LANG=C
  make || return 1
  make DESTDIR=${startdir}/pkg install
  sed -i "s:^countdictstack: \0:" ${startdir}/pkg/usr/bin/psset || return 1
}
