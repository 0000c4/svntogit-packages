# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

pkgname=quazip
pkgver=0.9.1
pkgrel=1
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package"
url="https://stachenov.github.io/quazip/"
license=(LGPL)
arch=(x86_64)
depends=(qt5-base)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::https://github.com/stachenov/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('5d36b745cb94da440432690050e6db45b99b477cfe9bc3b82fd1a9d36fff95f5')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
