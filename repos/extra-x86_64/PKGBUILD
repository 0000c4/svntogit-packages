# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: FJ <joostef@gmail.com>

pkgname=perl-template-toolkit
pkgver=3.006
pkgrel=1
pkgdesc="Perl template processing system"
arch=('x86_64')
url="https://search.cpan.org/dist/Template-Toolkit/"
license=('PerlArtistic')
depends=('perl-appconfig' 'perl')
checkdepends=('perl-cgi' 'perl-image-info' 'perl-test-leaktrace')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/A/AT/ATOOMIC/Template-Toolkit-$pkgver.tar.gz)
sha512sums=('09303482cff2111d009d4291b90d70302e5c74713a80e7d3363c99be608871b03d052d177e5aa45a0500397e7500532f479aa90c756fb7654bc6850f561c59c3')

build() {
  cd Template-Toolkit-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor TT_ACCEPT=y
  make
}

check() {
  cd Template-Toolkit-$pkgver
  make test
}

package() {
  cd Template-Toolkit-$pkgver
  make install DESTDIR="${pkgdir}"
}
