# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-user-share
pkgver=0.41
pkgrel=1
pkgdesc="Easy to use user-level file sharing for GNOME."
arch=('i686' 'x86_64')
url="http://ftp.gnome.org/pub/GNOME/sources/gnome-user-share/"
license=('GPL')
depends=('avahi' 'gconf>=2.24.0' 'hicolor-icon-theme' 'apache' 'libnotify')
makedepends=('intltool')
options=('!emptydirs')
install=gnome-user-share.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('103a8361c9efe6e20f1ae8bfb67dc175')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/gnome-user-share \
              --localstatedir=/var \
              --enable-avahi --disable-howl || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
