# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.2.4
pkgrel=1
pkgdesc="Advanced Linux Sound Architecture - Utilities"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('GPL2')
depends=('glibc' 'pciutils' 'psmisc')
makedepends=('alsa-lib' 'docbook-xsl' 'fftw' 'libsamplerate' 'ncurses'
'python-docutils' 'systemd' 'xmlto')
optdepends=('fftw: for alsabat')
# we require /var/lib/alsa for state file
options=(emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alsa-project/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('af1135d84b5cf3446ec2a67859f969b12d20d89733eed4947b81b03a793f6288ec9ccf0341fdd63e5584aa0e68de8d35dc741059b43318f5356bfe86bcaa77fc')
b2sums=('a1912128b21f2565283b7fd9450d9fa27036540a5235dbe8e2447df4a90d5e987e87aedab1a74f3222860097ec5bc55ae91c66a1a0dacbab891236db3b5313e6')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --disable-alsaconf \
              --sbindir=/usr/bin \
              --with-udev-rules-dir=/usr/lib/udev/rules.d \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  depends+=('libasound.so' 'libatopology.so' 'libformw.so' 'libmenuw.so'
  'libncursesw.so' 'libpanelw.so' 'libsamplerate.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # dir where to save ALSA state
  install -d "${pkgdir}/var/lib/alsa"
}
