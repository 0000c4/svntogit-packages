# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xkb-utils
pkgver=7.4
pkgrel=1
pkgdesc="X.org keyboard utilities"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxaw>=1.0.5' 'libxkbfile')
makedepends=(pkgconfig)
source=(${url}/releases/individual/app/xkbutils-1.0.1.tar.bz2
        ${url}/releases/individual/app/xkbcomp-1.0.5.tar.bz2
        ${url}/releases/individual/app/xkbevd-1.0.2.tar.bz2
        ${url}/releases/individual/app/setxkbmap-1.0.4.tar.bz2)
md5sums=('84396a3dd75337caaae29d8fa5616fb1'
         '6cc96c3e4ed5d9802fe717beac008f19'
         '68f2a143716c23b566f8509d9498f516'
         '6fab3bb176be9e510c5613d054ef1ca4')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      popd
    fi
  done
}
