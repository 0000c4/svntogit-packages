# $Id$
# Maintainer:

pkgname=freetds
pkgver=1.00.89
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='http://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('openssl' 'unixodbc')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/$pkgname-$pkgver.tar.bz2")
sha512sums=('f7dc34859081f887eafa199dcccc5fff2ba68b79d9aa54bed05cd213e05c1ef32b585bf5f5c9749c3488d42b8dce70d38c00aa501d4db1843f8e952678fee276')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/freetds --enable-msdblib \
    --with-tdsver=7.0 --with-unixodbc=/usr --with-openssl
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
