# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdazzle
pkgver=3.28.4
pkgrel=1
pkgdesc="A library to delight your users with fancy features"
url="https://gitlab.gnome.org/GNOME/libdazzle"
arch=(x86_64)
license=(LGPL)
depends=(gtk3)
makedepends=(git meson vala gobject-introspection gtk-doc)
checkdepends=(xorg-server-xvfb)
_commit=7e0b543bc45439d59872e6a5a222b6b946c9cf83  # tags/3.28.4^0
source=("git+https://gitlab.gnome.org/GNOME/libdazzle.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build -D enable_gtk_doc=true
  ninja -C build
}

check() {
  xvfb-run meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
