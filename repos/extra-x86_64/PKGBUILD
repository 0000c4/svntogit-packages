# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgbase=nasm
pkgname=('nasm' 'nasm-doc')
pkgver=2.09.04
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.nasm.us"
license=('BSD')
makedepends=('ghostscript')
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgbase}-${pkgver}.tar.bz2)
md5sums=('c11f083a501adae843d0bc3e7c106c73')
sha1sums=('87fb3f1d277f57cfd86408faa28685df0c887f59')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make everything
}

package_nasm() {
  pkgdesc="An 80x86 assembler designed for portability and modularity"
  depends=('glibc')
  install=nasm.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -d "${pkgdir}/usr/share/info"
  install -m644 doc/info/* "${pkgdir}/usr/share/info/"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/nasm/LICENSE"
}

package_nasm-doc() {
  pkgdesc="An 80x86 assembler designed for portability and modularity (manuals)"

  cd "${srcdir}/${pkgbase}-${pkgver}/doc"
  install -d "${pkgdir}/usr/share/doc/nasm/html"
  install -m644 html/* "${pkgdir}/usr/share/doc/nasm/html/"
  install -m644  nasmdoc.ps nasmdoc.pdf nasmdoc.txt  "${pkgdir}/usr/share/doc/nasm/"
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/nasm-doc/LICENSE"
}
