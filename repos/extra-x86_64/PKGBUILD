# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kigo
pkgver=20.04.0
pkgrel=2
pkgdesc="An open-source implementation of the popular Go game"
url="https://kde.org/applications/games/kigo/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames gnugo hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-420814a.patch::"https://cgit.kde.org/kigo.git/patch/?id=2ce84e64"
         kdebug-420814b.patch::"https://cgit.kde.org/kigo.git/patch/?id=d2eb6713")
sha256sums=('64162669455280470571099790c12c519f3439072ea5b47d5b60e8345cd21920'
            'SKIP'
            '0205a298e648a7fb1bad103c668ac0395f3938c69055108aff2c15d1e3cc0531'
            '77be70b4f3ff988488e2a18344f2c3b67ba8ecd2e461a002431650283729047a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-420814a.patch # Fix gnugo command invocation
  patch -p1 -i ../kdebug-420814b.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
