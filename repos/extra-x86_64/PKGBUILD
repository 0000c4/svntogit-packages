# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Martin Sandsmark <martin.sandsmark@kde.org>

pkgname=vlc
pkgver=2.0.0
pkgrel=6
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libxpm' 'libcdio'
         'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa' 'aalib'
         'libxinerama' 'libtar' 'libtiger' 'lua' 'v4l-utils' 'smbclient' 
         'libmatroska' 'zvbi' 'taglib' 'libmpcdec' 'ffmpeg' 'faad2' 
         'libupnp' 'libshout' 'libmad' 'libmpeg2' 'libmodplug' 'libass'
         'xcb-util-keysyms' 'ttf-freefont')
makedepends=('avahi' 'pkg-config' 'live-media' 'libnotify' 'libbluray'
             'flac' 'libtheora' 'alsa-lib' 'jack' 'kdelibs' 'libgcrypt'
             'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'libpulse'
             'lirc-utils' 'gnutls' 'libcaca' 'librsvg' 'portaudio' 'oss' 
             'ncurses' 'libgme' 'schroedinger' 'udev' 'projectm' 'twolame' 
             'libdvdcss' 'gnome-vfs' 'libgoom2' 'vcdimager' 'xosd')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'kdelibs: KDE Solid hardware integration'
            'libpulse: PulseAudio support'
            'vdpau-video: vdpau back-end for nvidia'
            'libva-driver-intel: back-end for intel cards'
            'libbluray: for Blu-Ray support' 
            'oss: for OSS audio support'
            'portaudio: for portaudio support'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'projectm: for ProjectM visualisation plugin'
            'udev: for service discovery using udev'
            'libcaca: for colored ASCII art video output'
            'libgme: for libgme plugin'
            'librsvg: for SVG plugin'
            'libgcrypt: support for some plugins'
            'gnome-vfs: for GNOME Virtual File System support'
            'libgoom2: for libgoom plugin'
            'vcdimager: navigate VCD with libvcdinfo'
            'xosd: for xosd support')
conflicts=('vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool')
install=vlc.install
source=("http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'access_smb_link_to_smbclient.patch')
md5sums=('8806bff2ea9c76791123d444a92f708c'
         '5f510fdea99cdb7941423eb40045968a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  patch -Np1 -i "${srcdir}/access_smb_link_to_smbclient.patch"
  autoreconf -fi

  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --enable-real \
              --enable-realrtsp \
              --enable-xosd \
              --enable-aa \
              --enable-vcdx
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
    install -D -m644 "${srcdir}/vlc-${pkgver}/share/icons/${res}x${res}/vlc.png" \
        "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}
