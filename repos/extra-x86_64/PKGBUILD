# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=3.34.1
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(geocode-glib geoclue2 gjs libchamplain libgee folks gfbgraph)
makedepends=(gobject-introspection git meson yelp-tools)
groups=(gnome)
_commit=6d4dcce79a551cc2eed62dcb3ea48950b7d34456  # tags/v3.34.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
