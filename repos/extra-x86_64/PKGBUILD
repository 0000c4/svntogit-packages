# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=garcon
pkgver=0.1.11
pkgrel=1
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://wiki.xfce.org/dev/garcon"
license=('LGPL')
groups=('xfce4')
depends=('glib2' 'libxfce4util')
makedepends=('pkgconfig' 'intltool' 'xfce4-dev-tools')
replaces=('libxfce4menu')
options=('!libtool' '!makeflags')
source=(http://archive.xfce.org/src/xfce/garcon/0.1/garcon-$pkgver.tar.bz2)
sha256sums=('272c1207a3449ce969a86ebdc61c6afcad05003f0a986e222bb90c01c1692dfc')

build() {
  cd "$srcdir/garcon-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/garcon-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
