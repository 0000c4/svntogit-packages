# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=(harfbuzz)
pkgname=(harfbuzz harfbuzz-icu)
pkgver=1.0.5
pkgrel=2
pkgdesc="OpenType text shaping engine"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=(MIT)
makedepends=(glib2 freetype2 graphite cairo icu gobject-introspection)
source=(http://www.freedesktop.org/software/harfbuzz/release/${pkgbase}-${pkgver}.tar.bz2
        hmtx.patch crash.patch)
sha256sums=('4d99d2c6a58ecb55538ccab522e68836383852f9f41f696d2b93cd8f85195440'
            '703249e3ecfcbf2d449e9cf1eacbfd5627cb8f8943af27637a612a36b05a1d10'
            '0016e865744feaac97130aa54f755cbc6ebecd75d4d21bc823eb7454d1c16972')

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python

  cd $pkgbase-$pkgver
  patch -Np1 -i ../hmtx.patch
  patch -Np1 -i ../crash.patch
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr \
    --with-glib --with-freetype --with-cairo --with-icu --with-graphite2 \
    --with-gobject
  make
}

check() {
  cd $pkgbase-$pkgver
  PATH="$srcdir/path:$PATH" make -k check
}

package_harfbuzz() {
  depends=(glib2 freetype2 graphite)
  optdepends=('cairo: hb-view program')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/harfbuzz/COPYING"

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/{include/harfbuzz,lib/pkgconfig}; cd ../hb-icu
  mv "$pkgdir"/usr/lib/libharfbuzz-icu* ./usr/lib
  mv "$pkgdir"/usr/lib/pkgconfig/harfbuzz-icu.pc ./usr/lib/pkgconfig
  mv "$pkgdir"/usr/include/harfbuzz/hb-icu.h ./usr/include/harfbuzz
}

package_harfbuzz-icu(){
  pkgdesc="$pkgdesc (ICU integration)"
  depends=(harfbuzz icu)

  mv hb-icu/* "$pkgdir"

  install -Dm644 $pkgbase-$pkgver/COPYING "$pkgdir/usr/share/licenses/harfbuzz-icu/COPYING"
}
