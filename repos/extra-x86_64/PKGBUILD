# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=soundtouch
pkgver=2.1.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
source=(https://gitlab.com/soundtouch/soundtouch/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('454712b08bf758f6a4c0df8821350c323e5b274443a20e21d193d17c5dc3d0d5')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
}

build() {
  cd $pkgname-$pkgver
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" pkgdocdir=/usr/share/doc/soundtouch install
}
