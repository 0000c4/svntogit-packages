# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.9.14
pkgrel=3
pkgdesc="Service logging user activities and events"
arch=(i686 x86_64)
url="http://zeitgeist-project.com/"
license=(GPL2 LGPL2.1)
depends=(json-glib telepathy-glib gtk3)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib)
provides=(zeitgeist-datahub)
conflicts=(zeitgeist-datahub)
replaces=(zeitgeist-datahub)
source=("https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz"
         gio-dependency.patch
         vala-0.24-fix.patch)
sha256sums=('769b5abfe67ef632414efa1d35a1462c5cd86c8792bb4635b4c3d93a5f15f061'
            '4f2403b0695a6bf7b7cc10833e0e3a07d14fa22afeccc4ec89af0e89bbb35291'
            '1a408fe26724b1d9e52bde6ea213e1b529d9f345f865d2c350e339b845841206')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/python -/$PYTHON -/' configure configure.ac
  patch -Np1 -i ../gio-dependency.patch
  patch -Np0 -i ../vala-0.24-fix.patch
}

build() {
  cd $pkgname-$pkgver
  export PYTHON=/usr/bin/python2
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname
  make
}

check() {
  cd $pkgname-$pkgver
  # Log test fails in chroot, as we have no X and no proper dbus
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
