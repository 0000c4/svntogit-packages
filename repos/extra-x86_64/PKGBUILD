# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=3.32.0
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://wiki.gnome.org/Apps/Swell%20Foop"
arch=(x86_64)
license=(GPL)
depends=(clutter-gtk librsvg)
makedepends=(gobject-introspection yelp-tools appstream-glib vala git meson)
groups=(gnome-extra)
_commit=5b7aa1d6d50e4c4c777dd44468b8a368917b2ce5  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/swell-foop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
