# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=tracker-miners
pkgver=2.2.1
pkgrel=1
pkgdesc="Collection of data extractors for Tracker/Nepomuk"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(tracker gst-plugins-base-libs exempi flac libexif libcue libgrss libgsf libgxps libiptcdata
         libosinfo libvorbis poppler-glib taglib totem-plparser giflib libgexiv2 gupnp-dlna upower
         libseccomp)
makedepends=(intltool git meson vala)
checkdepends=(python2-gobject)
groups=(gnome)
_commit=67958708286b39ef4b3f80c80a3950b2752ea2a3  # tags/2.2.1^0
source=("git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i '1s|python$|&2|' tests/functional-tests/*.py
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
