# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=genius
pkgver=1.0.24
pkgrel=1
pkgdesc="Advanced calculator including a mathematical programming language"
arch=('x86_64')
license=('GPL')
url="http://www.jirka.org/genius.html"
depends=('mpfr' 'glib2' 'hicolor-icon-theme' 'gtk2' 'gtksourceview2')
makedepends=('intltool' 'gnome-doc-utils' 'gtk-update-icon-cache' 'python')
options=('!makeflags')
source=(https://download.gnome.org/sources/genius/1.0/${pkgname}-${pkgver}.tar.xz)
sha256sums=('772f95f6ae4716d39bb180cd50e8b6b9b074107bee0cd083b825e1e6e55916b6')

build() {
  cd $pkgname-$pkgver
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/genius \
      --localstatedir=/var --disable-static \
      --disable-update-mimedb
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
