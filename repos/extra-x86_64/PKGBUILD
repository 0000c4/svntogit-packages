# $Id: PKGBUILD 168943 2012-10-16 16:31:44Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-nibbles
pkgver=3.10.0
pkgrel=1
pkgdesc="Guide a worm around a maze"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'clutter-gtk' 'libcanberra' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs' '!libtool')
install=gnome-nibbles.install
url="https://live.gnome.org/Nibbles"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('6bf774cbd86e617c49f1740e79d52a7d26bf23656a673db1efb20bcc893e614a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile \
      --with-scores-user=root --with-scores-group=games
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Remove all scores, we generate them from postinstall
  rm -rf "$pkgdir/var"
}
