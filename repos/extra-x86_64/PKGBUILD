# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcl
pkgver=8.6.0
pkgrel=2
pkgdesc="The Tcl scripting language"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
# tcl E: Dependency zlib detected and not included (libraries ['usr/lib/libz.so.1'] needed in files ['usr/lib/libtcl8.6.so'])
# depends=('zlib')  ???
md5sums=('573aa5fe678e9185ef2b3c56b24658d3')

build() {
  cd ${srcdir}/tcl${pkgver}/unix
  
  # maybe drop sqlite build at all? isn't this the same we already have in sqlite and sqlite-tcl pkg?
  # rm -rf ${srcdir}/tcl${pkgver}/pkgs/sqlite3*
  
  [[ $CARCH == "x86_64" ]] && BIT="--enable-64bit"
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads $BIT
  
  make
}

check() {
  cd ${srcdir}/tcl${pkgver}/unix
  make test
}

package()
{
  cd ${srcdir}/tcl${pkgver}/unix
  
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf tclsh8.6 ${pkgdir}/usr/bin/tclsh
  
  # install license
  install -Dm644 ../license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces
  sed -i "s#${srcdir}#/usr/src#" ${pkgdir}/usr/lib/{tcl,tdbc1.0.0/tdbc,itcl4.0.0/itcl}Config.sh
  
  # fix a fileconflitct - FS#33201
  rm ${pkgdir}/usr/share/man/mann/sqlite3.n
}
