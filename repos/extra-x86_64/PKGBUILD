# $Id$
# Maintainer: tobias <tobias@archlinux.org>

pkgname=gimp
pkgver=2.6.7
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL')
depends=('gtk2>=2.14.4' 'lcms>=1.17' 'libxpm>=3.5.7' 'libwmf>=0.2.8.4'
         'libxmu>=1.0.4' 'librsvg>=2.22.3' 'libmng>=1.0.10' 'dbus-glib>=0.76'
         'libexif>=0.6.16' 'pygtk>=2.13.0' 'desktop-file-utils' 'gegl>=0.1.0'
         'curl')
makedepends=('gutenprint>=5.0.2' 'intltool>=0.35.5' 'libwebkit'
             'gnome-python>=2.16.2' 'poppler-glib>=0.10.0' 'pkgconfig>=0.21'
             'alsa-lib>=1.0.16' 'libgnomeui>=2.22.1')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'libwebkit: for the help browser'
            'poppler-glib: for pdf support'
            'hal: for Linux input event controller module'
            'alsa-lib: for MIDI event controller module')
options=('!libtool' '!makeflags')
conflicts=('gimp-devel')
install=gimp.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        linux.gpl)
md5sums=('77ed86a8de9b48587efb24f2b115ad38' 'bb27bc214261d36484093e857f015f38')
sha1sums=('0661d162fecfd907e183ce42853dd5b9d3cb4c51' '110ce9798173b19a662d086ed7b882b4729f06cf')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   ./configure --prefix=/usr --sysconfdir=/etc \
     --enable-mp --enable-gimp-console --enable-gimp-remote \
     --disable-devel-docs --enable-python --without-gvfs \
     --without-gnomevfs --with-gif-compression=lzw --without-aa || return 1
   make || return 1
   make DESTDIR="${pkgdir}" install-strip || return 1
   install -Dm644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl" || return 1

   ln -sf gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1" || return 1
   ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool" || return 1
   ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1" || return 1
}
