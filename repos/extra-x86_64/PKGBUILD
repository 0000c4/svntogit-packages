# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=breeze-gtk
pkgver=5.6.0
pkgrel=2
pkgdesc="Breeze widget theme for GTK 2 and 3"
arch=(i686 x86_64)
url='https://quickgit.kde.org/?p=breeze-gtk.git'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"
	breeze-dark-gtk.patch::"https://quickgit.kde.org/?p=breeze-gtk.git&a=commitdiff&h=1a547a83&o=plain")
md5sums=('e46e9d3b90cf19b20afb4ec6789131e0'
         '260c22eae711464771212cc133c1b2d2')

prepare(){
  mkdir -p build

  cd $pkgname-$pkgver
# Fix installation of breeze-dark theme
  patch -p1 -i ../breeze-dark-gtk.patch
}

build(){
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
