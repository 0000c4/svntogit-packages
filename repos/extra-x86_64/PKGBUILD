# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=uim
pkgver=1.7.2
pkgrel=1
pkgdesc='Multilingual input method library'
arch=('i686' 'x86_64')
url='http://code.google.com/p/uim/'
license=('custom:BSD')
depends=('libxft' 'libedit' 'anthy' 'm17n-lib')
makedepends=('intltool' 'gettext' 'qt' 'gtk2' 'gtk3' 'gnome-panel')
optdepends=('qt: immodule and helper applications'
            'gtk2: immodule and helper applications'
            'gtk3: immodules and helper applications'
            'gnome-panel: gnome applet indicator')
options=('!libtool')
source=("http://uim.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('4982178aca6ee1ae775fc2a9b4bbb7a66e97cbd4')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/uim \
		--with-anthy-utf8 \
		--with-qt4 \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
