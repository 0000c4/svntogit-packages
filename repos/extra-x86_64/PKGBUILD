# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=nvidia-lts
pkgver=440.48.02
pkgrel=1
epoch=1
pkgdesc="NVIDIA drivers for linux-lts"
arch=('x86_64')
url="https://www.nvidia.com/"
makedepends=("nvidia-utils=${pkgver}" 'libglvnd' 'linux-lts-headers')
provides=('nvidia')
license=('custom')
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=(#"https://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        $_pkg.run::https://developer.nvidia.com/vulkan-beta-4404802-linux)
sha512sums=('735c6a7b7df5f280286ca4622922ca427f4c99c5f121450364cdeae83133c0b4335c4290f5ad202f2f34263462308d4c43142cd9fb9bdfa58c45bf7562667b88')

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
}

build() {
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/src/linux-lts module
}

package() {
    depends=('linux-lts' "nvidia-utils=${pkgver}" 'libglvnd')

    _extradir="/usr/lib/modules/$(</usr/src/linux-lts/version)/extramodules"
    install -Dt "${pkgdir}${_extradir}" -m644 \
      "${srcdir}/${_pkg}/kernel"/nvidia{,-modeset,-drm,-uvm}.ko

    find "${pkgdir}" -name '*.ko' -exec gzip -n {} +

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}
