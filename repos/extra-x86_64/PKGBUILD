# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=accountsservice
pkgver=0.6.10
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('dbus-glib' 'polkit')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
source=(http://cgit.freedesktop.org/accountsservice/snapshot/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e43d0ea4c0cb9291e60f800d9c2d0ba4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/lib/systemd/system \
    --localstatedir=/var --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
