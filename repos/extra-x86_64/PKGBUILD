# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libtasn1
pkgver=3.1
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('glibc' 'texinfo')
options=('!libtool')
install=libtasn1.install
source=(http://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig}
        buffer_overflow_fix.diff)
md5sums=('a3c713c41a9a73851235dc37051be46f'
         '43500f54858ffe021e5fd20896b165ad'
         '4c9e248a4198d9710bf2f822a7c447a5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # corrected possible buffer overflow in parser errors. 
  # http://git.savannah.gnu.org/gitweb/?p=libtasn1.git;a=commit;h=aa6e97f68d6c8d06f531d9b6bb462dc1c0720f77
  patch -Np1 -i ${srcdir}/buffer_overflow_fix.diff
  
  ./configure --prefix=/usr \
      --with-packager=Archlinux \
      --with-packager-bug-reports="http://bugs.archlinux.org/" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
