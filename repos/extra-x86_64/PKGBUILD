# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gupnp
pkgver=0.18.0
pkgrel=1
pkgdesc="An object-oriented UPNP framework"
arch=('i686' 'x86_64')
url="http://gupnp.org/"
license=('LGPL')
depends=('gssdp')
makedepends=('gobject-introspection' 'gtk-doc')
optdepends=('python2: gupnp-binding-tool')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('82f4758b26ef0531cf6301f526fe705f06c329eaef403bb30925128fcbb6a71a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '1s|^#!.*python$|&2|' tools/gupnp-binding-tool

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-context-manager=linux
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
