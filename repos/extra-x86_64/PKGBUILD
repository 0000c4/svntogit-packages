# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-twm
pkgver=1.0.6
pkgrel=1
pkgdesc="Tab Window Manager for the X Window System"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
backup=(etc/X11/twm/system.twmrc)
license=('custom')
depends=('libxmu')
makedepends=('pkgconfig')
groups=('xorg')
source=(${url}/releases/individual/app/twm-${pkgver}.tar.bz2)
md5sums=('952d06a0c2ec34687b536c7b619fc671')

build() {
  cd "${srcdir}/twm-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/twm-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/twm"
  install -m644 src/system.twmrc "${pkgdir}/etc/X11/twm/"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
