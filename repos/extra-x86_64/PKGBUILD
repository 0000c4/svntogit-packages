# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.10.1
pkgrel=2
pkgdesc="Extensions to Xfce by os-cillation"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui' 'gtk2' 'hicolor-icon-theme' )
makedepends=('perl-uri' 'intltool' 'gtk-doc' 'xorg-iceauth' 'xfce4-dev-tools')
optdepends=('perl-uri: for mail-compose helper script')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/0.10/$pkgname-$pkgver.tar.bz2
        drop-HAVE_GIO_UNIX-ifdef-and-load-gio-unix-2.0-pkg.patch)
sha256sums=('34a6d14c58a12d3372eb810d05ae425b8c8c32ec976b99b6ad274c3f7b3f6896'
            '9983b3206391f0e86816d70ded4fc5ddd48ac27dd29b266d4ba4a930ca663626')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix launching of .desktop files (https://bugs.archlinux.org/task/33125)
  patch -Np1 -i "$srcdir/drop-HAVE_GIO_UNIX-ifdef-and-load-gio-unix-2.0-pkg.patch"
  NOCONFIGURE=1 xdt-autogen

  # Python 2 fix
  export PYTHON=python2

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var  \
    --disable-static \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
