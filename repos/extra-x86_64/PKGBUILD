# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.20.0
pkgrel=2
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
         json-glib libgl libsm libxtst systemd)
makedepends=(intltool mesa gtk-doc xtrans)
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=(gnome)
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        0001-main-fix-starting-gnome-session-via-startx.patch
        timeout.patch)
sha256sums=('66ff72379a2e7ee11ab7fcec37ad911d36f12471845dc7755e1ce55d29301b34'
            '5b23b7718531d610bb94611599bd9771e1cd7ca1353dcb9868ddead76aa37c96'
            '9eaf31857b41db417475c3b14adc11b10c8226ed76978cdf96dd648fa6e505fc')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i ../0001-main-fix-starting-gnome-session-via-startx.patch

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch
}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-session \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
