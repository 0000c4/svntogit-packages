# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=swt
pkgver=3.7.2
pkgrel=1
_date=201202080800
pkgdesc="An open source widget toolkit for Java"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/swt/"
license=('EPL')
depends=('java-runtime>=6' 'gtk2>=2.20.1' 'libxtst')
optdepends=('libgnomeui' 'mesa' 'libwebkit')
makedepends=('java-environment' 'libxtst' 'mesa' 'libgnomeui' 'unzip' 'pkgconfig' 'libwebkit' 'apache-ant')
if [ "${CARCH}" = "i686" ]; then
  _carch=x86
  md5sums=('9a858b11e66de8af41786eaf09fc9ee2'
           'f5e548bc26a0f1f3c18131be76face40')
fi
if [ "${CARCH}" = "x86_64" ]; then
  _carch=x86_64
  md5sums=('b16ab5c9d274b9488432ebeae6e4af5a'
           'f5e548bc26a0f1f3c18131be76face40')
fi
source=(http://download.eclipse.org/eclipse/downloads/drops/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-${_carch}.zip
        build-swt.xml)
noextract=(swt-${pkgver}-gtk-linux-${_carch}.zip)
# To test this pkg:
# http://www.eclipse.org/swt/examples.php#standaloneOutsideEclipse
# http://download.eclipse.org/eclipse/downloads/drops/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-${_carch}.zip

build() {
  cd ${srcdir}
  unzip -oq swt-${pkgver}-gtk-linux-${_carch}.zip src.zip
  unzip -oq src.zip -d src

  . /etc/profile.d/jdk.sh
  . /etc/profile.d/apache-ant.sh

  # Shared objects
  cd src
  ./build.sh

  # SWT jar
  ant -f ../build-swt.xml compile
}

package() {
  cd ${srcdir}/src

  # Shared objects
  export OUTPUT_DIR=${pkgdir}/usr/lib
  install -dm755 ${OUTPUT_DIR} 
  make -f make_linux.mak install

  # SWT jar
  ant -f ../build-swt.xml jar
  install -Dm755 ../swt.jar ${pkgdir}/usr/share/java/swt-${pkgver}.jar
  ln -s swt-${pkgver}.jar ${pkgdir}/usr/share/java/swt.jar
}
