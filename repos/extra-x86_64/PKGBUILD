# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmygpo-qt
pkgver=1.1.0
pkgrel=1
pkgdesc='A C++/Qt client library for gpodder.net'
arch=('x86_64')
url="http://wiki.gpodder.org/wiki/Libmygpo-qt"
license=('LGPL2.1')
depends=('qjson')
makedepends=('cmake' 'doxygen')
source=("http://stefan.derkits.at/files/libmygpo-qt/libmygpo-qt.${pkgver}.tar.gz")
sha256sums=('82765a9baa7b6bb3a3277759cec9c3779eee691db9180affe35a67813346e1cd')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../libmygpo-qt.${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package(){
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
