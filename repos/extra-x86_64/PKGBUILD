# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=avogadrolibs
pkgver=0.9.0
pkgrel=2
pkgdesc="An advanced molecular editor"
arch=(i686 x86_64)
url="http://www.openchemistry.org/"
license=(custom)
depends=(openbabel qt5-base glew)
makedepends=(cmake eigen python2 boost)
source=("http://downloads.sourceforge.net/project/avogadro/avogadro2/$pkgver/$pkgname-$pkgver.tar.gz"
        avogadrolibs-gcc6.patch::"https://github.com/OpenChemistry/molequeue/commit/5d57279d.patch")
md5sums=('af5c0a0c782cfa528d1c203840c69cf6'
         'd57372453ba647ea741b0efb7e50dc50')

prepare() {
  mkdir -p build

# Fix GCC6 detection
  cd $pkgname-$pkgver
  patch -p1 -i ../avogadrolibs-gcc6.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DUSE_QT=ON \
    -DUSE_OPENGL=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
