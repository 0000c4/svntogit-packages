# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.4.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
groups=('gnome-extra')
depends=('gtk3' 'udisks2' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!libtool' '!emptydirs')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('1d5b7ba73ee2b5373ea8a087902ab05339723c72eab278670856375280e02f03')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
