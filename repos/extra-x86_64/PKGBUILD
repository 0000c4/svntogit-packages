# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=3.2
pkgrel=1
pkgdesc="A BitTorrent program for KDE"
arch=('i686' 'x86_64')
url="http://ktorrent.org"
license=('GPL2')
depends=('kdelibs>4' 'gmp' 'qca' 'libxft' 'kdebase-runtime')
# 'kdebase-runtime' is needed for some of the icons in the interface to show
makedepends=('autoconf' 'perl' 'automoc4' 'cmake' 'kdepimlibs')
source=(http://ktorrent.org/downloads/$pkgver/${pkgname}-${pkgver}.tar.bz2 \
	httpserver.patch)
install=$pkgname.install
options=('libtool')
md5sums=('e215de7a711b3f9c33044bf97a6d8bfb'
         '1f25b94615b0bbfa0457c99fb20de3e6')

build() {
	cd ${srcdir}/$pkgname-$pkgver

	# build time patch
	patch -Np0 -i $srcdir/httpserver.patch || return 1

	cmake .	-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr || return 1
	make || return 1
	make DESTDIR=${pkgdir} install || return 1
}
