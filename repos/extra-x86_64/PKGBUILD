# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=1.2.3
_pkgver=4.2.3
pkgrel=1
pkgdesc="PHP language and documentation plugin for KDevelop/Quanta"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevplatform' 'kdevelop-pg-qt')
makedepends=('cmake' 'automoc4')
replaces=('kdevelop-php-docs')
conflicts=('kdevelop-php-docs')
options=('!makeflags')
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        "http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-docs-${pkgver}.tar.bz2")
sha1sums=('b0dc43cf01d2005f2ad66872ce6b49b093a7ceb4'
          'e399465638457fe015e920227e3aca5a53716804')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make

  cd "${srcdir}"
  mkdir build-docs
  cd build-docs
  # kdevelop-php-docs
  cmake ../${pkgname}-docs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build-docs
  make DESTDIR="${pkgdir}" install
}
