# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.24.2+1+gf30b47f
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++' git mm-common perl)
license=('LGPL')
options=('!emptydirs' '!makeflags')
url="http://gtkmm.sourceforge.net/"
_commit=f30b47ffcecdac60b3c890da08fbf485f891f5c9  # master
source=("git://git.gnome.org/atkmm#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/$pkgbase"
  ./configure --prefix=/usr --disable-silent-rules --enable-maintainer-mode --enable-gtk-doc
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++')

  cd "${srcdir}/$pkgbase"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/$pkgbase"
  make -C doc DESTDIR="${pkgdir}" install
}
