# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-user-share
pkgver=3.32.0.1
pkgrel=1
pkgdesc="Easy to use user-level file sharing for GNOME"
arch=(x86_64)
url="https://git.gnome.org/browse/gnome-user-share"
license=(GPL)
depends=(mod_dnssd dconf gtk3 libnotify libcanberra)
makedepends=(libnautilus-extension itstool docbook-xsl gnome-common git)
options=(!emptydirs)
groups=(gnome)
_commit=10db229931064c8355cbbf12f3e90a4c6d8933eb  # tags/GNOME_USER_SHARE_3_32_0_1^0
source=("git+https://git.gnome.org/browse/gnome-user-share#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_USER_SHARE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --with-httpd=/usr/bin/httpd \
      --with-modules-path=/usr/lib/httpd/modules
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
