# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=libass
pkgver=0.15.0
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
arch=(x86_64)
url=https://github.com/libass/libass/
license=(BSD)
depends=(
  fontconfig
  fribidi
  glib2
  glibc
  harfbuzz
  libfreetype.so
)
makedepends=(
  git
  nasm
)
provides=(libass.so)
_tag=d149636f502f5774ae1a8fb4c554b122674393b2
source=(git+https://github.com/libass/libass.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd libass

  git describe --tags
}

prepare() {
  cd libass

  ./autogen.sh
}

build() {
  cd libass

  ./configure \
    --prefix=/usr \
    --enable-harfbuzz \
    --enable-fontconfig
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libass install
  install -Dm 644 libass/COPYING -t "${pkgdir}"/usr/share/licenses/libass/
}

# vim: ts=2 sw=2 et:
