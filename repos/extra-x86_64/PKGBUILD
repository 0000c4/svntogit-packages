# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-taquin
pkgver=3.32.0
pkgrel=1
pkgdesc="Move tiles so that they reach their places"
url="https://wiki.gnome.org/Apps/Taquin"
arch=(x86_64)
license=(GPL3)
depends=(gtk3 librsvg libcanberra)
makedepends=(meson vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=c6329a84d77eff06962a10a03fdf58c591550175  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-taquin.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
