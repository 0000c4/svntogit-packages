# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xman
pkgver=1.1.0
pkgrel=1
pkgdesc="Unix manual page viewer"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxt')
makedepends=('xorg-util-macros' 'xproto' 'libxmu')
source=(http://xorg.freedesktop.org/archive/individual/app/xman-${pkgver}.tar.bz2
        git-fixes.patch)
sha1sums=('645d8910f0febd0c4f00fee964221883a12d4af3'
          '20b72da444fb34c95e010c3c2cd9b990a3f8fd98')

build() {
  cd "${srcdir}/xman-${pkgver}"
  patch -Np1 -i "${srcdir}/git-fixes.patch"
  autoreconf
  ./configure --prefix=/usr
  make
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
