# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.26.3+2+g966ec1c5
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas evolution gnome-control-center)
makedepends=(python appstream-glib git meson gtk-doc)
groups=(gnome)
_commit=966ec1c554ff4c7e5385ab9389f3ca8cf5a8faa4  # gnome-3-26
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#commit=$_commit"
        null-icaltime.diff)
sha256sums=('SKIP'
            'd24ae7e985ab9d568685371473006f58d169744c94a2cc89835b6f39f2001898')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../null-icaltime.diff
}

build() {
  arch-meson $pkgname build -Denable-gtk-doc=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
