# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.11.4
pkgrel=2
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(i686 x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(libyaml libxml2 libstemmer glib2)
makedepends=(meson xmlto gobject-introspection python2 gtk-doc qt5-base itstool vala)
source=("https://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz"{,.asc}
        update-appstream-index.hook
        https://github.com/ximion/appstream/commit/5db6df1a.patch)
sha256sums=('d1458c3266e7a71ac9a80c2412bc3535fcb8e35aac2c7e493ba61c5609e17b09'
            'SKIP'
            'a08b972b31388efe7edfa6db8dbd6909914bb613dd39b608b1764ed040907f82'
            '8204883e4902e99a8c336f9dd19375f8eaf5a522fda68d6c9e1fe133844ec992')
validpgpkeys=(D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB) # Matthias Klumpp <matthias@tenstral.net>

prepare() {
  mkdir -p build

  cd AppStream-$pkgver
  patch -p1 -i ../5db6df1a.patch # don't check bitness when finding appstream-qt
}

build() {
  cd build

  meson ../AppStream-$pkgver \
    --prefix=/usr \
    -Dqt=true \
    -Dvapi=true
  ninja
}

package_appstream() {
  cd build

  DESTDIR="$pkgdir" ninja install

# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppStreamQt,lib/cmake,lib/libAppStreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-index.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-index.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build
  DESTDIR="$pkgdir" ninja install

# provided by appstream
  rm -r "$pkgdir"{/etc,/usr/{bin,include/appstream,lib/{girepository-1.0,libappstream*,pkgconfig},share}}
}
