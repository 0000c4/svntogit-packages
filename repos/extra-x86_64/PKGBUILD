# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor : Murtuza Akhtari <inxsible at gmail dot com>
# Contributor: Keshav P R <skodabenz at rocketmail dot com>

pkgname=efibootmgr
pkgver=0.6.0
pkgrel=3
pkgdesc="Tool to modify UEFI Firmware Boot Manager Variables."
arch=('i686' 'x86_64')
url="http://linux.dell.com/efibootmgr/"
license=('GPL2')
depends=('zlib' 'pciutils')
makedepends=()
provides=()
options=(strip purge docs zipman !emptydirs)

source=("http://linux.dell.com/efibootmgr/permalink/${pkgname}-${pkgver}.tar.gz"
        "efibootmgr-tilt_slashes-func.patch"
)

build () {	
	rm -rf ${srcdir}/efibootmgr-${pkgver}_build/ || true
	
	cp -r ${srcdir}/efibootmgr-${pkgver} ${srcdir}/efibootmgr-${pkgver}_build
	
	cd ${srcdir}/efibootmgr-${pkgver}_build/
	patch -Np1 -i ${srcdir}/efibootmgr-tilt_slashes-func.patch
	echo
	
	CFLAGS= make
	echo
	
}


package() {
	
	mkdir -p ${pkgdir}/usr/bin/
	mkdir -p ${pkgdir}/usr/share/man/man8/
	
	cd ${srcdir}/efibootmgr-${pkgver}_build/
	
	install -D -m755 ${srcdir}/efibootmgr-${pkgver}_build/src/efibootmgr/efibootmgr ${pkgdir}/usr/bin/efibootmgr
	install -D -m644 ${srcdir}/efibootmgr-${pkgver}_build/src/man/man8/efibootmgr.8 ${pkgdir}/usr/share/man/man8/efibootmgr.8
	
}
md5sums=('9e9a31d79e579644de83a14139b66d10'
         '3622c0e978cf859cae60803a6b6a0c5e')
