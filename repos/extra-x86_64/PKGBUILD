# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=3.28.2
pkgrel=1
pkgdesc="A character map application"
arch=(x86_64)
url="https://git.gnome.org/browse/gnome-characters"
license=(GPL2)
depends=(gjs gtk3 libunistring gnome-desktop)
makedepends=(meson intltool gobject-introspection appstream-glib git gperf)
groups=(gnome)
_commit=4bc74e63ebece8aa151b18f2a6c996ce4e7791e6  # tags/v3.28.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-characters.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
