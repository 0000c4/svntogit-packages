# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=dconf
pkgver=0.7.3
pkgrel=2
pkgdesc="A low-level configuration system."
arch=(i686 x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('glib2')
makedepends=('vala' 'gobject-introspection' 'gtk3')
optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://download.gnome.org/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.bz2
       load-enum.patch)
sha256sums=('84efc95cb62b6637b2131e110ff447908be739c2185d69bebb300293b561dfd9'
            '8ead56b2897b18c97b9b72b81bd6e9f9249e28743b05a2653bbf4e6af7d9f7cc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/load-enum.patch"
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
