# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=tcpdump
pkgver=4.8.1
pkgrel=1
pkgdesc="A tool for network monitoring and data acquisition"
arch=('i686' 'x86_64')
url="http://www.tcpdump.org"
license=('BSD')
depends=('libpcap' 'openssl')
source=(http://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('1F166A5742ABB9E0249A8D30E089DEF1D9C15D0D') # The Tcpdump Group
sha256sums=('20e4341ec48fcf72abcae312ea913e6ba6b958617b2f3fb496d51f0ae88d831c'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-ipv6

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "$pkgdir"/usr/bin/tcpdump.$pkgver
}
