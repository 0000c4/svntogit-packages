# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=liferea
pkgver=1.4.16
pkgrel=1
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gtk2>=2.10.11' 'gconf>=2.18.0.1-4' 'xulrunner>=1.8.1.11' 'libnotify>=0.4.4' 'libnetworkmanager' 'libxslt>=1.1.20' 'libglade' 'sqlite3')
makedepends=('perlxml' 'pkgconfig' 'diffutils')
options=('!libtool')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz)
md5sums=('591aa637cdcc9ada395b26b93b4d8714')
sha1sums=('1e89dbb29e3ee71491eeaa931a414cb1e06d505c')

build() {
  [ -z "${MOZ_PLUGIN_PATH}" ] && . /etc/profile.d/mozilla-common.sh

  cd ${startdir}/src/liferea-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-gtkhtml2 || return 1
  make LDFLAGS+="-R/usr/lib/xulrunner" || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1
  
  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
