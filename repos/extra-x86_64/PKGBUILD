# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Gan Lu <rhythm.gan@gmail.com>

pkgname=scim
pkgver=1.4.12
pkgrel=1
pkgdesc='Input method user interface and development platform'
url='http://www.scim-im.org/projects/scim'
license=('LGPL')
options=('!libtool')
arch=('i686' 'x86_64')
makedepends=('intltool')
depends=('libltdl' 'gtk2')
backup=('etc/scim/config' 'etc/scim/global')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'ftp://ftp.archlinux.org/other/scim/scim-docs-20120420.tar.xz')
sha1sums=('948180ec0af18e435a95edbdb840cf41f3b24d32'
          'b2cccb43673e1760abf6825768bc4354a981a0ad')

# Docs tarball temporarily generated with:
# 	svn co https://scim.svn.sourceforge.net/svnroot/scim/scim/trunk/docs docs

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	mv ../docs .
	./bootstrap
	sed 's/\$nls_cv_use_gnu_gettext/yes/' -i configure

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-static \
		--with-gtk-version=2 # still a couple issues with 3

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}
