# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.31.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('solid' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet' 'desktop-file-utils' 'kinit')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        kio-sanitize-url.patch::"https://cgit.kde.org/kio.git/patch/?id=f9d0cb47")
md5sums=('62f31e7a9cd0b875fce5b552ec9be3c7'
         'SKIP'
         '3044fd99f934879390f29ec71e2d795a')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
# Sanitize URLs before passing them to FindProxyForURL
  cd $pkgname-$pkgver
  patch -p1 -i ../kio-sanitize-url.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
