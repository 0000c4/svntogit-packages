# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libgsf
pkgver=1.14.45
pkgrel=1
arch=(x86_64)
pkgdesc="An extensible I/O abstraction library for dealing with structured file formats"
url="https://gitlab.gnome.org/GNOME/libgsf.git"
license=(GPL LGPL)
depends=(libxml2 gdk-pixbuf2 bzip2)
makedepends=(intltool gobject-introspection git gtk-doc autoconf-archive)
_commit=0e782357360f63e1e429d2abece71ceba1228654  # tags/LIBGSF_1_14_45^0
source=("git+https://gitlab.gnome.org/GNOME/libgsf.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^LIBGSF_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd "$pkgname"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-introspection --enable-gtk-doc
  make
}

package_libgsf() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}
