#$Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2008.12.20
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=(gcc-libs)
source=(http://live555.com/liveMedia/public/live.$pkgver.tar.gz)
md5sums=('266d9b15f57ca952667780d9c2185e5e')

build()
{
  cd $srcdir/live
  sed -i "s|COMPILE_OPTS =.*|COMPILE_OPTS = \$(INCLUDES) -I. -DSOCKLEN_T=socklen_t -D_LARGEFILE_SOURCE=1 $CFLAGS|g" config.linux
  ./genMakefiles linux
  make || return 1

  for dir in BasicUsageEnvironment UsageEnvironment groupsock liveMedia; do
    mkdir -p $pkgdir/usr/lib/$pkgname/$dir
    cp -r $dir/*.a $dir/include $pkgdir/usr/lib/$pkgname/$dir
  done

  mkdir -p $pkgdir/usr/bin
  for testprog in `find testProgs -type f -perm 755`; do
    install $testprog $pkgdir/usr/bin
  done
}
