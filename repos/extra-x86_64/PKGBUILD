# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi' 'kuiserver' 'plasma-wayland-session')
pkgver=5.7.4
pkgrel=2
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
# note on libxdamage:
# not detected by namcap because libgl depends on it
# but nvidia providing libgl does not depend on libxdamage
depends=('kjsembed' 'knotifyconfig' 'libxdamage' 'libksysguard' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot' 'iso-codes' 'kactivitymanagerd' 'kholidays'
         'xorg-xmessage' 'xorg-xprop' 'milou' 'prison' 'kwin' 'plasma-integration')
makedepends=('extra-cmake-modules' 'kdoctools' 'gpsd' 'baloo'
             'krunner' 'kxmlrpcclient' 'networkmanager-qt' 'kdesignerplugin')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgbase}-${pkgver}.tar.xz"{,.sig} 'kde.pam' klipper-prison-5.25.patch)
sha256sums=('5785b5dd3cd44847720f4e639ad4ccf4bdd842624abda5b78ffd49362c0f0e3d'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            '11a5afd87e776f31743515b5d934dba7e8d34bdd3acf8c45f02ff9a152f8b1d5')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd $pkgbase-$pkgver
  # be sure to use the Qt5 version of qtpaths
  sed -e 's:qtpaths:qtpaths-qt5:' -i startkde/start*.cmake

# Port to new prison API
  sed -e 's|EXACT QUIET|QUIET|' -i klipper/CMakeLists.txt
  patch -p1 -i ../klipper-prison-5.25.patch
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  depends+=('kuiserver')
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation')
  conflicts=('kdebase-workspace')

  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "$pkgdir"/usr/lib/drkonqi
  rm -r "$pkgdir"/usr/share/drkonqi

  # Remove conflicts with kuiserver
  rm "$pkgdir"/usr/bin/kuiserver5
  rm "$pkgdir"/usr/lib/libkdeinit5_kuiserver5.so
  rm "$pkgdir"/usr/share/dbus-1/services/kf5_org.kde.kuiserver.service
  rm "$pkgdir"/usr/share/kservices5/kuiserver.desktop

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kxmlrpcclient' 'kidletime' 'gdb')

  cd build/drkonqi
  make DESTDIR="$pkgdir" install
}

package_kuiserver() {
  pkgdesc='KDE Progress Info UI server'
  depends=('kxmlgui')

  cd build/kuiserver
  make DESTDIR="$pkgdir" install
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=('plasma-workspace' 'qt5-wayland' 'kwayland-integration' 'xorg-server-xwayland')
  groups=()

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
