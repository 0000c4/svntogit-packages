# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Thomas Jost <schnouki@schnouki.net>
# Contributor: JaDa <jada@archlinux.us>
# Contributor: Joaquim Pedro <osmano807@gmail.com>
# Contributor: Jan Rüegg <rggjan@gmail.com>

pkgname=gperftools
pkgver=2.0
pkgrel=4
pkgdesc="Fast, multi-threaded malloc() and nifty performance analysis tools"
arch=('i686' 'x86_64')
url="http://code.google.com/p/gperftools/"
license=('BSD')
depends=('perl')
optdepends=('graphviz: pprof graph generation'
            'gv: pprof postscript generation')
options=('!libtool')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz")
sha256sums=('7de3dd91f018825b1e7d332af1edace15c6211f430186febede1835069861080')

build() {
  cd "$pkgname-$pkgver"

  # fix for glibc 2.16
  sed -i 's:siginfo_t:siginfo:' src/base/linuxthreads.cc

  ./configure --prefix=/usr --enable-frame-pointers
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
