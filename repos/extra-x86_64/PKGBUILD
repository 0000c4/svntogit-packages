# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=mpg123
pkgver=1.13.4
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2.1')
depends=('libtool' 'alsa-lib')
makedepends=('sdl' 'jack' 'libpulse')
optdepends=('sdl: for sdl audio support'
            'jack: for jack audio support'
            'libpulse: for pulse audio support')
conflicts=('mpg321')
provides=('mpg321')
options=('libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('073620b3938c4cb9c4f70e8fe3e114b8')
sha1sums=('7fe195db9fe708c487a1d47a66e6e68d9b8e7a6e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-audio="alsa sdl jack pulse"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
