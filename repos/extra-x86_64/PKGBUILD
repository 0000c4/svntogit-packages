# $Id$ 
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango
pkgver=1.40.0
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libthai' 'cairo' 'libxft' 'harfbuzz')
makedepends=('harfbuzz' 'fontconfig' 'cairo' 'libxft' 'libthai' 'gobject-introspection' 'help2man')
checkdepends=('ttf-dejavu')
install=pango.install
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('da17985df314cb07d066ab5424f59c21ce973ece05b7de4df04d798ec8511c8b')

build() {
  cd ${pkgname}-${pkgver}

  rm -f pango-view/pango-view.1.in

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-included-modules=basic-fc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check || :
}

package() {
  cd ${pkgname}-${pkgver}
  make -j1 DESTDIR="${pkgdir}" install
}
