# $Id$
# Maintainer: Biru Ionut <ionut@archlinux.ro>
pkgname=empathy
pkgver=2.28.2
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('iso-codes' 'gnome-panel>=2.28.0' 'telepathy-mission-control>=5.3.1' 'telepathy-farsight>=0.0.12'
         'libcanberra' 'libnotify' 'libunique>=1.1.2' 'libwebkit>=1.1.15.4')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils>=0.18.0')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/2.28/$pkgname-$pkgver.tar.gz)

build() {
    cd "$srcdir/$pkgname-$pkgver"

    ./configure --prefix=/usr \
    	--sysconfdir=/etc  \
        --libexecdir=/usr/lib/empathy \
	--enable-shave \
	--disable-schemas-install
    make || return 1
    make DESTDIR=$pkgdir install || return 1

    # Merge schemas file in a single file and remove trash
    mkdir -p $pkgdir/usr/share/gconf/schemas
    gconf-merge-schema $pkgdir/usr/share/gconf/schemas/$pkgname.schemas \
        $pkgdir/etc/gconf/schemas/*.schemas
    rm -rf $pkgdir/etc/gconf
}
md5sums=('af4ac36f8162810dc97423d5cf1137fd')
