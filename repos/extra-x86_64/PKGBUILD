# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=irssi
pkgver=0.8.20
pkgrel=1
pkgdesc="Modular text mode IRC client with Perl scripting"
arch=('i686' 'x86_64')
url="http://irssi.org/"
license=('GPL')
depends=('glib2' 'openssl')
optdepends=('perl-libwww: for the scriptassist script')
backup=('etc/irssi.conf')
source=("https://github.com/irssi/irssi/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
        "https://github.com/irssi/irssi/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc")
md5sums=('67d48c5feec2d3b949d088aa4abc3601'
         'SKIP')
validpgpkeys=('7EE65E3082A5FB06AC7C368D00CCB587DDBEF0E1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-ipv6 \
	      --with-proxy \
	      --sysconfdir=/etc \
	      --with-perl-lib=vendor
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install 
}
