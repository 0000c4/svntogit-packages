# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libx11
pkgver=1.6.6
pkgrel=2
pkgdesc="X11 client-side library"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2{,.sig}
        0001-poll_for_response-Call-poll_for_event-again-if-xcb_p.patch)
sha256sums=('65fe181d40ec77f45417710c6a67431814ab252d21c2e85c75dd1ed568af414f'
            'SKIP'
            '12f33518da43e91edd98abf151bea841a6aaf8a8885cc046a1bedc04ce0e1ee7')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')
validpgpkeys+=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>
validpgpkeys+=('3BB639E56F861FA2E86505690FDD682D974CA72A') # Matt Turner <mattst88@gmail.com>

prepare() {
  cd "${srcdir}/libX11-${pkgver}"
  # https://bugs.freedesktop.org/show_bug.cgi?id=107992 -> xf86-video-amdgpu-18.1.0: video freezes in firefox
  patch -Np1 -i ../0001-poll_for_response-Call-poll_for_event-again-if-xcb_p.patch
}

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ./configure --prefix=/usr --disable-static --disable-xf86bigfont
  make
}

check() {
  cd "${srcdir}/libX11-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
