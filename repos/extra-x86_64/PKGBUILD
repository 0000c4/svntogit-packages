# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=xf86-video-ati
pkgver=6.9.0
pkgrel=6
pkgdesc="X.org ati video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libdrm>=2.3.1' 'libgl>=7.2' 'libpciaccess' 'ati-dri>=7.2')
makedepends=('pkgconfig' 'xorg-server>=1.5.0' 'fontsproto'
	     'glproto>=1.4.9' 'xf86driproto' 'diffutils' 'xorg-util-macros')
conflicts=('xorg-server<1.5.0')
groups=('xorg-video-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
       git-20081021.patch)
md5sums=('afa4873f58ae6fa31fedeb9ed31531db'
	 'f2ce4e7fb4e266bbc61fb2d9c7c20a3b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-20081021.patch" || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr \
              --enable-dri || return 1
  make || return 1
  make "DESTDIR=${pkgdir}" install || return 1
}
