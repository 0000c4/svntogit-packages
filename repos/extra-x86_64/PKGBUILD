# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gspell
pkgver=1.0.3
pkgrel=1
pkgdesc="a flexible API to implement the spell checking in a GTK+ application"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/gspell"
license=('GPL2')
depends=('gtk3' 'enchant' 'libxml2' 'iso-codes')
makedepends=('intltool' 'gobject-introspection' 'vala')
source=(https://download.gnome.org/sources/gspell/${pkgver:0:3}/gspell-${pkgver}.tar.xz)
sha256sums=('d95905e6c631e34911e8e3feeb364815bf3fd7ccd1dac118a34bcbae70261bd5')

build() {
  cd "$pkgname-$pkgver"
  ./configure  --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}
package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
