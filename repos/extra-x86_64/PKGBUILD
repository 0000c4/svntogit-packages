# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-core
pkgver=2.24.0
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus' 'glib2' 'libxtst')
makedepends=('intltool' 'gobject-introspection' 'git' 'gtk-doc')
_commit=bed6eca42bee07e7f8b5544213481ed75ce98aff  # tags/AT_SPI2_CORE_2_24_0^0
source=("git+https://git.gnome.org/browse/at-spi2-core#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/AT_SPI2_CORE_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
