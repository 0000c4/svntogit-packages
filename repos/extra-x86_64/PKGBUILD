# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ltrace
pkgver=0.7.0
pkgrel=1
pkgdesc="Tracks runtime library calls in dynamically linked programs"
arch=('i686' 'x86_64')
url="http://ltrace.alioth.debian.org/"
license=('GPL')
depends=('elfutils')
backup=('etc/ltrace.conf')
source=(https://alioth.debian.org/frs/download.php/3822/$pkgname-$pkgver.tar.bz2)
sha256sums=('5438ce286f072f0e0448db046657466f9bd142600013329063aecf7cf809fd10')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package(){
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # ltrace.conf.5 needs to be installed to man5 (fixed in upstream git repo)
  install -d "$pkgdir/usr/share/man/man5"
  mv "$pkgdir/usr/share/man/man"{1/ltrace.conf.1,5/ltrace.conf.5}
}

# vim:set ts=2 sw=2 et:
