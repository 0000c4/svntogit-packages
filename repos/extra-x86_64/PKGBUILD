# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vitaliy Berdinskikh <ur6lad@i.ua>

pkgname=tomcat-native
pkgver=1.2.24
pkgrel=1
pkgdesc='Optional component for Tomcat to use certain native resources for performance, compatibility'
arch=(x86_64)
url=https://tomcat.apache.org/native-doc/
license=(APACHE)
depends=(apr)
makedepends=(java-environment)
options=(!emptydirs)
source=(https://archive.apache.org/dist/tomcat/tomcat-connectors/native/${pkgver}/source/${pkgname}-${pkgver}-src.tar.gz{,.asc})
validpgpkeys=(
  A9C5DF4D22E99998D9875A5110C01C5A2F6059E7
  B3F49CD3B9BD2996DA90F817ED3873F5D3262722
)
sha512sums=('5dae151a60f8bd5a9a29d63eca838c77174426025ee65a826f0698943494dd3656d50bcd417e220a926b9ce111ea167043d4b806264030e951873d06767b3d6f'
            'SKIP')

build() {
  cd tomcat-native-${pkgver}-src/native

  ./configure \
    --prefix=/usr \
    --with-java-home=/usr/lib/jvm/default \
    --with-apr=/usr \
    --with-ssl=/usr
  make
}

package() {
  make DESTDIR="${pkgdir}" -C tomcat-native-${pkgver}-src/native install
}

# vim: ts=2 sw=2 et:
