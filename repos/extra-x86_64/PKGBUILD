# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=rtkit
pkgver=0.11
pkgrel=1
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('i686' 'x86_64')
url="http://git.0pointer.de/?p=rtkit.git"
license=(GPL 'custom:BSD')
depends=(dbus polkit)
install=rtkit.install
source=(http://0pointer.de/public/$pkgname-$pkgver.tar.xz)
md5sums=('a96c33b9827de66033d2311f82d79a5d')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/$pkgname \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # DBus configuration
  mkdir -p "$pkgdir/usr/share/dbus-1/interfaces"
  ./rtkit-daemon --introspect \
    > "$pkgdir/usr/share/dbus-1/interfaces/org.freedesktop.RealtimeKit1.xml"

  # Client license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  sed -ne '4,25p' rtkit.c >"$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
