# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vitaliy Berdinskikh <ur6lad@i.ua>

pkgname=tomcat-native
pkgver=1.2.23
pkgrel=1
pkgdesc='Optional component for Tomcat to use certain native resources for performance, compatibility'
arch=(x86_64)
url=https://tomcat.apache.org/native-doc/
license=(APACHE)
depends=(apr)
makedepends=(java-environment)
options=(!emptydirs)
source=(https://archive.apache.org/dist/tomcat/tomcat-connectors/native/${pkgver}/source/${pkgname}-${pkgver}-src.tar.gz{,.asc})
validpgpkeys=(
  A9C5DF4D22E99998D9875A5110C01C5A2F6059E7
  B3F49CD3B9BD2996DA90F817ED3873F5D3262722
)
sha512sums=('89a0363961d322a87f4e752f4727f54f28ac6e4ad10fa21b6b7390c62b041d4068672d95495d9233c1cad7d6c1dc3c85fbd0186894085b3b94e476876af160ee'
            'SKIP')

build() {
  cd tomcat-native-${pkgver}-src/native

  ./configure \
    --prefix=/usr \
    --with-java-home=/usr/lib/jvm/default \
    --with-apr=/usr \
    --with-ssl=/usr
  make
}

package() {
  make DESTDIR="${pkgdir}" -C tomcat-native-${pkgver}-src/native install
}

# vim: ts=2 sw=2 et:
