# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=freecell-solver
pkgver=6.0.0
pkgrel=1
pkgdesc="A program that automatically solves layouts of Freecell and similar variants of Card Solitaire"
arch=(x86_64)
url="https://fc-solve.shlomifish.org"
license=(MIT)
depends=(bash)
makedepends=(cmake gperf perl-moo perl-path-tiny perl-template-toolkit python-random2 python-six python-pysol_cards rinutils)
optdepends=('python-random2: for the Python interface' 'python-six: for the Python interface'
            'python-pysol_cards: for the Python interface')
source=(https://fc-solve.shlomifish.org/downloads/fc-solve/$pkgname-$pkgver.tar.xz
        freecell-solver-fix-build.patch)
sha256sums=('fcbf7d2bed6840cb340b035fe9ac8cdcdb30ab84fea72c864bec5ac748af3199'
            '6d5975823b55c8eb63fb96a6e06ff0346d86c9c1d3ec2989bc1947bd5f092134')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../freecell-solver-fix-build.patch # Fix out of tree build
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFCS_WITH_TEST_SUITE=OFF \
    -DBUILD_STATIC_LIBRARY=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/COPYING.html "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.html
}

