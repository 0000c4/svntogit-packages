# $Id$
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-battery-plugin
pkgver=1.0.0
pkgrel=3
pkgdesc="A battery monitor plugin for the Xfce panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://xfce-goodies.berlios.de/"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/1.0/$pkgname-$pkgver.tar.bz2
        show-time-when-on-battery.patch)
sha256sums=('c28bb580e6b932c017b5bc6b2e75de8c3557e08d9606c7a70134aa035f8a121f'
            '7cabd72a462406295e438a2aae89c09af9f759b5c257f7ecd19ae02c81250575')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Fix FS#27438: Remaining time not shown on some systems
  patch -Np1 -i "$srcdir/show-time-when-on-battery.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
