# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-epub-thumbnailer
pkgver=1.5
pkgrel=1
pkgdesc="Thumbnailer for EPub and MOBI books"
url="https://gitlab.gnome.org/GNOME/gnome-epub-thumbnailer"
license=(GPL)
arch=(x86_64)
_commit=cce8d8e594ef8fe77720ca33656c2f4368cd424a  # tags/1.5^0
depends=(gdk-pixbuf2 libarchive libxml2)
makedepends=(git gnome-common)
source=("git+https://gitlab.gnome.org/GNOME/gnome-epub-thumbnailer.git#commit=$_commit")
sha256sums=('SKIP')
 
pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-compile-warnings=minimum
  make
}

check() {
  make -C $pkgname check
}
 
package() {
  DESTDIR="$pkgdir" make -C $pkgname install
}

# vim:set sw=2 et:
