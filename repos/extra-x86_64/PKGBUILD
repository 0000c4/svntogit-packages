# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.20.1
pkgrel=3
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
depends=('intel-dri' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util>=0.3.9' 'libxfixes' 'systemd-tools')
makedepends=('xorg-server-devel>=1.12.0' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'libxrender')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xf86-video-intel-sna' 'xf86-video-intel-uxa' 'xorg-server<1.12.0' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/archive/individual/driver/${pkgname}-${pkgver}.tar.bz2
        gmux_backlight.diff
        sna_backlight_fixes.diff)
sha256sums=('de340a527dfa26bba6f79b423c2fe2b1d70657e46fa3fb468613c047443ff424'
            '87383122dae69512f86039e9d72110bd6bf4cf717756a31b34862c200277b31b'
            'a4a6bc1c4a6b79c80c56764eed8ab318a587ed554d5d9a381cc7238ab884ec04')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # https://bugs.freedesktop.org/show_bug.cgi?id=52423
  patch -Np1 -i ${srcdir}/gmux_backlight.diff
  patch -Np1 -i ${srcdir}/sna_backlight_fixes.diff
  ./configure --prefix=/usr \
    --enable-dri
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
