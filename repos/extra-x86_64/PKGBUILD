# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=hitori
pkgver=3.32.0
pkgrel=1
pkgdesc="GTK+ application to generate and let you play games of Hitori"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Hitori"
license=(GPL)
depends=(gtk3)
groups=(gnome-extra)
makedepends=(yelp-tools appstream-glib git meson)
_commit=0753a063677e2a4cfe5c0948d3a08402f9da7b67  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/hitori.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
