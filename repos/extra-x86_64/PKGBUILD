# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=polkit-qt
pkgver=0.96.1
pkgrel=1
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('polkit' 'qt')
makedepends=('cmake' 'automoc4')
source=("ftp://ftp.kde.org/pub/kde/stable/apps/KDE4.x/admin/${pkgname}-1-${pkgver}.tar.bz2"
	'fix-deprecated-warnings.patch'
	'null-checking.patch')
md5sums=('7d122aa67c6786ea7d0bb023701693a1'
         '2ff7e14cfa198eaca9189c15158adf37'
         'a34dfa8408cf7add41dd2c9c522a61ce')

build() {
	cd ${srcdir}/${pkgname}-1-${pkgver}
	patch -Np0 -i ${srcdir}/fix-deprecated-warnings.patch
	patch -Np0 -i ${srcdir}/null-checking.patch

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-1-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd ${srcdir}/build
	make DESTDIR=$pkgdir install
}
