# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima
pkgver=18.10.2
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('x86_64')
url="https://wxmaxima-developers.github.io/wxmaxima/"
license=('GPL2')
depends=('maxima' 'wxgtk3' 'ttf-linux-libertine')
makedepends=('cmake')
source=($pkgname-$pkgver.tar.gz::"https://github.com/wxMaxima-developers/wxmaxima/archive/Version-${pkgver}.tar.gz"
        8bc9d69b.patch)
sha512sums=('96cabe17a1df4134d991c4a1104da76375429038da03df6a09729b8afaf279788cb0859d723bb1c9cf3e24782f63aeb198b9407515108e0e637c48c410b70a37'
            'f9d083e3f752ca6bc04c152419b6bb1a71d3e5c25934de90cdf968240fe21d3574b180da7915acebaf15550f93a894e2505f1b4c9c3fc7b48caeee9bb3fab9af')

prepare() {
  cd $pkgname-Version-$pkgver
  patch -p1 -i ../8bc9d69b.patch # https://bugs.archlinux.org/task/60704
}

build() {
  cd ${pkgname}-Version-${pkgver}
  cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

#check() {
#  cd ${pkgname}-Version-${pkgver}
#  ./src/wxmaxima test/testbench_simple.wxmx
#}

package() {
  cd ${pkgname}-Version-${pkgver}

  make DESTDIR="${pkgdir}" install 

  # Fix category in .desktop file
  sed -i '/Categories=/c\Categories=Science;Math;' data/wxMaxima.desktop
}
