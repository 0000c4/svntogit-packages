# $Id: PKGBUILD,v 1.10 2008/05/13 19:03:39 ronald Exp $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima
pkgver=0.8.2
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('i686' 'x86_64')
url="http://wxmaxima.sourceforge.net/"
license=('GPL2')
depends=('maxima>=5.18.1' 'libxml2' 'wxgtk')
source=(http://downloads.sourceforge.net/$pkgname/wxMaxima-$pkgver.tar.gz)
install=wxmaxima.install
md5sums=('d3d4abde8495aebb295df587587de4a8')

build() {
  cd ${srcdir}/wxMaxima-$pkgver
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # Fix category in .desktop file
  sed -i -e 's/Utility;X-Red-Hat-Base;X-Red-Hat-Base-Only;/Science;Math;/' wxmaxima.desktop
  
  # Install desktop file and icon
  install -Dm644  wxmaxima.desktop \
  	${pkgdir}/usr/share/applications/wxmaxima.desktop
  install -Dm644  ${srcdir}/wxMaxima-${pkgver}/data/wxmaxima.png \
  	${pkgdir}/usr/share/pixmaps/wxmaxima.png
}
