# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=3.6.1
pkgrel=2
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!libtool' '!emptydirs')
depends=('gtk3' 'gst-plugins-good' 'totem-plparser' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'  'libcanberra' 'dvd+rw-tools' 'dconf' 'libsm' 'libtracker-sparql')
makedepends=('gtk-doc' 'intltool' 'libburn' 'libisofs' 'nautilus' 'gobject-introspection' 'gconf' 'itstool' 'docbook-xsl')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
install=brasero.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        use-mpegaudioparse.patch)
sha256sums=('d293eb28dc45af3535f09cf91aa6ccc523f3f3d8b7ad3339b5e79b4acc19b932'
            'c75331d130c248edeaa68211cf96f386d86905510d09b104e4e0cd0ff4c92218')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../use-mpegaudioparse.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-caches \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
}

