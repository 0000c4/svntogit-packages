# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=19.04.0
pkgrel=2
pkgdesc="Offers a selection of solitaire card games"
url="https://kde.org/applications/games/kpatience/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames freecell-solver black-hole-solver hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/applications/$pkgver/src/kpat-$pkgver.tar.xz"{,.sig}
        kpatience-bhs-0.20.patch)
sha256sums=('bcd295a3df422b5cfa8258bb3cd0be7e4405f44604681d98589f2982c44414a1'
            'SKIP'
            'd99035dc97d7480060cd466e966f1db48ff6e6c45a6aa0ad7076ebda57a107ba')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd kpat-$pkgver
  patch -p1 -i ../kpatience-bhs-0.20.patch # Fix build with black-hole-solver 0.20
}

build() {
  cd build
  cmake ../kpat-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_BH_SOLVER=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
