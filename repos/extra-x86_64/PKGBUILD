# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Antonio Rojas

pkgname=sddm-kcm
pkgver=5.7.4
pkgrel=2
pkgdesc='KDE Config Module for SDDM'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('GPL')
depends=('sddm' 'libxcursor' 'kio')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools' 'python')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        sddm-0.14.patch::"https://git.reviewboard.kde.org/r/128815/diff/raw/")
sha256sums=('b01854f130e0a7ea322995e371c7b4a058adf809313082bb69ad18562fca922a'
            'SKIP'
            '9fff462258d12578a6731d76ac49264f10b14354e5788ca897fec7801045d6d9')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

# Fix theme list with SDDM 0.14
  cd $pkgname-$pkgver
  patch -p1 -i ../sddm-0.14.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

