# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=5.3.2
pkgrel=1
pkgdesc="PHP language and documentation plugin for KDevelop"
arch=(x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt)
source=("https://download.kde.org/stable/kdevelop/$pkgver/src/kdev-php-$pkgver.tar.xz"{,.asc})
sha256sums=('9ac87343185a6f48411334744e942f5e9fde7cb33fcca40d000980ef2075dd4a'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'  # Sven Brauch <mail@svenbrauch.de>
              '364EFA5F639572907D392999C64CF56B13CACE5D') # Kevin Funk <kfunk@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdev-php-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
