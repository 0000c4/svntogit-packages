# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kontrast
pkgver=20.12.1
pkgrel=1
pkgdesc="Tool to check contrast for colors that allows verifying that your colors are correctly accessible"
arch=(x86_64)
url='https://apps.kde.org/es/kontrast'
license=(GPL)
depends=(kirigami2 kdeclarative qt5-svg hicolor-icon-theme)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-accessibility)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('79099358ad697dd89c8cbb58ae185a9d42e175fa6be87ba28350a84dac0e4333'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

