# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgbase=nasm
pkgname=('nasm' 'nasm-doc')
pkgver=2.10
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.nasm.us"
license=('BSD')
makedepends=('ghostscript')
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgbase}-${pkgver}.tar.bz2)
sha1sums=('acdcae5366230e2e19b2ad36f952df0232c80950')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make everything
}

package_nasm() {
  pkgdesc="An 80x86 assembler designed for portability and modularity"
  depends=('glibc')
  install=nasm.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -d "${pkgdir}"/usr/share/info
  install -m644 doc/info/* "${pkgdir}"/usr/share/info/
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/nasm/LICENSE
}

package_nasm-doc() {
  pkgdesc="An 80x86 assembler designed for portability and modularity (manuals)"

  cd "${srcdir}/${pkgbase}-${pkgver}/doc"
  install -d "${pkgdir}"/usr/share/doc/nasm/html
  install -m644 html/* "${pkgdir}"/usr/share/doc/nasm/html/
  install -m644  nasmdoc.ps nasmdoc.pdf nasmdoc.txt  "${pkgdir}"/usr/share/doc/nasm/
  install -D -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/nasm-doc/LICENSE
}
