# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Gan Lu <rhythm.gan@gmail.com>

pkgname=scim
pkgver=1.4.13
pkgrel=1
pkgdesc='Input method user interface and development platform'
url='http://www.scim-im.org/projects/scim'
license=('LGPL')
options=('!libtool')
arch=('i686' 'x86_64')
makedepends=('intltool')
depends=('libltdl' 'gtk2')
backup=('etc/scim/config' 'etc/scim/global')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('dafe08bbdd3faa0324800cb63e44926d71be2e47')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CXXFLAGS+=' -fpermissive' # so configure detects gettext with gcc-4.7

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-static \
		--with-gtk-version=2 # still a couple issues with 3

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}
