# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libproxy
pkgver=0.4.13
pkgrel=2
pkgdesc="A library that provides automatic proxy configuration management"
arch=(i686 x86_64)
license=('LGPL')
depends=('gcc-libs')
optdepends=('networkmanager: NetworkManager configuration module'
            'perl: Perl bindings'
            'python2: Python bindings'
            'glib2: gsettings configuration module'
            'webkit2gtk: PAC proxy support')
makedepends=('cmake' 'networkmanager' 'python2' 'perl' 'webkit2gtk')
url="http://libproxy.github.io/libproxy/"
source=(https://github.com/libproxy/libproxy/archive/${pkgver}.tar.gz
        libproxy-0.4.11-crash.patch)
md5sums=('de293bb311f185a2ffa3492700a694c2'
         'a45f2195c164a48639e963e8341f5d93')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np1 -i ../libproxy-0.4.11-crash.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=yes \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_WEBKIT3=ON \
    -DWITH_MOZJS=OFF \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2 \
    -DBIPR=0
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
