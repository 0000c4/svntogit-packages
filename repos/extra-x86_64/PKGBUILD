# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.17.0
pkgrel=3
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/plasma-framework'
license=('LGPL')
depends=('qt5-quickcontrols' 'kactivities')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        fix-crash.patch::"https://quickgit.kde.org/?p=plasma-framework.git&a=commitdiff&h=bca8e8&o=plain"
	bug356938.patch::"https://quickgit.kde.org/?p=plasma-framework.git&a=commitdiff&h=c64a94&o=plain")
md5sums=('5b0f2f31497c81870f3ccc68e3e7a5fa'
         '568e37898934ab113850084f2a515007'
         '005062c7a8383a4386c9e87953e6996a')

prepare() {
  mkdir -p build

# Fix crash http://bugs.kde.org/show_bug.cgi?id=356428
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-crash.patch
# Fix taskbar tooltips causing performance issues http://bugs.kde.org/show_bug.cgi?id=356938
  patch -p1 -i ../bug356938.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
