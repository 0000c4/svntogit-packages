# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Nikolay Rysev <mad.f3ka@gmail.com>

pkgname=fprintd
pkgver=0.4.1
pkgrel=3
pkgdesc="D-Bus service to access fingerprint readers"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/fprint/fprintd"
license=(GPL)
depends=(libfprint dbus-glib polkit)
makedepends=(intltool gtk-doc)
groups=(fprint)
options=(!libtool)
_snap=V_${pkgver//./_}
source=(http://cgit.freedesktop.org/libfprint/$pkgname/snapshot/$_snap.tar.bz2)
md5sums=('58fcac035c181dce8f49c374181aa2ae')

build() {
  cd "$srcdir/$_snap"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --disable-static
  make
}

package() {
  cd "$srcdir/$_snap"
  make DESTDIR="$pkgdir" pammoddir="/lib/security" install
}
