# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libqalculate
pkgver=0.9.8
pkgrel=2
pkgdesc="Multi-purpose desktop calculator"
arch=('i686' 'x86_64')
url="http://qalculate.sourceforge.net/"
license=('GPL')
depends=('libxml2' 'cln' 'glib2')
makedepends=('perl-xml-parser' 'intltool')
optdepends=('gnuplot: for plotting support' 
            'wget: for retrieval of exchange rates (not needed if you use one of the GUI)'
            'gnome-vfs: for retrieval of exchange rates (not needed if you use one of the GUI)')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/qalculate/${pkgname}-${pkgver}.tar.gz
        libqalculate-fix-crash.patch::"https://github.com/Qalculate/libqalculate/commit/eade7b72.patch")
sha1sums=('d9023ae524aebf8e8b90e4e0cb76e04b77f60fd4'
          '5d35a6b431e417d6334ed717be2eac2fa8e85750')

prepare() {
  cd $pkgname-$pkgver
# Fix segfault when locale missing
  patch -p1 -i ../libqalculate-fix-crash.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
