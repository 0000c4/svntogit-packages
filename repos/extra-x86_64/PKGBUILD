# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : Damir Perisa <damir.perisa@bluewin.ch>

pkgname=celestia
pkgver=1.6.2
pkgrel=1
pkgdesc="Real-time space simulation"
arch=('x86_64')
license=('GPL')
url="https://celestia.space/"
depends=('gtk2' 'libtheora' 'lua53' 'gtkglext' 'glut' 'libxmu' 'glu' 'libjpeg' 'mesa')
options=('!makeflags')
source=("https://downloads.sourceforge.net/sourceforge/celestia/${pkgname}-${pkgver}.tar.gz")
sha1sums=('c94562902e3fcfa72203f96d5f711edb868dfc7e')

prepare() {
    cd Celestia-${pkgver}
    autoreconf -vi
}

build() {
    cd Celestia-${pkgver}
    ./configure --prefix=/usr \
                --with-lua=/usr \
                --datadir=/usr/share \
                --with-gtk \
                --disable-rpath \
                --with-lua
    make
}

package() {
    cd Celestia-${pkgver}

    make DESTDIR="${pkgdir}" MKDIR_P='mkdir -p' install
}

