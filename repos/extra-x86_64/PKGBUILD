# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=nmap
pkgver=4.68
pkgrel=1
pkgdesc="A network exploration tool and security/port scanner"
arch=(i686 x86_64)
url="http://nmap.org"
license=('custom:GPL')
depends=('pcre' 'openssl' 'libpcap>=0.9.8' 'lua')
# python is needed for zenmap
makedepends=('python')
options=('!makeflags')
source=(http://download.insecure.org/nmap/dist/$pkgname-$pkgver.tgz
        zenmap_pixmaps.patch)

md5sums=('a1373c992e4b21d8db4d65c6eb8fec42'
         '91674cf81562064627bc5359277857fb')

build() {
  cd $startdir/src/$pkgname-$pkgver
  # patch so zenmap pixmaps are installed in a subdirectory
  patch -Np1 < ../zenmap_pixmaps.patch

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --libexecdir=/usr/lib
  make || return 1
  make DESTDIR="$startdir/pkg" install

  # super ugly- we have to move these in the source too
  mv zenmap/share/zenmap/pixmaps/{*.png,*.svg} $startdir/pkg/usr/share/pixmaps/zenmap/
  
  # remove zenmap uninstall script
  rm "$startdir/pkg/usr/bin/uninstall_zenmap"

  # install custom GPL2 license
  install -D -m644 COPYING $startdir/pkg/usr/share/licenses/$pkgname/LICENSE
}
