# $Id$
# Maintainer : Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>
# Contributor: Hervé YVIQUEL <elldekaa@gmail.com>

pkgname=hwloc
pkgver=1.1.1
pkgrel=2
pkgdesc="Portable Hardware Locality is a portable abstraction of hierarchical architectures"
arch=('i686' 'x86_64')
url="http://www.open-mpi.org/projects/hwloc/"
license=('BSD')
depends=('sh')
makedepends=('pkg-config')
optdepends=('cairo: Graphical output' 'libxml2: XML export')
options=('!libtool' '!docs')
source=(http://www.open-mpi.org/software/hwloc/v1.1/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('ca99f6e7c9cb41ca0ce89171c817c1de1d9bbde1')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   ./configure --prefix=/usr
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install

   install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

   # Rename hwloc-gather-topology.sh script to be consistent with the upcoming version 1.2
   mv "${pkgdir}"/usr/bin/hwloc-gather-topology{.sh,}
   mv "${pkgdir}"/usr/share/man/man1/hwloc-gather-topology{.sh.1,.1}
   sed -i -e 's/hwloc-gather-topology.sh/hwloc-gather-topology/g' "${pkgdir}"/usr/share/man/man1/hwloc-gather-topology.1
}
