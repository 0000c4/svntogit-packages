# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir.archlinux.org>

pkgname=muparser
pkgver=2.3.0
pkgrel=1
pkgdesc="A fast math parser library"
arch=('x86_64')
url="http://muparser.sourceforge.net/"
depends=('gcc-libs')
makedepends=('cmake')
license=('custom')
options=('!makeflags')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/beltoforion/muparser/archive/v$pkgver.tar.gz")
sha512sums=('5a641489ceb0bb5ca57853637ab5063a2850e00c5066a2b5f62a73ec06a006f306bf2ca63c4673973b423b01fe1ccfafb698de063f1a39844652051f55836f52')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -D -m644 $pkgname-$pkgver/License.txt \
	"$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
