# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Link Dupont <link@subpop.net>

pkgname=mpc
pkgver=0.29
pkgrel=2
pkgdesc='Minimalist command line interface to MPD'
url='https://www.musicpd.org/clients/mpc/'
license=('GPL2')
arch=('x86_64')
makedepends=('meson' 'python-sphinx')
depends=('libmpdclient')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512')
source=("https://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        '2d6e3fe90ae2c24d29305037b452b4cb18bceaa0.patch')
sha256sums=('02f1daec902cb48f8cdaa6fe21c7219f6231b091dddbe437a3a4fb12cb07b9d3'
            'SKIP'
	    '2f5c84c3869c8f36924f2707c1e956b49a397d6d93ad20c002a82eaae9ecc6ac')

options=('!emptydirs')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../2d6e3fe90ae2c24d29305037b452b4cb18bceaa0.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	arch-meson . build
	ninja -C build
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	DESTDIR="${pkgdir}" ninja -C build install 
	install -Dm644 contrib/mpc-completion.bash "${pkgdir}/usr/share/bash-completion/completions/mpc" 
}
