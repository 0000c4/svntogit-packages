# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fping
pkgver=2.4b2
pkgrel=5
pkgdesc="A utility to ping multiple hosts at once"
arch=('i686' 'x86_64')
url="http://packages.qa.debian.org/"
license=('custom') 
depends=('glibc')
source=("http://ftp.de.debian.org/debian/pool/main/f/fping/fping_2.4b2-to-ipv6.orig.tar.gz"
        'fping_2.4b2-to-ipv6-16.1.diff'
        'fping-2.4b2_p161-min-time.patch')
md5sums=('3ad516765514249a40d3c5b6caab812a'
         '4881e981971379a0948b31719e4505f5'
         '36753d9556eba1a6159c20c8db3e2232')

build() {
  cd $srcdir/$pkgname-2.4b2_to-ipv6

  patch -Np1 -i ${srcdir}/fping_2.4b2-to-ipv6-16.1.diff
  patch -Np1 -i ${srcdir}/fping-2.4b2_p161-min-time.patch

  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/$pkgname-2.4b2_to-ipv6

  make DESTDIR="${pkgdir}" install

  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
  chmod 4555 ${pkgdir}/usr/sbin/fping
}
