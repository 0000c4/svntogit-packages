# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=1.11.1.2
pkgrel=1
pkgdesc="C library for finding and handling crystal symmetries"
arch=(x86_64)
url="https://atztogo.github.io/spglib/"
license=(custom)
depends=(glibc)
makedepends=()
source=($pkgname-$pkgver.tar.gz::"https://github.com/atztogo/spglib/archive/v$pkgver.tar.gz")
sha256sums=('d99dab24accd269df65c01febd05cb5dd1094a89d7279f8390871f0432df2b56')

prepare() {
  cd $pkgname-$pkgver
  touch INSTALL NEWS README AUTHORS
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
