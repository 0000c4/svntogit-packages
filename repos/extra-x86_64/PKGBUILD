# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=md4c
pkgver=0.4.1
pkgrel=1
pkgdesc="C Markdown parser"
arch=(x86_64)
url="https://github.com/mity/md4c"
license=(MIT)
depends=(glibc)
makedepends=(cmake chrpath)
source=($pkgname-$pkgver.tar.gz::"https://github.com/mity/md4c/archive/release-$pkgver.tar.gz")
sha256sums=('4b1fe340fdefdab6e36d1421b465e943f56d3891c9e020236e253c32508a6489')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-release-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-release-$pkgver/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/

  chrpath -d "$pkgdir"/usr/bin/md2html # Fix RPATH
}
