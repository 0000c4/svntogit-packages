# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgxps
pkgver=0.2.5+1+g764ae1e
pkgrel=1
pkgdesc="XPS Documents library"
url="https://wiki.gnome.org/Projects/libgxps"
arch=(i686 x86_64)
license=(GPL2)
depends=(cairo libarchive libjpeg-turbo libtiff lcms2)
makedepends=(gobject-introspection gtk-doc git)
_commit=764ae1e1e22c3f1bb58543aa40fa80488ee49a47  # master
source=("git+https://git.gnome.org/browse/libgxps#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
