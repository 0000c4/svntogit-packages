# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kholidays
pkgver=17.04.2
pkgrel=2
pkgdesc="KDE library for regional holiday information"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-declarative qt5-tools)
optdepends=('qt5-declarative: QML bindings')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        fix-tr-holidays.patch::https://cgit.kde.org/kholidays.git/patch/?id=f0faec0f)
sha256sums=('a9071febc48749d54f24b78f8edb38fc874e71a1208f64cbf1d3c685c063d6b3'
            'SKIP'
            '2a813362b95e118079af8cf3088f60d88d4fca393fbdd3a5c844dd1d30c9091e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix Turkish holidays
  patch -p1 -i ../fix-tr-holidays.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
