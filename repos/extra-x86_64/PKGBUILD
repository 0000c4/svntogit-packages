# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=libass
pkgver=0.13.4
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
arch=('i686' 'x86_64')
url='https://github.com/libass/libass/'
license=('BSD')
depends=('fribidi' 'fontconfig')
makedepends=('pkg-config' 'yasm')
provides=('libass.so')
source=("https://github.com/libass/libass/releases/download/${pkgver}/libass-${pkgver}.tar.xz")
sha256sums=('d84a2fc89011b99d87fc47af91906622707c165d1860e9f774825ebbbc9c9fb6')

build() {
  cd libass-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-harfbuzz \
    --enable-fontconfig
  make
}

package() {
  cd libass-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libass/
}

# vim: ts=2 sw=2 et:
