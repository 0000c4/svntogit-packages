# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon
pkgver=4.5.1
pkgrel=2
epoch=1
arch=('i686' 'x86_64')
url="http://phonon.kde.org"
license=('LGPL')
pkgdesc="The multimedia framework for KDE"
depends=('qt' 'phonon-backend' 'libpulse' 'libqzeitgeist')
optdepends=('pulseaudio: PulseAudio support')
makedepends=('cmake' 'automoc4' 'pulseaudio')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
       'libqzeitgeist08.patch')
md5sums=('3df071b7f9996afcd67b18958955ecdd'
         '99de3dc908277199b502e54613925ff6')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}"/libqzeitgeist08.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package(){
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
