# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=akonadi
pkgver=17.08.0
pkgrel=2
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=(i686 x86_64)
url='https://community.kde.org/KDE_PIM/Akonadi'
license=(LGPL)
depends=(kio kitemmodels mariadb hicolor-icon-theme)
makedepends=(extra-cmake-modules postgresql qt5-tools boost python kdesignerplugin)
optdepends=('postgresql: PostgreSQL backend')
conflicts=(akonadi-client)
provides=(akonadi-client)
replaces=(akonadi-client)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-384024.patch::"https://cgit.kde.org/akonadi.git/patch/?id=2fcd7c4c")
sha256sums=('d7fea93ea49cc82fc25ad36b1bcfd45d5102c841d5cbdfdb598f854df71bb202'
            'SKIP'
            'ae1219a1d7ece5f0f05caa51176598e1e7333c4e2ef3ee896656cc82889d6311')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-384024.patch # Fix DB upgrade with sqlite backend
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
