# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=9.0.2
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(i686 x86_64)
license=(GPL)
depends=(dconf gtk3)
makedepends=(gtk-doc intltool gobject-introspection itstool docbook-xsl
             python2 appdata-tools unicode-character-database unzip gnome-common git)
options=(!emptydirs)
groups=(gnome)
_commit=4b64d8358ecc21005964e27f35d4c22064dcb94c  # tags/9.0.2^0
source=("git://git.gnome.org/gucharmap#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-introspection \
      --with-unicode-data=/usr/share/unicode-character-database
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
