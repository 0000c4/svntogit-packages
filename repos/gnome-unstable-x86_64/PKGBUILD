# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gnome-dictionary
pkgver=3.4.0
pkgrel=1
pkgdesc="Look up words in dictionary sources"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('dconf' 'gtk3')
makedepends=('intltool' 'gnome-doc-utils')
install=$pkgname.install
options=(!libtool)
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('70a15987617f5ae39f1fc27f7ed0a039193b4704cef7b89ed3aea30ac38f56af')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
