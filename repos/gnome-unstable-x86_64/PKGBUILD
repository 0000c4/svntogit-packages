# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.16
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'gnome-doc-utils' 'perl')
makedepends=('pkgconfig' 'jade')
optdepends=('jade: SGML support')
url="http://www.gtk.org/gtk-doc/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('552fd0a5e2bb104a9d8d5afebcf2292fbee83cd115e9f903df89e4518343eefb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
