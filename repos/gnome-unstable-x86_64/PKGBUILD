# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy
pkgver=3.1.92
pkgrel=3
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('libpulse' 'clutter-gtk' 'clutter-gst' 'telepathy-mission-control' 'telepathy-logger' 'telepathy-farsight' 'telepathy-farstream' 'folks' 'gnome-keyring' 'libcanberra' 'iso-codes' 'libchamplain' 'networkmanager')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/3.1/${pkgname}-${pkgver}.tar.xz)
sha256sums=('bfab43c6296bd7e67006f4582589ff98c6c2c9223b550a851734a2f826febb31')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-static \
      --disable-scrollkeeper \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
