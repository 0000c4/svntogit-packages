# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.31.0
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=(i686 x86_64)
license=('LGPL')
depends=('gdk-pixbuf2>=2.21.7' 'libcroco>=0.6.2' 'pango>=1.28.1' 'libgsf>=1.14.16')
makedepends=('intltool' 'pkg-config' 'gtk-doc')
optdepends=('python2: rsvg commandline utility')
options=('!libtool')
url="http://librsvg.sourceforge.net/"
install=librsvg.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.31/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('320b61ef5e6d65624f3733a83134df012a4156ed7f7ae38a6ed19febe1bfa732')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' rsvg.in
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} \
              --with-croco --disable-static \
	      --with-svgz --disable-gtk-theme
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
