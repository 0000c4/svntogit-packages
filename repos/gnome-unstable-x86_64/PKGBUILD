# $Id$
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgver=3.27.92
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
url="https://git.gnome.org/browse/gnome-desktop"
arch=(x86_64)
license=(GPL LGPL)
depends=(gsettings-desktop-schemas gtk3 libxkbfile xkeyboard-config iso-codes libseccomp bubblewrap)
makedepends=(intltool gobject-introspection git autoconf-archive gtk-doc yelp-tools)
_commit=bd6d8d2ca81adc2ec05fc29812bfb1c96ad957d4  # tags/3.27.92^0
source=("git+https://git.gnome.org/browse/gnome-desktop#commit=$_commit"
        bwrap-var.diff)
sha256sums=('SKIP'
            '701bfa5779ced0f50241d4370068fc58246d47088c6e95be27d204847866f106')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../bwrap-var.diff  # evince-thumbnailer needs fontconfig cache
  intltoolize
  gtkdocize
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --enable-gtk-doc \
    --with-gnome-distributor="Arch Linux"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
