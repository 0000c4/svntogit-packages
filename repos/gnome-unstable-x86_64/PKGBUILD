# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.30.0
pkgrel=1
pkgdesc="Configuration database system"
url="https://wiki.gnome.org/Projects/dconf"
arch=(x86_64)
license=(LGPL2.1)
depends=(glib2)
makedepends=(vala dbus git gtk-doc python meson bash-completion)
_commit=5310788b908725641e8fcb031ea5718ba8daee13  # tags/0.30.0^0
source=("git+https://gitlab.gnome.org/GNOME/dconf.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  # dbus tests can't connect and dbus-run-session doesn't help
  meson test -C build || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
