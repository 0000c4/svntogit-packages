# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libgpod
pkgver=0.8.0
pkgrel=2
pkgdesc="A shared library to access the contents of an iPod"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk2' 'mutagen' 'sg3_utils' 'libimobiledevice')
makedepends=('intltool' 'swig' 'docbook-xsl' 'pygobject-devel' 'gtk-sharp-2')
optdepends=('gtk-sharp-2: Mono bindings')
url="http://www.gtkpod.org/libgpod/"
source=(http://downloads.sourceforge.net/sourceforge/gtkpod/${pkgname}-${pkgver}.tar.gz)
options=('!libtool' '!emptydirs')
md5sums=('6660f74cc53293dcc847407aa5f672ce')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --enable-udev --with-python=/usr/bin/python2
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

