# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=empathy
pkgver=2.29.92
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('iso-codes' 'telepathy-mission-control>=5.3.1' 'telepathy-farsight>=0.0.12' 'libcanberra' 'libnotify' 'libunique>=1.1.6' 'libwebkit>=1.1.22' 'evolution-data-server>=2.29.92')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils>=0.19.5' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.29/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('5f12e254c899cc8ab88b480d08a4e014a74ad0ec516f1047c648eaacaa507a34')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-schemas-install || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
      DESTDIR="${pkgdir}" install || return 1

  # Merge schemas file in a single file and remove trash
  mkdir -p "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
      --domain empathy "${pkgdir}/etc/gconf/schemas/"*.schemas
  rm -rf "${pkgdir}/etc/gconf"
}
