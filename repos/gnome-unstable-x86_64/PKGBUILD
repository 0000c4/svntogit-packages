# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=3.3.91
pkgrel=1
pkgdesc="The Control Center for GNOME"
arch=('i686' 'x86_64')
depends=('gtk3' 'gsettings-desktop-schemas' 'gnome-menus' 'gnome-desktop' 'gnome-settings-daemon'
         'upower' 'libgtop' 'cups-pk-helper' 'accountsservice' 'sound-theme-freedesktop'
         'gnome-online-accounts' 'network-manager-applet' 'gnome-bluetooth' 'libsocialweb')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'apg: adds password generation for user accounts'
            'gnome-color-manager: for color management tasks'
            'cheese: take account photos via webcam')
makedepends=('gnome-doc-utils' 'intltool' 'gnome-common' 'cheese')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=(http://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        systemd-fallback.patch)
sha256sums=('6a08f50d6760ef62e567aa1240fca054cc33e8aac37a4fb318cfb4d7b611abfb'
            '8c9647c3bd1a69d9ce4d3716c235463f1cd33527cd8a241a2e88829f14da0494')

build() {
  cd ${pkgname}-${pkgver}
  
  patch -Np1 -i ../systemd-fallback.patch
  gnome-autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-systemd --with-libsocialweb \
      --disable-update-mimedb

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

