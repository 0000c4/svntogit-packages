# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=4.99.0
pkgrel=1
pkgdesc="Data abstraction layer with mysql, pgsql, xml, sqlite providers"
arch=(i686 x86_64)
license=('GPL')
depends=('gtksourceview3' 'libunique3' 'libxslt' 'db' 'ncurses' 'libsoup'
         'libmysqlclient' 'postgresql-libs' 'python2' 'libgnome-keyring'
         'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool')
options=('!libtool' '!makeflags')
url="http://www.gnome-db.org"
install=libgda.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('6fda4d3a4da931f1c8477bd910251da14cc464e9f272075cccf7b823d0af1a44')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '1s/python$/&2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
