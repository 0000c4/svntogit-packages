# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.8.0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop libnotify
         hicolor-icon-theme gnome-themes-standard)
makedepends=(intltool vala gobject-introspection)
options=(!libtool)
groups=(gnome-extra)
install=gnome-clocks.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::3}/${pkgname}-${pkgver}.tar.xz
        http://git.gnome.org/browse/gnome-clocks/plain/data/css/{gnome-clocks.css,button-border-{go,stop}{,-active}.svg})
sha256sums=('35c78a9b1cab465b5c5ccd9c3591cfc3b27eaa0b9ad927bc52642ea3908d64e0'
            '3ce4afb848ea2a2728c53a9f4bfca5f12d9719e3935d1acd2d90056cb1c464fc'
            'a44df652c8034ccbbc87a62f92ab6efbf6dfabead44e3316b4b9e85d851a6b24'
            'e12ac57acaa43b25a36d9fd9b6dbe906980a5e8429840d98c604bf28515749e0'
            'e71fa928cf5812fb1cc4ea62e55176be14716b670c37c7899368b0f8f020876c'
            '34916c190c259788bcb561f0b4cce73b47a39e50326e1a53a2a91c2725e9d8a3')

build() {
  cd $pkgname-$pkgver

  # Fix files missing from tarball
  mkdir data/css
  cp ../*.{css,svg} data/css

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
