# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=3.1.92
pkgrel=1
pkgdesc="GNOME file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gvfs' 'desktop-file-utils'
         'gnome-icon-theme' 'dconf' 'libtracker-sparql')
makedepends=('intltool' 'gobject-introspection')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('1ed8577f6e617bd5eb3e4f1a6c7ff9e4e7a63fcde525fa532419270ff0cef463')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
