# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=webkitgtk
pkgname=('webkitgtk3')
pkgver=2.0.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
makedepends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk2' 'gtk3' 'gst-plugins-base-libs' 'gperf' 'gobject-introspection' 'python' 'mesa' 'ruby' 'gtk-doc' 'libsecret' 'libwebp')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)
sha256sums=('454cdc4beffc2010f83661059178e33b6d9a07bcd60d09eabdf5d7cda17b88e4')

build() {
  cd "$srcdir/webkitgtk-$pkgver"
  mkdir build-gtk3
  cd build-gtk3
  ../configure --prefix=/usr \
    --enable-introspection \
    --disable-silent-rules \
    --libexecdir=/usr/lib/webkitgtk3
  make all stamp-po
}

package_webkitgtk3() {
  pkgdesc="GTK+ Web content engine library for GTK+ 3.0"
  depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk3' 'gst-plugins-base-libs' 'libsecret' 'libwebp')
  conflicts=('libwebkit3')
  provides=("libwebkit3=${pkgver}")
  replaces=('libwebkit3')
  optdepends=('gtk2: Webkit2 plugin support')
  
  cd "$srcdir/webkitgtk-$pkgver/build-gtk3"
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
