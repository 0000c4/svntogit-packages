# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=2.91.90
pkgrel=1
pkgdesc="Archive manipulator for GNOME2"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils' 'pkg-config' 'nautilus' 'libsm' 'namcap')
optdepends=('nautilus: nautilus shell extension'
	    'unrar: the RAR uncompression program'
	    'zip: creates PKZIP-compatible .zip files'
	    'unzip: unpacks .zip archives'
	    'p7zip: 7zip compression utility'
	    'xz: managing LZMA and XZ compressed files'
	    'arj: ARJ archiver'
	    'unace: extraction tool for the proprietary ace archive format')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c441c1fcc94c0a5ce3213f6ed8266ff4a5ff8f93db8e81dea4e83440e91540a9')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/file-roller \
        --localstatedir=/var --disable-static \
        --disable-scrollkeeper --disable-schemas-compile
  make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
