# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=at-spi2-atk
pkgver=0.4.0
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus-core>=1.4.0' 'dbus-glib=>0.86' 'atk>=1.32.0' 'gconf>=2.32.0')
makedepends=('intltool')
install=at-spi2-atk.install
options=('!libtool')
source=(http://download.gnome.org/sources/${pkgname}/0.4/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('2e2308cd67fe256e08085e499808f6e41b8d4967d91831ff7f25953898fc5b46')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain at-spi2-atk ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
