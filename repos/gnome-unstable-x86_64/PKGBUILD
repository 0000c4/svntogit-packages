# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=vte
pkgname=(vte vte3 vte-common)
pkgver=0.27.5
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
makedepends=('pygtk' 'intltool' 'gobject-introspection' 'gtk2' 'gtk3' 'pygobject-devel' 'gtk-doc')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/vte/0.27/vte-${pkgver}.tar.bz2)
sha256sums=('47541d480c50c95b53eb6bf1187561ae7eebc19556aec0c5dd281b5cb5c316c0')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  #warning: type-punning to incomplete type might break strict-aliasing rules
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  mkdir build-gtk2
  pushd build-gtk2
  PYTHON=python2 ../configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=2.0 \
      --enable-gtk-doc --disable-schemas-compile
  make
  popd

  mkdir build-gtk3
  pushd build-gtk3
  PYTHON=python2 ../configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=3.0 \
      --enable-gtk-doc --disable-schemas-compile
  make
  popd
}

package_vte(){
  pkgdesc+=" GTK2"
  depends=('gtk2' 'vte-common')
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk2
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/lib/vte/gnome-pty-helper
  rm "${pkgdir}"/usr/share/glib-2.0/schemas/org.gnome.vte.v0.enums.xml
}

package_vte3(){
  pkgdesc+=" GTK3"
  depends=('gtk3' 'vte-common')
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk3
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/lib/vte/gnome-pty-helper
  rm "${pkgdir}"/usr/share/glib-2.0/schemas/org.gnome.vte.v0.enums.xml
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  install=vte-common.install
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk3

  make -C gnome-pty-helper DESTDIR="${pkgdir}" install
  make -C src DESTDIR="${pkgdir}" install-dist_gsettingsschemaDATA
}
