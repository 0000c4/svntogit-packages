# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=2.29.5
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=('GPL')
depends=('libspectre>=0.2.3' 'gsfonts' 'poppler-glib>=0.12.3' 'libdjvu>=3.5.22' 'gnome-icon-theme>=2.28.0' 't1lib' 'libgnome-keyring>=2.29.4' 'desktop-file-utils')
makedepends=('gnome-doc-utils>=0.18.0' 'nautilus>=2.29.90' 'pkgconfig' 'texlive-bin' 'intltool')
optdepends=('texlive-bin: DVI support')
replaces=('gpdf')
groups=('gnome-extra')
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.29/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('5e2f0579d64ea2bcba17a0164f1b6e48773637b71a20d631aae1bc036df3634c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/evince \
      --disable-static --enable-nautilus \
      --enable-pdf --enable-tiff \
      --enable-djvu --enable-dvi \
      --enable-t1lib --enable-pixbuf \
      --enable-comics --enable-impress \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain evince ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
