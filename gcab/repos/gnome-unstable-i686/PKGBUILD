# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gcab
pkgver=0.7+7+g71da703
pkgrel=1
pkgdesc="A GObject library to create cabinet files"
arch=('i686' 'x86_64')
license=('LGPL')
url="https://wiki.gnome.org/msitools"
depends=('glib2')
makedepends=('gobject-introspection' 'vala' 'zlib' 'git' 'gtk-doc' 'intltool')
_commit=71da7035108eb7b1da72ff8d318432fa3bc0af58
source=("git://git.gnome.org/gcab#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/^v//' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
