# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=valadoc
pkgver=20160518+611+g273bf7a
pkgrel=2
pkgdesc="A documentation tool for vala"
arch=(i686 x86_64)
url="https://git.gnome.org/browse/valadoc"
license=(GPL2)
depends=(vala gtk-doc graphviz libgee)
makedepends=(git)
_commit=273bf7a7334da8c7f7500d0ffb98a877c69c63b3
source=("git://git.gnome.org/valadoc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  _date=$(git show --format="%cd" --date=format:"%Y%m%d" --no-patch)
  _count=$(git rev-list --count HEAD)
  _commit=$(git rev-parse --short HEAD)
  echo $_date+$_count+g$_commit
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
