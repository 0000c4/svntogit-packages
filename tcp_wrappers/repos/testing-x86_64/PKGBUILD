# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=tcp_wrappers
pkgver=7.6
pkgrel=9
pkgdesc="Monitors and Controls incoming TCP connections"
arch=(i686 x86_64)
url="ftp://ftp.porcupine.org/pub/security/index.html"
license=('custom')
groups=('base')
backup=(etc/hosts.allow etc/hosts.deny)
depends=('bash' 'glibc')
source=(ftp://ftp.porcupine.org/pub/security/${pkgname}_$pkgver.tar.gz \
        hosts.allow hosts.deny try-from.8 safe_finger.8 gcc340.patch \
        shared_lib_plus_plus-1.patch)
md5sums=('e6fa25f71226d090f34de3f6b122fb5a' 
	 '32cfeeed797161034f62bb45f3167baa'
         'a0ee30f6aeaca241c4d44f7c177eca6b' 
	 '4a8f40f9a69f0848df92b232072e8561'
         '1a6d7b11abb1fd69ace775d02a1c72cf' 
	 '19c3badd4fbee547eb9de61ed93691af'
         'e39dc7e099b741b6d2b1799a56ab77af')

build() {
  cd $srcdir/${pkgname}_$pkgver
  patch -Np1 -i ../gcc340.patch || return 1
  patch -Np1 -i ../shared_lib_plus_plus-1.patch || return 1
  make REAL_DAEMON_DIR=/usr/sbin STYLE=-DSYS_ERRLIST_DEFINED linux || return 1
  # dumb makefile
  mkdir -p $pkgdir/usr/{include,lib,sbin}
  mkdir -p $pkgdir/usr/share/man/man{3,5,8}
  make DESTDIR=$pkgdir install
  install -D -m644 ../hosts.allow $pkgdir/etc/hosts.allow
  install -D -m644 ../hosts.deny $pkgdir/etc/hosts.deny
  # add manpage symlinks for hosts.deny and hosts.allow
  cd $pkgdir/usr/share/man/man5
  ln -s hosts_access.5.gz hosts.allow.5.gz
  ln -s hosts_access.5.gz hosts.deny.5.gz
  # install license
  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  install -m644 $srcdir/${pkgname}_$pkgver/DISCLAIMER $pkgdir/usr/share/licenses/$pkgname/license.txt
}
