# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkcddb
pkgver=4.9.80
pkgrel=1
pkgdesc="KDE CDDB library"
url='https://projects.kde.org/projects/kde/kdemultimedia/libkcddb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'libmusicbrainz5')
makedepends=('cmake' 'automoc4')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('b7bfdec3f562deed9e2537051d497a72b1ec26a4')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
