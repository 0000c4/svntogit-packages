# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkcddb
pkgver=4.10.80
pkgrel=1
pkgdesc="KDE CDDB library"
url='https://projects.kde.org/projects/kde/kdemultimedia/libkcddb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'libmusicbrainz5')
makedepends=('cmake' 'automoc4')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('370d477be8f6377825e5cd9c7473fe201bad7dfe')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
