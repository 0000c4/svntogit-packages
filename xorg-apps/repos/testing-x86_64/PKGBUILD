# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-apps
pkgver=7.5
pkgrel=1
pkgdesc="Various X.Org applications"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(libxcursor libxkbfile libpng libxft libfontenc libxaw)
makedepends=(pkgconfig xbitmaps)
source=(${url}/releases/individual/app/oclock-1.0.1.tar.bz2
        ${url}/releases/individual/app/luit-1.0.4.tar.bz2
        ${url}/releases/individual/app/xclock-1.0.4.tar.bz2
        ${url}/releases/individual/app/xpr-1.0.2.tar.bz2
        ${url}/releases/individual/app/xwd-1.0.2.tar.bz2
        ${url}/releases/individual/app/xwud-1.0.1.tar.bz2
        ${url}/releases/individual/app/x11perf-1.5.tar.bz2
        ${url}/releases/individual/app/xbiff-1.0.1.tar.bz2
        ${url}/releases/individual/app/xclipboard-1.0.1.tar.bz2
        ${url}/releases/individual/app/xconsole-1.0.3.tar.bz2
        ${url}/releases/individual/app/xcursorgen-1.0.2.tar.bz2
        ${url}/releases/individual/app/xeyes-1.0.991.tar.bz2
        ${url}/releases/individual/app/xkill-1.0.1.tar.bz2
        ${url}/releases/individual/app/xload-1.0.2.tar.bz2
        ${url}/releases/individual/app/xlogo-1.0.1.tar.bz2
        ${url}/releases/individual/app/xmag-1.0.3.tar.bz2
        ${url}/releases/individual/app/xmessage-1.0.2.tar.bz2
        ${url}/releases/individual/app/xcalc-1.0.2.tar.bz2
	${url}/releases/individual/app/xman-1.0.3.tar.bz2
	${url}/releases/individual/app/xedit-1.1.2.tar.bz2
	${url}/releases/individual/app/xmh-1.0.1.tar.bz2 LICENSE)
md5sums=('91f49547f9ed3cd0137c8b7c3183e360'
         '4e45233e310d72dce307709761cf241b'
         'bb9fd5e00d39c348a0078b97fdf8258f'
         '6b3a6896081f628bf5a2c9129417c86f'
         '0a6ef08a2ac08ad5c4dd1522eb3788a3'
         '6e3c5d0297d88e890b6f5df31f73dd60'
         '31283bfc3c78718ac1bd71e510d4e774'
         '404f5add4537d22dd109c33e518a5190'
         '2c6ecedb10dc51adbb64c95f22fd99c2'
         '0e1a3110bebabecc2897d67a973526b0'
         '6fc90896b8c786cb1a2100b4167f7874'
         'ba1b886bd6104018fe78ab431d83ce4d'
         'f66d76abb0f75514ca32272e23cca757'
         'b41ed6b4bcfc9897366c27a94d2bf150'
         '4c5482552f38a7d42398a694cc9b2ee6'
         '32f7ed4c089365cadb9382f6fbd750a9'
         'b4b561ef11fd184989a6062962e86748'
         'd31a99795b9668f047aa11bf36df6df0'
         '3d3a4b310a65ccce82472ef83acbbf97'
         '67193be728414d45a1922911e6437991'
         '656bcbdd41818a8b5a9f7dba77a3eeba'
         '1a3977bcff6dc0fc1086e0650fcea31a')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in 
        x11perf*)
          sed -e 's|^LIBPATH = $(libdir)|LIBPATH = $(datadir)|' \
	      -i Makefile.* || return 1
	;;
	xedit*)
	  sed -e 's|^XEDITDIR = ${libdir}|XEDITDIR = ${datadir}|' \
	      -i Makefile.* || return 1
	;;
      esac
      ./configure --prefix=/usr --disable-xprint --mandir=/usr/share/man \
                  --with-localealiasfile=/usr/share/X11/locale/locale.alias
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      popd
    fi
  done
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
