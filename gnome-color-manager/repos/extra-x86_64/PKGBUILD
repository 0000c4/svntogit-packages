# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=gnome-color-manager
pkgver=3.2.1
pkgrel=1
pkgdesc="Color profile manager for the GNOME desktop"
arch=(i686 x86_64)
url="http://projects.gnome.org/gnome-color-manager/"
license=(GPL2)
depends=('libcanberra' 'vte3' 'exiv2' 'hicolor-icon-theme' 'desktop-file-utils' 'clutter-gtk' 'mash')
makedepends=('intltool' 'gtk-doc')
install=gnome-color-manager.install
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('bc4651b58a4dc4b812803e8f81520488e6b452969028f04987cb463b8e218436')

build(){
  cd "$srcdir/$pkgname-$pkgver"

  # man pages need docbook-sgml, which we don't have
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-color-manager \
      --disable-static --disable-man-pages

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
