# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-color-manager
pkgver=3.32.0
pkgrel=1
pkgdesc="GNOME Color Profile Tools"
url="https://gitlab.gnome.org/GNOME/gnome-color-manager"
arch=(x86_64)
license=(GPL2)
depends=(libcanberra vte3 exiv2 shared-color-targets colord-gtk libexif)
makedepends=(yelp-tools git appstream-glib meson docbook-utils docbook-sgml perl-sgmls)
checkdepends=(xorg-server-xvfb)
optdepends=('argyllcms: color calibration')
groups=(gnome)
_commit=4d7296b72c36ab46b5aded52f7fb2ebcae22063a  # tags/GNOME_COLOR_MANAGER_3_32_0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-color-manager.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_COLOR_MANAGER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D packagekit=false
  ninja -C build
}

check() {
  xvfb-run meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
