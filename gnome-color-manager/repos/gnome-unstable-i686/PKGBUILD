# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=gnome-color-manager
pkgver=3.1.91
pkgrel=1
pkgdesc="Color profile manager for the GNOME desktop"
arch=(i686 x86_64)
url="http://projects.gnome.org/gnome-color-manager/"
license=(GPL2)
depends=(colord gnome-settings-daemon gnome-control-center vte3 exiv2
         hicolor-icon-theme desktop-file-utils clutter-gtk mash)
makedepends=(intltool gtk-doc)
install=gnome-color-manager.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('bc8907cd72bdc399b6cd900f940eb3baa858d52e5ca15883fc5a849bdf76c6a6')

build(){
  cd "$srcdir/$pkgname-$pkgver"

  # man pages need docbook-sgml, which we don't have
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --disable-man-pages

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
