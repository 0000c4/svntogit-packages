# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-color-manager
pkgver=3.26.0+22+g4de1fd1
pkgrel=1
pkgdesc="GNOME Color Profile Tools"
url="https://git.gnome.org/browse/gnome-color-manager"
arch=(x86_64)
license=(GPL2)
depends=(libcanberra vte3 exiv2 shared-color-targets colord-gtk libexif)
makedepends=(itstool git appstream-glib meson docbook-utils docbook-sgml perl-sgmls)
optdepends=('argyllcms: color calibration')
groups=(gnome)
_commit=4de1fd10d4fcc17188af7558ea95ea8d766c2682  # master
source=("git+https://git.gnome.org/browse/gnome-color-manager#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_COLOR_MANAGER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-packagekit=false
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
