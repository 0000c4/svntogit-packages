# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-todo
pkgver=3.20.2+6+g8f1522f
pkgrel=1
pkgdesc="Task manager for GNOME"
url="https://wiki.gnome.org/Apps/Todo"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server libpeas)
makedepends=(intltool gobject-introspection gnome-common appstream-glib git)
groups=(gnome-extra)
_commit=8f1522fd8e0f325a22c5f567c07992011dcf01be  # gnome-3-20
source=("git://git.gnome.org/gnome-todo#commit=$_commit"
        buildfix.patch)
sha256sums=('SKIP'
            'ab88726d81ba076dd30067c0ccb08fe946b76b06c6e8664648d2ac1ff496f821')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_TODO_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f GNOME_TODO_3_20_2 50f0362fb87355d9cfed95c9c69b3f383a5eaa85
  patch -Np1 -i ../buildfix.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
