# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-todo
pkgver=3.22.1
pkgrel=1
pkgdesc="Task manager for GNOME"
url="https://wiki.gnome.org/Apps/Todo"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server libpeas)
makedepends=(intltool gobject-introspection gnome-common appstream-glib git)
optdepends=('python3: Some plugins')
groups=(gnome-extra)
_commit=ffe016ce5874cf8d70ce29cd96bf948d977c6025  # tags/GNOME_TODO_3_22_1^0
source=("git+https://git.gnome.org/browse/gnome-todo#commit=$_commit"
        buildfix.patch)
sha256sums=('SKIP'
            '5018a974f63ef29801740638638b08a5766ddebfd004fe162acfed8022b6a415')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_TODO_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../buildfix.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
