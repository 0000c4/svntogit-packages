# $Id$
# Maintainer:

pkgname=freetds
pkgver=1.00.85
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='http://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('openssl' 'unixodbc')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/$pkgname-$pkgver.tar.bz2")
sha512sums=('db756f05d6a30279f9445d5da64e17615a49226445fcae5a5ff75cf1809bceca46e8cda4740ad4ffb6d736e9404ec62e9ec81029f646b0dab37241fb6f379bdd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/freetds --enable-msdblib \
    --with-tdsver=7.0 --with-unixodbc=/usr --with-openssl
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
