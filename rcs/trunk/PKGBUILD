# $Id$
# Contributor: dorphell <dorphell@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=rcs
pkgver=5.8.1
pkgrel=1
pkgdesc='Revision Control System: manages multiple revisions of files'
url='http://www.gnu.org/software/rcs/'
license=('GPL3')
arch=('i686' 'x86_64')
source=("http://ftpmirror.gnu.org/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('3317a9e0f324a094e48584831e1b23852f907af9')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make prefix="${pkgdir}/usr" install
}
