# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
pkgname=haskell-stm
pkgver=2.1.2.1
pkgrel=1
pkgdesc="A modular composable concurrency abstraction."
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/stm"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 'haskell-array=0.3.0.1' sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/stm/$pkgver/stm-$pkgver.tar.gz)
install=$pkgname.install

build() {
    cd $srcdir/stm-$pkgver
    runhaskell Setup configure -p --enable-split-objs --enable-shared --prefix=/usr || return 1
    runhaskell Setup build                   || return 1
    runhaskell Setup register   --gen-script || return 1
    runhaskell Setup unregister --gen-script || return 1
    install -D -m744 register.sh   $pkgdir/usr/share/haskell/$pkgname/register.sh
    install    -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
    runhaskell Setup copy --destdir=$pkgdir || return 1
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE || return 1
}
md5sums=('3e88c921b0f06701b3adada24b54576e')
