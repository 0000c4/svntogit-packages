# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=17.11.90
pkgrel=2
pkgdesc="Common libraries for KDE PIM"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(libkdepim kpimtextedit knewstuff kimap)
makedepends=(extra-cmake-modules python boost qt5-tools kdesignerplugin)
conflicts=('libkdepim<16')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig} pimcommon-qt5.10.patch)
sha256sums=('9a97ec976935ac634d15cee6da33d634cc496680eacae6bc9d2ed2453fb30edc'
            'SKIP'
            '3b548daac533b3f0bcca5820c90b8c8d49a6e91d55c49469ef8f5b098dbe4d3b')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../pimcommon-qt5.10.patch # Qt API change
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
