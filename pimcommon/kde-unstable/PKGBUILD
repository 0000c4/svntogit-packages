# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=16.03.90
pkgrel=1
pkgdesc="Common libraries for KDE PIM"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(libkdepim kpimtextedit knewstuff kimap)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools)
conflicts=('libkdepim<16')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('5bc1ace71007e61387073355e1ba9090')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
