# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-exiftool
_srcname=Image-ExifTool
pkgver=8.50
pkgrel=1
license=('GPL')
pkgdesc="Reader and rewriter of EXIF informations that supports raw files"
url="http://www.sno.phy.queensu.ca/~phil/exiftool/"
arch=(any)
depends=('perl')
source=(http://search.cpan.org/CPAN/authors/id/E/EX/EXIFTOOL/${_srcname}-${pkgver}.tar.gz)
options=(!emptydirs)
md5sums=('58b56828b56b5be92c857ec89ae93925')

build() {
  cd ${srcdir}/${_srcname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd ${srcdir}/${_srcname}-${pkgver}
  make DESTDIR=${pkgdir} install
  # remove *.pod, .packlist, and empty dirs:
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}

