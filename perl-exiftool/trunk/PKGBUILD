# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-exiftool
_srcname=Image-ExifTool
pkgver=8.72
pkgrel=1
pkgdesc="Reader and rewriter of EXIF informations that supports raw files"
arch=('any')
url="http://www.sno.phy.queensu.ca/~phil/exiftool/"
license=('GPL')
depends=('perl')
options=('!emptydirs')
source=(http://www.sno.phy.queensu.ca/~phil/exiftool/${_srcname}-$pkgver.tar.gz)
md5sums=('9ed54dc1b981188e34013911ce4e24f5')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  make test
}


package() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # remove *.pod, .packlist, and empty dirs:
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}

