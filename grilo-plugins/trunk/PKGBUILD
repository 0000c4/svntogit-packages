# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.1
pkgrel=1
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('grilo' 'sqlite' 'libgcrypt')
makedepends=(gupnp-av libgdata libquvi gmime rest libtracker-sparql totem-plparser)
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'libquvi: Youtube plugin'
            'sqlite: Podcasts plugin'
            'gmime: Podcasts plugin'
            'rest: Blip.tv plugin'
            'libtracker-sparql: Tracker plugin'
            'totem-plparser: Optical media plugin')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('d437e146bfdf7f2f4b6306131676f564f5ad02f904089e43b1b8a1f1fe99f895')

build() {
  cd $pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
