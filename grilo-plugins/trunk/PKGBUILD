# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.8
pkgrel=1
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo sqlite libgcrypt)
makedepends=(gupnp-av libgdata libquvi gmime rest libtracker-sparql totem-plparser libdmapsharing
             json-glib intltool)
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'libquvi: Youtube plugin, Vimeo plugin'
            'gmime: Podcasts plugin'
            'rest: Blip.tv plugin'
            'libtracker-sparql: Tracker plugin'
            'totem-plparser: Optical media plugin'
            'libdmapsharing: DMAP sharing plugin'
            'json-glib: TMDb plugin')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('28f83fd701b33aed7b843e5e3850d7d961310c7cb33ffebcb652160fff8ccd80')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
