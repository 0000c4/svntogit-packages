# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.3.5+12+g7ecf4b2
pkgrel=1
pkgdesc="A collection of plugins for the Grilo framework"
url="https://git.gnome.org/browse/grilo-plugins"
arch=(x86_64)
license=(LGPL)
depends=(grilo lua libmediaart libgdata tracker gom libdmapsharing chromaprint gst-plugins-bad
         gmime3)
makedepends=(git meson gperf)
checkdepends=(gst-plugins-good gst-plugins-base)
optdepends=('dleyna-server: DLNA plugin'
            'tracker-miners: Tracker plugin')
groups=(gnome)
_commit=7ecf4b230c7b0144e579f89b594bf2ce871d19f1  # master
source=("git+https://git.gnome.org/browse/grilo-plugins#commit=$_commit"
        luafiles.diff)
sha256sums=('SKIP'
            '73b388d9564ae8c1d3f464831082acffd46b35bdc85a3a8bdb25f408f9c6dc48')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^grilo-plugins-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../luafiles.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  # Unreliable tests using websites...
  meson test || :
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
