# $Id$
# Maintainer: Jürgen Hötzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
_pkgver=3.8c
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('openssl' 'sqlite' 'alsa-lib' 'emacs-24.1')
makedepends=('java-environment' 'emacs' 'zip')
optdepends=('emacs' 'java-environment')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${pkgname}${_pkgver}.tar.gz)
md5sums=('ceea21b14c8f7270be5223dce713ec6d')

build() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --disable-gstreamer \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --jvm=yes
  make build compile-bee
}

check() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install install-bee
  make -C manuals DESTDIR="${pkgdir}" install-bee
  chmod 644 "${pkgdir}"/usr/lib/bigloo/${_pkgver%-*}/*.a
}
