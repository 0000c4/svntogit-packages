# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jonahtan Schaeffer <joschaeffer@gmail.com>
# COntributor: Drake Arconis <lightdrake@gmail.com>

pkgname=jsoncpp
pkgver=1.7.3
pkgrel=2
pkgdesc='A C++ library for interacting with JSON'
url='https://github.com/open-source-parsers/jsoncpp'
license=('MIT' 'custom:Public_Domain')
arch=('i686' 'x86_64')
depends=('gcc-libs')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/open-source-parsers/${pkgname}/archive/${pkgver}.tar.gz"
        fix_pkgconfig.patch::https://github.com/open-source-parsers/jsoncpp/commit/101fcf0806bbdf8a9202cabd668e650c9ce8cfb1.patch)
md5sums=('aff6bfb5b81d9a28785429faa45839c5'
         'b717297986fba231c97e16a9be40f3c9')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../fix_pkgconfig.patch
}

build() {
  cd ${pkgname}-${pkgver}
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=OFF
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd  ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

