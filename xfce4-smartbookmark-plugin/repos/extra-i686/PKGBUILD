# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-smartbookmark-plugin
pkgver=0.4.4
pkgrel=1
pkgdesc="Plugin for the Xfce4 panel that let you quicksearch from selected websites"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-smartbookmark-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4')
makedepends=('intltool' 'libxt')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/0.4/${pkgname}-${pkgver}.tar.bz2
	xfce4-smartbookmark-plugin-archlinux.patch)
md5sums=('273e38306a82f14e1b8c2c5db912f6c3'
         '323ac898cfcfdb078f67f9ecd1905aec')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/xfce4-smartbookmark-plugin-archlinux.patch"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
