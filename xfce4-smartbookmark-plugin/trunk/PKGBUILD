# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-smartbookmark-plugin
pkgver=0.4.2
pkgrel=9
pkgdesc="plugin for the Xfce4 panel that let you quicksearch from selected websites"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-smartbookmark-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4')
makedepends=('intltool' 'libxt')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/0.4/${pkgname}-${pkgver}.tar.gz
	xfce4-smartbookmark-plugin-0.4.2-desktop-file.patch
	xfce4-smartbookmark-plugin-archlinux.patch)
md5sums=('284e26595637dd2e900b75534372496b'
         'ca6ecc8ff90cc13ef81d6412978dfac7'
         '323ac898cfcfdb078f67f9ecd1905aec')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # fix path, taken from Fedora
  patch -Np0 -i ${srcdir}/xfce4-smartbookmark-plugin-0.4.2-desktop-file.patch || return 1
  patch -Np0 -i ${srcdir}/xfce4-smartbookmark-plugin-archlinux.patch || return 1
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
