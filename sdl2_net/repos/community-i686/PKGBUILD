# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=sdl2_net
pkgver=249
pkgrel=1
pkgdesc="A small sample cross-platform networking library (Version 2)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=(sdl2)
makedepends=(mercurial)
options=(!libtool)
source=("sdl2_net::hg+http://hg.libsdl.org/SDL_net#revision=$pkgver")
md5sums=('SKIP')

build() {
  cd "${srcdir}/sdl2_net/"

  ./autogen.sh
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd "${srcdir}/sdl2_net/"

  make DESTDIR="${pkgdir}/" install

  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
