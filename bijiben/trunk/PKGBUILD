# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=bijiben
pkgver=3.30.3
pkgrel=1
pkgdesc="Write out notes, every detail matters"
url="https://wiki.gnome.org/Apps/Bijiben"
arch=(x86_64)
license=(GPL)
depends=(tracker webkit2gtk gnome-online-accounts evolution-data-server)
makedepends=(evolution python appstream-glib yelp-tools git meson)
_commit=60268fcb82f76c11de4b0f21a8f231444bdd6d6c  # tags/BIJIBEN_3_30_3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-notes.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd gnome-notes
  git describe --tags | sed 's/^BIJIBEN_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd gnome-notes

  git submodule init
  git config --local submodule.subprojects/libgd.url "$srcdir/libgd"
  git submodule update
}

build() {
  arch-meson gnome-notes build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
