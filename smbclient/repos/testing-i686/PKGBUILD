# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=smbclient
pkgver=3.2.4
# We use the 'A' to fake out pacman's version comparators.  Samba chooses
# to append 'a','b',etc to their subsequent releases, which pamcan
# misconstrues as alpha, beta, etc.  Bad samba!
_realver=3.2.4
pkgrel=3
pkgdesc="Tools to access a server's filespace and printers via SMB"
arch=(i686 x86_64)
url="http://www.samba.org"
license=('GPL3')
depends=('popt' 'libldap' 'libcap' 'heimdal>=1.2-1' 'db>=4.7' 'tdb=3.2.4')
source=(http://us1.samba.org/samba/ftp/samba-${_realver}.tar.gz)
options=(!makeflags)

build() {
  cd ${startdir}/src/samba-${_realver}/source

  ./configure --prefix=/usr --with-cifsmount --with-fhs --with-pam \
              --with-datadir=/usr/share --with-configdir=/etc/samba \
              --with-lockdir=/var/run/samba --with-ads --with-acl-support \
	      --localstatedir=/var --disable-dnssd  --libdir=/usr/lib/samba

  make proto bin/smbclient libsmbclient \
       bin/rpcclient bin/smbspool bin/smbtree \
       bin/smbcacls bin/smbcquotas bin/smbget \
       bin/net bin/nmblookup bin/mount.cifs bin/umount.cifs || return 1
  mkdir -p ${startdir}/pkg/usr/bin ${startdir}/pkg/sbin ${startdir}/pkg/usr/lib/samba
  install -m755 bin/{smbclient,rpcclient,smbspool,smbtree,smbcacls,smbcquotas,smbget,net,nmblookup} ${startdir}/pkg/usr/bin/
  install -m755 bin/{mount.cifs,umount.cifs} ${startdir}/pkg/sbin/
  mv  bin/*.so* ${startdir}/pkg/usr/lib/samba
  install -m755 script/smbtar ${startdir}/pkg/usr/bin/
  mkdir -p ${startdir}/pkg/usr/lib/cups/backend
  ln -sf /usr/bin/smbspool ${startdir}/pkg/usr/lib/cups/backend/smb
  for i in ${startdir}/pkg/usr/lib/samba/*; do
   	ln -sf samba/$(basename $i) ${startdir}/pkg/usr/lib/$(basename $i)
  done
  rm -rf $startdir/pkg/usr/lib/libtdb*
  mkdir -p ${startdir}/pkg/usr/include
  install -m644 include/libsmbclient.h ${startdir}/pkg/usr/include/
  install -m644 lib/talloc/talloc.h ${startdir}/pkg/usr/include/
  install -m644 lib/netapi/netapi.h ${startdir}/pkg/usr/include/
  mkdir -p ${startdir}/pkg/usr/share/man/man{1,7,8}
  for man in smbspool \
      umount.cifs mount.cifs net; do
    install -m644 ../docs/manpages/${man}.8 ${startdir}/pkg/usr/share/man/man8/
  done
  for man in rpcclient smbcacls smbclient smbcquotas smbget \
      smbtree smbtar nmblookup; do
    install -m644 ../docs/manpages/${man}.1 ${startdir}/pkg/usr/share/man/man1/
  done
  install -m644 ../docs/manpages/libsmbclient.7 ${startdir}/pkg/usr/share/man/man7/
}
md5sums=('7870a6bb3a025c6081cd56688acd494a')
