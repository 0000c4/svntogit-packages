# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=mkinitcpio-busybox
pkgver=1.15.1
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('uclibc-runtime')
makedepends=('uclibc' 'gcc-uclibc')
source=(http://busybox.net/downloads/busybox-${pkgver}.tar.bz2
        config)
sha256sums=('7f623cb0c9281aefafeacf3b4f925debc7516d263ff469a82e548da415e575cd'
            '1d3eaf5d2272eff8dc3d0b95f4427d7de834567bd313a0f6506c38fa7b027538')

build() {
  cd "${srcdir}/busybox-${pkgver}"

  if [ ${CARCH} = "x86_64" ]; then
    sed 's|^CONFIG_CROSS_COMPILER_PREFIX=.*|CONFIG_CROSS_COMPILER_PREFIX="x86_64-unknown-linux-uclibc-"|' "${srcdir}/config" > .config
  else
    sed 's|^CONFIG_CROSS_COMPILER_PREFIX=.*|CONFIG_CROSS_COMPILER_PREFIX="i686-pc-linux-uclibc-"|' "${srcdir}/config" > .config
  fi
  make || return 1
  install -D -m755 busybox "${pkgdir}/usr/lib/initramfs/base/busybox"
}
# vim:set ts=2 sw=2 et:
