# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=jsch
pkgname=java-${_libname}
pkgver=0.1.48
pkgrel=1
pkgdesc="A pure Java implementation of SSH2"
arch=('any')
url="http://www.jcraft.com/jsch/"
license=('BSD')
depends=('java-runtime-headless')
makedepends=('apache-ant')
source=(http://downloads.sourceforge.net/project/${_libname}/${_libname}/${pkgver}/${_libname}-${pkgver}.zip)
md5sums=('9422dbbce4466be80be35b47e60dae25')

build() {
  cd "${srcdir}/${_libname}-${pkgver}"
  ant dist
}

package() {
  install -Dm644 ${srcdir}/${_libname}-${pkgver}/dist/lib/${_libname}-${pkgver}.jar \
                 ${pkgdir}/usr/share/java/${_libname}-${pkgver}.jar
  ln -s ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}.jar
  install -Dm644 ${srcdir}/${_libname}-${pkgver}/LICENSE.txt \
                 ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
