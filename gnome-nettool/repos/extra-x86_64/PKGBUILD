# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-nettool
pkgver=3.2.0
pkgrel=2
pkgdesc="A Collection of GNOME3 Networking Tools."
arch=(i686 x86_64)
license=('GPL')
depends=('gtk3' 'libgtop' 'ldns' 'iputils' 'dconf' 'whois' 'hicolor-icon-theme')
makedepends=('gnome-doc-utils' 'intltool' 'nmap' 'net-tools')
optdepends=('nmap: Port scanning'
            'net-tools: Network statistics')
groups=('gnome-extra')
url="http://www.gnome.org"
install=gnome-nettool.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        ldns.patch)
sha256sums=('9888202f1c13f98fd26b417468e6f2345f885557f3a98fb365eeea52d792bf79'
            '8bd83fe863d2945689afd753e5908a2bc033206e25f28d88783ae8d859d6bfeb')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../ldns.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
