# $Id$
# Contributor: Tom K <tomk@runbox.com>

pkgname=epdfview
pkgver=0.1.6
pkgrel=10
pkgdesc="A free lightweight PDF document viewer."
arch=('i686' 'x86_64')
url="http://www.emma-soft.com/projects/epdfview/"
license=('GPL')
depends=('poppler-glib>=0.8.3' 'heimdal>=1.2-1' 'libcups>=1.3.7-2' 'gnutls>=2.4.1' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkgconfig')
install=epdfview.install
source=(http://www.emma-soft.com/projects/epdfview/chrome/site/releases/epdfview-${pkgver}.tar.bz2
	epdfview-0.1.6-print-segfault.patch
	IDocumentLink.patch
	epdfview.desktop.patch)
md5sums=('cce9edb41b4a8308e0ef0eea24b5a1ab' 'c083ee8e0e226b308e1c38cb94ee42e7'\
         '65444f1e85b635c37a28e070fe43eeaf' 'c7f887fb9da98d49bb35bdde68073440')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ../epdfview-0.1.6-print-segfault.patch || return 1
  patch -Np0 -i ../IDocumentLink.patch || return 1
  patch -Np0 -i ../epdfview.desktop.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -d ${pkgdir}/usr/share/icons/hicolor/{24x24,32x32,48x48}/apps || return 1
  install -m644 data/icon_epdfview-24.png "${pkgdir}/usr/share/icons/hicolor/24x24/apps/epdfview.png" || return 1
  install -m644 data/icon_epdfview-32.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/epdfview.png" || return 1
  install -m644 data/icon_epdfview-48.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/epdfview.png" || return 1
}
