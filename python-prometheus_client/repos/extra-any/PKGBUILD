# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=(python-prometheus_client python2-prometheus_client)
pkgver=0.4.2
pkgrel=1

pkgdesc="Prometheus instrumentation library for Python applications"
url="https://github.com/prometheus/client_python"
arch=('any')
license=('APACHE')

makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
checkdepends=('python-twisted' 'python2-twisted' 'python2-futures')

source=(client_python-$pkgver.tar.gz::https://github.com/prometheus/client_python/archive/v${pkgver}.tar.gz)
sha512sums=('5e0fee9f9fb4376dcf2959e8285b57a9156d3cf8b9d9118c7b95d2ff63f4dd925bc8cea817bf2fc1ba660e6f5c6dd8f4cf494125cf2a3673443abe0f3a4657c4')

prepare() {
  cp -r client_python-$pkgver client_python-$pkgver-py2
}

build() {
  cd client_python-$pkgver
  python setup.py build

  cd "$srcdir"/client_python-$pkgver-py2
  python2 setup.py build
}

check() {
  cd client_python-$pkgver
  python setup.py test

  cd "$srcdir"/client_python-$pkgver-py2
  python2 setup.py test
}

package_python-prometheus_client() {
  depends=('python')

  cd client_python-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}

package_python2-prometheus_client() {
  depends=('python2')

  cd client_python-$pkgver
  python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
}
