# $Id$
# Maintainer : Aaron Griffin <aaron@archlinux.org>

pkgname=oprofile
pkgver=0.9.6
pkgrel=3
pkgdesc="A system-wide profiler for Linux systems, capable of profiling all running code at low overhead"
arch=('i686' 'x86_64')
url="http://oprofile.sourceforge.net"
license=('GPL')
depends=('popt')
makedepends=('qt3')
optdepends=('qt3: for oprof_start gui')
options=('!libtool')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('4e407093ac06200185d5a5e6437d7242')
sha1sums=('cc62cc58c574e235bc146c8ddc9d9a9af0972fd1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-kernel-support \
    --with-x
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

# installing development libraries
  install -m644 libutil/op_types.h libop/op_sample_file.h libutil/op_list.h libop/op_events.h libop/op_cpu_type.h libop/op_config.h libdb/odb.h "$pkgdir/usr/include"
  install -m644 libop/libop.a libdb/libodb.a "$pkgdir/usr/lib/"
  install -m644 libabi/libabi.a "$pkgdir/usr/lib/libopabi.a"
  install -m644 libutil/libutil.a "$pkgdir/usr/lib/liboputil.a"
  install -m644 libutil++/libutil++.a "$pkgdir/usr/lib/liboputil++.a"
}
