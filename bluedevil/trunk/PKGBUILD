# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=bluedevil
pkgver=5.6.3
pkgrel=1
epoch=1
pkgdesc='Integrate the Bluetooth technology within KDE workspace and applications'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('GPL2')
depends=('bluez-qt' 'kio')
makedepends=('extra-cmake-modules' 'plasma-framework' 'kded' 'python')
conflicts=('bluedevil-frameworks')
replaces=('bluedevil-frameworks')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('fa6623e533a81208b47184eb4bc7bd03')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
