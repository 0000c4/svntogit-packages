# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil
pkgver=2.0.gitdfbf95b
pkgrel=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil')
makedepends=('cmake' 'automoc4' 'git')
install="${pkgname}.install"
source=("git://anongit.kde.org/${pkgname}.git#branch=bluez5")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
  echo "2.0.git$(git log --pretty=format:'%h' -n 1)"
}

prepare() {
  mkdir build
}

build(){
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
