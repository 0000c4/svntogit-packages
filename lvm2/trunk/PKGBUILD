# $Id$
# Maintainer: Eric Bélanger <eric@archlinux.org>
# Maintainer: Thomas Bächler <thomas@archlinux.org>

pkgbase=lvm2
pkgname=('lvm2' 'device-mapper')
pkgver=2.02.82
_pkgverlvm=${pkgver}
pkgrel=2
arch=('i686' 'x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2.1')
groups=('base')
conflicts=('mkinitcpio<0.5.99')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_pkgverlvm}.tgz
        lvm2_install
        lvm2_hook
        lvm2_hotfix_udevsync.patch)
md5sums=('21c0ea7bb961d9340cd8bde2de5e2389' 'e29bc80d636ed17b617d9b384a5f3aa0'\
         '7103e3fef1180478811ac4e32aec540d'
         '438dde628a7abed5de3ea03713f05898')
sha1sums=('fa6258c307b5da807747d2b58d3cbc9b3ac99fa1' '662fc54ce291fd79bd09701e4c3e10854f325282'\
         '769767d84c73674cbb68c765460e57c428c314a0'
         '1728f06ef116d9428bcf32037fafa08ef16c7b22')

build() {
  cd "${srcdir}/LVM2.${_pkgverlvm}"
  sed -i 's|/usr/bin/tr|/bin/tr|' scripts/lvmdump.sh
  patch -p1 -i "${srcdir}"/lvm2_hotfix_udevsync.patch
  unset LDFLAGS
  ./configure --prefix= --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
    --includedir=/usr/include --with-usrlibdir=/usr/lib \
    --enable-pkgconfig --enable-readline --enable-dmeventd --enable-cmdlib --enable-applib \
    --with-udevdir=/lib/udev/rules.d/ --enable-udev_sync --enable-udev_rules
  make
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"
  depends=('glibc' 'udev')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_device-mapper
}

package_lvm2() {
  pkgdesc="Logical Volume Manager 2 utilities"
  depends=('bash' "device-mapper>=${pkgver}" 'udev' 'readline')
  conflicts=('lvm' 'mkinitcpio<0.5.99')
  backup=('etc/lvm/lvm.conf')
  options=('!makeflags')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_lvm2
  # install applib
  cd liblvm
  make DESTDIR="${pkgdir}" install
  cd ..
  # /etc directories
  install -d "${pkgdir}"/etc/lvm/{archive,backup}
  # mkinitcpio hook
  install -D -m644 "${srcdir}/lvm2_hook" "${pkgdir}/lib/initcpio/hooks/lvm2"
  install -D -m644 "${srcdir}/lvm2_install" "${pkgdir}/lib/initcpio/install/lvm2"
}
