# $Id$
# Maintainer:
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=2.32.0
pkgrel=2
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('libunique>=1.1.6' 'libnotify>=0.7.1' 'hicolor-icon-theme' 'gvfs-obexftp>=1.6.3' 'obexd-client>=0.33' 'dconf')
makedepends=('intltool' 'pygtk>=2.22' 'gnome-doc-utils>=0.20.1' 'nautilus-sendto>=2.28.4')
replaces=('bluez-gnome')
conflicts=('bluez-gnome')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        61-gnome-bluetooth-rfkill.rules
        gnome-bluetooth-2.32.0-libnotify-0.7.patch)
sha256sums=('57b1f06c96a1b85e1c19ff919d708cc38e95edae658881ed99968c325839a973'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474'
            '42b8388156249bd7c8a18db6d6aef18895ce861f2a79cb937e55eda864245838')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/gnome-bluetooth-2.32.0-libnotify-0.7.patch"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile \
    --enable-introspection=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m644 -D "${srcdir}/61-gnome-bluetooth-rfkill.rules" \
                     "${pkgdir}/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
