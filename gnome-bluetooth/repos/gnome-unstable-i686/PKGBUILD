# $Id$
# Maintainer:
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=2.91.91
pkgrel=2
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gnome-control-center' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus-sendto' 'gobject-introspection')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2
        61-gnome-bluetooth-rfkill.rules)
sha256sums=('5f4a27f62cb3587831da5d4183b9898ba48c354735c0c66e191e7aa327fecf55'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m644 -D "${srcdir}/61-gnome-bluetooth-rfkill.rules" \
                     "${pkgdir}/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/gnome-bluetooth" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
}
