# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=0.12.0
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem."
arch=('i686' 'x86_64')
url="http://www.usefulinc.com/software/gnome-bluetooth/"
license=('GPL' 'LGPL')
depends=('libbtctl>=0.10.0' 'libgnomeui>=2.24.1' 'desktop-file-utils' 'gconf>=2.26.0-2')
makedepends=('gob2' 'intltool' 'pygtk>=2.14.1')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-bluetooth/0.12/${pkgname}-${pkgver}.tar.bz2)
md5sums=('881a01ce964b8c02b5d40be8b5aedcaf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  aclocal || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var || return 1
  sed -e 's|^pythondir = /usr|pythondir = |' \
      -e 's|^pydir = /usr|pydir = |' \
      -i src/Makefile || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
     
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas" || return 1
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-bluetooth ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
