# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs
pkgver=4.1.4
pkgrel=5
pkgdesc="A kernel-based automounter for Linux."
arch=('i686' 'x86_64')
url="ftp://ftp.kernel.org/pub/linux/daemons/autofs"
license=('GPL2')
depends=('libldap')
backup=(etc/autofs/auto.master etc/conf.d/autofs)
source=($url/v4/${pkgname}-${pkgver}.tar.bz2
        ${pkgname}
        ${pkgname}.conf.d
        auto.master
        auto.misc
        auto.net)
md5sums=('7e3949114c00665b4636f0c318179657' '80b77182aa957646a2074433d3890518'\
         'ddd69346754419faee15fc4c7edeb8b8' '0543a0d172eaec300e8d08708440dd4b'\
         '0be409ef0cdf0255ce701ed4ba9e46b5' '74d102e637e53050ef095f1cbd9d461b')

build() {
  export MAKEFLAGS="-j1"

  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i "s:-O3 -fomit-frame-pointer -Wall:${CFLAGS}:" \
    Makefile.rules
  sed -i "s:SUBDIRS = lib daemon modules man samples:SUBDIRS = lib daemon modules man:" \
    Makefile.rules
  ./configure --prefix=/usr --sysconfdir=/etc/autofs
  make || return 1
  make INSTALLROOT=${pkgdir} install
  
  local conf_file
  for conf_file in auto.{master,misc}; do
    install -D -m 644 ${srcdir}/$conf_file \
      ${pkgdir}/etc/autofs/$conf_file
  done

  install -m 755 ${srcdir}/auto.net \
    ${pkgdir}/etc/autofs/auto.net
  sed -i "s:/etc:/etc/autofs:g" \
    ${pkgdir}/usr/man/man5/auto.master.5
  install -D -m 755 ${srcdir}/autofs ${pkgdir}/etc/rc.d/autofs
  install -D -m 644 ${srcdir}/autofs.conf.d \
    ${pkgdir}/etc/conf.d/autofs
}
