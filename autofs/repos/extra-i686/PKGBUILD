# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs
pkgver=4.1.4
pkgrel=5
pkgdesc="A kernel-based automounter for Linux."
arch=('i686' 'x86_64')
url="http://wiki.autofs.net/FrontPage"
license=('GPL2')
depends=('libldap')
backup=(etc/autofs/auto.master etc/conf.d/autofs)
source=($url/v4/${pkgname}-${pkgver}.tar.bz2
        ${pkgname}
        ${pkgname}.conf.d
        auto.master
        auto.misc
        auto.net)
md5sums=('7e3949114c00665b4636f0c318179657'
         '4eec0f479d5a85ca9ca515fd41937a4e'
         'ddd69346754419faee15fc4c7edeb8b8'
         '2fd068c5b84a3f408670fe23afd34759'
         '6d84d7ea43cdf092c64956f8cf4977f9'
         '74d102e637e53050ef095f1cbd9d461b')

build() {
  export MAKEFLAGS="-j1"

  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i "s:-O3 -fomit-frame-pointer -Wall:${CFLAGS}:" \
    Makefile.rules
  sed -i "s:SUBDIRS = lib daemon modules man samples:SUBDIRS = lib daemon modules man:" \
    Makefile.rules
  ./configure --prefix=/usr --sysconfdir=/etc/autofs
  make || return 1
  make INSTALLROOT=${pkgdir} install
  
  local conf_file
  for conf_file in auto.{master,misc}; do
    install -D -m 644 ${srcdir}/$conf_file \
      ${pkgdir}/etc/autofs/$conf_file
  done

  install -m 755 ${srcdir}/auto.net \
    ${pkgdir}/etc/autofs/auto.net
  sed -i "s:/etc:/etc/autofs:g" \
    ${pkgdir}/usr/man/man5/auto.master.5
  install -D -m 755 ${srcdir}/autofs ${pkgdir}/etc/rc.d/autofs
  install -D -m 644 ${srcdir}/autofs.conf.d \
    ${pkgdir}/etc/conf.d/autofs
}
