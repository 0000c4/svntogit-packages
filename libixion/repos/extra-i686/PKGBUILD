# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.11.0
pkgrel=2
pkgdesc="A general purpose formula parser & interpreter"
arch=('i686' 'x86_64')
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python')
makedepends=('boost' 'mdds')
source=("http://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz"
        LICENSE)
sha256sums=('97a6e7f2b1fcbff69e76fe4e1df62f1cfcc353820472991e37de00aacb024293'
            '2bfdca60adf803108d4c7f009000bea76ad00e621e163197881b0eaae91b530e')

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr \
	  --disable-static
	make
}

check() {
    cd ${pkgname}-${pkgver}
    make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
	# LICENSE file is missing in tarball
	install -Dm644 $srcdir/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

