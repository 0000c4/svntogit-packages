# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=grantlee-qt5
pkgver=5.1.0
pkgrel=2
pkgdesc="A string template engine based on the Django template system and written in Qt5"
arch=('i686' 'x86_64')
url="https://www.gitorious.org/grantlee/pages/Home"
license=('LGPL3')
depends=('qt5-script')
makedepends=('cmake' 'doxygen')
source=("http://downloads.grantlee.org/grantlee-${pkgver}.tar.gz")
md5sums=('57989ae9f7c113e682ef1713a6f1e92a')

prepare() {
  mkdir -p build

# Unbreak nested templates https://github.com/steveire/grantlee/issues/27
  cd grantlee-$pkgver
  find -type f -name '*.h' -exec sed -e 's|>>|> >|g' -i {} \;
}

build() {
  cd build
  cmake ../grantlee-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
