# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>
pkgname=opal
pkgver=3.10.1
pkgrel=1
pkgdesc="Open Phone Abstraction Library"
arch=(i686 x86_64)
url="http://www.ekiga.org"
license=('GPL')
depends=('ptlib' 'libtheora' 'x264' 'speex')
makedepends=('ffmpeg')
options=(!makeflags)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('093ff27589f5884af452e6e954ab4da1cc36ff4cc27138230b7d551f22d4f230')

build() {
  cd "${srcdir}/opal-${pkgver}"
  CXXFLAGS="$CXXFLAGS -fpermissive" \
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/opal-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
