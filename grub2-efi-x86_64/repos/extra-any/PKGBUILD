# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

## This PKGBUILD has the same structure as the main grub2 split PKGBUILD for grub2-common, grub2-bios and grub2-efi-i386 .

_grub2_rev="3750"

_grub2_lua_ver="20"
_grub2_gpxe_ver="12"

pkgname="grub2-efi-x86_64"
pkgver='1.99'
pkgrel=3
epoch=1
pkgdesc="The GNU GRand Unified Bootloader version 2 - x86_64 UEFI version"
url="http://www.gnu.org/software/grub/"
arch=('any')
license=('GPL3')

makedepends=('xz' 'python2' 'autogen' 'texinfo' 'help2man' 'gettext' 'device-mapper' 'fuse')
depends=("grub2-common=${epoch}:${pkgver}" 'dosfstools' 'efibootmgr' 'sh')
optdepends=('mtools: for manipulating FAT fs image files')

options=('!strip' '!emptydirs')

# source=("ftp://ftp.gnu.org/gnu/grub/grub-${pkgver}.tar.xz"
source=("ftp://ftp.archlinux.org/other/grub2/grub2_r${_grub2_rev}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_lua_r${_grub2_lua_ver}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz"
        'grub2_automake_1.11.2_pkglib_to_pkgdata.patch')

noextract=("grub2_extras_lua_r${_grub2_lua_ver}.tar.xz"
           "grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz")

sha1sums=('3fab3260a11756f2cfc39f13279a2a633b814d31'
          '9f2dbf7a3faab24ca92266400aa513fecd3895c8'
          'b04994c005910b4bf123f034b30109d5c8d6fd86'
          'e149c8f14a74a9c367852a1615e68758f2c71d29')

build() {

	if [[ "${CARCH}" == 'i686' ]]; then
		echo "This package can be built only in a x86_64 system. Exiting."
		exit 1
	fi

	cd "${srcdir}/grub-${pkgver}"

	## Fix automake 1.11.2 autogen.sh pkglib_DATA and pkglib_SCRIPTS error
	patch -Np1 -i "${srcdir}/grub2_automake_1.11.2_pkglib_to_pkgdata.patch"

	## add grub-extras
	export GRUB_CONTRIB="${srcdir}/grub-${pkgver}/grub2-extras/" 
	install -d "${srcdir}/grub-${pkgver}/grub2-extras"

	bsdtar xf "${srcdir}/grub2_extras_lua_r${_grub2_lua_ver}.tar.xz" \
		-C "${srcdir}/grub-${pkgver}/grub2-extras"

	bsdtar xf "${srcdir}/grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz" \
		-C "${srcdir}/grub-${pkgver}/grub2-extras"

	## The below step is not required as the script now executes with python3, hence makedepends change from python2 to python pkg
	## Seems like python2 is required again - as on 04-JAN-2012 - grub2 bzr mainline rev 3732
	## Need to use python2
	sed 's|python |python2 |g' -i "${srcdir}/grub-${pkgver}/autogen.sh"
	echo

	## start the actual build process
	cd "${srcdir}/grub-${pkgver}/"
	./autogen.sh
	echo

	CFLAGS="" ./configure \
		--with-platform="efi" \
		--target="x86_64" \
		--host="${CARCH}-unknown-linux-gnu" \
		--disable-efiemu \
		--enable-mm-debug \
		--enable-nls \
		--enable-device-mapper \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--infodir="/usr/share/info" \
		--datadir="/usr/lib" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix=""
	echo

	CFLAGS="" make
	echo

}

package() {

	cd "${srcdir}/grub-${pkgver}"
	make DESTDIR="${pkgdir}/" install 
	echo

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}
	rm -f "${pkgdir}/usr/lib/grub"/{grub-mkconfig_lib,update-grub_lib}
	rm -f "${pkgdir}/usr/lib/grub"/{ascii,euro,unicode}.pf2
	rm -f "${pkgdir}/usr/lib/grub"/{ascii,widthspec}.h

}
