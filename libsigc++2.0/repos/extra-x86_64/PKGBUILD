# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=libsigc++2.0
pkgname=('libsigc++2.0' 'libsigc++2.0-docs')
pkgver=2.2.6
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
url="http://libsigc.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/libsigc++/2.2/libsigc++-${pkgver}.tar.bz2)
makedepends=('gcc-libs>=4.4.3')
options=(!libtool !emptydirs)
sha256sums=('93bfd6fc867c99dd3cad695908b45d35b640c6cd12dace43bfa38e48e9a54caa')

build() {
  cd "${srcdir}/libsigc++-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package_libsigc++2.0() {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2"
  depends=('gcc-libs>=4.4.3')
  cd "${srcdir}/libsigc++-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install || return 1
}

package_libsigc++2.0-docs() {
  pkgdesc="Developer documentation for libsigc++"
  cd "${srcdir}/libsigc++-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install || return 1
}

