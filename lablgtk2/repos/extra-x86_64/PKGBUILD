#Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lablgtk2
pkgver=2.18.10
pkgrel=1
pkgdesc="An Objective Caml interface to gtk2"
arch=(x86_64)
license=('LGPL')
url="http://lablgtk.forge.ocamlcore.org/"
depends=('gtk2' 'librsvg' 'gtksourceview2')
makedepends=('ocaml-compiler-libs>=4.0.9' 'camlp5>=7.10' 'libglade')
optdepends=('ocaml: for using the tools')
DLAGENTS=('https::/usr/bin/curl -fLC - --insecure --retry 3 --retry-delay 3 -o %o %u')
source=("https://github.com/garrigue/lablgtk/archive/${pkgver}.tar.gz" gcc10.patch)
sha256sums=('6f00f6b3114b468eb9e743d27511a6063936f6798f890e44b494f71a5cf95470'
            '3d3c11ed9f906b11043c9e0ec3df3b4a0da1fcb8c35b8c4e6792bf0bf5ab4268')
options=(!makeflags staticlibs)

prepare() {
  cd "${srcdir}/lablgtk-${pkgver}/src"
  patch -p0 -i "${srcdir}/gcc10.patch"
}

build() {
  cd "${srcdir}/lablgtk-${pkgver}"
  ./configure --prefix=/usr \
    --without-gnomeui \
    --without-gnomecanvas \
    --without-panel \
    --without-gtkspell \
    --without-gtksourceview
  make world
  make opt
}

package() {
  cd "${srcdir}/lablgtk-${pkgver}"
  make DESTDIR="${pkgdir}" old-install
  install -m644 META "${pkgdir}/usr/lib/ocaml/lablgtk2/"
}
