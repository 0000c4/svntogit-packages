# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=less
pkgver=443
pkgrel=2
pkgdesc="A terminal based program for viewing text files"
license=('GPL3')
arch=('i686' 'x86_64')
url="http://www.greenwoodsoftware.com/less"
groups=('base')
depends=('ncurses' 'pcre')
source=(http://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('47db098fb3cdaf847b3c4be05ee954fc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix="${pkgdir}"/usr install
  install -dm755 "${pkgdir}"/bin
  mv "${pkgdir}"/usr/bin/${pkgname} "${pkgdir}"/bin
}
