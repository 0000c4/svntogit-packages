# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=16.07.80
pkgrel=1
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://community.kde.org/KDE_PIM'
license=('GPL' 'LGPL' 'FDL')
depends=(libkgapi libkolab akonadi-socialutils akonadi-calendar
         knotifyconfig kross kalarmcal kmbox kimap kde-syndication)
makedepends=(extra-cmake-modules kdoctools boost python kdesignerplugin)
conflicts=(kio-pim)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('cbfab9e45fcf06c9a77ce40e8a012640b9083988')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
