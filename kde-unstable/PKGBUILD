# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=20.03.90
pkgrel=2
pkgdesc="Common libraries for KDE PIM"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libkdepim kpimtextedit knewstuff kimap purpose)
makedepends=(extra-cmake-modules boost qt5-tools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         pimcommon-revert-qt5.15-port.patch::"https://cgit.kde.org/pimcommon.git/patch/?id=ee1958fc")
sha256sums=('5c5351d9e0e4acc3f5a5ad99cf76ab8db20738397f3b16e63e01faf38eab8b9b'
            'SKIP'
            'ac3c372d4d39315489fa5e2a6df630f4c418dce8c677d39e8d041a25f192f6fc')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../pimcommon-revert-qt5.15-port.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
