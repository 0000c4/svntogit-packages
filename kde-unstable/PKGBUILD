# $Id: PKGBUILD 290221 2017-03-09 16:12:45Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kajongg
pkgver=17.03.80
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="https://kde.org/applications/games/kajongg/"
arch=(any)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(python-twisted python-pyqt5 libkmahjongg hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('vorbis-tools: for playing sounds')
conflicts=(kdegames-kajongg)
replaces=(kdegames-kajongg)
source=("https://download.kde.org/stable/applications/$pkgver/src/kajongg-$pkgver.tar.xz"{,.sig})
sha256sums=('a204bb7cca644636144a1f05acf6874ac25cddf5564d053db9539c18a10f2e56'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
