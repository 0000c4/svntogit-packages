# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=ksshaskpass
pkgver=5.6.95
pkgrel=1
pkgdesc='ssh-add helper that uses kwallet and kpassworddialog'
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kwallet')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
provides=('x11-ssh-askpass' 'ksshaskpass-frameworks')
conflicts=('ksshaskpass-frameworks')
replaces=('ksshaskpass-frameworks')
groups=('plasma')
source=("http://download.kde.org/unstable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig})
install=$pkgname.install
sha256sums=('00b7b72f92b17d81bdea9fb60df5f25c1e74bc2434067fa9bca2f9f5ea27edf8'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ksshaskpass-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}
package() {
  cd build
  make DESTDIR="$pkgdir" install
}
