# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbreakout
pkgver=17.07.80
pkgrel=1
pkgdesc="A Breakout-like game"
url="https://kde.org/applications/games/kbreakout/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-kbreakout')
replaces=('kdegames-kbreakout')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/kbreakout-${pkgver}.tar.xz"{,.sig})
sha256sums=('ae674f4bed851fc5b47779385aac7ad80c511e6644987f023581fdf5a3c32410'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
