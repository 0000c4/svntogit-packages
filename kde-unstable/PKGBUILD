# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeplasma-addons
pkgname=('kdeplasma-addons-applets'
         'kdeplasma-addons-dataengines'
         'kdeplasma-addons-libs'
         'kdeplasma-addons-runners'
         'kdeplasma-addons-wallpapers')
pkgver=4.2.96
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
groups=('kde' 'kdeplasma-addons')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-workspace' 'kdegraphics-libs' 'kdeedu-marble')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('bfb975facc9993c99c773940579b89e1')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeplasma-addons-applets() {
	pkgdesc='KDE Plasma applets'
	depends=('kdegraphics-libs' 'kdeplasma-addons-libs')
	replaces=('kdeaddons' 'lancelot')
	install='kdeplasma-addons.install'
	cd $srcdir/build/applets
	make DESTDIR=$pkgdir install
	# FIXME
	rm $pkgdir/usr/share/icons/oxygen/scalable/apps/accessories-dictionary.svgz
}

package_kdeplasma-addons-dataengines() {
	pkgdesc='KDE Plasma data engines'
	depends=('kdeplasma-addons-libs')
	cd $srcdir/build/dataengines
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-libs() {
	pkgdesc='Library for KDE Plasma'
	groups=()
	depends=('kdebase-workspace')
	cd $srcdir/build/libs
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-runners() {
	pkgdesc='KDE Plasma runners'
	depends=('kdeplasma-addons-libs')
	cd $srcdir/build/runners
	make DESTDIR=$pkgdir install
}

package_kdeplasma-addons-wallpapers() {
	pkgdesc='KDE Plasma wallpapers'
	depends=('kdeplasma-addons-libs' 'kdeedu-marble')
	cd $srcdir/build/wallpapers
	make DESTDIR=$pkgdir install
}
