# $Id: PKGBUILD 261475 2016-03-15 10:26:40Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmahjongg
pkgver=16.03.90
pkgrel=1
pkgdesc="A tile matching game for one or two players"
url="http://kde.org/applications/games/kmahjongg/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkmahjongg libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdegames-kmahjongg)
replaces=(kdegames-kmahjongg)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('c33c76125188b51c72b5bc4da6461ffd52827e09')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
