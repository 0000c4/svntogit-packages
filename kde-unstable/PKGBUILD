# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon
pkgname=('phonon' 'phonon-xine' 'phonon-gstreamer')
pkgver=4.3.80
pkgrel=1
pkgdesc="The multimedia API for KDE4"
arch=('i686' 'x86_64')
url="http://phonon.kde.org"
license=('LGPL')
makedepends=('cmake' 'automoc4' 'qt' 'xine-lib' 'gstreamer0.10-base')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgname}/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('6b0c5554291615433c14c3c38f741690')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
    -DWITH_PulseAudio=OFF
  make || return 1
}

package_phonon(){
  depends=('qt' 'phonon-backend')
  cd ${srcdir}/build/phonon
  make DESTDIR=${pkgdir} install || return 1
}

package_phonon-gstreamer(){
  pkgdesc="Phonon Gstreamer backend"
  depends=('gstreamer0.10-base')
  provides=('phonon-backend')
  cd ${srcdir}/build/gstreamer
  make DESTDIR=${pkgdir} install || return 1
}

package_phonon-xine(){
  pkgdesc="Phonon Xine backend"
  depends=('xine-lib')
  provides=('phonon-backend')
  cd ${srcdir}/build/xine
  make DESTDIR=${pkgdir} install || return 1
}
