# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kolourpaint
pkgver=20.03.80
pkgrel=1
pkgdesc="Paint Program"
url="https://kde.org/applications/graphics/kolourpaint/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(libksane kio)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kolourpaint-qt5.15.patch::"https://cgit.kde.org/kolourpaint.git/patch/?id=75f54996")
sha256sums=('16e23c0bac8783cad21a9f02c1cf96b0321dcfbe0b81bd9f78be0639f11294bd'
            'SKIP'
            '3a4bb64cacb5c508b2d0126316bea725850574da0063425c0fcea30af5fdb8ed')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kolourpaint-qt5.15.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
