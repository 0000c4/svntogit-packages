# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kfind
pkgver=17.07.80
pkgrel=1
pkgdesc="Find Files/Folders"
arch=(i686 x86_64)
url="https://www.kde.org/applications/utilities/kfind/"
license=(LGPL)
depends=(kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin python)
conflicts=(kdebase-kfind)
replaces=(kdebase-kfind)
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b5ec77bd70376353bbffc4acb175fc5fa12291c740115457cd1653989d0ed962'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
