# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmailtransport
pkgver=16.07.80
pkgrel=1
pkgdesc="Mail Transport Service"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(akonadi-mime kcmutils kdelibs4support)
makedepends=(extra-cmake-modules python kdoctools boost kdesignerplugin)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('aa6423197a0ccdcb87c53d161e861bde')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
