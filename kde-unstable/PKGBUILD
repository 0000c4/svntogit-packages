# Maintainer: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=17.07.80
pkgrel=1
pkgdesc="KWallet signon extension"
arch=('i686' 'x86_64')
url="https://kde.org"
license=('LGPL')
groups=('kde-applications' 'kdenetwork')
depends=('signond' 'kwallet')
makedepends=('extra-cmake-modules')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('96d4ef5cfc3fa107c6d6bebea91ac3f4fbc81aa4d81d76ed2fefcedf1d0d46f4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
