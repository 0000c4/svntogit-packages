# $Id: PKGBUILD 262594 2016-03-26 20:49:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kleopatra
pkgver=16.03.90
pkgrel=1
pkgdesc='Certificate Manager and Unified Crypto GUI'
arch=(i686 x86_64)
url='https://www.kde.org/applications/utilities/kleopatra/'
license=(GPL LGPL FDL)
depends=(hicolor-icon-theme libkleo kcmutils kmime)
makedepends=(extra-cmake-modules kdoctools boost python)
groups=(kde-applications kdepim)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('de8e2e128c6875f32ce1bcf370eeaee19fb64aa9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
