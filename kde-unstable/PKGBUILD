# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=markdownpart
pkgver=20.11.80
pkgrel=1
pkgdesc='KPart for rendering Markdown content'
arch=(x86_64)
url='https://kde.org/'
license=(GPL)
depends=(kparts)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ea96f415ab9488d7ddebc8fff636286f3ba583cb2fb2fa3e151776f4c4c72097'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
