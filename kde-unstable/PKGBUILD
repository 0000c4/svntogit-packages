# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmime
pkgver=16.07.90
pkgrel=1
pkgdesc="Library for handling mail messages and newsgroup articles"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('1c716191f44d64c42e65ea89f696d32b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
