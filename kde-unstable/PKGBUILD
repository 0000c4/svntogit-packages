# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdcraw
pkgver=4.6.80
pkgrel=1
pkgdesc="A C++ interface used to decode RAW picture"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'lcms')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('19fe60a51ede7bf5b4047f8fff22cd8361ebd90c')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
