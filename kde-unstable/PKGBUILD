# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-addons
pkgver=19.03.90
pkgrel=1
pkgdesc='Addons for KDE PIM applications'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(incidenceeditor ktnef libksieve akonadi-notes akonadi-import-wizard kparts poppler-qt5 kitinerary discount)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0f810b150aa4a13a563c10cc5d911f202d69e670cd4039478177ab6c4ec94d60'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
