# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libksieve
pkgver=19.03.90
pkgrel=1
pkgdesc="KDE PIM library for managing sieves"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kidentitymanagement pimcommon kmailtransport)
makedepends=(extra-cmake-modules boost kdoctools)
conflicts=('libkdepim<16' kio-pim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('95851a675d50b3c691fc703a043dd79f2dcb42ea871000751bf24ba4665201ab'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
