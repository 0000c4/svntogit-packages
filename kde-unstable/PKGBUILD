# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kate
pkgname=('kwrite'
         'kate')
pkgver=15.12.0
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'plasma-framework' 'knewstuff' 'ktexteditor'
             'threadweaver' 'kitemmodels' 'kactivities-frameworks')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz"
	lingering-process.patch::"https://quickgit.kde.org/?p=kate.git&a=commitdiff&h=cd0163d7&o=plain")
sha1sums=('f58ba9cfdf86cf6ff1132715579c64cda3709cdb'
          '228db8d6d450364317913f3ac05f34a7884a0363')

prepare() {
  mkdir -p build

# Fix lingering process after closing window
  cd $pkgbase-$pkgver
  patch -p1 -i ../lingering-process.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=('kde-applications' 'kdebase')
  url="http://www.kde.org/applications/utilities/kwrite/"
  depends=('ktexteditor' 'kded' 'kactivities-frameworks' 'hicolor-icon-theme')
  install='kwrite.install'
  replaces=('kdebase-kwrite')
  conflicts=('kdebase-kwrite')
  provides=('kdebase-kwrite')

  cd build/kwrite
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kwrite
  make DESTDIR="${pkgdir}" install
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=('kde-applications' 'kdebase')
  url="http://www.kde.org/applications/utilities/kate/"
  depends=('knewstuff' 'ktexteditor' 'threadweaver' 'kded' 'kitemmodels' 'kactivities-frameworks' 'hicolor-icon-theme')
  install='kate.install'
  replaces=('kdesdk-kate')
  conflicts=('kdesdk-kate')
  provides=('kdesdk-kate')
  optdepends=('konsole: open a terminal in Kate')

  cd build/kate
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kate
  make DESTDIR="${pkgdir}" install

  cd ../../../build/addons
  make DESTDIR="${pkgdir}" install
}
