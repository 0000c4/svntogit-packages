# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kfind
pkgver=20.03.90
pkgrel=2
pkgdesc="Find Files/Folders"
arch=(x86_64)
url="https://www.kde.org/applications/utilities/kfind/"
license=(LGPL)
depends=(kio kfilemetadata)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('mlocate: search using mlocate index')
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kfind-revert-qt5.15-port.patch::"https://cgit.kde.org/kfind.git/patch/?id=1ac41b6b")
sha256sums=('54e85a8f0c11bd0502b95e66ab370d695520b0dd48234a67a683ba4967039fa6'
            'SKIP'
            'a4f0a4a781c1e7803c8f1c382b4e393f5a1de4570d60d5f22f11eb3d031a4c40')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kfind-revert-qt5.15-port.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
