# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=17.11.90
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=(x86_64)
url='https://community.kde.org/KDE_PIM'
license=(GPL LGPL FDL)
depends=(libkgapi libkolabxml kdav akonadi-calendar knotifyconfig kalarmcal kmbox pimcommon akonadi-notes)
makedepends=(extra-cmake-modules kdoctools boost python kdesignerplugin)
conflicts=(kio-pim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdepim-runtime-qt5.10.patch::"https://cgit.kde.org/kdepim-runtime.git/patch/?id=e4792e2f")
sha256sums=('5a2ce923cd76ddb71364051396ae1707a5d3ba94524eb8e8b060fa6faaebcdaa'
            'SKIP'
            'ebaca14f242cb0a23408ba11ea031f896697928f83a2a83155da0abd4e22e3d0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdepim-runtime-qt5.10.patch # Qt 5.10 rc2 API changes
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
