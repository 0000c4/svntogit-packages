# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=(akonadi libakonadi)
pkgver=19.11.80
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
makedepends=(extra-cmake-modules postgresql qt5-tools boost kitemmodels kaccounts-integration)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         akonadi-qt-5.14.patch::"https://cgit.kde.org/akonadi.git/patch/?id=1d4bb01a")
sha256sums=('42e92dc06da05041ac6892901dbc92bd8748cdc0ca048599aada2c8b4bec82b9'
            'SKIP'
            '4453c18007be84da1542480a17a7cc0e42f94d2b221ada7e7f2ddabed1618092')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../akonadi-qt-5.14.patch # Fix function redefinition error with Qt 5.14
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package_libakonadi() {
  pkgdesc='Libraries used by applications based on Akonadi'
  depends=(kitemmodels kaccounts-integration hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/bin # Provided by akonadi
}

package_akonadi() {
  depends=(libakonadi mariadb)
  optdepends=('postgresql: PostgreSQL backend')

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}} # Provided by libakonadi
}
