# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=17.07.80
pkgrel=1
pkgdesc="Common libraries for KDE PIM"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(libkdepim kpimtextedit knewstuff kimap)
makedepends=(extra-cmake-modules kdoctools python boost kdesignerplugin qt5-tools)
conflicts=('libkdepim<16')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('eca32f0f6b98e268cb9c68aa0caa8cf7fb2be629f3e8c637e3480db2b78057ac'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
