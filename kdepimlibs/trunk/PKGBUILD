# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdepimlibs
pkgname=(akonadi-contact akonadi-mime akonadi-notes akonadi-socialutils kio-pim)
pkgver=16.04.0
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=(i686 x86_64)
url='https://community.kde.org/KDE_PIM'
license=(GPL LGPL)
makedepends=(extra-cmake-modules qt5-tools kcalcore kmbox kcontacts kldap akonadi prison-frameworks kdoctools boost python grantlee-qt5)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz")
sha1sums=('9b943113845edd18718c2373437746243bc1d179')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package_akonadi-contact() {
  depends=(akonadi kmime kcontacts prison-frameworks qt5-webkit grantlee-qt5)

  cd build/akonadi-contact
  make DESTDIR="$pkgdir" install
}

package_akonadi-mime() {
  depends=(akonadi kmime)
  install=akonadi-mime.install

  cd build/akonadi-mime
  make DESTDIR="$pkgdir" install
}

package_akonadi-notes() {
  depends=(kmime)

  cd build/akonadi-notes
  make DESTDIR="$pkgdir" install
}

package_akonadi-socialutils() {
  depends=(akonadi)
  install=akonadi-socialutils.install

  cd build/akonadi-socialutils
  make DESTDIR="$pkgdir" install
}

package_kio-pim() {
  pkgdesc='PIM kioslaves'
  depends=(kldap kio)

  cd build/kioslave
  make DESTDIR="$pkgdir" install
}
