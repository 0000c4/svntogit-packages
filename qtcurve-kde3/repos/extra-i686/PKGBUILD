# $Id: PKGBUILD,v 1.43 2008/07/14 01:06:07 Pierre Exp $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=qtcurve-kde3
pkgver=1.0.0
pkgrel=1
arch=('i686' 'x86_64')
groups=('qtcurve')
license='GPL'
pkgdesc='A configurable set of widget styles for KDE and Gtk'
url='http://www.kde-look.org/content/show.php?content=40492'
source=("http://craigd.wikispaces.com/file/view/QtCurve-KDE3-${pkgver}.tar.bz2" 'kdeglobals')
depends=('kdelibs3')
makedepends=('cmake')
md5sums=('b3bd5675fc954a4a01e691e7d5be35ee'
         '05a6294c7f0e239e698e7e5224fc8e7f')

build() {
	. /etc/profile.d/qt3.sh
	. /etc/profile.d/kde3.sh
	cd $srcdir
	mkdir build
	cd build
	cmake ../QtCurve-KDE3-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/opt/kde
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install

	# remove kcontrol plugins
	rm -f $pkgdir/opt/kde/lib/kde3/kstyle_qtcurve_config.*

	# set default theme
	install -D -m644 $srcdir/kdeglobals $pkgdir/opt/kde/share/config/kdeglobals
}
