# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=paprefs
pkgver=0.9.10+16+g534d20a
pkgrel=1
pkgdesc="Configuration dialog for PulseAudio"
arch=(x86_64)
url="https://freedesktop.org/software/pulseaudio/paprefs/"
license=(GPL2)
depends=(gtkmm3 libsigc++ dbus-glib pulseaudio)
makedepends=(intltool lynx git)
_commit=534d20a1d738f21365b56a08f6553d1be8ca8914  # master
source=("git+https://anongit.freedesktop.org/git/pulseaudio/paprefs#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./bootstrap.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 doc/README
}
