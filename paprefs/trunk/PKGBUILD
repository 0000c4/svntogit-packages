# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=paprefs
pkgver=0.9.10+17+gc6f1217
pkgrel=1
pkgdesc="Configuration dialog for PulseAudio"
url="https://freedesktop.org/software/pulseaudio/paprefs/"
license=(GPL2)
arch=(x86_64)
depends=(gtkmm3 libsigc++ dbus-glib pulseaudio)
makedepends=(meson lynx tidy git)
_commit=c6f1217d4365e9ca9e126baed08334b23721c684  # master
source=("git+https://anongit.freedesktop.org/git/pulseaudio/paprefs#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D lynx=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
