# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Jason Taylor <jftaylor21@gmail.com>

pkgbase=lapack
pkgname=('blas' 'lapack')
pkgver=3.2.2
pkgrel=1
url="http://www.netlib.org/lapack"
makedepends=('gcc-fortran')
arch=('i686' 'x86_64')
license=("custom")
source=(http://www.netlib.org/${pkgbase}/${pkgbase}-${pkgver}.tgz
        lapack-3.1.1-make.inc.patch
        Makefile.blas
        Makefile.lapack
        LICENSE.blas)
md5sums=('10832891c9e8100f81108c1ec7da0da4'
         'cdfcb9d9b162c18d9acbf63c5579ea26'
         'c7d3530faf06c817c5f41873adb5eecf'
         '8743396d6a95657bfaf7cd339e7b5fe9'
         '38b6acb8ed5691d25863319d30a8b365')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/lapack-3.1.1-make.inc.patch"
  cp -f INSTALL/make.inc.gfortran make.inc
  cp -f "${srcdir}/Makefile.blas" BLAS/SRC/Makefile
  cp -f "${srcdir}/Makefile.lapack" SRC/Makefile

  export FC=gfortran

  pushd BLAS/SRC
  make clean
  FFLAGS="${CFLAGS/-O?/-O0}" make dcabs1.o
  FFLAGS="${CFLAGS} -fPIC" CFLAGS="${CFLAGS} -fPIC" make shared
  cp libblas.so.${pkgver} "${srcdir}/${pkgbase}-${pkgver}/"
  popd
  ln -s libblas.so.${pkgver} libblas.so

  pushd INSTALL
  make clean
  make NOOPT="${CFLAGS/-O?/-O0} -fPIC" OPTS="${CFLAGS} -fPIC"
  popd

  pushd SRC
  make clean
  make FFLAGS="${CFLAGS} -fPIC" CFLAGS="${CFLAGS} -fPIC" shared
 # FFLAGS="${CFLAGS} -fPIC" CFLAGS="${CFLAGS} -fPIC" cc -shared -Wl,-soname,liblapack.so.3 -o liblapack.so.3.2.2 *.o -L.. -lblas -lm -lc
  cp liblapack.so.${pkgver} "${srcdir}/${pkgbase}-${pkgver}/"
  popd
}

package_blas() {
  pkgdesc="Basic Linear Algebra Subprograms"
  depends=('gcc-libs>=4.5.0')
  options=('force')
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 "${srcdir}/${pkgbase}-${pkgver}/libblas.so.${pkgver}" \
    "${pkgdir}/usr/lib/" || return 1
  ln -sf libblas.so.${pkgver} "${pkgdir}/usr/lib/libblas.so"
  ln -sf libblas.so.${pkgver} "${pkgdir}/usr/lib/libblas.so.3"
  
  install -m755 -d "${pkgdir}/usr/share/licenses/blas"
  install -m644 "${srcdir}/LICENSE.blas" \
    "${pkgdir}/usr/share/licenses/blas/LICENSE" || return 1
}

package_lapack() {
  pkgdesc="Linear Algebra PACKage"
  depends=("blas=${pkgver}")
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 "${srcdir}/${pkgbase}-${pkgver}/liblapack.so.${pkgver}" \
    "${pkgdir}/usr/lib/" || return 1
  ln -sf liblapack.so.${pkgver} "${pkgdir}/usr/lib/liblapack.so"
  ln -sf liblapack.so.${pkgver} "${pkgdir}/usr/lib/liblapack.so.3"

  install -m755 -d "${pkgdir}/usr/share/licenses/lapack"
  install -m644 "${srcdir}/${pkgbase}-${pkgver}/LICENSE" \
    "${pkgdir}/usr/share/licenses/lapack/" || return 1
}
md5sums=('10832891c9e8100f81108c1ec7da0da4'
         'cdfcb9d9b162c18d9acbf63c5579ea26'
         '0d96e988563c1011fabced7b33946f30'
         'acbda6ea8934b36b56d9dc2cbe1a9ba8'
         '38b6acb8ed5691d25863319d30a8b365')
md5sums=('10832891c9e8100f81108c1ec7da0da4'
         'cdfcb9d9b162c18d9acbf63c5579ea26'
         '0d96e988563c1011fabced7b33946f30'
         '760cc15bc502f45594918731e93fbc08'
         '38b6acb8ed5691d25863319d30a8b365')
md5sums=('10832891c9e8100f81108c1ec7da0da4'
         'cdfcb9d9b162c18d9acbf63c5579ea26'
         '0d96e988563c1011fabced7b33946f30'
         '436c22c3d28ab4f32e8c7d101cbb10c1'
         '38b6acb8ed5691d25863319d30a8b365')
