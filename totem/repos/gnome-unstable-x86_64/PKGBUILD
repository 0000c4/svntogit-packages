# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=totem
pkgname=('totem' 'totem-plugin')
pkgver=3.1.92
pkgrel=1
pkgdesc="A GNOME3 integrated movie player based on Gstreamer."
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('GPL2' 'custom')
depends=('gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'totem-plparser' 'libxxf86vm'
         'libxtst' 'desktop-file-utils' 'iso-codes' 'python2' 'libpeas' 'hicolor-icon-theme'
         'gnome-icon-theme-symbolic' 'gsettings-desktop-schemas' 'dconf' 'clutter-gst'
         'clutter-gtk' 'mx')
makedepends=('intltool' 'gtk-doc' 'python2-gobject' 'nautilus' 'libgdata' 'xulrunner' 'lirc-utils'
             'libepc' 'bluez' 'vala' 'grilo')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz
        convert.patch::http://git.gnome.org/browse/totem/patch/?id=75d00dfd0d3a6779f879e52c96babbbd52816d75)
sha256sums=('a7186a3c3a62f5ec0a8749aba9718c999507b43ab99711ebf4f7b65c24e995ab'
            '243656eed0beda9fd097a24f33683dbcf39dbe7fdf385f6e5bc5fc85d5b34c0c')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -Np1 -i ${srcdir}/convert.patch
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/totem \
    --localstatedir=/var \
    --disable-static \
    --enable-python \
    --enable-nautilus
  make
}

package_totem() {
  groups=('gnome-extra')
  install=totem.install
  optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
              'gstreamer0.10-bad-plugins: Extra media codecs'
              'gstreamer0.10-ffmpeg: Extra media codecs'
              'lirc-utils: Infrared Remote Control plugin'
              'libepc: Publish Playlist plugin'
              'libgdata: YouTube Browser plugin'
              'bluez: Bemused plugin'
              'grilo: Grilo plugin')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}/usr/lib/mozilla"
  rm "${pkgdir}/usr/lib/totem/totem-plugin-viewer"

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/lib/totem/{totem/totem-bugreport.py,plugins/iplayer/iplayer2.py}

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/totem/COPYING"
}

package_totem-plugin() {
  pkgdesc="Totem mozilla/firefox plugin"
  depends=("totem=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C browser-plugin \
    plugindir=/usr/lib/mozilla/plugins \
    xptdir=/usr/lib/mozilla/plugins \
    DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/totem-plugin/COPYING"
}
