# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=totem
pkgname=('totem' 'totem-plugin')
pkgver=2.91.7
pkgrel=1
url="http://www.hadess.net/totem.php3"
arch=('i686' 'x86_64')
license=('GPL2' 'custom')
makedepends=('intltool' 'gnome-doc-utils' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'totem-plparser'
    'libpeas' 'pygobject' 'pygobject-devel' 'nautilus' 'libxxf86vm' 'libxtst' 'desktop-file-utils' 'iso-codes' 'libgdata'
    'xulrunner' 'lirc-utils' 'libepc' 'bluez' 'vala')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/totem/2.91/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('be8259b306d3def99341b8cf3eef7acdf813ea71286d89dc1eeed8b7f74b8c53')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/totem \
      --localstatedir=/var --disable-static \
      --enable-python --enable-nautilus \
      --with-dbus --disable-scrollkeeper
  make
}

package_totem() {
  pkgdesc="A GNOME3 integrated movie player based on Gstreamer."
  depends=('gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'totem-plparser' 'libxxf86vm' 'libxtst' 'desktop-file-utils' 'iso-codes' 'python2' 'libpeas')
  groups=('gnome-extra')
  install=totem.install
  optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
              'gstreamer0.10-bad-plugins: Extra media codecs'
              'gstreamer0.10-ffmpeg: Extra media codecs'
              'lirc-utils: Infrared Remote Control plugin'
              'libepc: Publish Playlist plugin'
              'libgdata: YouTube Browser plugin'
              'bluez: Bemused plugin')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/lib/mozilla"
  rm -f "${pkgdir}/usr/lib/totem/totem-plugin-viewer"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain totem ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/lib/totem/{totem/totem-bugreport.py,plugins/iplayer/iplayer2.py}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_totem-plugin() {
  pkgdesc="Totem mozilla/firefox plugin"
  depends=("totem=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}/browser-plugin"
  make plugindir=/usr/lib/mozilla/plugins \
      xptdir=/usr/lib/mozilla/plugins \
      DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share"
}

