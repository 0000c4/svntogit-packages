# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=totem
pkgname=('totem' 'totem-plugin')
pkgver=2.27.92
pkgrel=1
url="http://www.hadess.net/totem.php3"
arch=(i686 x86_64)
license=('GPL2' 'custom')
makedepends=('gstreamer0.10-good-plugins>=0.10.16' 'totem-plparser>=2.27.2' 'libxxf86vm' 'libxtst' 'desktop-file-utils' 'iso-codes>=3.10.1' 'libepc>=0.3.10' 'python>=2.6' 'libgdata>=0.4.0' 'nautilus>=2.27.92' 'xulrunner>=1.9.1' 'pkgconfig' 'bluez>=4.39' 'lirc-utils' 'gnome-doc-utils>=0.17.5' 'gnome-control-center>=2.27.91' 'libtracker>=0.6.95' 'intltool' 'pygtk>=2.16.0')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/totem/2.27/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('4634c56b882bf7c1f50166e6b010c6b98208d0690e7ed4fcabef057717d17a9f')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/totem \
      --localstatedir=/var --disable-static \
      --enable-python --enable-nautilus \
      --with-dbus --disable-scrollkeeper || return 1
  make || return 1
}

package_totem() {
  pkgdesc="A GNOME2 integrated movie player based on Gstreamer."
  depends=('gstreamer0.10-good-plugins' 'totem-plparser>=2.27.2' 'libxxf86vm' 'libxtst' 'desktop-file-utils' 'iso-codes>=3.10.1' 'libepc>=0.3.10' 'python>=2.6'  'libgdata>=0.4.0' 'libunique>=1.1.2')
  groups=('gnome-extra')
  install=totem.install
  optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
              'gstreamer0.10-bad-plugins: Extra media codecs'
              'gstreamer0.10-ffmpeg: Extra media codecs')
  conflicts=('totem-xine')
  replaces=('totem-xine')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  rm -f ${pkgdir}/usr/share/totem/vanity.*
  rm -f "${pkgdir}/usr/share/pixmaps/vanity.png"

  rm -rf "${pkgdir}/usr/lib/mozilla"
  rm -f "${pkgdir}/usr/lib/totem/totem-plugin-viewer" || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain totem ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}

package_totem-plugin() {
  pkgdesc="Totem mozilla/firefox plugin"
  depends="totem=${pkgver}"

  cd "${srcdir}/${pkgbase}-${pkgver}/browser-plugin"
  make plugindir=/usr/lib/mozilla/plugins \
      xptdir=/usr/lib/mozilla/plugins \
      DESTDIR="${pkgdir}" install || return 1

  rm -rf "${pkgdir}/usr/share"
}

