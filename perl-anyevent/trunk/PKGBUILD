# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-anyevent
_cpanname=AnyEvent
pkgver=6.1
pkgrel=1
epoch=1
pkgdesc="Framework for multiple event loops"
arch=('any')
url="http://search.cpan.org/dist/${_cpanname}/"
license=('GPL')
depends=('perl-event' 'glib-perl' 'perl>=5.14.0')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/${_cpanname}-${pkgver}.tar.gz)
md5sums=('62534a2b7bd86c5aa9d72181913ae1cd')

build() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
