# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-anyevent
pkgver=7.02
pkgrel=1
pkgdesc='the DBI of event loop programming'
epoch=4
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl 'perl-ev>=4.00' 'perl-guard>=1.02' 'perl-async-interrupt>=1.0')
optdepends=('perl-net-ssleay: TLS/SSL connections with AnyEvent::Handle'
            'perl-json-xs: send or receive JSON with AnyEvent::Handle')
url=https://metacpan.org/release/AnyEvent
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/AnyEvent-$pkgver.tar.gz")
md5sums=(4eaf54ed326578ae5e3be39dadd13c5f)
sha512sums=(6a7a98cb7d5d135cc7a697dfba02723f64d2ec40669f609e087c4d55a50ac6fbddb68b827863ba123844b4e5bcd7a4546673ffdb34391dd8156aa7c5bce86f40)
_dir="$srcdir/AnyEvent-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  export PERL_ANYEVENT_LOOP_TESTS=1 PERL_ANYEVENT_NEW_TESTS=1
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
