# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.07.80
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('ef03c69105b57b7a8cf076fc1ac34be7'
         '3d7298baed956b05be95f962a368d12d'
         'e0951db9aab559e5905c4a0758903145'
         '657e4a9000c3fca767e4876da0e1823d'
         'ef1be36fd6dd81c148d03f64a55b271e'
         '889d380d1aae71a987af777dbaeb1667'
         '747eecd332c4eddd3aa7f9d96d6a0ef8'
         '96ba6b0c2df6db78226ffa184075128a'
         '5a714ebc20c41271c1fe8d7f6c7634ba'
         '2c8a0fe9c695aca46256bc396ef78d9f'
         'be93b9073d0d36cf6de2e37369057f85'
         '9a82e30a8f60ef5a8fa7f2b409792615'
         'e170eed5a4e84efdada2a0e7abbcc392'
         'f3ef3d2a2ba8a433441493c89b7b9ef7'
         '81e5b67139fbe09f177e89fb802de242'
         'c49907d9144b29a3678840d837d52d7b'
         '66624580953c0e688526c09b6d718e7f'
         '756db19625136e5d232a766609eacd45'
         'ba06d309c693feaa0b7ba4b2fec4c9cc'
         'e53f0e0451903b7aa3858ff2473e1d2f'
         'dc2d4166041639c87f150b9147140db0'
         'd11b9a6bf5e710e4b8078a85eb13c6d5'
         '8ebf4be1798eb4e370a61a1ada34b511'
         'f810d0d606b587e773911d9cfc9e07dd'
         '6259f9e486f6f636287efa76de1c2714'
         'b15da72ccc530c2a2523a35517ee2fb9'
         'a6800f24cafcb76e9f3c796cc48e62a1'
         'b7fb993e458326f4de43a00ddc6c7de7'
         '17d92b2bb39c6fd4c6e3443e13b69548'
         '05cb54246198edcdd8f5488de94b9a30'
         '6e809691e17e8c9273f78d3c52eac72a'
         '8e4f26591e4833fd2962b912d9798933'
         '8bb93be392029989923d661ed5debb9b'
         'bc8d24cce0e1e11ac8fae742874aec0d'
         '7ca0d7a8ec896fc89432c053050fa103'
         '524adf9d28bd8e7be2c0e29d08527132'
         '4e2d6da6a235180c384a8d8a56fe1467'
         'b49f8f4f654e86742234300ab661df08'
         '248efd7dde84ec332d61218c73166a0c'
         'c49e2809579845eac33ef3f43a24fb13'
         'ee0ca312436e0eeaad0b3d0bd4b9834f'
         'ec7ac4f26a4255845f6f1eb89c89637a'
         'aacb5f99185023e18c96a95bc05a0c56'
         '0c7c552c59bac671eaf9b9da816eb9de'
         '981437dbd9187b5fc5ace713298f0d04'
         'dd752486915b5175023dadd7b6129e28'
         '1a808ef37105ae4a47b56ac72c5381f8'
         '75fba238b73edf164cc1012fbe631c9b'
         'f62c510c8bc944f1fe2928226faabdb3'
         '9b8b368224c5aef0f9bb1622ae37d54e'
         '3738c73e02931024cc9ee108fa8f001e'
         '3884fba487db05adc13d1c3ba61e3870'
         'a08a554cd536a2c09cdc3594a4a5f7c2'
         '50301c82c32e62d8087ef5192afaf1c4'
         'b492a1592af5845184807a32ae5ce31a')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true
# kde-l10n-sr needs special casing
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/*/docs/CMakeLists.txt || true
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
