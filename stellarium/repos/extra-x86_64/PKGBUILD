# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=stellarium
pkgver=0.19.1
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="http://stellarium.org/"
license=('GPL2')
depends=('libpng' 'libgl' 'freetype2' 'openssl' 'qt5-script'
	'qt5-serialport' 'qt5-multimedia' 'qt5-location' 'gpsd')
makedepends=('cmake' 'boost' 'mesa' 'qt5-tools')
source=(https://github.com/Stellarium/stellarium/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc}
        stellarium-gpsd-3.19.patch)
validpgpkeys=('79151C2E6351E7278DA1A730BF38D4D02A328DFF')  # Alexander Wolf <alex.v.wolf@gmail.com>
sha256sums=('0f25ad2acb59b75a49568c82123219aed5a14252c2ef39d0444540057d2cfb68'
            'SKIP'
            '37e2b4ce4d4059f82c4a627e4b2de699167833938e06fd65b3a8aae022eb69dc')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../stellarium-gpsd-3.19.patch # Fix build with gpsd 3.19
}

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}


package() {
  cd ${pkgname}-${pkgver}
 
  make DESTDIR="${pkgdir}" install
}
