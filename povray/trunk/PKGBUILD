# $Id$
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: tobias <tobias@archlinux.org>
# Modifications made by: red_over_blue and neri

pkgname=povray
pkgver=3.6.1
_majorver=3.6
pkgrel=9
pkgdesc="A script based raytracer for high-quality three-dimensional graphics"
arch=('i686' 'x86_64')
license=('custom')
url="http://povray.org"
depends=('gcc-libs' 'libtiff' 'libpng' 'libxpm')
backup=('etc/povray/3.6/povray.conf' 'etc/povray/3.6/povray.ini')
source=("ftp://ftp.povray.org/pub/povray/Official/Unix/$pkgname-$pkgver.tar.bz2"
        'libpng-1.4.patch' 'povray-3.6.1-use-system-libpng.patch')
md5sums=('b5789bb7eeaed0809c5c82d0efda571d'
         '043d88384956ac41322dee2bf15585cd'
         '36e21e100ef73d5d3fc0f72615cf381f')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 < ../libpng-1.4.patch
  patch -p1 < ../povray-3.6.1-use-system-libpng.patch
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
    COMPILED_BY="Arch Linux" --disable-optimiz --enable-strip
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  # make install tries to write into a home dir
  export HOME=$(mktemp -d)
  make DESTDIR=${pkgdir} sysconfdir=${pkgdir}/etc install

  # correct the paths in the ini file
  sed -i "s|/usr/local/share|/usr/share|g" ${pkgdir}/etc/$pkgname/$_majorver/povray.ini

  # install licenses
  install -Dm 644 doc/povlegal.doc \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm 644 doc/distribution-license.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/distribution-license.txt
  install -Dm 644 doc/source-license.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/source-license.txt
}
