# $Id$
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: tobias <tobias@archlinux.org>
# Modifications made by: red_over_blue and neri

pkgname=povray
pkgver=3.7.0.RC3
pkgrel=1
pkgdesc="A script based raytracer for high-quality three-dimensional graphics"
arch=('i686' 'x86_64')
license=('custom')
url="http://povray.org"
depends=('libtiff' 'libpng' 'boost-libs')
makedepends=('boost')
backup=("etc/povray/${pkgver%.*.*}/povray.conf" "etc/povray/${pkgver%.*.*}/povray.ini")
source=(http://www.povray.org/redirect/www.povray.org/beta/source/povray-3.7.0.RC3.tar.bz2
        povray3.7.0_rc3-user-conf.patch distribution-license.txt LICENSE source-license.txt)
md5sums=('66480f1ca6b1b1b5198605b6caf3f8e8'
         '74607f5ce7e07ae250cf135991c8ebc2'
         '6b22ae20b8ff2064290cacb25543a16a'
         'c3ddad39c4188b846c97b1fb3b3b4bac'
         '884f6f3d74080d9028be506078703b12')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../povray3.7.0_rc3-user-conf.patch
  COMPILED_BY="Arch Linux" ./configure --prefix=/usr --sysconfdir=/etc --disable-optimiz
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  # install licenses
  install -Dm 644 ../LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 ../distribution-license.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/distribution-license.txt"
  install -Dm 644 ../source-license.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/source-license.txt"
}
