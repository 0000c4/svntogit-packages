# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libpeas
pkgver=1.8.1
pkgrel=1
pkgdesc="A GObject-based plugins engine"
arch=(i686 x86_64)
url="http://www.gtk.org/"
license=(GPL2)
depends=(gtk3 hicolor-icon-theme gobject-introspection)
makedepends=(gtk-doc intltool python-gobject python2-gobject gjs seed glade)
optdepends=('gjs: gobject-based plugin engine - gjs runtime loader'
            'seed: gobject-based plugin engine - seed runtime loader')
options=('!libtool')
install=libpeas.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz
        js17.patch)
sha256sums=('ff0e7cd1f0da5dc1289efdd4662a06188f8f0fb088eac67abe05ffa66ba7751d'
            '094f28f911de979167c870b3ed51df09dbe3cacc271b375d891d7601c51ad44f')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../js17.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
