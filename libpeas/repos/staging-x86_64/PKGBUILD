# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libpeas
pkgver=1.8.0
pkgrel=2
pkgdesc="A GObject-based plugins engine"
arch=(i686 x86_64)
url="http://www.gtk.org/"
license=(GPL2)
depends=(gtk3 hicolor-icon-theme gobject-introspection)
makedepends=(gtk-doc intltool python-gobject python2-gobject gjs seed glade)
optdepends=('gjs: gobject-based plugin engine - gjs runtime loader'
            'seed: gobject-based plugin engine - seed runtime loader')
options=('!libtool')
install=libpeas.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz
        js17.patch)
sha256sums=('ffbad2d2aad8237722342432f9737a2ba5a2022e1b8171ab57db67995ce2e0fe'
            '1a023c402e34203f13f3ffbb27afc9bbe46003cf2c911ceb2a0fc02742e9c889')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../js17.patch
  ./configure --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
