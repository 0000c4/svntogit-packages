# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
pkgname=libpeas
pkgver=1.1.3
pkgrel=1
pkgdesc="A GObject-based plugins engine"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('gtk3' 'hicolor-icon-theme' 'gobject-introspection')
makedepends=('gtk-doc' 'intltool' 'vala' 'python2-gobject' 'gjs' 'seed')
optdepends=('gjs: gobject-based plugin engine - gjs runtime loader'
            'seed: gbject-based plugin engine - seed runtime loader')
options=('!libtool')
install=libpeas.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz)
sha256sums=('74e80a86f7f34bb03ab914ecd6f5fb8201c062f577b9b60317e6cc72bbedb2c0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
