# $Id$
# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: arjan <arjan@archlinux.org>

pkgname=libusb-compat
pkgver=0.1.4
pkgrel=2
pkgdesc="Library to enable user space application programs to communicate with USB devices"
arch=('i686' 'x86_64')
url="http://libusb.sourceforge.net/"
license=('LGPL')
depends=('libusb' 'sh')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname%-*}/${pkgname}-${pkgver%.*}/$pkgname-$pkgver/${pkgname}-${pkgver}.tar.bz2"
        "libusb-0.1-libusbx.patch")   
md5sums=('2ca521fffadd0c28fdf174e6ec73865b'
         '1612c3982f307da1332d6db57b16742b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # http://libusb.org/ticket/138 can be removed with libusbx 1.0.13
  patch -Np0 -i ../libusb-0.1-libusbx.patch
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

