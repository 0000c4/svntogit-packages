# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=3.32.0
pkgrel=1
pkgdesc="GNOME Text Editor"
url="https://wiki.gnome.org/Apps/Gedit"
arch=(x86_64)
license=(GPL)
depends=(gtksourceview4 gsettings-desktop-schemas libpeas gspell python-gobject dconf)
makedepends=(yelp-tools intltool vala gobject-introspection gnome-common git gtk-doc meson)
optdepends=('gedit-plugins: Additional features')
groups=(gnome)
_commit=b2f8efd099584fbe767c6eccbd293a760a2bfea4  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gedit.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
