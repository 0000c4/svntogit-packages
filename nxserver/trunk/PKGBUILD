# $Id$
# Maintainer Tobias Powalowski <tpowa@archlinux.org>
# Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org>

pkgname=nxserver
pkgver=3.3.0
pkgrel=4
pkgdesc="NoMachine NX is the next-generation X compression and roundtrip suppression scheme."
arch=(i686 x86_64)
url="http://nomachine.com/"
license=('GPL')
depends=("nx-common>=$pkgver" 'libxaw' 'libxrender' 'libxp' 'gcc-libs' 'libjpeg' \
	 'libxpm' 'libpng' 'libxdamage' 'libxrandr' 'libxcomposite' 'libxtst' 'freetype2')
makedepends=('imake')
source=(
#X11 support programs and libraries
http://64.34.161.181/download/$pkgver/sources/nx-X11-$pkgver-5.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxwin-$pkgver-2.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxauth-$pkgver-1.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcomp-$pkgver-3.tar.gz
#X11 Agent sources
http://64.34.161.181/download/$pkgver/sources/nxagent-$pkgver-10.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompsh-$pkgver-1.tar.gz
#Compression libs and proxy sources
http://64.34.161.181/download/$pkgver/sources/nxproxy-$pkgver-2.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompext-$pkgver-3.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompshad-$pkgver-3.tar.gz
#64bit fixes
NXproto.h.64bit.diff
# gcc 43 fix
nxcompsh-gcc43.patch
nxcompshad-gcc43.patch)
options=(!libtool) 
md5sums=('6dae2fddc8081d30530f68e806105fd7'
         '71de8e60c0cdee887e00d1a44f337ad1'
         '6d59de2cbf61430ac513ddfa6b05bc76'
         '2327cca8e6116fd6a96345566336d81d'
         '7abd7812428aebcc48b996bcb5f1970b'
         'ed47951ffceb9e8483f7b4202b1cddb0'
         '047206e5a811b915aac4ae09bddef207'
         'f138420fc7a005495675146a1e1539f2'
         '02f051366c4aae96bfefda60d701d8b4'
         '58341ba70dfab92ff38570071fbbf88a'
         'b6c279654dac421fc3dd1a27d66ff53c'
         '01cea8bc5997ae2c3790cbcb7d624c86')

build() {
  cd $startdir/src
  mkdir -p ${pkgdir}/opt/NX/bin
  mkdir -p ${pkgdir}/opt/NX/lib
  patch -Np0 -i ../nxcompsh-gcc43.patch || return 1
  patch -Np0 -i ../nxcompshad-gcc43.patch || return 1
  cd ${srcdir}/nxcomp
  if [ "$CARCH" = "x86_64" ]; then
    patch -Np1 -i ../NXproto.h.64bit.diff  || return 1
  fi
  ./configure --prefix=/opt/NX
  make || return 1

  cd ${srcdir}/nxcompshad
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompshad.so* ${pkgdir}/opt/NX/lib

  cd ${srcdir}/nxcompsh
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompsh.so* ${pkgdir}/opt/NX/lib

  cd ${srcdir}/nxproxy
  ./configure --prefix=/opt/NX
  make || return 1
  make prefix=${pkgdir}/opt/NX install || return 1

  cd ${srcdir}/nx-X11
  make World || return 1
  cp -a lib/X11/libX11.so* ${pkgdir}/opt/NX/lib
  cp -a lib/Xext/libXext.so* ${pkgdir}/opt/NX/lib
  cp -a lib/Xrender/libXrender.so* ${pkgdir}/opt/NX/lib
  install -D -m755 programs/Xserver/nxagent ${pkgdir}/opt/NX/bin/nxagent
  install -D -m755 programs/nxauth/nxauth ${pkgdir}/opt/NX/bin/nxauth

  cd ${srcdir}/nxcompext 
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompext.so* ${pkgdir}/opt/NX/lib

  # fix libXcompext linking
  cd ${pkgdir}/opt/NX/lib
  ln -s libXcompext.so.3 libXcompext.so.1
}

