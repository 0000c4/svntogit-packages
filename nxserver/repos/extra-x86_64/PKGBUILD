# $Id$
# Maintainer Tobias Powalowski <tpowa@archlinux.org>
# Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org>

pkgname=nxserver
pkgver=3.4.0
pkgrel=2
pkgdesc="NoMachine NX is the next-generation X compression and roundtrip suppression scheme."
arch=(i686 x86_64)
url="http://nomachine.com/"
license=('GPL')
depends=("nx-common>=$pkgver" 'libxaw' 'libxrender' 'libxp' 'gcc-libs' 'libjpeg>=8' \
	 'libxpm' 'libpng>=1.4.0' 'libxdamage' 'libxrandr' 'libxcomposite' 'libxtst' 'freetype2')
makedepends=('imake')
source=(
#X11 support programs and libraries
http://64.34.161.181/download/$pkgver/sources/nx-X11-$pkgver-1.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxwin-$pkgver-2.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxauth-$pkgver-1.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcomp-$pkgver-1.tar.gz
#X11 Agent sources
http://64.34.161.181/download/$pkgver/sources/nxagent-$pkgver-3.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompsh-$pkgver-1.tar.gz
#Compression libs and proxy sources
http://64.34.161.181/download/$pkgver/sources/nxproxy-$pkgver-2.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompext-$pkgver-1.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompshad-$pkgver-1.tar.gz
#64bit fixes
NXproto.h.64bit.diff
# gcc 43 fix
nxcompsh-gcc43.patch
nxcompshad-gcc43.patch
nx-gcc44.patch)
options=(!libtool) 

build() {
  cd $startdir/src
  mkdir -p ${pkgdir}/opt/NX/bin
  mkdir -p ${pkgdir}/opt/NX/lib
  patch -Np0 -i ../nxcompsh-gcc43.patch || return 1
  patch -Np0 -i ../nxcompshad-gcc43.patch || return 1
  patch -Np1 -i ../nx-gcc44.patch || return 1
  
  cd ${srcdir}/nxcomp
  if [ "$CARCH" = "x86_64" ]; then
    patch -Np1 -i ../NXproto.h.64bit.diff  || return 1
  fi
  ./configure --prefix=/opt/NX
  make || return 1

  cd ${srcdir}/nxcompshad
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompshad.so* ${pkgdir}/opt/NX/lib

  cd ${srcdir}/nxcompsh
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompsh.so* ${pkgdir}/opt/NX/lib

  cd ${srcdir}/nxproxy
  ./configure --prefix=/opt/NX
  make || return 1
  make prefix=${pkgdir}/opt/NX install || return 1

  cd ${srcdir}/nx-X11
  make World || return 1
  cp -a lib/X11/libX11.so* ${pkgdir}/opt/NX/lib
  cp -a lib/Xext/libXext.so* ${pkgdir}/opt/NX/lib
  cp -a lib/Xrender/libXrender.so* ${pkgdir}/opt/NX/lib
  install -D -m755 programs/Xserver/nxagent ${pkgdir}/opt/NX/bin/nxagent
  install -D -m755 programs/nxauth/nxauth ${pkgdir}/opt/NX/bin/nxauth

  cd ${srcdir}/nxcompext 
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompext.so* ${pkgdir}/opt/NX/lib

  # fix libXcompext linking
  cd ${pkgdir}/opt/NX/lib
  ln -s libXcompext.so.3 libXcompext.so.1
}
md5sums=('94b805bb120541effab45c92ffe3eacd'
         'aa3ce13aef5dc74a4b763f180509537d'
         'dd92b9077716e309a0a78234d93ac2d1'
         '3dc3d0bca883ceb0d74d9774d63668c6'
         'b99ed0e1ab28f24107968f935f927432'
         '9f1e7735411d3746d0cf6a5843a265d9'
         '95ce93520d463a3d18cdd5d19c321e85'
         '605a8e2a136f89477f0059a0d2af4582'
         'c47bbb68c3a41124a3484c9968784ee9'
         '58341ba70dfab92ff38570071fbbf88a'
         'b6c279654dac421fc3dd1a27d66ff53c'
         '01cea8bc5997ae2c3790cbcb7d624c86'
         'c805442f1500d8d6de15ec224f63277e')
