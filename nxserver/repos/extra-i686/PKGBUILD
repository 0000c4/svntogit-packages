# $Id$
# Maintainer Tobias Powalowski <tpowa@archlinux.org>
# Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org>
pkgname=nxserver
pkgver=3.2.0
pkgrel=1
pkgdesc="NoMachine NX is the next-generation X compression and roundtrip suppression scheme."
arch=(i686 x86_64)
url="http://nomachine.com/"
license=('GPL')
depends=('nx-common>=$pkgver' 'libxaw' 'libxrender' 'libxp' 'gcc-libs' 'libjpeg' \
	 'libxpm' 'libpng' 'libxdamage' 'libxrandr' 'libxcomposite' 'libxtst' 'freetype2')
makedepends=('imake')
source=(\
#X11 support programs and libraries
http://64.34.161.181/download/$pkgver/sources/nx-X11-$pkgver-2.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxwin-$pkgver-4.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxauth-$pkgver-1.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxcomp-$pkgver-7.tar.gz \
#X11 Agent sources
http://64.34.161.181/download/$pkgver/sources/nxagent-$pkgver-10.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxcompsh-$pkgver-1.tar.gz \
#Compression libs and proxy sources
http://64.34.161.181/download/$pkgver/sources/nxproxy-$pkgver-1.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxcompext-$pkgver-1.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxcompshad-$pkgver-3.tar.gz \
#64bit fixes
NXproto.h.64bit.diff
# gcc 43 fix
nxcompsh-gcc43.patch
nxcompshad-gcc43.patch)
options=(!libtool) 

build() {
  cd $startdir/src
  mkdir -p $startdir/pkg/opt/NX/bin
  mkdir -p $startdir/pkg/opt/NX/lib
  patch -Np0 -i ../nxcompsh-gcc43.patch || return 1
  patch -Np0 -i ../nxcompshad-gcc43.patch || return 1
  cd $startdir/src/nxcomp
  if [ "$CARCH" = "x86_64" ]; then
    patch -Np1 -i ../NXproto.h.64bit.diff  || return 1
  fi
  ./configure --prefix=/opt/NX
  make || return 1

  cd $startdir/src/nxcompshad
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompshad.so* $startdir/pkg/opt/NX/lib

  cd $startdir/src/nxcompsh
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompsh.so* $startdir/pkg/opt/NX/lib

  cd $startdir/src/nxproxy
  ./configure --prefix=/opt/NX
  make || return 1
  make prefix=$startdir/pkg/opt/NX install || return 1

  cd $startdir/src/nx-X11
  make World || return 1
  cp -a lib/X11/libX11.so* $startdir/pkg/opt/NX/lib
  cp -a lib/Xext/libXext.so* $startdir/pkg/opt/NX/lib
  cp -a lib/Xrender/libXrender.so* $startdir/pkg/opt/NX/lib
  install -D -m755 programs/Xserver/nxagent $startdir/pkg/opt/NX/bin/nxagent
  install -D -m755 programs/nxauth/nxauth $startdir/pkg/opt/NX/bin/nxauth

  cd $startdir/src/nxcompext 
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcompext.so* $startdir/pkg/opt/NX/lib

  # fix libXcompext linking
  cd $startdir/pkg/opt/NX/lib
  ln -s libXcompext.so.3 libXcompext.so.1
}
md5sums=('0a969199c77a604a488794c56176000f'
         '3a96ece73da0699f1e6eb7165b16d141'
         '18519f2bcf30b10b766a60926fbe1017'
         '5ea64a557c770d9f5cc4b9a7a9d1343c'
         '9123c0bbd184cdeb343c7c6f1e267161'
         '57cc9cb3acebf792383bfcc50e715d48'
         'ac31e8f2f112e3720f3c00cec67c0734'
         'cd1296ebd24b1d7c4f82537a395ad6e8'
         '6edfa4f65f579306f05af2451249c2bf'
         '58341ba70dfab92ff38570071fbbf88a'
         'b6c279654dac421fc3dd1a27d66ff53c'
         '01cea8bc5997ae2c3790cbcb7d624c86')
