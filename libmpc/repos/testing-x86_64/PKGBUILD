# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=libmpc
pkgver=1.0
pkgrel=1
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"
arch=('i686' 'x86_64')
url="http://www.multiprecision.org/"
license=('LGPL')
depends=('mpfr>=3.0.0')
options=('!libtool')
install=libmpc.install
source=(http://www.multiprecision.org/mpc/download/mpc-${pkgver/_/-}.tar.gz)
md5sums=('13370ceb2e266c5eeb2f7e78c24b7858')

build() {
  cd "${srcdir}/mpc-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/mpc-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/mpc-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/share/info/{mpc,libmpc}.info
}
