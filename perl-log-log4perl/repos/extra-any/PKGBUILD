# $Id$
# Packager: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-log-log4perl
pkgver=1.40
pkgrel=1
pkgdesc='Log4j implementation for Perl'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Log-Log4perl
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHILLI/Log-Log4perl-$pkgver.tar.gz")
md5sums=('e05ab4508863116bad03ef26dd9f98fd')

build()
(
  cd Log-Log4perl-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd Log-Log4perl-$pkgver
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd Log-Log4perl-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
