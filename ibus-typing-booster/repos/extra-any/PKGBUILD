# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=ibus-typing-booster
pkgver=1.5.16
pkgrel=2
pkgdesc="Predictive input method for the IBus platform"
url="https://fedorahosted.org/ibus-typing-booster/"
arch=(any)
license=(GPL3)
depends=(ibus python-gobject python-dbus python-pyenchant m17n-lib sqlite)
makedepends=(git m17n-db hunspell hunspell-{de,el,en,es,fr,he,hu,it,nl,pl,ro})
_commit=1127c43096d0c267d7b18cf9c86417b78d7ae61c  # master
source=("git+https://git.fedorahosted.org/git/ibus-typing-booster#commit=$_commit"
        0001-Unbreak-sqlite-on-Python-3.6.patch)
sha512sums=('SKIP'
            '86a0a24baa2f55a3140bcd98e5d442ec823e4032cc5069357d7be76b2d1bf58eb73ea4b464fdc92bf2b98f42e3f12559da715374e06de8173da0f5dc6c02b15d')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../0001-Unbreak-sqlite-on-Python-3.6.patch
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  gzip --force --best "$pkgdir"/usr/share/$pkgname/data/*.{xml,txt,json}
}

