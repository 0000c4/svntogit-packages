# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyasn1-modules
pkgname=('python-pyasn1-modules' 'python2-pyasn1-modules')
pkgver=0.0.11
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=('any')
license=('BSD')
url="http://pypi.python.org/pypi/pyasn1-modules"
makedepends=('python-pyasn1' 'python2-pyasn1' 'python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/pyasn1-modules/pyasn1-modules-$pkgver.tar.gz")
sha512sums=('0c9943f1fe49453a97ede8aa14248340dc1e331d0d80fa948b507743d3f30fe61d99323b99ca43930d22c863611ae4a2bd9859726620756f345ff2d5873efb8d')

prepare() {
  cp -a pyasn1-modules-$pkgver{,-py2}
}

build() {
  # Build python 3 module
  cd "$srcdir"/pyasn1-modules-$pkgver
  python3 setup.py build
 
  # Build python 2 module
  cd "$srcdir"/pyasn1-modules-$pkgver-py2
  python2 setup.py build
}
 
package_python-pyasn1-modules() {
  depends=('python-pyopenssl' 'python-pyasn1')
 
  cd pyasn1-modules-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
 
package_python2-pyasn1-modules() {
  depends=('python2-pyopenssl' 'python2-pyasn1')
 
  cd pyasn1-modules-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
