# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyasn1-modules
pkgname=('python-pyasn1-modules' 'python2-pyasn1-modules')
pkgver=0.0.9
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=('any')
license=('BSD')
url="http://pypi.python.org/pypi/pyasn1-modules"
makedepends=('python-pyasn1' 'python2-pyasn1' 'python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/pyasn1-modules/pyasn1-modules-$pkgver.tar.gz")
sha512sums=('5f00192493b50998e6d8b44d2f6134124e60166fb8d0ec22a6108fa1cf8f2e5cf134326f94347029fbf15319ff36070bf68eb13c306f3839f36c42df4c7b5f64')

prepare() {
  cp -a pyasn1-modules-$pkgver{,-py2}
}

build() {
  # Build python 3 module
  cd "$srcdir"/pyasn1-modules-$pkgver
  python3 setup.py build
 
  # Build python 2 module
  cd "$srcdir"/pyasn1-modules-$pkgver-py2
  python2 setup.py build
}
 
package_python-pyasn1-modules() {
  depends=('python-pyopenssl' 'python-pyasn1')
 
  cd pyasn1-modules-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
 
package_python2-pyasn1-modules() {
  depends=('python2-pyopenssl' 'python2-pyasn1')
 
  cd pyasn1-modules-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
