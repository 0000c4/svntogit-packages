# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.1
pkgrel=3
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs')
makedepends=('boost' 'python2' 'python3')
options=('!emptydirs')
_pkgver=${pkgver//./_}
source=(https://github.com/arvidn/libtorrent/archive/libtorrent-${_pkgver}/$pkgname-$pkgver.tar.gz
        libtorrent-rasterbar-1.1.0-fix-get-ip-filter.patch
        libtorrent-rasterbar-1.1.0-python-fixes.patch
        libtorrent-rasterbar-1.1.0-fix-invalid-input-crash.patch)
sha1sums=('ac6e871d3b71a56e849ab1fc6369165a80acfd32'
          'b05f12f7d43960273b2d7e15aeeeb2bbc755b58b'
          '5ade010320bde598f48351f0659f88d9d980fad4'
          '3dd8a74d5ce785755efbf8fc10d3013bba4bc261')

prepare() {
  mkdir py2 py3
  cd libtorrent-libtorrent-${_pkgver}

  # Backports from master, patches sourced from
  # https://build.opensuse.org/package/show/devel:libraries:c_c++/libtorrent-rasterbar
  patch -Np1 -i ../libtorrent-rasterbar-1.1.0-fix-get-ip-filter.patch
  patch -Np1 -i ../libtorrent-rasterbar-1.1.0-python-fixes.patch
  patch -Np1 -i ../libtorrent-rasterbar-1.1.0-fix-invalid-input-crash.patch

  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  ./autotool.sh
}

_build() (
  cd py$1
  # https://github.com/qbittorrent/qBittorrent/issues/5265#issuecomment-220007436
  CXXFLAGS="$CXXFLAGS -std=c++11" \
  PYTHON=/usr/bin/python$1 \
  ../libtorrent-libtorrent-${_pkgver}/configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv
)

build() {
  _build 2
  _build 3
}

package() {
  make -C py2 DESTDIR="$pkgdir" install
  make -C py3 DESTDIR="$pkgdir" install
  install -Dm644 libtorrent-libtorrent-${_pkgver}/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
