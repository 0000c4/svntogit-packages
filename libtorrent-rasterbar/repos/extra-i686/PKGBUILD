# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=0.16.10
pkgrel=2
epoch=1
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python2')
makedepends=('boost')
options=('!emptydirs')
source=(http://libtorrent.googlecode.com/files/$pkgname-$pkgver.tar.gz
        'boost-154.patch')
sha1sums=('8b24442132e52f8765c79b20ffdea8b33bc5aa42'
          'b1e66ffbeba5c2b217bc0d02bc2c49aa6b603fa3')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "${srcdir}"/boost-154.patch
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --enable-python-binding \
      --with-libgeoip=system
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
