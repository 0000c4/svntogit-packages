# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.1.5
pkgrel=2
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs')
makedepends=('boost' 'python2' 'python')
options=('!emptydirs')
_pkgver=${pkgver//./_}
source=(https://github.com/arvidn/libtorrent/archive/libtorrent-${_pkgver}/$pkgname-$pkgver.tar.gz
        boost-1.66.patch
        boost-1.66-2.patch)
sha512sums=('edaff6347bf922af8b6d74e332e180fe122f64152a152fb905f4f2c3371dc29d668fc2b0ffcda3f56b50a1a51b25d1a3ae4284ed6e09f6a2eb7b34bcce8dd9c7'
            'e26464b2921f5e197252c3452ba6271a22e64ebc486d00c26870c8ad0751a5de13cb01dd1ab1f04ec9358e7c58831599360f3d4205bf709ebbf686372d37f9de'
            '69231b14900a303a9e89803f19700eadfdae997f8cac8d562e6076f3c7e5eddeb2123017acc3b21caf1c1a102c369a7fe53d1f1876c3ad9d6efd2f2032fa2267')

prepare() {
  mkdir py2 py3
  cd libtorrent-libtorrent-${_pkgver}

  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  # Fix build with boost 1.66
  patch -p1 -i "$srcdir"/boost-1.66.patch
  patch -p1 -i "$srcdir"/boost-1.66-2.patch

  ./autotool.sh
}

_build() (
  cd py$1

  # FS#50745
  _boost="boost_python"
  if [ $1 -eq 3 ]; then _boost="boost_python3"; fi

  # https://github.com/qbittorrent/qBittorrent/issues/5265#issuecomment-220007436
  CXXFLAGS="$CXXFLAGS -std=c++11" \
  PYTHON=/usr/bin/python$1 \
  ../libtorrent-libtorrent-${_pkgver}/configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv \
    --with-boost-python=$_boost
)

build() {
  _build 2
  _build 3
}

package() {
  make -C py2 DESTDIR="$pkgdir" install
  make -C py3 DESTDIR="$pkgdir" install
  install -Dm644 libtorrent-libtorrent-${_pkgver}/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
