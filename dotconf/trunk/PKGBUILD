# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Chris Brannon <cmbrannon@cox.net>

pkgname=dotconf
pkgver=1.3
pkgrel=1
pkgdesc='A C library for parsing configuration files'
url="https://github.com/williamh/dotconf"
arch=('i686' 'x86_64')
depends=('glibc')
makedepends=('pkgconfig' 'findutils')
license=('LGPL2.1')
source=("http://files.opentts.org/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('35c19ec9b96648f53e987e47e52dbc4c')
options=('!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # There are no manpages.  Docu consists of textfiles and small examples.
  # "make install" didn't install the docu, so do it manually.
  install -d "${pkgdir}"/usr/share/doc/${pkgname}
  cp -a doc/*.txt README examples/ "${pkgdir}"/usr/share/doc/${pkgname}
  # Get rid of files specific to Microsoft products:
  find "${pkgdir}"/usr/share/doc/${pkgname} -name '*.dsp' | xargs rm -f

  # Dotconf uses libpool internally for memory management.  No need to install.
  rm -f "${pkgdir}"/usr/lib/libpool.a
  rm -f "${pkgdir}"/usr/include/libpool.h
}
