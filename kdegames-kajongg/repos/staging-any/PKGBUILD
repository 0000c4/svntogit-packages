# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kajongg
pkgver=16.11.80
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="http://kde.org/applications/games/kajongg/"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'python2-twisted' 'kdebindings-python2' 'libkmahjongg')
makedepends=('cmake' 'automoc4')
optdepends=('vorbis-tools: for playing sounds')
conflicts=('libkmahjongg4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kajongg-${pkgver}.tar.xz"{,.sig})
sha1sums=('53c64b20d0a330b2e283a732c09eecdc44402796'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

  cd kajongg-${pkgver}
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    src/{deferredutil,kajongg,kajonggserver,server}.py
}

build() {
  cd build
  cmake ../kajongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_KAJONGG=TRUE
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# Use tilesets from KF5 libkhmahjongg
  ln -s /usr/share/kmahjongglib "$pkgdir"/usr/share/apps/kmahjongglib
}
