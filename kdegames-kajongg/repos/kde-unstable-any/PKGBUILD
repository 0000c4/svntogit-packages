# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kajongg
pkgver=16.07.80
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="http://kde.org/applications/games/kajongg/"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'python2-twisted' 'kdebindings-python2' 'libkmahjongg')
makedepends=('cmake' 'automoc4')
optdepends=('vorbis-tools: for playing sounds')
conflicts=('libkmahjongg4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kajongg-${pkgver}.tar.xz")
sha1sums=('0da36c39e1405245c7088cb1f1d8f216b43db6e7')

prepare() {
  mkdir -p build

  cd kajongg-${pkgver}
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    src/{deferredutil,kajongg,kajonggserver,server}.py
}

build() {
  cd build
  cmake ../kajongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DINSTALL_KAJONGG=TRUE
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# Use tilesets from KF5 libkhmahjongg
  ln -s /usr/share/kmahjongglib "$pkgdir"/usr/share/apps/kmahjongglib
}
