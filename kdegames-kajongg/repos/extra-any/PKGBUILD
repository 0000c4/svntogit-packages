# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kajongg
pkgver=16.08.2
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="http://kde.org/applications/games/kajongg/"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'python-twisted' 'kdebindings-python' 'libkmahjongg')
makedepends=('cmake' 'automoc4')
optdepends=('vorbis-tools: for playing sounds')
conflicts=('libkmahjongg4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kajongg-${pkgver}.tar.xz")
sha1sums=('e6eeaecfb642edfcd87e6950908bc87294732dde')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kajongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_KAJONGG=TRUE
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# Use tilesets from KF5 libkhmahjongg
  ln -s /usr/share/kmahjongglib "$pkgdir"/usr/share/apps/kmahjongglib
}
