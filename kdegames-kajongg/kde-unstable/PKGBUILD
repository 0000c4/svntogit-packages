# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kajongg
pkgver=16.11.90
pkgrel=3
pkgdesc="The ancient Chinese board game for 4 players"
url="http://kde.org/applications/games/kajongg/"
arch=(any)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(kdebase-runtime python2-twisted python2-pyqt4 libkmahjongg)
makedepends=()
optdepends=('vorbis-tools: for playing sounds')
source=("http://download.kde.org/unstable/applications/$pkgver/src/kajongg-$pkgver.tar.xz"{,.sig}
        kajongg-open-settings.patch::"https://cgit.kde.org/kajongg.git/patch/?id=d801f84d")
sha1sums=('909230a180548b898571ce032a908bf51fa3ca5f'
          'SKIP'
          'eb65b697088d378b80b838bcec4b6bf4316e8251')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  cd kajongg-$pkgver
# Fix opening settings
  patch -p1 -i ../kajongg-open-settings.patch
# Fix shebang
  find . -type f | xargs sed -e 's|\/usr\/bin\/env python|/usr/bin/env python2|' -i   
}

build() {
  cd kajongg-$pkgver
  python2 setup.py build
}

package() {
  cd kajongg-$pkgver
  python2 setup.py install --root="$pkgdir"
}
