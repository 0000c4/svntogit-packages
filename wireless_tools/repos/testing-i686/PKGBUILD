# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=wireless_tools
pkgver=29
pkgrel=6
pkgdesc='Wireless Tools'
url='http://www.hpl.hp.com/personal/Jean_Tourrilhes/Linux/Tools.html'
arch=('i686' 'x86_64')
license=('GPL')
source=("http://www.hpl.hp.com/personal/Jean_Tourrilhes/Linux/${pkgname}.${pkgver}.tar.gz")
sha1sums=('22040ac1497f4c5f8ddeca365591b01ae3475710')

build() {
	cd "${srcdir}/${pkgname}.${pkgver}"
	make CFLAGS="${CFLAGS} -Wall -Wextra -I." LDFLAGS="${LDFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}.${pkgver}"
	make \
		INSTALL_DIR="${pkgdir}/usr/sbin" \
		INSTALL_LIB="${pkgdir}/usr/lib" \
		INSTALL_INC="${pkgdir}/usr/include" \
		INSTALL_MAN="${pkgdir}/usr/share/man" \
		install
}
