# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kimap
pkgver=16.07.90
pkgrel=1
pkgdesc="Job-based API for interacting with IMAP servers"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kmime kio)
makedepends=(extra-cmake-modules python kdoctools)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('3f3fbe041b5efbb7c4ef3e8dbf692cbc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
