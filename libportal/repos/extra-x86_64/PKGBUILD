# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
 
pkgname=libportal
pkgver=0.0.1
pkgrel=1
pkgdesc="GIO-style async APIs for most Flatpak portals"
url="https://github.com/flatpak/libportal"
arch=(x86_64)
license=(LGPL)
depends=(glib2)
makedepends=(git meson gtk-doc gtk3 gst-plugins-base)
optdepends=('gtk3: portal-test app'
            'gst-plugins-base: portal-test app')
_commit=67d780f4cd4d398c7621b16de36c7c00307d62cd  # tags/0.0.1^0
source=("git+https://github.com/flatpak/libportal#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
