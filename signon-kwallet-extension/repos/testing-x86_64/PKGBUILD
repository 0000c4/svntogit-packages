# Maintainer: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=17.08.0
pkgrel=1
pkgdesc="KWallet signon extension"
arch=('i686' 'x86_64')
url="https://kde.org"
license=('LGPL')
groups=('kde-applications' 'kdenetwork')
depends=('signond' 'kwallet')
makedepends=('extra-cmake-modules')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('af19be2c7b9109e82995b31e0ff28645f0c76eba8d8dce63bd823d887296ef5b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
