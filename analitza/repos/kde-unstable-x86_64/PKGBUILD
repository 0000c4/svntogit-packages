# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=analitza
pkgver=16.07.80
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://edu.kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('qt5-svg' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'eigen')
replaces=('kdebase-analitza')
conflicts=('kdebase-analitza')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/analitza-${pkgver}.tar.xz")
sha1sums=('69ca3a6be3a5c0685a50ab4c7bc5b0e57fa666a8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
