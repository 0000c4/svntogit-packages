# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=kdeplasma-applets-networkmanager
pkgver=1047027
pkgrel=1
pkgdesc="KDE4 NetworkManager plasmoid"
arch=('i686' 'x86_64')
url="http://plasma.kde.org/"
license=('GPL')
depends=('kdelibs>=4.3.0' 'kdebase-workspace>=4.3.0' 'networkmanager>=0.7.1')
makedepends=('cmake' 'python' 'automoc4')
conflicts=('networkmanager-plasmoid-svn')
replaces=('networkmanager-plasmoid-svn')
#svn co -q svn://websvn.kde.org:443/home/kde/trunk/playground/base/plasma/applets/networkmanager/ kdeplasma-applets-networkmanager
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-r${pkgver}.tar.bz2"
	'remove-i-know-what-am-doing.patch')
md5sums=('ab554dfe2f503f176d33168f3042c80b'
         'f4edca116beef0d70a93b5709b854e54')

build() {
  cd ${srcdir}/${pkgname}
  # we can't build plasmoid with qt 4.5
  patch -R -p6 -i ${srcdir}/remove-i-know-what-am-doing.patch || return 1

  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d
  make  || return 1
  make DESTDIR=${pkgdir} install
}
