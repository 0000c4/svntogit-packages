# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=kdeplasma-applets-networkmanager
pkgver=1040936
pkgrel=1
pkgdesc="KDE4 NetworkManager plasmoid"
arch=('i686' 'x86_64')
url="http://plasma.kde.org/"
license=('GPL')
depends=('kdelibs>=4.3.0' 'kdebase-workspace>=4.3.0' 'networkmanager>=0.7.1')
makedepends=('cmake' 'subversion' 'python' 'automoc4')
conflicts=('networkmanager-plasmoid-svn')
replaces=('networkmanager-plasmoid-svn')
#svn co -q svn://websvn.kde.org:443/home/kde/trunk/playground/base/plasma/applets/networkmanager/ kdeplasma-applets-networkmanager
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-r${pkgver}.tar.bz2")
md5sums=('da1b7a439ea03b7db5339b90d313f2ad')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d
  make  || return 1
  make DESTDIR=${pkgdir} install
}
