# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons
pkgver=5.10.3
pkgrel=2
pkgdesc='All kind of addons to improve your Plasma experience'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('plasma-workspace')
makedepends=('extra-cmake-modules' 'kdoctools' 'kross' 'python' 'kdesignerplugin')
optdepends=('kross: comic applet'
            'purpose: Quickshare applet'
            'quota-tools: disk quota applet')
conflicts=('kdebase-workspace' 'kdeplasma-addons-frameworks')
replaces=('kdeplasma-addons-frameworks')
groups=('plasma')
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-382261.patch::"https://cgit.kde.org/kdeplasma-addons.git/patch/?id=0ed491a1")
sha256sums=('fa3e6639cd002b6a4d90f4f8cc0a784b72489098410ef2f149ae10148ae2f3d3'
            'SKIP'
            'e75edf65c2fb1fee0d35ee1d7c1b9f2fa145ac927048583662fdee80e0e83f7d')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-382261.patch # Fix color picker on multi-screen
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
