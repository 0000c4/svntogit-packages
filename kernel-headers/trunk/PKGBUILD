# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: kernel-headers->glibc->binutils->gcc-libs->gcc->binutils->glibc

pkgname=kernel-headers
pkgver=2.6.30.1
_basever=2.6.30
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/libc"
groups=('base')
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-${_basever}.tar.bz2
	http://www.kernel.org/pub/linux/kernel/v2.6/patch-${pkgver}.bz2)
md5sums=('7a80058a6382e5108cdb5554d1609615'
         'ff5bdcc9624b2653d49efa0b305b1e57')

build() {
  cd ${srcdir}/linux-${_basever}
  patch -Np1 -i ${srcdir}/patch-${pkgver} || return 1

  make mrproper
  make headers_check || return 1
  make INSTALL_HDR_PATH=${pkgdir}/usr headers_install

  rm -rf ${pkgdir}/usr/include/drm
}
