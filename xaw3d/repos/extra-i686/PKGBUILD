# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=xaw3d
pkgver=1.5E
pkgrel=1
pkgdesc="Three-D Athena widgets"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/xaw3d/d"
license=('MIT')
depends=('libxp' 'libxmu' 'libxpm')
makedepends=('imake')
options=('!makeflags' 'force')
source=(ftp://ftp.visi.com/users/hawkeyd/X/Xaw3d-${pkgver}.tar.gz \
        Xaw3d-1.5E-xorg-imake.patch Xaw3d-1.5-i18n.patch \
        Xaw3d-1.5-box.c.patch Xaw3d-1.5-debian-fixes.patch \
        xaw3d.patch LICENSE)
md5sums=('29ecfdcd6bcf47f62ecfd672d31269a1' '2cca7950cf7d270609bc810b8996f0a9'\
         'e7105bc1452f0c5f5d0bcdcf212aac0c' 'c061ab65467766f5817752ef216e3cc2'\
         '33bf0f1b52822b7293a3f45474178dfe' '5acb23e5a52c75e62a22aef59882df14'\
         '5b6b0242b3f53b7fe308e702abe28561')
sha1sums=('efc5b923feda52866c859c59a5b553cb675a69d1'
          '8c3e57bd48028e8e8219fb43bfd3804bbd09ed43'
          '27d409ce223d4fc3b81dd8217d5774b877695087'
          'b57f68f19d6c9f501be94606c649fcaf8e1bd3ec'
          'b1821f7b7d7aa01d255c99d2a713af5174674ea8'
          '18274b06ccff395fe541b44c8821b55a2c15ebb4'
          'f4c6ea3294b6ca41e17b7a83530bd694af6f1f5a')

build() {
  cd ${srcdir}/xc/lib/Xaw3d
  patch -p0 < ${srcdir}/Xaw3d-1.5E-xorg-imake.patch || return 1
  patch -p4 < ${srcdir}/xaw3d.patch || return 1
  patch -p4 < ${srcdir}/Xaw3d-1.5-i18n.patch || return 1
  patch -p4 < ${srcdir}/Xaw3d-1.5-box.c.patch || return 1
  patch -p4 < ${srcdir}/Xaw3d-1.5-debian-fixes.patch || return 1

  mkdir X11
  cd X11
  ln -sf ../../Xaw3d .
  cd ..

  xmkmf || return 1
  # fix for the lib location
  [ "$CARCH" = "x86_64" ] && (sed -i -e "s:lib64:lib:g" Makefile || return 1)
  make includes || return 1
  make depend || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
