# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xaw3d
pkgver=1.6.2
pkgrel=3
pkgdesc="Three-D Athena widgets"
arch=('x86_64')
url="https://directory.fsf.org/project/xaw3d/"
license=('MIT')
depends=('libxmu')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/lib/libXaw3d-${pkgver}.tar.bz2)
sha512sums=('11b15884e47efc139aa019f0a76086913535767fb2e20fcd837d810d3b6b4a7bda3408be5fa0cbe90ae16eeaebe5db9b0ec77a0e2b5767590662ef3a7c9c96ea')

build() {
  cd libXaw3d-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXaw3d-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
