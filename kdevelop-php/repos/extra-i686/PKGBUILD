# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=1.3.1
_pkgver=4.3.1
pkgrel=1
pkgdesc="PHP language and documentation plugin for KDevelop/Quanta"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop-pg-qt')
makedepends=('cmake' 'automoc4')
replaces=('kdevelop-php-docs')
conflicts=('kdevelop-php-docs')
source=(http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-{,docs-}${pkgver}.tar.bz2)
sha1sums=('c5d31de0bf0ae4b242b6265851807e5b44606525'
          '2b5c3dd2b26196a83bc95cfe8ee595e160f595f0')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make

  # kdevelop-php-docs
  cd "${srcdir}"
  mkdir build-docs
  cd build-docs
  cmake ../${pkgname}-docs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  # kdevelop-php-docs
  cd "${srcdir}"/build-docs
  make DESTDIR="${pkgdir}" install
}
