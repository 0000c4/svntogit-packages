# $Id: PKGBUILD 248919 2015-10-12 10:26:59Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=4.90.91
pkgrel=1
pkgdesc="PHP language and documentation plugin for KDevelop"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt)
source=(http://download.kde.org/unstable/kdevelop/$pkgver/src/kdev-php-$pkgver.tar.xz)
sha1sums=('1b84ee03bfe94d14edf90304a78b7ff405935cd0')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdev-php-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
