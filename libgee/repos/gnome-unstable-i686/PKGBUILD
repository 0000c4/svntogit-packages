# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libgee
pkgver=0.8.0
pkgrel=1
pkgdesc="GObject collection library"
url="http://live.gnome.org/Libgee"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz)
sha256sums=('5e3707cbc1cebea86ab8865682cb28f8f80273869551c3698e396b5dc57831ea')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --disable-static
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
