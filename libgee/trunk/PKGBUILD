# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libgee
pkgver=0.6.6.1
pkgrel=1
pkgdesc="GObject collection library"
url="http://live.gnome.org/Libgee"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz)
sha256sums=('ac6598776cda8d1c8689930f48df074e88f17e9f5832bc300902ded5ab8144dd')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --disable-static
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
