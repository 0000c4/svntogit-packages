# $Id $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=4.4.92
pkgrel=1
pkgdesc='KDE PIM Runtime Environment'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdepimlibs' 'kdebase-runtime')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'docbook-xsl')
install=${pkgname}.install
source=("http://download.kde.org/unstable/kdepim/${pkgname}-${pkgver}.tar.bz2"
	'missing-include.patch')
sha1sums=('5673fa8183a02e715c00fe068b57a9956b54eb80'
	'a5bc5a221b84976330568a9612aa0005214d3311')

build() {
	cd ${srcdir}
	patch -Np0 -i ${srcdir}/missing-include.patch

	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}
