# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kanagram
pkgver=17.04.3
pkgrel=1
pkgdesc="Letter Order Game"
url="https://kde.org/applications/education/kanagram/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libkeduvocdocument' 'kdeclarative' 'knewstuff')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-kanagram')
conflicts=('kdeedu-kanagram')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kanagram-${pkgver}.tar.xz"{,.sig})
sha256sums=('0c74249e44d5dede18c931d863f5b91d45017a6e90dad1f04c4e51a87d4e8704'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kanagram-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
