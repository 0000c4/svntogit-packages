# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=('jre7-openjdk' 'jdk7-openjdk' 'openjdk7-src')
pkgbase=java7-openjdk
_java_ver=7
_openjdk_build=b147
_openjdk_date=27_jun_2011
_icedtea_ver=1.14
_date=20110920

_forest_openjdk_ver=3defd24c2671
_forest_jdk_ver=80fe75968a0b
_langtools_ver=fb7fb3071b64
_hotspot_ver=1dd9b3d73b22

pkgver=${_java_ver}.${_openjdk_build}_${_icedtea_ver}
pkgrel=1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org"
license=('custom')
makedepends=('eclipse-ecj' 'libcups' 'libxp' 'libxtst' 'libxi' 'libxt' 'freetype2'
             'alsa-lib' 'xalan-java' 'glib2' 'gtk2' 'apache-ant>=1.6.5' 'giflib'
             'libjpeg>=6b' 'zlib' 'rhino' 'libpulse>=0.9.11' 'zip' 'unzip' 'cpio' 'lcms2')
source=(icedtea7-${_date}-hg.tar.xz
        http://icedtea.classpath.org/download/drops/jdk7-jaf-2010_08_19.zip
        # openjdk.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/archive/${_forest_openjdk_ver}.tar.gz
        # corba.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/corba/archive/616c760dc288.tar.gz
        http://icedtea.classpath.org/download/drops/jdk7-jaxws2_2_4-b03-2011_05_27.zip
        # jaxp.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/jaxp/archive/c40983d6ae70.tar.gz
        # jaxws.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/jaxws/archive/83db5e316798.tar.gz
        # jdk.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/jdk/archive/${_forest_jdk_ver}.tar.gz
        # langtools.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/langtools/archive/${_langtools_ver}.tar.gz
        # hotspot.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/hotspot/archive/${_hotspot_ver}.tar.gz
        # drops/jaxp145_01.zip
        http://icedtea.classpath.org/download/drops/jaxp145_01.zip
        fontconfig-paths.diff
        fix_corba_cmds_path.diff
        openjdk7_fix_jdk_cmds_path.diff
        openjdk7_nonreparenting-wm.diff
        jdk7-openjdk.profile
        jdk7-openjdk.profile.csh
        jre7-openjdk.profile
        jre7-openjdk.profile.csh
        jconsole.desktop
        policytool.desktop)

#http://www.java.net/download/openjdk/jdk${_java_ver}/promoted/${_openjdk_build}/openjdk-${_java_ver}-fcs-src-${_openjdk_build}-${_openjdk_date}.zip

noextract=("jdk7-jaf-2010_08_19.zip"
           "${_forest_openjdk_ver}.tar.gz"
           "616c760dc288.tar.gz"
           "jdk7-jaxws2_2_4-b03-2011_05_27.zip"
           "c40983d6ae70.tar.gz"
           "83db5e316798.tar.gz"
           "${_forest_jdk_ver}.tar.gz"
           "${_langtools_ver}.tar.gz"
           "${_hotspot_ver}.tar.gz"
           "jaxp145_01.zip")
md5sums=('ef6f0078fe49cafaa5d9962e056843cd'
         '18d15dfd71117daadb332af003d08212'
         'd089de63d3031ccb08b4d1a21d876ee0'
         '28ea16faf07532fd31a88b11d8d27fe2'
         '2f5b829ade70f67fe272d0b322e3e702'
         '147b1a3e68111c18d44aab307ac1efc4'
         '2d7fb6f6d8c49ef85c3fe924a2c9235b'
         '2c1cfa47adad60bc275fbe47bfb1c4d3'
         'b22833c93b5485f4dde9ab1aa9cfc504'
         'b8c8f766e64d4133ece5cf522e75c1be'
         '32394c780c8fb5e29775f623525993c0'
         'ee1afda124d5927345014ab382ef581e'
         'f7e7a212e50abb56a6ef1a2b1bd27405'
         'c195c4865b84d9e2e0fd71ac6d88eadb'
         '203640d6e79e41b0065e016818c17ccd'
         'fe39da48a62b1fdd8fc24de0e0c4b525'
         'cdabafad0ec413d9a983888bf445a443'
         '612b0fec7e0943c37a6de77c43622007'
         '62443459da0cb28181feb260dc0e5ce7'
         '8e346f19a69b11b8dc4fcd8ea9d9d8f1'
         'b6357228d29836504a90abe006d86e56')

# source PKGBUILD && mksource
#makedepends+=('mercurial')
mksource() {
  mkdir /tmp/icedtea7-${_date}
  pushd /tmp/icedtea7-${_date}
  hg -v clone http://icedtea.classpath.org/hg/icedtea7
  rm -rf icedtea7/.hg*
  tar -cvJf /tmp/icedtea7-${_date}/icedtea7-${_date}-hg.tar.xz *
  popd
}

build() {
  cd "${srcdir}/icedtea7"

  unset JAVA_HOME
  unset CLASSPATH
  unset MAKEFLAGS

  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"

  . /etc/profile.d/apache-ant.sh

  cp ${srcdir}/*.diff ${srcdir}/icedtea7/patches
  export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff patches/fix_corba_cmds_path.diff patches/openjdk7_fix_jdk_cmds_path.diff patches/openjdk7_nonreparenting-wm.diff"

  # Bootstrap IcedTea with ecj and a GNU Classpath-based JDK:
  autoreconf --force --install

  ./configure \
        --disable-tests \
        --with-pkgversion=ArchLinux-${pkgver}-${pkgrel}-${CARCH} \
        --disable-bootstrap \
        --with-openjdk-src-zip=${srcdir}/${_forest_openjdk_ver}.tar.gz \
        --with-hotspot-src-zip=${srcdir}/${_hotspot_ver}.tar.gz \
        --with-corba-src-zip=${srcdir}/616c760dc288.tar.gz \
        --with-jaxp-src-zip=${srcdir}/c40983d6ae70.tar.gz \
        --with-jaxws-src-zip=${srcdir}/83db5e316798.tar.gz \
        --with-jdk-src-zip=${srcdir}/${_forest_jdk_ver}.tar.gz \
        --with-langtools-src-zip=${srcdir}/${_langtools_ver}.tar.gz \
        --with-jaxp-drop-zip=${srcdir}/jaxp145_01.zip \
        --with-jaf-drop-zip=${srcdir}/jdk7-jaf-2010_08_19.zip \
        --with-jaxws-drop-zip=${srcdir}/jdk7-jaxws2_2_4-b03-2011_05_27.zip \
        --enable-pulse-java #--help
	
#	  --enable-systemtap      Enable inclusion of SystemTap trace support
#	  --enable-nss            Enable inclusion of NSS security provider
#	  --with-abs-install-dir  The absolute path where the j2sdk-image dir will be installed

        
  #LD_PRELOAD="" 
  make
}

check() {
  cd "${srcdir}/icedtea7"
  make -k check
}

package_jre7-openjdk() {
  pkgdesc="Free Java environment based on OpenJDK 7.0 with IcedTea7 replacing binary plugs - runtime environment"
  depends=('gcc-libs' 'hicolor-icon-theme' 'ca-certificates-java' 'libxtst'
           'libxt' 'nss' 'libjpeg' 'freetype2' 'libxrender' 'libpng')
  optdepends=('icedtea-web: web browser plugin + Java Web Start'
             'alsa-lib: for sound'
             'giflib: for gif format support')
  provides=('java-runtime=7')
  conflicts=('java-runtime')
  backup=(etc/profile.d/jre7-openjdk.sh)
  install=jre7-openjdk.install

  _jvmdir=/usr/lib/jvm/java-7-openjdk

  cd "${srcdir}/icedtea7/openjdk.build/j2sdk-image/jre"

  install -d -m755 ${pkgdir}/${_jvmdir}/jre/
  cp -a bin lib ${pkgdir}/${_jvmdir}/jre

  mv ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.Ubuntu.properties.src \
     ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.properties.src
  mv ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.Ubuntu.bfc \
     ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.bfc
  rm -f ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.*.bfc
  rm -f ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.*.properties.src

  # Install man pages
  pushd ../../j2re-image/man
  install -m755 -d ${pkgdir}/usr/share/man/{,ja/}man1/
  install -m644 man1/*.1 ${pkgdir}/usr/share/man/man1
  install -m644 ja_JP.UTF-8/man1/*.1 ${pkgdir}/usr/share/man/ja/man1
  popd

  # Install icons and menu entries
  for s in 16 24 32 48 ; do
    install -m755 -d ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/
    install -m644 ../../../openjdk/jdk/src/solaris/classes/sun/awt/X11/java-icon${s}.png \
                  ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/java.png
  done

  # Link binaries into /usr/bin
  pushd ${pkgdir}/${_jvmdir}/jre/bin
  install -m755 -d ${pkgdir}/usr/bin/
  for file in *; do
    ln -sf ${_jvmdir}/jre/bin/${file} ${pkgdir}/usr/bin
  done
  popd

  # Link JKS keystore from ca-certificates-java
  rm -f ${pkgdir}/${_jvmdir}/jre/lib/security/cacerts
  ln -sf /etc/ssl/certs/java/cacerts "${pkgdir}/${_jvmdir}/jre/lib/security/cacerts"

  # Set some variables
  install -m755 -d ${pkgdir}/etc/profile.d/
  install -m755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/${pkgname}.sh
  install -m755 ${srcdir}/${pkgname}.profile.csh ${pkgdir}/etc/profile.d/${pkgname}.csh

  # Install license
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgbase}/
  install -m644 ASSEMBLY_EXCEPTION LICENSE THIRD_PARTY_README \
                 ${pkgdir}/usr/share/licenses/${pkgbase}
}

package_jdk7-openjdk() {
  pkgdesc="Free Java environment based on OpenJDK 7.0 with IcedTea7 replacing binary plugs - SDK"
  depends=('jre7-openjdk')
  provides=('java-environment=7')
  conflicts=('java-environment')
  backup=(etc/profile.d/jdk7-openjdk.sh)

  _jvmdir=/usr/lib/jvm/java-7-openjdk

  cd "${srcdir}/icedtea7/openjdk.build/j2sdk-image"

  # Main files
  install -m755 -d ${pkgdir}/${_jvmdir}/

  cp -a demo include lib sample ${pkgdir}/${_jvmdir}

  # 'bin' files
  pushd bin
  install -m755 -d ${pkgdir}/${_jvmdir}/bin/ \
                   ${pkgdir}/usr/bin/ \
                   ${pkgdir}/usr/share/man/{,ja/}man1/

  # 'java-rmi.cgi' will be handled separately as it should not be in the PATH and has no man page
  for b in $(ls | grep -v java-rmi.cgi); do
    if [ -e ../jre/bin/${b} ]; then
      # Provide a link of the jre binary in the jdk/bin/ directory
      ln -s ../jre/bin/${b} ${pkgdir}/${_jvmdir}/bin/${b}
    else
      # Copy binary to jdk/bin/
      install -m755 ${b} ${pkgdir}/${_jvmdir}/bin/${b}
      # Copy man page
      install -m644 ../man/man1/${b}.1 ${pkgdir}/usr/share/man/man1/${b}.1
      install -m644 ../man/ja/man1/${b}.1 ${pkgdir}/usr/share/man/ja/man1/${b}.1
      # Link from /bin/
      ln -s ${_jvmdir}/bin/${b} ${pkgdir}/usr/bin/${b}
    fi
  done
  popd

  # Handling 'java-rmi.cgi' separately
  install -m755 -D bin/java-rmi.cgi ${pkgdir}/${_jvmdir}/bin/java-rmi.cgi

  # Desktop files
  install -m755 -d ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/{jconsole,policytool}.desktop ${pkgdir}/usr/share/applications/

  # Set some variables
  install -m755 -d ${pkgdir}/etc/profile.d/
  install -m755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/${pkgname}.sh
  install -m755 ${srcdir}/${pkgname}.profile.csh ${pkgdir}/etc/profile.d/${pkgname}.csh
}

package_openjdk7-src() {
  pkgdesc="Free Java environment based on OpenJDK 7.0 with IcedTea7 replacing binary plugs - sources"

  install -D ${srcdir}/icedtea7/openjdk.build/j2sdk-image/src.zip \
             ${pkgdir}/${_jvmdir}/src.zip
}
