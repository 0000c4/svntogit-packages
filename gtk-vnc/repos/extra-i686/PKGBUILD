# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=gtk-vnc
pkgver=0.6.0
pkgrel=1
pkgdesc="A VNC viewer widget for GTK+"
url="https://wiki.gnome.org/Projects/gtk-vnc"
arch=(i686 x86_64)
license=(LGPL)
depends=(libgcrypt gtk3 gnutls libpulse)
makedepends=(intltool gtk-doc vala gobject-introspection)
source=(https://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('9559348805e64d130dae569fee466930175dbe150d2649bb868b5c095f130433')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-examples --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
