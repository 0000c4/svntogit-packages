# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=gtk-vnc
pkgver=0.4.2
pkgrel=1
pkgdesc="A VNC viewer widget for GTK"
arch=('i686' 'x86_64')
url="http://live.gnome.org/gtk-vnc"
license=('LGPL')
depends=('gobject-introspection')
makedepends=('pkgconfig' 'intltool' 'pygtk' 'perl-text-csv')
options=('!libtool' '!makeflags')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.4/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('96e948b62984a1a3a32fd83793fb645c493c1bc93b303eb831ecafee01a956d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-python \
      --with-examples --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
