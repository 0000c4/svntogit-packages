# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kgamma5
pkgver=5.7.1
pkgrel=1
pkgdesc="Adjust your monitor's gamma settings"
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kdelibs4support)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('54d7a8ba2669c9c86e13735779714e508a90e1cd6a49e250b1e256999294ddd7'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
