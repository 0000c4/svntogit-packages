# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=polkit-qt
pkgver=0.9.3
pkgrel=1
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('policykit' 'qt')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/apps/KDE4.x/admin/${pkgname}-${pkgver}.tar.bz2")
md5sums=('8be0205f8cb91161fdaf527f7cb6852d')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
	make || return 1
	make DESTDIR=$pkgdir install

	# install cmake files, needed by KDE 4.4
	install -d ${pkgdir}/usr/share/cmake-2.8/Modules
	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/cmake/modules/{FindPolkitQt,InstallSettings}.cmake \
	  ${pkgdir}/usr/share/cmake-2.8/Modules/ || return 1
}
