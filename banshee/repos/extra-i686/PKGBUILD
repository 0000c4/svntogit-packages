# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=banshee
pkgver=1.2.1
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee-project.org/"
license=('MIT')
depends=('sqlite3' 'ndesk-dbus-glib>=0.4.1' 'mono-addins>=0.3.1' 'taglib-sharp>=2.0.3.0' 'gstreamer0.10-good-plugins' 'gstreamer0.10-base-plugins' 'gnome-sharp>=2.20.1' 'libmtp>=0.2.6.1' 'ipod-sharp>=0.8.0' 'boo')
makedepends=('perlxml' 'pkgconfig')
options=('!libtool' '!makeflags' '!emptydirs')
install=banshee.install
source=(http://download.banshee-project.org/${pkgname}/${pkgname}-1-${pkgver}.tar.gz)
md5sums=('1a3e8906e6db5fd30259156dc2a265b8')

build() {
  export MONO_SHARED_DIR=${srcdir}/.wabi
  mkdir -p "${MONO_SHARED_DIR}"
  cd ${srcdir}/${pkgname}-1-${pkgver}
  LD_PRELOAD="" ./configure --prefix=/usr \
        --sysconfdir=/etc --localstatedir=/var \
	--disable-docs --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1

  install -d -m755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
