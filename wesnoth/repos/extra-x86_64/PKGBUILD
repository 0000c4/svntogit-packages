# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jacobo Arvelo <unix4all@ya.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=wesnoth
pkgver=1.8.6
pkgrel=2
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.wesnoth.org/"
depends=('sdl_ttf' 'sdl_net' 'sdl_mixer' 'sdl_image' 'fribidi' 'boost-libs' 'pango' 'lua' "wesnoth-data=${pkgver}" 'dbus-core')
makedepends=('boost')
install=wesnoth.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        wesnoth-libpng-1.4.0.patch
        wesnoth-1.8.6-boost-foreach.patch)
md5sums=('f1c3179869b01b61e253e74aeb241034'
         'ca7412ffc4682ef55ddf82d56b0e2f08'
         'dc414ab7e1334941548e9db6db9c2460')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/wesnoth-libpng-1.4.0.patch"
  patch -Np1 -i "${srcdir}/wesnoth-1.8.6-boost-foreach.patch"
  ./autogen.sh
  ./configure --prefix=/usr --localstatedir=/var --enable-tools --enable-editor --enable-server
  make DESTDIR="${pkgdir}" install-exec
}
