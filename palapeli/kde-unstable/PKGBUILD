# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=palapeli
pkgver=20.03.80
pkgrel=1
pkgdesc="A single-player jigsaw puzzle game"
url="https://kde.org/applications/games/palapeli/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames qhull hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         palapeli-qt5.15.patch::"https://cgit.kde.org/palapeli.git/patch/?id=6d1a089e")
sha256sums=('825f0e11e4e6a3c8a0ab225a2302b7ba092a3a4905bed6a3f2613a81fa5536ba'
            'SKIP'
            '510dbfb01d242315ba3acb57e3bcb5d942bc87781d5bfdcb6791dfc7e1a6f723')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../palapeli-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
