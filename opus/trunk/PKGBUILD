# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: pumbur

pkgname=opus
pkgver=1.1.5
pkgrel=1
pkgdesc="Totally open, royalty-free, highly versatile audio codec"
arch=(i686 x86_64)
url="https://www.opus-codec.org/"
license=(BSD)
depends=(glibc)
makedepends=(doxygen)
source=(https://archive.mozilla.org/pub/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('eb84981ca0f40a3e5d5e58d2e8582cb2fee05a022825a6dfe14d14b04eb563e4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-custom-modes --disable-static \
    --enable-intrinsics --enable-rtcd --enable-ambisonics
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
