# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=3.10.0
pkgrel=2
pkgdesc="Create and modify archives"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'dconf' 'libarchive' 'file' 'json-glib' 'libnotify' 'p7zip')
makedepends=('intltool' 'gnome-doc-utils' 'pkg-config' 'libnautilus-extension' 'itstool' 'docbook-xsl')
optdepends=('lrzip: lrzip archive support'
            'unace: extraction tool for the proprietary ace archive format')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        bug708854.patch
        bug709035.patch)
sha256sums=('d889bb9904146e5715c082aa9a6d96b0606a066ba2bf05fbfb7030f2bf274f13'
            'a8f4e856924b6b78e47cc134c723d1d93119f90ec9ab69dd0b59ec8a934688ba'
            '59f07968b720ccd9c70e6232b2bfcaa3cf9298d6ce48be4882154d69cbe7a909')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../bug708854.patch
  patch -Np1 -i ../bug709035.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/file-roller \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
