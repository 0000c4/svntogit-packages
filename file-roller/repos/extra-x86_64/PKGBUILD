# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=3.0.1
pkgrel=2
pkgdesc="Archive manipulator for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'dconf' 'nautilus')
makedepends=('intltool' 'gnome-doc-utils' 'pkg-config' 'nautilus' 'libsm')
optdepends=('nautilus: nautilus shell extension'
            'unrar: the RAR uncompression program'
            'zip: creates PKZIP-compatible .zip files'
            'unzip: unpacks .zip archives'
            'p7zip: 7zip compression utility'
            'xz: managing LZMA and XZ compressed files'
            'arj: ARJ archiver'
            'unace: extraction tool for the proprietary ace archive format')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('805f5fb77fa8c2f982a2cee0db1b56fbc335f63a1eed34e0bac228ce8349d3ce')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/file-roller \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --disable-schemas-compile
  make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
