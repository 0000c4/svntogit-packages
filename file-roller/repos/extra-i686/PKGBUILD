# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=2.24.3
pkgrel=1
pkgdesc="Archive manipulator for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('libgnomeui>=2.24.0' 'desktop-file-utils') 
makedepends=('intltool' 'gnome-doc-utils>=0.14.2' 'pkgconfig' 'nautilus>=2.24.2')
optdepends=('nautilus: nautilus shell extension')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2
	fr-process-kill.patch
	fr-rpm-bsdtar.patch)
md5sums=('20264b13cd27c6542c8289256d768c9c'
	 '9da94cd67fee551bec5290844a48d76e'
	 '918057080ef487c625f326ed5637f490')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/fr-process-kill.patch" || return 1
  patch -Np0 -i "${srcdir}/fr-rpm-bsdtar.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/file-roller \
              --localstatedir=/var --disable-static \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
