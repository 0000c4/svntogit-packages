# $Id$
# Maintainer:
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=abcde
pkgver=2.4.0
pkgrel=2
pkgdesc="A frontend command-line utility that grabs tracks off a CD, encodes them to ogg or mp3 format, and tags them, all in one go."
depends=('bash' 'cd-discid' 'wget')
optdepends=('eject: Eject support'
	'cdparanoia: Paranoia ripping support'
	'cdrkit: icedax ripping support'
	'lame: MP3 encoding support'
	'vorbis-tools: Ogg Vorbis encoding support'
	'flac: FLAC encoding support'
	'id3: ID3 v1/v2 tag support'
	'id3v2: ID3 v2 tag support')
backup=(etc/$pkgname.conf)
arch=('any')
url="http://code.google.com/p/abcde"
license=('GPL')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	'fix-mp3-tagging.patch')
md5sums=('4a65d9ae97b99d34257e2892cfe6ea41'
         'bc9be1bc3b4f6dc1f90de67debce94ec')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np0 -i ${srcdir}/fix-mp3-tagging.patch || return 1

  sed -e "s:normalize-audio:normalize:g" -i $pkgname
  sed -e "s:/usr/bin/less:/bin/less:g" -i $pkgname

  install -Dm 0755 $pkgname $pkgdir/usr/bin/$pkgname || return 1
  install -Dm 0755 cddb-tool $pkgdir/usr/bin/cddb-tool || return 1
  install -Dm 0644 cddb-tool.1 $pkgdir/usr/share/man/man1/cddb-tool.1 || return 1
  install -Dm 0644 $pkgname.1 $pkgdir/usr/share/man/man1/$pkgname.1 || return 1
  install -Dm 0644 $pkgname.conf $pkgdir/etc/$pkgname.conf || return 1
}
