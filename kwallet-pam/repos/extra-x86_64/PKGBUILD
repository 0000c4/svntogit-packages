# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwallet-pam
pkgver=5.12.5
pkgrel=3
pkgdesc='KWallet PAM integration'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(pam libgcrypt socat)
makedepends=(extra-cmake-modules qt5-base)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        CVE-2018-10380-1.patch::"https://cgit.kde.org/kwallet-pam.git/patch/?id=2134dec8"
        CVE-2018-10380-2.patch::"https://cgit.kde.org/kwallet-pam.git/patch/?id=01d4143f"
        kdebug-393856.patch::"https://cgit.kde.org/kwallet-pam.git/patch/?id=8da1a470")
sha256sums=('6374132fe2d142c9eb999e90c636b973fe9715335de3cfa86f0b5f4258d5eba2'
            'SKIP'
            'bc509c7d04aa21c35caac263720967dd098af47e6d282e437f1b69de38f42d66'
            'b3c8500c7951b4a919875907abcefe817d8d613e31a2eb4ccf63b0038a4f5b62'
            'acead343253db2bd7dab373f115d4202954842e976c69b0e07c23696d36e9fc1')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build{,4}

  cd $pkgname-$pkgver
# https://www.kde.org/info/security/advisory-20180503-1.txt
  patch -p1 -i ../CVE-2018-10380-1.patch
  patch -p1 -i ../CVE-2018-10380-2.patch
# Fix regression from CVE fix
  patch -p1 -i ../kdebug-393856.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make

  cd ../build4
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DKWALLET4=1
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  cd ../build4
  make DESTDIR="$pkgdir" install
}
