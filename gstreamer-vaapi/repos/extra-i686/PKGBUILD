# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gstreamer-vaapi
pkgver=1.8.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VAAPI Plugins"
arch=(i686 x86_64)
license=(LGPL)
url="http://gstreamer.freedesktop.org/"
depends=(gst-plugins-base gst-plugins-bad libva libxrandr libvpx)
makedepends=(gtk-doc)
options=(!emptydirs)
source=($url/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('2580a00fc626ecc60a6fef9a6366f18b6d451db1ca3fe0daf81c6a416a4a33c1')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --disable-builtin-libvpx --enable-gtk-doc \
    --with-package-name="GStreamer VAAPI Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
