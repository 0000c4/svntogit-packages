# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libglade
pkgver=2.6.3
pkgrel=2
pkgdesc="Allows you to load glade interface files in a program at runtime"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk2>=2.14.7' 'libxml2>=2.7.3')
makedepends=('python' 'pkgconfig')
optdepends=('python: libglade-convert script')
options=('!libtool')
install=glade.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.6/${pkgname}-${pkgver}.tar.bz2
	libglade-2.0.1-nowarning.patch
	bgo555149.patch)
url="http://www.gnome.org"
md5sums=('1c359860b1a0f369f0b6e3f96fd985c2'
         '4f879c0ce550004905aa0fb24f25c353'
         '02e1e7f3538b9d8f0c0c64eb01e535c6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libglade-2.0.1-nowarning.patch" || return 1
  patch -Np0 -i "${srcdir}/bgo555149.patch" || return 1
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 libglade-convert "${pkgdir}/usr/bin/" || return 1
}
