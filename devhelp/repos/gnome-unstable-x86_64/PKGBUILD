# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=3.22.0+2+g0aab69d
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="https://wiki.gnome.org/Apps/Devhelp"
depends=(webkit2gtk)
makedepends=(pkgconfig intltool python gnome-common git)
groups=(gnome-extra)
_commit=0aab69dce5c508fa532b5d94b260ce1159ad85da  # master
source=("git://git.gnome.org/devhelp#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
