# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=0.19.1
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://www.imendio.com/projects/devhelp/"
depends=('gconf>=2.20.1' 'libglade>=2.6.2' 'libwnck>=2.22.1' 'xulrunner>=1.8.1.12' 'desktop-file-utils')
makedepends=('perlxml' 'pkgconfig')
options=('!libtool' '!emptydirs')
install="devhelp.install"
source=(http://ftp.gnome.org/pub/GNOME/sources/devhelp/0.19/devhelp-${pkgver}.tar.bz2)
md5sums=('6c12a272a94ac9cfc1dc6234590b46d8')

build() {
  [ -z "${MOZ_PLUGIN_PATH}" ] && . /etc/profile.d/mozilla-common.sh

  cd ${startdir}/src/devhelp-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static || return 1
  make LDFLAGS+="-R /usr/lib/xulrunner" || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
