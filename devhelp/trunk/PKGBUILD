# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=0.19.1
pkgrel=2
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://www.imendio.com/projects/devhelp/"
depends=('gconf>=2.20.1' 'libglade>=2.6.2' 'libwnck>=2.22.1' 'xulrunner>=1.9' 'desktop-file-utils')
makedepends=('perlxml' 'pkgconfig')
options=('!libtool' '!emptydirs')
install="devhelp.install"
source=(http://ftp.gnome.org/pub/GNOME/sources/devhelp/0.19/devhelp-${pkgver}.tar.bz2
	04_devhelp-xul-1.9.m4.patch
	04_devhelp-xul-1.9.patch)
md5sums=('6c12a272a94ac9cfc1dc6234590b46d8'
         '3af13e389f67d21b0d3c353f2c09c818'
         'f91585725faa95ec8796143e956e527c')

build() {
  cd ${startdir}/src/devhelp-${pkgver}
  patch -Np1 -i ${startdir}/src/04_devhelp-xul-1.9.m4.patch || return 1
  patch -Np1 -i ${startdir}/src/04_devhelp-xul-1.9.patch || return 1
  libtoolize --force --copy || return 1
  aclocal -I m4|| return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --with-gecko=libxul-embedding || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
