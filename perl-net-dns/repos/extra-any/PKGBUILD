# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-net-dns
pkgver=1.15
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Net-DNS/"
depends=('perl-digest-hmac' 'perl-net-ip')
checkdepends=('perl-test-pod' 'perl-mime-base32' 'perl-net-dns-sec')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-socket6: IPv6 support')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-$pkgver.tar.gz)
sha512sums=('9c28814620b065348df18046bc50680c02354e63435e94c4aabf12adc9991567b624c6f6c20aa102dfed06a7795ffa01b99d26cde58a0bcb32ada0a0afe4524b')

build() {
  cd Net-DNS-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-DNS-${pkgver}
  make test
}

package() {
  cd Net-DNS-${pkgver}
  make DESTDIR="${pkgdir}" install
}
