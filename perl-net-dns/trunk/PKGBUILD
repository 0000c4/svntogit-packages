# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-net-dns
pkgver=1.13
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Net-DNS/"
depends=('perl-digest-hmac' 'perl-net-ip')
checkdepends=('perl-test-pod' 'perl-mime-base32' 'perl-net-dns-sec')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-socket6: IPv6 support')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-$pkgver.tar.gz)
sha512sums=('ac42d5b5d1849de11813404b9915f2b54cc4d44e2f6a52c8c5c714ae30bab594b2d27701957238c1dbb09544320822c25f19ff50ce33210c289379aa67400636')

build() {
  cd Net-DNS-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-DNS-${pkgver}
  make test
}

package() {
  cd Net-DNS-${pkgver}
  make DESTDIR="${pkgdir}" install
}
