# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-core
pkgver=0.6.0
pkgrel=2
pkgdesc="Library of utility functions that are used by the higher level dLeyna"
arch=(x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(gupnp)
makedepends=(git)
_commit=32207688478b890c91eb084a84f96b40a729ab14  # tags/v0.6.0
source=("git+https://github.com/01org/dleyna-core#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i 's/gupnp-1\.0/gupnp-1.2/g' configure.ac *.pc.in
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
