# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Lukas Sabota <punkrockguy318@cocmast.net> (Timidity Patch)

pkgname=sdl_mixer
pkgver=1.2.10
pkgrel=2
pkgdesc="A simple multi-channel audio mixer"
arch=('i686' 'x86_64')
url="http://www.libsdl.org/projects/SDL_mixer/"
license=('LGPL' 'GPL')
depends=('sdl>=1.2.12' 'libvorbis' 'libmikmod' 'smpeg')
options=('!libtool')
source=(http://www.libsdl.org/projects/SDL_mixer/release/SDL_mixer-${pkgver}.tar.gz
        multisound-crash.patch)
md5sums=('a357558552436b0b5ea0333b3e2327df'
         '4669f453a7fe29e2987c0b2b8e4c5c82')

build() {
  cd ${srcdir}/SDL_mixer-${pkgver}
  patch -Np2 -i $srcdir/multisound-crash.patch
  sed -i 's|/usr/local/lib/timidity|/usr/lib/timidity|' timidity/config.h || return 1
  sed -i 's|/etc/timidity/timidity.cfg|/etc/timidity++/timidity.cfg|' timidity/config.h || return 1
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=${pkgdir} install
}
