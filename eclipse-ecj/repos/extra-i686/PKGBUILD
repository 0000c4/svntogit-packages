# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=eclipse-ecj
pkgver=3.3.2
pkgrel=2
_date=200802211800
pkgdesc="Eclipse java bytecode compiler"
arch=('i686' 'x86_64')
license=('EPL')
url="http://www.eclipse.org/"
depends=('gcc-gcj')
makedepends=('apache-ant')
source=(http://download.eclipse.org/eclipse/downloads/drops/R-${pkgver}-${_date}/ecjsrc.zip
	ecj
        ecj-gccmain.diff)
md5sums=('c1d89b9de8170fc91e30c3a02002244f'
         'c72fe244e4d4b7a3c437604805b988ff'
         '2cc746a12da0978aef2ae504a208fd6f')

build() {
  patch -Np3 -i ${startdir}/src/ecj-gccmain.diff || return 1

  rm -rf org/eclipse/jdt/internal/compiler/{tool,apt}
  mkdir -p build/bin
  cp -a org build/bin/
  find build/bin -name '*.java' > build/bin/sources
  gcj -v -d build/bin -C -g -I/usr/lib/ant.jar -Ibuild/bin `cat build/bin/sources` || return 1

  rm -f build/bin/sources
  find build/bin -name '*.java' -delete || return 1
  find build/bin -name '*.html' -delete || return 1

  mkdir -p build/bootstrap
  gjar -c -C build/bin org -f build/bootstrap/eclipse-ecj.jar || return 1

  rm -rf build/bin
  mkdir build/bin
  cp -a org build/bin/

  gij -classpath build/bootstrap/eclipse-ecj.jar:/usr/lib/ant.jar \
      org.eclipse.jdt.internal.compiler.batch.Main \
      -bootclasspath /usr/share/java/libgcj-4.3.jar build/bin || return 1

  find build/bin -name '*.java' -delete || return 1
  find build/bin -name '*.html' -delete || return 1

  mkdir -p build/dist
  gjar -c -C build/bin org -f build/dist/eclipse-ecj.jar || return 1

  install -m755 -d ${startdir}/pkg/usr/share/java
  install -m755 -d ${startdir}/pkg/usr/bin
  install -m644 build/dist/eclipse-ecj.jar ${startdir}/pkg/usr/share/java/ || return 1
  install -m755 ecj ${startdir}/pkg/usr/bin/ || return 1
}
