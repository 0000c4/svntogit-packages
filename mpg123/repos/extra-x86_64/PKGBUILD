# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=mpg123
pkgver=1.15.3
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2.1')
depends=('libltdl' 'alsa-lib')
makedepends=('sdl' 'jack' 'libpulse')
optdepends=('sdl: for sdl audio support'
            'jack: for jack audio support'
            'libpulse: for pulse audio support')
conflicts=('mpg321')
provides=('mpg321')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha1sums=('915eb8782106a62b103a1171364571d69cb64e25'
          '90614c85da88f965d9aa3165c111b6eeba711903')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-int-quality --with-module-suffix=.so \
    --with-audio="alsa oss sdl jack pulse"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 scripts/conplay "${pkgdir}/usr/bin/conplay"
}
