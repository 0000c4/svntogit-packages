# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=mpg123
pkgver=1.13.2
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'libtool' 'alsa-lib')
makedepends=('esound' 'sdl' 'jack' 'libpulse')
optdepends=('esound: for esd audio support'
            'sdl: for sdl audio support'
            'jack: for jack audio support'
            'libpulse: for pulse audio support')
conflicts=('mpg321')
provides=('mpg321')
replaces=('mpg321')
options=('libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7cb959877d205f89215a32bf446f5737')
sha1sums=('31a9c27f5fa80f930dbe598846c847f9b35d9dc3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-audio="alsa esd sdl jack pulse"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
