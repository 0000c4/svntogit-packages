# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=('x264' 'libx264' 'libx264-10bit')
pkgver=148.20170121
pkgrel=5
epoch=2
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git' 'ffmpeg' 'l-smash')
_commit='97eaef2ab82a46d13ea5e00270712d6475fbe42b'
source=("git://git.videolan.org/x264.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgbase}

  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _date=$(git log -1 --format="%cd" --date=short | tr -d -)

  echo ${_ver}.${_date}
}

prepare() {
  cp -r ${pkgbase} ${pkgbase}-10bit
}

build() {
  cd ${pkgbase}

  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --enable-pic
  make

  cd ../${pkgbase}-10bit

  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --enable-pic \
    --bit-depth='10'
  make
}

package_x264() {
  pkgdesc='CLI tools for encoding H264/AVC video streams'
  depends+=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'liblsmash.so'
            'libswscale.so')
  provides=('libx264-8bit.so' 'libx264-10bit.so')

  local _ver=$(grep '#define X264_BUILD' "${pkgbase}"/x264.h | cut -d' ' -f3)

  install -dm 755 "${pkgdir}"/usr/lib/x264

  make -C ${pkgbase}-10bit DESTDIR="${pkgdir}" install-cli install-lib-shared
  mv "${pkgdir}"/usr/lib/libx264.so.${_ver} "${pkgdir}"/usr/lib/x264/libx264-10bit.so.${_ver}
  ln -s libx264-10bit.so.${_ver} "${pkgdir}"/usr/lib/x264/libx264-10bit.so
  mv "${pkgdir}"/usr/bin/x264{,-10bit}

  make -C ${pkgbase} DESTDIR="${pkgdir}" install-cli install-lib-shared
  mv "${pkgdir}"/usr/lib/libx264.so.${_ver} "${pkgdir}"/usr/lib/x264/libx264-8bit.so.${_ver}
  ln -s libx264-8bit.so.${_ver} "${pkgdir}"/usr/lib/x264/libx264-8bit.so
  rm "${pkgdir}"/usr/lib/libx264.so
}

package_libx264() {
  pkgdesc='Library for encoding H264/AVC video streams'

  local _ver=$(grep '#define X264_BUILD' "${pkgbase}"/x264.h | cut -d' ' -f3)

  install -dm 755 "${pkgdir}"/usr/lib
  ln -s x264/libx264-8bit.so.${_ver} "${pkgdir}"/usr/lib/libx264.so
  ln -s x264/libx264-8bit.so.${_ver} "${pkgdir}"/usr/lib/libx264.so.${_ver}
}

package_libx264-10bit() {
  pkgdesc='Library for encoding H264/AVC video streams (10bit-depth)'
  provides=('libx264')
  conflicts=('libx264')

  local _ver=$(grep '#define X264_BUILD' "${pkgbase}"/x264.h | cut -d' ' -f3)

  install -dm 755 "${pkgdir}"/usr/lib
  ln -s x264/libx264-10bit.so.${_ver} "${pkgdir}"/usr/lib/libx264.so
  ln -s x264/libx264-10bit.so.${_ver} "${pkgdir}"/usr/lib/libx264.so.${_ver}
}

# vim: ts=2 sw=2 et:
