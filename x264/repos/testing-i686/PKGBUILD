# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=x264
pkgver=20130206
pkgrel=1
pkgdesc="free library for encoding H264/AVC video streams"
arch=('i686' 'x86_64')
url="http://www.videolan.org/developers/x264.html"
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git')
source=(ftp://ftp.archlinux.org/other/packages/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('f952248492745a077ad6a77465276166')

build() {
    cd $pkgname

    ./configure --enable-shared

    make
}
package() {
    cd $pkgname

    make DESTDIR="$pkgdir" \
      bindir=/usr/bin \
      libdir=/usr/lib \
      includedir=/usr/include \
      install
}

# vim:set ts=2 sw=2 et:
