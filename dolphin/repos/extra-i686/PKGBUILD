# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=16.04.1
pkgrel=2
pkgdesc="File Manager"
arch=(i686 x86_64)
url="http://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kuiserver kcmutils)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 
            'ruby: installing new service menus with GHNS' 'plasma-workspace: remote:// protocol'
            'kdegraphics-thumbnailers: PDF and PS thumbnails' 'konsole: terminal panel')
conflicts=('kdebase-dolphin<15.08')
replaces=(kdebase-dolphin)
groups=(kde-applications kdebase)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
        kdebug-357479.patch::"https://quickgit.kde.org/?p=dolphin.git&a=commitdiff&h=bed16191&o=plain")
md5sums=('a4e3daa271c15608ff5c29ce8a8936bf'
         '5b603e55a2fafaeb355de25d1c5a40ac')

prepare() {
  mkdir -p build

# Fix crash with owncloud plugin http://bugs.kde.org/show_bug.cgi?id=357479
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-357479.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
