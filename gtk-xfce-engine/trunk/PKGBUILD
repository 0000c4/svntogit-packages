# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=gtk-xfce-engine
pkgver=2.10.1
pkgrel=2
pkgdesc="Xfce Gtk+-2.0 engine and themes"
arch=('x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('gtk2')
provides=("gtk2-xfce-engine=$pkgver-$pkgrel")
conflicts=('gtk2-xfce-engine')
replaces=('gtk2-xfce-engine' 'gtk3-xfce-engine')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha512sums=('1ddf7d7d4d53665a2a4a6e1cb0a8c012047304b2dd18d4303712e6fa6128effbbe582cbdea55e3f013012c0414256fa72a9b27b194b1f313394856b0da162d22')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
