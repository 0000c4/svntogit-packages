# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=postgresql-old-upgrade
pkgver=9.0.6
_majorver=${pkgver%.*}
pkgrel=1
pkgdesc="Minimal PostgreSQL build for migrating between major versions with pg_upgrade"
arch=('i686' 'x86_64')
url="http://www.postgresql.org/"
license=('custom:PostgreSQL')
depends=("postgresql-libs>=${_majorver}" 'libxml2' 'openssl>=1.0.0')
source=(ftp://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2)

build() {
  cd "${srcdir}/postgresql-${pkgver}"

  ./configure --prefix=/opt/pgsql-${_majorver} \
  --with-libxml --with-openssl --without-perl \
  --without-python --with-pam --without-readline \
  --with-system-tzdata=/usr/share/zoneinfo --disable-nls \
  --enable-thread-safety

  make -C src all
}

package() {
  cd "${srcdir}/postgresql-${pkgver}"

  # install
  make DESTDIR="${pkgdir}" -C src install

  # install license
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # clean up unneeded installed items
  rm -rf "${pkgdir}/opt/pgsql-${_majorver}/include/"
}

md5sums=('4ad7e4fc31ef8b8a75a3e4243933963f')
sha256sums=('317d99d2ff9e1de8da0cae107bc06c9fd455b1df0c0fb77b8fbb61fb08276056')
