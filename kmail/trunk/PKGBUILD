# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail
pkgver=16.12.0
pkgrel=1
pkgdesc="KDE mail client"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kdepim-runtime kmail-account-wizard akonadi-import-wizard mbox-importer pim-sieve-editor
         pim-storage-service-manager kontactinterface libktnef)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin boost python)
optdepends=('kleopatra: mail encryption support' 'kdepim-addons: additional plugins')
conflicts=(ktnef)
replaces=(ktnef)
groups=(kde-applications kdepim)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('63939b64d6990064fd2d37e6450f00f9'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
