# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail
pkgver=18.12.0
pkgrel=2
pkgdesc="KDE mail client"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kmail-account-wizard akonadi-import-wizard mbox-importer
         pim-data-exporter pim-sieve-editor kontactinterface ktnef)
makedepends=(extra-cmake-modules kdoctools boost)
optdepends=('kleopatra: mail encryption support' 'kdepim-addons: additional plugins')
conflicts=('ktnef<18')
replaces=('ktnef<18')
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-401419.patch::"https://cgit.kde.org/kmail.git/patch/?id=4c7adc35")
sha256sums=('5d1018d901faa4c72910f1214fb3b3dbe1335f6a4e96b696c75b3179d9a17f47'
            'SKIP'
            '7ba5197d5d729cd16b1f1c617a68b380bdc914c2f7bd51822990de2a7026f5e3')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-401419.patch # Fix crash when opening messages
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
