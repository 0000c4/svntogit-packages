# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=atomix
pkgver=3.30.0.1
pkgrel=1
pkgdesc="Build molecules out of single atoms"
url="https://wiki.gnome.org/Apps/Atomix"
arch=(x86_64)
license=(GPL2)
depends=(gtk3 libgnome-games-support)
makedepends=(meson appstream-glib git)
_commit=66184fb77b1ca86054e321a177eb24d916026a12  # tags/3.30.0.1^0
source=("git+https://gitlab.gnome.org/GNOME/atomix.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
