# $Id$
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: alzen <mynick2(at)gmail(dot)com>

pkgname=bmpx
pkgver=0.40.14
pkgrel=1
pkgdesc="A media player and the successor to BMP"
arch=('i686' 'x86_64')
url="http://bmpx.backtrace.info/site/BMPx_Homepage"
depends=('boost' 'fam' 'gstreamer0.10-good-plugins' 'gstreamer0.10-base-plugins'
	 'hal' 'libglademm' 'libofa' 'librsvg' 'libsexymm' 'libsoup>=2.4.0'
	 'sqlite3' 'startup-notification>=0.8' 'taglib')
# 0.40.0: bmpx provide their own neon and musicbrainz implementation.
makedepends=('bison' 'flex' 'pkgconfig' 'perlxml')
options=('!libtool' '!makeflags')
license=('GPL' 'custom')
install=bmpx.install
source=( http://files.backtrace.info/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 bmpx-libsoup24.patch bmpx-compile.patch)
md5sums=('c741e05a82a82b14b6775d44a7c93c15'
         'ef433f8b306b5d9e7d3f71dade83b751'
	 'c8a3eb79e54d82bfa7c89d8874ff43dc')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 < ../bmpx-libsoup24.patch || return 1
  patch -Np1 < ../bmpx-compile.patch || return 1
  ./configure --prefix=/usr --enable-amazon --libexecdir=/usr/lib/bmpx || return 1
  sed -e '/BUILD_BUILDUSER/ s@".*"@"http://www.archlinux.org"@' -i build.h || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  #Fix invalid symlink pointing to builddir
  cd ${pkgdir}/usr/bin
  rm -f bmp2
  ln -sf beep-media-player-2 bmp2
}
