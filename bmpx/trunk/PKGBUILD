# $Id$
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: alzen <mynick2(at)gmail(dot)com>

pkgname=bmpx
pkgver=0.40.13
pkgrel=2
pkgdesc="A media player and the successor to BMP"
arch=(i686 x86_64)
url="http://bmpx.backtrace.info/site/BMPx_Homepage"
depends=('boost' 'fam' 'gstreamer0.10-good-plugins' 'gstreamer0.10-base-plugins'
	 'hal' 'libglademm' 'libofa' 'librsvg' 'libsexymm' 'libsoup>=2.4.0'
	 'sqlite3' 'startup-notification>=0.8' 'taglib')
# 0.40.0: bmpx provide their own neon and musicbrainz implementation.
makedepends=('bison' 'flex' 'pkgconfig' 'perlxml')
options=('!libtool' '!makeflags')
license=('GPL' 'custom')
install=bmpx.install
source=( http://files.backtrace.info/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 bmpx-libsoup24.patch bmpx-gcc34.diff)
md5sums=('11da3d4c63e77dc15a53d0c6c25ddadb' 'ef433f8b306b5d9e7d3f71dade83b751'\
         'ac2032eef21102d078b4b03f99af17a6')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 < ../bmpx-libsoup24.patch || return 1
  patch -Np1 < ../bmpx-gcc34.diff || return 1
  ./configure --prefix=/usr --enable-amazon --libexecdir=/usr/lib/bmpx || return 1
  sed -e '/BUILD_BUILDUSER/ s@".*"@"http://www.archlinux.org"@' -i build.h || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  # install license
  install -D -m644 COPYING ${startdir}/pkg/usr/share/licenses/${pkgname}/LICENSE || return 1

  #Fix invalid symlink pointing to builddir
  cd ${startdir}/pkg/usr/bin
  rm -f bmp2
  ln -sf beep-media-player-2 bmp2
}
