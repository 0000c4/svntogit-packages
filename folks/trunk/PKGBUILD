# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=folks
pkgver=0.6.2.1
pkgrel=2
pkgdesc="Library to aggregates people into metacontacts "
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/Folks"
license=('LGPL2.1')
depends=('telepathy-glib' 'libgee' 'libxml2' 'evolution-data-server' 'libsocialweb')
makedepends=('gobject-introspection' 'vala' 'intltool' 'libtracker-sparql')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.6/${pkgname}-${pkgver}.tar.xz
        Bug_658631_crash_at_empathy_startup.patch)
sha256sums=('3eda79a71890b51b6f723067b61c32ef25e9041a9e188b483d7f23e3dcd328ee'
            '4ef4caaab73e799fe31e7a93ba1f3f088e99f66600e644828bbcc4fb966c1196')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix a crash
  patch -Np1 -i "$srcdir/Bug_658631_crash_at_empathy_startup.patch"
  find . -name '*.stamp' -delete

  # Change tracker version
  grep -lr sparql-0.10 . | xargs sed -i 's/sparql-0.10/sparql-0.12/g'

  ./configure --prefix=/usr --enable-tracker-backend
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
