# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.11.3+43+g780d64a9
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/Folks"
license=(LGPL2.1)
depends=(telepathy-glib libgee libxml2 evolution-data-server)
makedepends=(gobject-introspection intltool vala git)
_commit=780d64a984fb014e0ee7b633a24a9c4fda1df21e  # master
source=("git+https://git.gnome.org/browse/folks#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --disable-schemas-compile \
    --disable-libsocialweb-backend --disable-fatal-warnings

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
