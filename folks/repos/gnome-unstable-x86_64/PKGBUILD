# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.13.1
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Folks"
license=(LGPL2.1)
depends=(telepathy-glib libgee libxml2 evolution-data-server)
makedepends=(gobject-introspection gtk-doc vala git meson python2)
checkdepends=(python-dbusmock)
_commit=9edee2d746c7b0d5a3a9d32a00ff041309f9206f  # tags/0.13.1^0
source=("git+https://gitlab.gnome.org/GNOME/folks.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D docs=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
