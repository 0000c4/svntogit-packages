# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.11.0
_origver=0.11.0-rc4
pkgrel=2
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=("i686" "x86_64")
license=('GPL' 'LGPL' 'FDL')
url="http://kmplayer.kde.org"
depends=('kdebase-runtime>=4.1' 'gtk2')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://kmplayer.kde.org/pkgs/kmplayer-${_origver}.tar.bz2")
md5sums=('ec9267dd78db1b6148c103c6a38116a3')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${_origver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
	
	# icons are included in kdebase-runtime
	rm -f $pkgdir/usr/share/icons/oxygen/{128x128,16x16,22x22,32x32,48x48,64x64}/apps/kmplayer.png
	rm -f $pkgdir/usr/share/icons/oxygen/scalable/apps/kmplayer.svgz
}
