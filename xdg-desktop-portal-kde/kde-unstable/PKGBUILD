# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-desktop-portal-kde
pkgver=5.13.90
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal using Qt/KF5'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(ki18n knotifications pipewire libepoxy kwayland kwidgetsaddons)
makedepends=(extra-cmake-modules ladspa)
provides=(xdg-desktop-portal-impl)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        no-spa1.patch::"https://cgit.kde.org/xdg-desktop-portal-kde.git/patch/?id=23e139b7"
        no-spa2.patch::"https://cgit.kde.org/xdg-desktop-portal-kde.git/patch/?id=77c9ec54")
sha256sums=('25f4fb15a4d5d20e385ea0f70bb4cd3386b7cc9b70a7679f5709f3ea6b357a8a'
            'SKIP'
            'fa464973403efdb1805d8454297d182e5f8740c6c749a745aa044598a71d1058'
            '7656eb8e33344b824c3218e4a969838df854f105b681b502e20cbe2ee1ef77e0')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../no-spa1.patch # Don't search for libspa, it no longer exists
  patch -p1 -i ../no-spa2.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
