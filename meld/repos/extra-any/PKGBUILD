# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=meld
pkgver=1.6.0
pkgrel=1
pkgdesc='Visual diff and merge tool'
arch=('any')
url='http://meld.sourceforge.net/'
license=('GPL')
depends=('pygtk')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'gnome-python: gnome integration')
install=meld.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('a0ec4e19e85ea4df0214da4c92a4069095d37a252ab240fc1bc00f29a627fea8')

build() {
    cd "$pkgname-$pkgver"
    make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
    cd "$pkgname-$pkgver"
    make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="$pkgdir" install
    sed -i -e 's#env python#env python2#' "$pkgdir/usr/bin/meld"
}
