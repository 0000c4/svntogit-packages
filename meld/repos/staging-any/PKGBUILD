# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=meld
pkgver=1.3.2
pkgrel=2
pkgdesc='Visual diff and merge tool for GNOME'
arch=('any')
url='http://meld.sourceforge.net/'
license=('GPL')
depends=('pygtk>=2.16.0')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'gnome-python: gnome integration')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.3/${pkgname}-${pkgver}.tar.gz")
sha1sums=('954985c81b6551174248b2bfcf537c5489cdb606')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PYTHON=python2 prefix=/usr
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PYTHON=python2 prefix=/usr DESTDIR="${pkgdir}" install
        sed -i -e 's#env python#env python2#' "${pkgdir}"/usr/bin/meld
}
