# $Id$
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=1.7.4
pkgrel=1
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
depends=('pygtk' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('17c1230512b4b788c4b7b0c6bbd72b10c1005a810c1c9c8db9a0bf1cb1f7d56f')

install=meld.install
options=('!emptydirs')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/update-/#update-/' Makefile
  sed -i 's/env python/env python2/' bin/meld
}

build() {
  cd $pkgname-$pkgver
  make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
  cd $pkgname-$pkgver
  make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="$pkgdir" install
}
