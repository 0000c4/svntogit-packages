# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=meld
pkgver=1.5.2
pkgrel=1
pkgdesc='Visual diff and merge tool'
arch=('any')
url='http://meld.sourceforge.net/'
license=('GPL')
depends=('pygtk')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'gnome-python: gnome integration')
install=meld.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.5/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bddd8322437fc4d5cf0d17e6f7e85ee273d86275a209640813d0e2451d9e2b17')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PYTHON=python2 prefix=/usr
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PYTHON=python2 prefix=/usr DESTDIR="${pkgdir}" install
    sed -i -e 's#env python#env python2#' "${pkgdir}"/usr/bin/meld
}
