# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=meld
pkgver=1.7.0
pkgrel=1
pkgdesc='Visual diff and merge tool'
arch=('any')
url='http://meld.sourceforge.net/'
license=('GPL')
depends=('pygtk' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'gnome-python: gnome integration')
install=meld.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('d355dba06a39e38ffee93a6b205888db648a0308bc8a5efe3a9c7a42ed91370b')

build() {
    cd $pkgname-$pkgver
    make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
    cd $pkgname-$pkgver
    make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="$pkgdir" install
    sed -i -e 's#env python#env python2#' "$pkgdir/usr/bin/meld"
}
