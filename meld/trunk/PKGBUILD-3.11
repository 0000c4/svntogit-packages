# $Id: PKGBUILD 204566 2014-01-22 23:15:09Z heftig $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.11.1
pkgrel=1
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
makedepends=('intltool' 'itstool')
depends=('python2-gobject' 'gtk3' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('gtksourceview3: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('d7a641bcb67dbae5087708bfe256a8385f015e13')

# Don't forget to add the glib-compile-schemas call to install when using this PKGBUILD
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python2 setup.py build
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# using --skip-build breaks install
	python2 setup.py --no-update-icon-cache --no-compile-schemas \
		install --prefix=/usr --root="${pkgdir}" --optimize=1
}
