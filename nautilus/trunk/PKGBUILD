# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=nautilus
pkgname=(nautilus libnautilus-extension)
pkgver=3.20.1+gf84d431
pkgrel=1
pkgdesc="GNOME file manager"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils dconf 
         libtracker-sparql nautilus-sendto)
makedepends=(intltool gobject-introspection python packagekit python2 gnome-common git gtk-doc)
url="http://www.gnome.org"
options=('!emptydirs')
source=('git://git.gnome.org/nautilus#commit=f84d4319dcd5fcfa460a37dfdb78c93d188e2703')
sha256sums=('SKIP')

prepare() {
  cd $pkgbase
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-update-mimedb \
      --disable-schemas-compile \
      --disable-selinux --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_nautilus() {
  depends+=(libnautilus-extension)
  groups=(gnome)

  cd $pkgbase
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libnautilus-extension uninstall
  make DESTDIR="$pkgdir" -C docs/reference/libnautilus-extension uninstall

}

package_libnautilus-extension() {
  pkgdesc="Library for extending the $pkgdesc"
  depends=(gtk3)

  cd $pkgbase
  make DESTDIR="$pkgdir" -C libnautilus-extension install
  make DESTDIR="$pkgdir" -C docs/reference/libnautilus-extension install
}
