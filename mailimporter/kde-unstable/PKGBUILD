# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailimporter
pkgver=19.11.90
pkgrel=1
pkgdesc="Mail importer library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libkdepim)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('32592c909afd2293ba9ac18120c3693823a91ff0bebfade08ba78a24f79b0da5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
