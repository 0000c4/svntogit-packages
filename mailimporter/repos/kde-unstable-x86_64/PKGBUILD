# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailimporter
pkgver=19.03.80
pkgrel=1
pkgdesc="Mail importer library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libkdepim)
makedepends=(extra-cmake-modules boost)
conflicts=('libkdepim<16')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c3da47bce83f9b8392ce4967dfb9c9da57d20e994e31cd0822495b5be79d16de'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
