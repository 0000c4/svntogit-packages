# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreenlocker
pkgver=5.10.0
pkgrel=2
pkgdesc='Library and components for secure lock screen architecture'
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
groups=(plasma)
depends=(kidletime kwayland kdeclarative)
makedepends=(extra-cmake-modules python kdoctools kcmutils libxcursor)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-380491.patch::"https://cgit.kde.org/kscreenlocker.git/patch/?id=23fa33ce")
sha256sums=('ee625488caa933fb9c174b80b10b425f51ad23eca3aac7d7de20ac1da7d62ed4'
            'SKIP'
            'e2a39c47a77617d062199ffa877bdeb444b34b5abad16f7283f99f83e2dcc4a5')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

# Fix https://bugs.kde.org/show_bug.cgi?id=380491
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-380491.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=/usr/lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
