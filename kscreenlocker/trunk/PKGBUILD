# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreenlocker
pkgver=5.5.4
pkgrel=2
pkgdesc='Library and components for secure lock screen architecture'
arch=(i686 x86_64)
url='https://projects.kde.org/kscreenlocker'
license=(LGPL)
groups=(plasma)
depends=(kdelibs4support plasma-framework kidletime kwayland libxcursor)
makedepends=(extra-cmake-modules python kdoctools)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"
	bug358125.patch::"https://quickgit.kde.org/?p=kscreenlocker.git&a=commitdiff&h=fae65f1c&o=plain")
md5sums=('5073ee898dac459f53200daba96d73ba'
         '378d62c696d206f0d9609bf9a51a030c')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix security issue https://www.kde.org/info/security/advisory-20160209-1.txt
  patch -p1 -i ../bug358125.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
