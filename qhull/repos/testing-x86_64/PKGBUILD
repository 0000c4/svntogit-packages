# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=qhull
pkgver=2019.1
_pkgver=7.3.2
pkgrel=4
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=(x86_64)
url="http://www.qhull.org/"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=("http://www.qhull.org/download/qhull-2019-src-$_pkgver.tgz"
         qhull-pic.patch::"https://github.com/qhull/qhull/commit/a5d0e644.patch"
         qhull-no-static.patch)
sha256sums=('2b7990558c363076261564f61b74db4d0d73b71869755108a469038c07dc43fb'
            'edb77b3b949628eb6759920de8cd3216e179f8b54f16e357c0d4b5fe8bc06131'
            '8c9e7bda62da8f9c02429b0839610adf6e22525e0de6eb6df2a95cf3dad8b7ab')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../qhull-pic.patch # Build libqhullcpp with PIC
  patch -p1 -i ../qhull-no-static.patch # Disable static libraries
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() { 
  cd build

  make DESTDIR="$pkgdir" install

  # Install license
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
