# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=cifs-utils
pkgver=5.4
pkgrel=1
pkgdesc="CIFS filesystem user-space tools"
arch=(i686 x86_64)
url="http://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap-ng' 'keyutils' 'krb5' 'talloc' 'libwbclient')
source=(ftp://ftp.samba.org/pub/linux-cifs/cifs-utils/$pkgname-$pkgver.tar.bz2
        mount.cifs-fix-up-some-D_FORTIFY_SOURCE-2-warnings.patch)

build() {
  cd "$srcdir/$pkgname-$pkgver"
   # add fedora build patch
  patch -Np1 -i ../mount.cifs-fix-up-some-D_FORTIFY_SOURCE-2-warnings.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # set mount.cifs uid, to enable none root mounting form fstab
  chmod +s $pkgdir/sbin/mount.cifs
}
md5sums=('dde98336c833b59777114145cb8f3c64'
         'b1b0a5a21aedd5421ff6e1e1a8720771')
