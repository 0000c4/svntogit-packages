# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Georg Vogetseder <georg.vogetseder@gmail.com>

pkgname=hamster-applet
pkgver=2.32.0
pkgrel=2
pkgdesc="GNOME time tracking applet"
arch=('i686' 'x86_64')
url="http://projecthamster.wordpress.com/"
license=('GPL')
depends=('gnome-python>=2.28.1' 'dbus-python>=0.83.1' 'python-wnck>=2.32' 'python-pysqlite>=2.6.0' 'pyxdg')
makedepends=('intltool' 'gnome-doc-utils>=0.20.1' 'gnome-control-center')
optdepends=('python-gnomeapplet: applet support')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=hamster-applet.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('416cbacbe7432fd665d5de7d94ab62735b76907b41d1cd73a1bada26c4db01d4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" waf

  ./waf --prefix=/usr configure build
  ./waf install --destdir=${pkgdir} --libdir=/usr/lib \
      --libexecdir=/usr/lib/hamster-applet \
      --sysconfdir=/etc \
      --localstatedir=/var

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" $pkgdir/usr/bin/hamster-service

  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    $pkgdir/usr/share/docky/helpers/hamster_control.py \
    $pkgdir/usr/share/dockmanager/scripts/hamster_control.py \
    $pkgdir/usr/lib/hamster-applet/hamster-applet \
    $pkgdir/usr/bin/{hamster-cli,{gnome,hamster}-time-tracker}

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain hamster-applet ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
