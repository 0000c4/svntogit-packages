# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Georg Vogetseder <georg.vogetseder@gmail.com>

pkgname=hamster-applet
pkgver=2.24.3
pkgrel=1
pkgdesc="Project Hamster is a GNOME time tracking applet."
arch=('i686' 'x86_64')
url="http://projecthamster.wordpress.com/"
license=('GPL')
depends=('gnome-python>=2.22.3' 'python-pysqlite>=2.4.1' 'gnome-python-desktop>=2.24.0' 'dbus-python>=0.83.0' 'gnome-panel>=2.24.3')
makedepends=('intltool')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install="hamster-applet.install"
source=(ftp://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('34708555bd77fe012419584213911315')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
