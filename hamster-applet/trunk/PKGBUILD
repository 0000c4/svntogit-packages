# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Georg Vogetseder <georg.vogetseder@gmail.com>

pkgname=hamster-applet
pkgver=2.28.1
pkgrel=1
pkgdesc="GNOME time tracking applet"
arch=('i686' 'x86_64')
url="http://projecthamster.wordpress.com/"
license=('GPL')
depends=('gnome-python>=2.28.0' 'python-pysqlite>=2.5.5' 'gnome-python-desktop>=2.28.0' 'dbus-python>=0.83.0' 'gnome-panel>=2.28.0')
makedepends=('intltool')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=hamster-applet.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('ab9ae10380aeb0aea09e42fd5e7ebd7b09116c0608740b5fa9eda8433d9e31c9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain hamster-applet ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
