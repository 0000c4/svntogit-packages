# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-documents
pkgver=3.29.91+6+gbc9b6cc1
pkgrel=2
pkgdesc="A document manager application for GNOME"
url="https://wiki.gnome.org/Apps/Documents"
arch=(x86_64)
license=(GPL)
depends=(evince gjs gtk3 gnome-desktop gnome-online-accounts libgdata tracker-miners libzapojit
         webkit2gtk gnome-online-miners librsvg libgepub)
makedepends=(gobject-introspection git meson yelp-tools inkscape)
optdepends=('libreoffice-fresh: Support for Libreoffice document types')
groups=(gnome)
_commit=bc9b6cc1838d0c97f6ae33c0f56ec1c7d3a32b48  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-documents.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git"
        js60.diff)
sha256sums=('SKIP'
            'SKIP'
            '26e86a8a5185f0a4b564b0f4d6658bfa63bd65f1f1a4c0ac7416e183bca4a136')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  patch -Np1 -i ../js60.diff

  git submodule init
  git config --local submodule.subprojects/libgd.url "$srcdir/libgd"
  git submodule update
}
  

build() {
  arch-meson $pkgname build -D getting_started=true
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
