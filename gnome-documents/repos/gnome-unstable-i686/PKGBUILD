# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-documents
pkgver=3.22.0+1+g234a492
pkgrel=1
pkgdesc="A document manager application for GNOME"
url="https://wiki.gnome.org/Apps/Documents"
arch=(i686 x86_64)
license=(GPL)
groups=(gnome-extra)
depends=(evince gjs gtk3 gnome-desktop gnome-online-accounts libgdata
         tracker libzapojit webkit2gtk gnome-online-miners librsvg libgepub)
makedepends=(intltool gobject-introspection git gnome-common)
optdepends=('libeoffice-fresh: Support for Libreoffice document types')
_commit=234a49215ad59c1bc5906c8af0eaeb4b93cfd10d
source=("git://git.gnome.org/gnome-documents#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
