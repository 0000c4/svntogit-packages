# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-documents
pkgver=0.4.1
pkgrel=1
pkgdesc="Documents Manager for GNOME"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
groups=('gnome-extra')
depends=('clutter-gtk' 'evince' 'gjs' 'gtk3' 'gnome-desktop' 'gnome-online-accounts' 'libgdata' 'tracker')
makedepends=('intltool')
optdepends=('unoconv: Support for Libreoffice document types')
options=('!libtool')
install=gnome-documents.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('2b2c6b15ed9e6a46602977f1e8dc15e6655c171df8ede6a3f8a749ed6682da46')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-documents
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
