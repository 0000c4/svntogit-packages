# $Id$
#Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libdmapsharing
pkgver=2.9.36+1+g6b3d20e
pkgrel=1
pkgdesc="A library that implements the DMAP family of protocols"
arch=(i686 x86_64)
url="http://www.flyn.org/projects/libdmapsharing/index.html"
license=(LGPL2.1)
depends=(libsoup avahi gst-plugins-base-libs gdk-pixbuf2)
makedepends=(git gtk-doc)
_commit=6b3d20ec55b0fe69218e873a35871b93679c6c4f
source=("git://git.gnome.org/libdmapsharing#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^LIBDMAPSHARING_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --with-mdns=avahi --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
