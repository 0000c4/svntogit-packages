# Contributor: Kaiting Chen <kaiting.chen@kiwilight.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname='cronie'
pkgver=1.4.7
pkgrel=4
pkgdesc='Daemon that runs specified programs at scheduled times and related tools'
url='https://fedorahosted.org/cronie/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('pam')

source=("https://fedorahosted.org/releases/c/r/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'crontab'
        'conf.d'
        'pam.d'
        'rc.d'
        'run-cron')
sha1sums=('c6644ba0e58bcb14e0bb3f925e3e8cc3f0d47a7f'
          'c2aae6a8f2634af8c751c6a3a9507ba02403569d'
          '69d1e76af4efa5dddb10170008a8f51f5c9486e9'
          '6d8aef6880935b3dcc3e28481111d036544eeae5'
          '4d6f14eae01154b6997926fa0eb5c1673a3f3321'
          '53e732ca5748766f33dac09a9e0653f3d069f78e')

backup=(etc/{crontab,cron.deny,{conf.d,pam.d}/crond})

conflicts=('dcron')
replaces=('dcron')
provides=('cron')
groups=('base')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-anacron \
		--with-inotify \
		--with-pam \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/etc/cron.{d,hourly,daily,weekly,monthly}
	install -d "${pkgdir}"/var/spool/{ana,}cron
	chmod u+s "${pkgdir}"/usr/bin/crontab

	install -Dm755 ../rc.d "${pkgdir}"/etc/rc.d/crond
	install -Dm755 ../run-cron "${pkgdir}"/usr/bin/run-cron
	install -Dm644 ../conf.d "${pkgdir}"/etc/conf.d/crond
	install -Dm644 ../pam.d "${pkgdir}"/etc/pam.d/crond
	install -Dm644 ../crontab "${pkgdir}"/etc/crontab
	touch "${pkgdir}"/etc/cron.deny

	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/cronie/COPYING
}
