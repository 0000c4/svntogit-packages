# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=deskbar-applet
pkgver=2.27.92
pkgrel=1
pkgdesc="An all-in-one search bar for the GNOME panel"
arch=(i686 x86_64)
url="http://raphael.slinckx.net/deskbar/"
license=('GPL')
depends=('gnome-desktop>=2.27.92' 'gnome-python-desktop>=2.27.3' 'gnome-panel>=2.27.92' 'dbus-python>=0.83.0')
makedepends=('evolution-data-server>=2.27.92' 'gettext' 'intltool' 'pkgconfig' 'gnome-doc-utils>=0.17.4')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install="deskbar-applet.install"
source=(http://ftp.gnome.org/pub/GNOME/sources/deskbar-applet/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('daf9b692fe891923c9569560f13efd8e7b3883d80b6a1c2deb065af90ad564c0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  LD_PRELOAD="" ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-evolution || return 1

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain deskbar-applet ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
