# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=deskbar-applet
pkgver=2.24.1
pkgrel=1
pkgdesc="An all-in-one search bar for the GNOME panel"
arch=(i686 x86_64)
url="http://raphael.slinckx.net/deskbar/"
license=('GPL')
depends=('gnome-desktop>=2.24.0' 'gnome-python-desktop>=2.24.0' 'gnome-panel>=2.24.0' 'libbonoboui>=2.24.0' 'dbus-python>=0.83.0')
makedepends=('evolution-data-server>=2.24.0' 'gettext' 'perlxml' 'pkgconfig' 'gnome-doc-utils>=0.14.0')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install="deskbar-applet.install"
source=(http://ftp.gnome.org/pub/GNOME/sources/deskbar-applet/2.24/${pkgname}-${pkgver}.tar.bz2)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  LD_PRELOAD="" ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-scrollkeeper \
    --enable-beagle \
    --enable-evolution || return 1

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

md5sums=('b44038bc973ea031ea850bc703107b72')
