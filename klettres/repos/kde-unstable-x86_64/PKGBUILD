# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klettres
pkgver=16.03.90
pkgrel=1
pkgdesc="Learn The Alphabet"
url="http://kde.org/applications/education/klettres/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('knewstuff' 'kemoticons' 'kitemmodels' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
replaces=('kdeedu-klettres')
conflicts=('kdeedu-klettres')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/klettres-${pkgver}.tar.xz")
sha1sums=('1f4108bae70a448b7af0571d76b42b1698502697')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../klettres-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
