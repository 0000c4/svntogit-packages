# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kshisen
pkgver=17.04.3
pkgrel=1
pkgdesc="A solitaire-like game played using the standard set of Mahjong tiles"
url="https://kde.org/applications/games/kshisen/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'libkmahjongg' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-kshisen')
replaces=('kdegames-kshisen')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kshisen-${pkgver}.tar.xz"{,.sig})
sha256sums=('deff42f0f1f7d7a153998720c577137fd83ae87cfcec770406593e5668e5ba91'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../kshisen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
