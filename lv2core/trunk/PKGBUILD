# $Id$
# Maintainer: Ray Rashif <schivmeister@gmail.com>

pkgname=lv2core
pkgver=3.0
pkgrel=1
pkgdesc="LV2: Successor to the LADSPA audio plug-in standard"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=('i686' 'x86_64')
makedepends=('python2')
provides=('lv2')
changelog=$pkgname.changelog
source=(http://lv2plug.in/spec/$pkgname-$pkgver.tar.bz2)
md5sums=('382f7d96ff0374c0c495336e1c8bb999')

build() {
	cd "$srcdir/$pkgname-$pkgver"

  # fix for python 2.7
  sed -i 's:^#!.*bin/env python:#!/usr/bin/env python2:' waf

	./waf configure --prefix=/usr
	./waf build $MAKEFLAGS
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	./waf install --destdir="$pkgdir"

	install -Dm0644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
