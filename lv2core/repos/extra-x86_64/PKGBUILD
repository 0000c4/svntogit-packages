# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>

pkgname=lv2core
pkgver=4.0
pkgrel=1
pkgdesc="LV2: Successor to the LADSPA audio plug-in standard"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=('i686' 'x86_64')
makedepends=('python2')
provides=('lv2')
changelog=$pkgname.changelog
install=$pkgname.install
source=(http://lv2plug.in/spec/$pkgname-$pkgver.tar.bz2)
md5sums=('5097d964f3559a1ecec2d2fc822ef53a')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	python2 waf configure --prefix=/usr
	python2 waf build $MAKEFLAGS
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	python2 waf install --destdir="$pkgdir"

	install -Dm0644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
