# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.40
pkgrel=1
pkgdesc="Fully featured MPD client which runs in a terminal"
url='https://www.musicpd.org/clients/ncmpc/'
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'glibc' 'lirc' 'pcre')
makedepends=('boost' 'libmpdclient' 'meson' 'ncurses' 'python-sphinx')
optdepends=('ruby: for lyrics plugin')
source=("https://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha512sums=('9b31567ef461a14ebfe76996b377767c716146f30766770c03c02e9ec3e213298510cfbb2f260a480bed70352b87e47544272da715cd0436609eaaf986fc8b39'
            'SKIP')
b2sums=('cd5823df0f90b10622c5ad54735bc95c21f783e76133ff478ccf3cbf4027a105593697a7e4ba776ffbe0f40c47eb05855c3397afa634b5afc1ffd4b73bc059e4'
        'SKIP')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  cd "${pkgname}-${pkgver}"
  meson --prefix /usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        -D lyrics_screen=true \
        -D chat_screen=true \
        build
  ninja -C build
}

package() {
  depends+=('libmpdclient.so' 'libncursesw.so')
  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -C build install
}
