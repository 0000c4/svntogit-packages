# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.38
pkgrel=1
pkgdesc='Fully featured MPD client using ncurses'
url='https://www.musicpd.org/clients/ncmpc/'
arch=('x86_64')
license=('GPL')
depends=('ncurses' 'libmpdclient' 'boost-libs' 'lirc')
optdepends=('ruby: for lyrics plugin')
makedepends=('meson' 'python-sphinx' 'boost')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512')
source=("https://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('2bc1aa38aacd23131895cd9aa3abd9d1ca5700857034d9f35209e13e061e27a2'
            'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed "s/curses == 'ncursesw'/false/g" -i meson.build
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	arch-meson . build -Dlyrics_screen=true
	ninja -C build
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	DESTDIR="${pkgdir}" ninja -C build install
}
