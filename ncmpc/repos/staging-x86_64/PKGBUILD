# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.20
pkgrel=2
pkgdesc='Fully featured MPD client using ncurses'
url='http://mpd.wikia.com/wiki/Client:Ncmpc'
arch=('i686' 'x86_64')
license=('GPL')
depends=('ncurses' 'glib2' 'libmpdclient')
optdepends=('python2: to enable lyrics plugin'
            'ruby: to enable lyrics plugin')
source=("http://downloads.sourceforge.net/musicpd/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('da106de4b11a48eb2d63619da1316713f95cc9cb')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's#python#python2#' lyrics/30-leoslyrics.py
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-lyrics-screen \
		--with-lyrics-plugin-dir=/usr/share/ncmpc/lyrics
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	for i in lyrics/*; do install -Dm755 "$i" "${pkgdir}/usr/share/ncmpc/$i"; done
}
