# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apc
pkgver=3.1.6
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A free, open, and robust framework for caching and optimizing PHP intermediate code'
url='http://pecl.php.net/package/APC'
depends=('php')
license="PHP"
source=("http://pecl.php.net/get/APC-${pkgver}.tgz"
        'suppress_stat_warning.patch')
backup=('etc/php/conf.d/apc.ini')
md5sums=('624b8030254f897face30789d64e6b7e'
         'cf5b72c9808273e782bdc11421b4a512')

build() {
	cd $srcdir/APC-$pkgver
	# suppress warning when stat=0 is used with rel. paths
	# see http://pecl.php.net/bugs/bug.php?id=19610
	patch -p0 -i ${srcdir}/suppress_stat_warning.patch
	phpize
	./configure --prefix=/usr
	make
}

package() {
	cd $srcdir/APC-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';extension=apc.so' > apc.ini
	install -D -m644 apc.ini $pkgdir/etc/php/conf.d/apc.ini
	install -D -m644 apc.php $pkgdir/usr/share/php-apc/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apc/install.txt
}
