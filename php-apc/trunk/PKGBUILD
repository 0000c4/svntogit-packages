# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apc
pkgver=3.1.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A free, open, and robust framework for caching and optimizing PHP intermediate code'
url='http://pecl.php.net/package/APC'
depends=('php')
license="PHP"
source=("http://pecl.php.net/get/APC-${pkgver}.tgz")
backup=('etc/php/conf.d/apc.ini')
md5sums=('94239866e5d1b90e02675e988a5cbad4')

build() {
	cd $srcdir/APC-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

package() {
	cd $srcdir/APC-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';extension=apc.so' > apc.ini
	install -D -m644 apc.ini $pkgdir/etc/php/conf.d/apc.ini
	install -D -m644 apc.php $pkgdir/usr/share/php-apc/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apc/install.txt
}
