# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6
pkgname=python-pyqt6
pkgdesc="A set of Python bindings for the Qt6 toolkit"
pkgver=6.0.0
pkgrel=1
arch=(x86_64)
url="https://riverbankcomputing.com/software/pyqt/intro"
license=(GPL)
groups=(pyqt6)
depends=(qt6-base)
optdepends=('qt6-tools: QtHelp, QtDesigner'
            'qt6-svg: QtSvg'
            'qt6-declarative: QtQml, qmlplugin'
            'qt6-quick3d: QtQuick3D')
makedepends=(sip pyqt-builder qt6-tools qt6-svg qt6-declarative qt6-quick3d python-opengl)
source=("https://pypi.python.org/packages/source/P/PyQt6/PyQt6-$pkgver.tar.gz"
         fix-build.patch)
sha256sums=('c758b0568127bffd7147f461d74bbf06249177eae908efed8099eec30984f1b3'
            '6cd36d08d38d8904a89ae40a4c18c1a6225e6dba2e6cef4d6f14005d2cd7895c')

prepare() {
  patch -d PyQt6-$pkgver -p1 -i ../fix-build.patch
}

build() {
  cd PyQt6-$pkgver
  sip-build \
    --confirm-license \
    --no-make \
    --qmake=/usr/bin/qmake-qt6
  cd build
  make
}

package_python-pyqt6(){
  cd PyQt6-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install

  # compile Python bytecode
  python -m compileall -d / "$pkgdir"/usr/lib
  python -O -m compileall -d / "$pkgdir"/usr/lib
}
