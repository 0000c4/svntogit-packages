# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kshutdown
pkgver=3.0beta2
pkgrel=1
pkgdesc="Shutdown Utility for KDE"
arch=(i686 x86_64)
url="http://kshutdown.sourceforge.net/"
license=('GPL')
depends=('kdebase-workspace')
makedepends=('pkgconfig' 'cmake' 'automoc4')
install='kshutdown.install'
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-source-${pkgver}.zip"
        'kshutdown_3.0beta2-mga-fix_requestshutdown_call.patch')
md5sums=('1621e19cc74c9578b6cfc3e32166f853'
         'c03185629d063c8ebbf59a423cf78980')

build() {
  cd "${srcdir}"
  patch -Np0 -i "${srcdir}/kshutdown_3.0beta2-mga-fix_requestshutdown_call.patch"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
