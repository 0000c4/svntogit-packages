# $Id: PKGBUILD 59825 2009-11-28 01:40:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebindings
pkgname=('kdebindings-csharp'
         'kdebindings-python'
         'kdebindings-ruby'
         'kdebindings-smoke')
pkgver=4.5.5
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'python2-qt' 'kdepim-runtime' 'mono'
	'kdegraphics-okular' 'qwt' 'docbook-xsl' 'boost' 'qscintilla' 'ruby')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
        'sip4.11.2.patch'
        'sip4.12.patch')
sha1sums=('5f754f0b6989df9557c5ccec18b4f96207fceb94'
          '36b52d147fee24a65c756baaad00e3d1ac081d3d'
          '59b3992de8def0e415a055246d2d3d0a2934d8d9')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	patch -Np0 -i ${srcdir}/sip4.11.2.patch
	patch -Np1 -i ${srcdir}/sip4.12.patch

	# fix ruby
	for f in `find ruby -name CMakeLists.txt`; do
	  sed -i "s|\${RUBY_INCLUDE_PATH}|\${RUBY_INCLUDE_PATH} /usr/include/ruby-1.9.1/${_ARCHITECTURE}-linux|" "${f}"
	done
	
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd ${srcdir}/build

	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_falcon=OFF \
		-DBUILD_java=OFF \
        -DBUILD_perl=OFF \
		-DBUILD_php=OFF \
		-DWITH_PolkitQt=OFF
	make
}

package_kdebindings-csharp() {
	pkgdesc='KDE bindings for mono'
	depends=('kdebindings-smoke' 'mono')
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build/csharp
	make DESTDIR=$pkgdir install
}

package_kdebindings-python() {
	pkgdesc='KDE bindings for python'
	depends=('kdepim-runtime' 'python2-qt' 'qscintilla')
	cd $srcdir/build/python
	make DESTDIR=$pkgdir install

	# Use the python2 executable
	find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_kdebindings-ruby() {
	pkgdesc='KDE Bindings for ruby'
	depends=('kdebindings-smoke' 'ruby')
	cd $srcdir/build/ruby
	make DESTDIR=$pkgdir install
}

package_kdebindings-smoke() {
	pkgdesc='Language independent library for Qt and KDE bindings'
	depends=('qscintilla' 'kdegraphics-okular' 'kdepim-runtime')
	cd $srcdir/build/smoke
	make DESTDIR=$pkgdir install
}
