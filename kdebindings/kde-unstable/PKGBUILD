# $Id: PKGBUILD 59825 2009-11-28 01:40:17Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebindings
pkgname=('kdebindings-python'
	 'kdebindings-ruby'
         'kdebindings-smoke')
groups=('kdebindings')
pkgver=4.3.80
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'pyqt' 'polkit-qt' 'qimageblitz' 'qscintilla' 'kdegraphics-okular')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('3fd12db9000aa2c11e8812182d7a0820')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_csharp=OFF \
		-DBUILD_java=OFF \
		-DBUILD_php=OFF \
		-DBUILD_falcon=OFF
	make
}

package_kdebindings-python() {
	pkgdesc='KDE bindings for python'
	depends=('kdepimlibs' 'pyqt' 'polkit-qt')
	cd $srcdir/build/python
	make DESTDIR=$pkgdir install
}

package_kdebindings-ruby() {
	pkgdesc='KDE bindings for ruby'
	depends=('kdebindings-smoke' 'ruby')
	cd $srcdir/build/ruby
	make DESTDIR=$pkgdir install
}

package_kdebindings-smoke() {
	pkgdesc='Language independent library for Qt and KDE bindings'
	depends=('kdepimlibs' 'qscintilla')
	groups=()
	cd $srcdir/build/smoke
	make DESTDIR=$pkgdir install
}
