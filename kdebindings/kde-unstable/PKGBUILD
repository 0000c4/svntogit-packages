# $Id: PKGBUILD 59825 2009-11-28 01:40:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebindings
pkgname=('kdebindings-csharp'
         'kdebindings-python'
#         'kdebindings-ruby'
         'kdebindings-smoke')
pkgver=4.4.85
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'pyqt' 'kdepim-runtime' 'ruby' 'mono'
	'kdegraphics-okular' 'qwt' 'docbook-xsl')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('a46c3d549ba83a16614e8a412a5feece49a1cc98')

build() {
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build

	# fix build with ruby 1.9
	# see http://bugs.archlinux.org/task/16175
	find ../$pkgbase-$pkgver/ruby -name CMakeLists.txt \
		-exec sed -i "s|\${RUBY_INCLUDE_PATH}|\${RUBY_INCLUDE_PATH} /usr/include/ruby-1.9.1/${CARCH}-linux|" {} \;

	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_falcon=OFF \
		-DBUILD_java=OFF \
		-DBUILD_php=OFF \
		-DBUILD_ruby=OFF \
		-DWITH_PolkitQt=OFF
	make
}

package_kdebindings-csharp() {
	pkgdesc='KDE bindings for mono'
	depends=('kdebindings-smoke' 'mono')
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build/csharp
	make DESTDIR=$pkgdir install
}

package_kdebindings-python() {
	pkgdesc='KDE bindings for python'
	depends=('kdepim-runtime' 'pyqt')
	cd $srcdir/build/python
	make DESTDIR=$pkgdir install
}

package_kdebindings-ruby() {
	pkgdesc='KDE bindings for ruby'
	depends=('kdebindings-smoke' 'ruby')
	cd $srcdir/build/ruby
	make DESTDIR=$pkgdir install
}

package_kdebindings-smoke() {
	pkgdesc='Language independent library for Qt and KDE bindings'
	depends=('qscintilla' 'kdegraphics-okular' 'kdepim-runtime')
	cd $srcdir/build/smoke
	make DESTDIR=$pkgdir install
}
