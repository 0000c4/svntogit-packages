# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libphonenumber
pkgver=8.5.0
pkgrel=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(i686 x86_64)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest git java-environment)
license=("Apache")
_commit=52cff9d8837f6e224a78d712d4d8fa1863f77956  # tags/v8.5.0^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/-/+/g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  CXXFLAGS+=" -Wno-error=unused-variable"  # this is nuts
  cmake ../$pkgname/cpp \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd build
  make tests
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
