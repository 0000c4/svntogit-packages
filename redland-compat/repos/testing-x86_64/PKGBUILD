# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Francois Charette <francois.archlinux.org> 

pkgbase=redland-compat
pkgname=('redland-compat' 'redland-compat-devel')
pkgver=1.0.12
pkgrel=3
url="http://librdf.org/"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=('rasqal-compat-devel>=0.9.21-2' 'raptor1>=1.4.21' 'libtool' 'db>=5.1' 'postgresql-libs' 'libmysqlclient' 'unixodbc' 'sqlite3')
options=('!libtool' '!emptydirs')
source=(http://download.librdf.org/source/"${pkgbase/-compat/}"-$pkgver.tar.gz
	    rpath.diff)
md5sums=('40f37a5ad97fdfbf984f78dcea0c6115'
         'acc85e784f01a656bd56777f95880787')

build() {
  cd ${srcdir}/"${pkgname/-compat/}"-${pkgver}
  ./configure --prefix=/usr \
	--enable-release \
	--disable-static \
	--with-sqlite=3 \
	--disable-modular

  # nuke rpath
  patch -Np0 -i ${srcdir}/rpath.diff

  make
}

package_redland-compat() {

 pkgdesc="Library that provides a high-level interface to RDF  data; compatibility version providing an older API"
 depends=('rasqal-compat>=0.9.21' 'raptor1>=1.4.21' 'libtool' 'db>=5.1' 'postgresql-libs' 'libmysqlclient' 'unixodbc' 'sqlite3')
 replaces=('redland-compat-storage-mysql' 'redland-compat-storage-postgresql' 'redland-compat-storage-virtuoso' 'redland-compat-storage-sqlite')

  cd ${srcdir}/"${pkgbase/-compat/}"-${pkgver}
  make -C src DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/librdf.so
  rm -rf ${pkgdir}/usr/{include,share}
}

package_redland-compat-devel() {

 pkgdesc="Library that provides a high-level interface to RDF  data; compatibility version providing an older API - devel files"
 depends=('redland-compat')
 
  cd ${srcdir}/"${pkgbase/-compat/}"-${pkgver}
  #make -C src DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/librdf.so.*
  rm -rf ${pkgdir}/usr/share
}
