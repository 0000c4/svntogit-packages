# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Francois Charette <francois.archlinux.org> 

pkgbase=redland-compat
_pkgbase=redland
pkgname=('redland-compat' 'redland-compat-storage-mysql' 'redland-compat-storage-postgresql' 'redland-compat-storage-virtuoso' 'redland-compat-storage-sqlite')
pkgver=1.0.12
pkgrel=1
url="http://librdf.org/"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=('rasqal-compat>=0.9.21' 'raptor1>=1.4.21' 'db>=5.1' 'postgresql-libs' 'libmysqlclient' 'unixodbc' 'sqlite3')
options=('!libtool')
source=(http://download.librdf.org/source/$_pkgbase-$pkgver.tar.gz
	    rpath.diff)
md5sums=('40f37a5ad97fdfbf984f78dcea0c6115'
         'acc85e784f01a656bd56777f95880787')

build() {
  cd ${srcdir}/${_pkgbase}-${pkgver}
  ./configure --prefix=/usr \
	--enable-release \
	--disable-static \
	--with-raptor=system \
	--with-rasqal=system \
	--with-sqlite=3

  # nuke rpath
  patch -Np0 -i ${srcdir}/rpath.diff

  make
}

package_redland-compat() {
 pkgdesc="Library that provides a high-level interface to RDF data"
 depends=('rasqal-compat>=0.9.21' 'raptor1>=1.4.21' 'libtool')
 conflicts=('redland')
 provides=("redland=${pkgver}")
 
  cd ${srcdir}/${_pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/lib/redland
}

package_redland-compat-storage-mysql() {
 pkgdesc="MySQL storage support for Redland"
 depends=('redland-compat' 'libmysqlclient')
 conflicts=('redland-storage-mysql')
 provides=("redland-storage-mysql=${pkgver}")

  cd ${srcdir}/${_pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_mysql.so ${pkgdir}/usr/lib/redland/librdf_storage_mysql.so 
}

package_redland-compat-storage-postgresql() {
 pkgdesc="PostgreSQL storage support for Redland"
 depends=('redland-compat' 'postgresql-libs')
 conflicts=('redland-storage-postgresql')
 provides=("redland-storage-postgresql=${pkgver}")

  cd ${srcdir}/${_pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_postgresql.so ${pkgdir}/usr/lib/redland/
}

package_redland-compat-storage-virtuoso() {
 pkgdesc="Virtuoso storage support for Redland"
 depends=('redland-compat' 'unixodbc' 'db')
 conflicts=('redland-storage-virtuoso')
 provides=("redland-storage-virtuoso=${pkgver}")

  cd ${srcdir}/${_pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_virtuoso.so ${pkgdir}/usr/lib/redland/
}

package_redland-compat-storage-sqlite() {
 pkgdesc="SQLite storage support for Redland"
 depends=('redland-compat' 'sqlite3' 'db')
 conflicts=('redland-storage-sqlite')
 provides=("redland-storage-sqlite=${pkgver}")
 
  cd ${srcdir}/${_pkgbase}-${pkgver}
  install -dm755 ${pkgdir}/usr/lib/redland
  install -m755 src/.libs/librdf_storage_sqlite.so ${pkgdir}/usr/lib/redland/
}
