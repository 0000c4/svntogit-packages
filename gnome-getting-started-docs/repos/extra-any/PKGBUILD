# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-getting-started-docs
pkgver=3.35.92+1+g635b61d
pkgrel=1
pkgdesc="Help a new user get started in GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-getting-started-docs"
license=(CCPL:by-sa)
arch=(any)
# gst plugins required to decode help videos
depends=(yelp gst-plugins-base gst-plugins-good)
makedepends=(yelp-tools git)
groups=(gnome)
_commit=635b61d6cf8a34c7b37b0e51228ef0af5d4e76ba  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-getting-started-docs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
