# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=nmap
pkgver=6.00
pkgrel=1
pkgdesc='Utility for network discovery and security auditing'
url='http://nmap.org/'
arch=('i686' 'x86_64')
license=('GPL')
options=('!makeflags')
optdepends=('pygtk: zenmap')
makedepends=('pygtk')
depends=('pcre' 'openssl' 'libpcap' 'lua')
source=("http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('11d8f418e31a85e5b5ad6b4cf98f3c1b5c19ddb2')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Show icon only if runnable; see FS#12045
	echo 'TryExec=/usr/bin/pygtk-demo' >> zenmap/install_scripts/unix/zenmap.desktop 
	echo 'TryExec=/usr/bin/pygtk-demo' >> zenmap/install_scripts/unix/zenmap-root.desktop 

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--mandir=/usr/share/man \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/uninstall_zenmap
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
