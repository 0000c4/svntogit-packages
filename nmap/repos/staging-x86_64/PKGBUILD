# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=nmap
pkgver=5.51
pkgrel=4
pkgdesc="A network exploration tool and security/port scanner"
arch=('i686' 'x86_64')
url="http://nmap.org"
license=('GPL')
depends=('pcre' 'openssl' 'libpcap' 'lua')
# python is needed for zenmap
makedepends=('python2')
optdepends=('pygtk: gui for nmap')
options=('!makeflags')
source=(http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0b80d2cb92ace5ebba8095a4c2850275')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  echo "TryExec=/usr/bin/pygtk-demo" >> zenmap/install_scripts/unix/zenmap.desktop 
  echo "TryExec=/usr/bin/pygtk-demo" >> zenmap/install_scripts/unix/zenmap-root.desktop 

  # Force usage of external libpcap
  # OSX bug check fails because of missing types
  # autoreconf fails because our autotools are too new (?)
  sed -i 's/have_libpcap=no/have_libpcap=yes/g' {.,ncat,nping}/configure

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --libexecdir=/usr/lib
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # remove zenmap uninstall script
  rm "${pkgdir}/usr/bin/uninstall_zenmap"

  # install custom GPL2 license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
