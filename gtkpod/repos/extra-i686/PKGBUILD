# $Id$
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=gtkpod
pkgver=2.1.2
pkgrel=4
pkgdesc="A platform independent GUI for Apple's iPod using GTK3"
arch=('i686' 'x86_64')
url="http://gtkpod.sourceforge.net"
license=('GPL')
depends=('libanjuta' 'curl' 'flac' 'libid3tag' 'libgpod' 'awk' 'hicolor-icon-theme' 'dconf' 'libwebkit3')
makedepends=('flex' 'intltool' 'libvorbis' 'faad2')
optdepends=('libmp4v2: MP4/h264 support'
            'vorbis-tools: OGG support'
	    'libvorbis: OGG support'
            'id3v2: mp3 conversion support'
            'faad2: m4a conversion support')
install=gtkpod.install
options=('!libtool')
source=("http://downloads.sourceforge.net/gtkpod/${pkgname}-${pkgver}.tar.gz"
        "gtkpod-2.1.2-anjuta.patch")
md5sums=('88fef3f947cbb7ae6b714080733a3b6b'
         '0a5f4ce0c9ef3d4d8db57290df43db27')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/gtkpod-2.1.2-anjuta.patch"
  sed -i 's#python#python2#' scripts/sync-palm-jppy.py
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
