# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=gtkpod
pkgver=2.1.0
pkgrel=1
pkgdesc="A platform independent GUI for Apple's iPod using GTK2"
arch=('i686' 'x86_64')
url="http://gtkpod.sourceforge.net"
license=('GPL')
depends=('anjuta' 'curl' 'dconf' 'flac' 'libid3tag' 'libgpod>=0.8.0')
makedepends=('flex' 'intltool')
optdepends=('libmp4v2: MP4/h264 support'
            'vorbis-tools: OGG support'
            'id3v2: mp3 conversion support')
install=gtkpod.install
source=(http://downloads.sourceforge.net/gtkpod/${pkgname}-${pkgver}.tar.gz)
md5sums=('8e01f7cf2db1a421140eab561aee26d7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#python#python2#' scripts/sync-palm-jppy.py
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
