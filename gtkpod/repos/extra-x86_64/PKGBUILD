# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=gtkpod
pkgver=1.0.0
pkgrel=2
pkgdesc="A platform independent GUI for Apple's iPod using GTK2"
arch=('i686' 'x86_64')
url="http://gtkpod.sourceforge.net"
license=('GPL')
depends=('awk' 'libgpod>=0.8.0' 'libid3tag' 'flac' 'libvorbis' 'curl' 'libglade' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('libmp4v2: MP4/h264 support'
            'vorbis-tools: OGG support'
            'id3v2: mp3 conversion support')
install=gtkpod.install
source=(http://downloads.sourceforge.net/gtkpod/${pkgname}-${pkgver}.tar.gz)
md5sums=('cadd402dcd1cfbedda0357bf24965a7c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#python#python2#' scripts/sync-palm-jppy.py
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
