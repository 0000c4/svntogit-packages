# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-zeroconf-ioslave
pkgver=17.07.80
pkgrel=1
pkgdesc='Network Monitor for DNS-SD services (Zeroconf)'
url='https://www.kde.org/applications/internet/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
conflicts=('kdenetwork-kdnssd')
replaces=('kdenetwork-kdnssd')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/zeroconf-ioslave-${pkgver}.tar.xz"{,.sig})
sha256sums=('f3106a6a94ce45ee83690a54483fbe7c5b2320be6e3b13c41609db4ef02092db'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../zeroconf-ioslave-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
