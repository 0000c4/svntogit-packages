# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hunspell-es
pkgname=(hunspell-es hyphen-es mythes-es)
pkgver=2.1.1
pkgrel=1
epoch=1
pkgdesc="Spanish hunspell dictionaries"
arch=(any)
url="https://github.com/sbosio/rla-es"
license=('LGPL3')
optdepends=('hunspell:	the spell checking libraries and apps')
noextract=(es_ANY.oxt es_AR.oxt es_BO.oxt es_CL.oxt es_CO.oxt 
           es_CR.oxt es_CU.oxt es_DO.oxt es_EC.oxt es_ES.oxt 
           es_GT.oxt es_HN.oxt es_MX.oxt es_NI.oxt es_PA.oxt 
           es_PE.oxt es_PR.oxt es_PY.oxt es_SV.oxt es_UY.oxt es_VE.oxt)
source=("https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_ANY.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_AR.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_BO.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_CL.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_CO.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_CR.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_CU.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_DO.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_EC.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_ES.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_GT.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_HN.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_MX.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_NI.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_PA.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_PE.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_PR.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_PY.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_SV.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_UY.oxt"
        "https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_VE.oxt")
md5sums=('05c257b89373d068eb33debccafaba1d'
         '734c4926d8ada7ea7209636035739fe2'
         '17812d2473dd4e18e14cbe5a92af3e17'
         'ad00085f2caffdc762515e6944afa850'
         '4d4f710dbd147c0aea97331d75b1a35c'
         '3427143afec8833ab4752220be5d70b1'
         '8874b4ab59c193b0e81cec9d3b87eda0'
         'e7ab39bdff1991273c8f8144ab83a7f5'
         '8a9bc2ca06a66102d66f92c7d6389cdc'
         'ee9b7f7eb7c8ff9adab90eda3e506923'
         '5075980a9e5380cfbe41d8c1f1a4bc8e'
         '0a4515bd8229f119a75f75a66d844bba'
         'ec2c12efdce4a2366323dffdea0c10bf'
         'c6008281b5ada374e5edfa29bd2beb36'
         '570ce39f322d2796db7a0a2e9891a021'
         'dfe4947d55e4bac1e931d95e24c4ec57'
         '61e46c42e08741246f04dfd80badf316'
         'cdc8bde78fb2c4e1bf2cdefcbca0ca92'
         'd15bceb79dea5a014441b4e71b1976b8'
         '865eea12715c68226b0b874d57f3b256'
         '2f0684ec913a8496721410dc34e5d91d')

package_hunspell-es() {
  makedepends=('hunspell')

  for file in *.oxt; do
    LANG=en_US.utf8 bsdtar -xf $file
  done

  install -dm755 "${pkgdir}/usr/share/hunspell"
  install -m644 es_*.dic es_*.aff "$pkgdir/usr/share/hunspell"

  # the symlinks
  install -dm755 "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
  
  # docs
  install -dm755 "${pkgdir}/usr/share/doc/$pkgname"
  install -m644 README.txt "$pkgdir/usr/share/doc/$pkgname"
}

package_hyphen-es() {
  pkgdesc="Spanish hyphenation rules"
  optdepends=('hyphen: offers hyphenation library functions')

  install -Dm644 hyph_es_ANY.dic "$pkgdir/usr/share/hyphen/hyph_es_ANY.dic"

  pushd "$pkgdir/usr/share/hyphen/"
  es_ES_aliases="es_AR es_BO es_CL es_CO es_CR es_CU es_DO es_EC es_ES es_GT es_HN es_MX es_NI es_PA es_PE es_PR es_SV es_UY es_VE"
  for lang in $es_ES_aliases; do
        ln -s hyph_es_ANY.dic hyph_$lang.dic
  done
  popd

  # the symlinks
  install -dm755 "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
  
  # docs
  install -Dm644 README_hyph_es_ANY.txt "${pkgdir}/usr/share/doc/$pkgname/README_hyph_es_ANY.txt"
}

package_mythes-es() {
  pkgdesc="Spanish thesaurus"
  optdepends=('libmythes: offers thesaurus library functions')

  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt
  install -dm755 "${pkgdir}/usr/share/mythes"
  install -m644 th_es_ES_v2.* "$pkgdir/usr/share/mythes"

  pushd "$pkgdir/usr/share/mythes/"
  es_ES_aliases="es_AR es_BO es_CL es_CO es_CR es_CU es_DO es_EC es_GT es_HN es_MX es_NI es_PA es_PE es_PR es_SV es_UY es_VE"
  for lang in $es_ES_aliases; do
        ln -s th_es_ES_v2.idx "th_"$lang"_v2.idx"
        ln -s th_es_ES_v2.dat "th_"$lang"_v2.dat"
  done
  popd

  # the symlinks
  install -dm755 "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
  for file in "$pkgdir"/usr/share/mythes/*; do
    ln -sv /usr/share/mythes/$(basename $file) .
  done
  popd
  
  # docs
  install -Dm644 README_th_es_ES.txt "${pkgdir}/usr/share/doc/$pkgname/README_th_es_ES.txt"
}
