# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hunspell-es
pkgname=(hunspell-es_any hyphen-es mythes-es)
pkgver=2.5
pkgrel=1
epoch=1
arch=(any)
url="https://github.com/sbosio/rla-es"
license=(LGPL3)
makedepends=(qt5-webengine)
_countries=(
  'AR "Argentina"'
  'BO "Bolivia"'
  'CL "Chile"'
  'CO "Colombia"'
  'CR "Costa Rica"'
  'CU "Cuba"'
  'DO "Dominican Republic"'
  'EC "Ecuador"'
  'ES "Spain"'
  'GT "Guatemala"'
  'HN "Honduras"'
  'MX "Mexico"'
  'NI "Nicaragua"'
  'PA "Panama"'
  'PE "Peru"'
  'PR "Puerto Rico"'
  'PY "Paraguay"'
  'SV "El Salvador"'
  'UY "Uruguay"'
  'VE "Venezuela"'
)

source=(https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_ANY.oxt)
noextract=(es_ANY.oxt)

for _co in "${_countries[@]}"; do
  _code=${_co%% *}
  noextract+=(es_$_code.oxt)
  source+=("https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_$_code.oxt")
  _pkgname=hunspell-es_${_code,,}
  pkgname+=($_pkgname)
  eval "package_$_pkgname() {
    _package $_co
  }"
done

sha256sums=('55a7be2423dc106557fa762c8f921c89071ff7cc8a54019af8cafc3c833e3571'
            '03c2d2123d8878e320a2b00ed6d774deab1b4130a93ff441718771d2f974f01c'
            '25571eb8f612f46e33434de1f51e5ab9a7ff682070fc743c30bcb024a16b5075'
            'd920be97cf2461c8af4dac3a73f1b7c3ea6e224bae9f3f6b3742fb55c35f2343'
            '77727e25209fea5924dadebc6cab9073bf0b14cc1c1610eec80afc4fc519ece6'
            '01de57648f1574bdb4ba202b0ab299d888af6c1f3f5e921f7db2a3ccdf63ec4e'
            '2a641171114843347bea0dc21acab47a96306b93fc1a5e3f6ef6716d364ea93a'
            '0029d089a530eb7b8611957a731ef25bd8d5b4399a161e3f0c604b5beeaf2bc6'
            '2ba3219bd4ad5f57e40831e44fbdcc95a8a5237864189505e8d681fb300fc990'
            '22a3b973f1667ddfd2d5c130623a16e22f9722a27437ba66d1de4fe569d32d0f'
            '986331bc087ba745a106863dae80f1c9cc1c9ebd6eff39f54da8d5eda572742a'
            'baa77e8afbe5a020040ddb110349e5d1b3e2175b6b83c5cd2592e68a0f865ccd'
            'ca0283a44e91a727a1eed8d423e078d9214e84861f00681e40f22ac94c6fb1da'
            '79a203a285329f11507e1f721f1f47c354f04bae6a8b344b2f072f3684ffc9a2'
            '28c1de0c6fa068fba45805f79b9eb9390f5e6438bff9e78dea96d56c61ce93f7'
            '3fe0b1e8818a984b23cce5d75f0e08e457bcddbf399500b5193c17d922fb8566'
            '20615ff4b5245cf91541f928f328d0e87a6d888fe2262aeaf0c8572835b94edd'
            '804adfaa115423cab482baa0fc1966fd1f7417d7c8cd0caeb55ea8d498fd4428'
            'ce1b8a025df061fd91502b0cd4a76e3ba94da3d5b8ecf3c3162cdb4ee15a4490'
            '3eaeba94eedf5b37ab2b2362282c6e78a40b640043d7429dea824e2642c7cbbe'
            'cb2e2e43e9f3f1ed7b10967a5e44d3883c3e0bb4385ec04355d1cd56a628d92d')

_package() {
  pkgdesc="Spanish ($2) hunspell dictionary"
  depends=(hunspell)
  conflicts=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es_$1.oxt

  install -dm755 "$pkgdir"/usr/share/hunspell
  install -Dm644 es_$1.dic es_$1.aff "$pkgdir/usr/share/hunspell"

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir/usr/share/myspell/dicts"
    for _file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $_file) .
    done
  popd

# Install webengine dictionaries
  install -d "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
    _filename=$(basename $_file)
    qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  done

  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hunspell-es_any() {
  pkgdesc="Spanish (generic) hunspell dictionary"
  depends=(hunspell)
  conflicts=(hunspell-es)
  replaces=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt

  install -Dm644 es_ANY.dic es_ANY.aff -t "$pkgdir"/usr/share/hunspell

  install -d "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
  
# Install webengine dictionaries
  install -d "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  done

  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hyphen-es() {
  pkgdesc="Spanish hyphenation rules"
  depends=(hyphen)

  install -Dm644 hyph_es_ANY.dic -t "$pkgdir"/usr/share/hyphen/

  pushd "$pkgdir"/usr/share/hyphen/
  for _co in "${_countries[@]}"; do
    ln -s hyph_es_ANY.dic hyph_es_${_co%% *}.dic
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
  
  install -Dm644 README_hyph_es_ANY.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_mythes-es() {
  pkgdesc="Spanish thesaurus"
  depends=(libmythes)

  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt
  install -Dm644 th_es_ES_v2.* -t "$pkgdir"/usr/share/mythes

  pushd "$pkgdir"/usr/share/mythes/
  for _co in "${_countries[@]}"; do
    ln -s th_es_ES_v2.idx "th_es_"${_co%% *}"_v2.idx" || true
    ln -s th_es_ES_v2.dat "th_es_"${_co%% *}"_v2.dat" || true
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
  for file in "$pkgdir"/usr/share/mythes/*; do
    ln -sv /usr/share/mythes/$(basename $file) .
  done
  popd
  
  install -Dm644 README_th_es_ES.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}
