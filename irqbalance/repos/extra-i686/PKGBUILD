# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

pkgname=irqbalance
pkgver=0.56
pkgrel=3
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('i686' 'x86_64')
url="http://www.irqbalance.org/"
license=('GPL')
depends=(glib2)
makedepends=(pkgconfig)
backup=(etc/conf.d/irqbalance)
source=(http://irqbalance.googlecode.com/files/irqbalance-$pkgver.tbz2
        irqbalance.conf.d
        irqbalance.rc.d
        current-trunk.patch
        Fix-detection-of-CPUs-in-sysfs.patch
        Special-interrupt-counts-line-NMI-may-start-with-a-s.patch)
md5sums=('cd0c4d3b2bb84778a04fc594ad83949a'
         '336c1ee99818f9ecda1687e34c69fd6b'
         'fb82fc5d267d39110baf720d81282a7c'
         '64df09f54cf80d9ce5ff2751a882032d'
         '00dcb394dac884c116657ade4fe623ee'
         'e774aff57054ce3c8b5adc7c3e5a74c2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 < ../current-trunk.patch
  patch -Np1 < ../Fix-detection-of-CPUs-in-sysfs.patch
  patch -Np1 < ../Special-interrupt-counts-line-NMI-may-start-with-a-s.patch
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  #install -D -m755 irqbalance $pkgdir/usr/sbin/irqbalance
  #install -D -m644 irqbalance.1 $pkgdir/usr/share/man/man1/irqbalance.1
  install -D -m644 ../irqbalance.conf.d "$pkgdir"/etc/conf.d/irqbalance
  install -D -m755 ../irqbalance.rc.d "$pkgdir"/etc/rc.d/irqbalance
}
