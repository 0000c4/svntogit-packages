# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dconf-editor
pkgver=3.28.0
pkgrel=1
pkgdesc="dconf Editor"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(GPL3)
groups=(gnome-extra)
depends=(gtk3 dconf)
makedepends=(vala meson appstream-glib git)
_commit=faa4bc96e52a0cfc27c40203a88bf34e1b96a2e5  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/dconf-editor#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
