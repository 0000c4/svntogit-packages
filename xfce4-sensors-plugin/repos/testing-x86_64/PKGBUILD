# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Merk Matthias <macem@chello.at>

pkgname=xfce4-sensors-plugin
pkgver=1.2.3
pkgrel=5
pkgdesc="A lm_sensors plugin for the Xfce panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-sensors-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'lm_sensors' 'libnotify' 'hddtemp' 'hicolor-icon-theme')
makedepends=('intltool' 'gnu-netcat')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/1.2/$pkgname-$pkgver.tar.bz2)
sha256sums=('3dd27348f7b38b55e53aa8d851cdb80e090b8830ca9e705bf32641679f08d326')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Satisfy the check that hddtemp is queryable via netcat
  # (https://bugs.archlinux.org/task/28275)
  echo ohai | nc -l -p 7634 -s localhost -c &

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --datadir=/usr/share \
    --datarootdir=/usr/share \
    --disable-static \
    --with-pathhddtemp=/usr/sbin/hddtemp \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
