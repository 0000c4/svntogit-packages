# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Merk Matthias <macem@chello.at>

pkgname=xfce4-sensors-plugin
pkgver=1.0.0
pkgrel=4
pkgdesc="A lm_sensors plugin for the Xfce panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-sensors-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4' 'lm_sensors' 'libnotify>=0.7.1' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'hddtemp>=0.3.beta15.45-2' 'gnu-netcat' 'xfce4-dev-tools')
optdepends=('gnu-netcat: for hddtemp access')
options=(!libtool)
install=${pkgname}.install
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/1.0/${pkgname}-${pkgver}.tar.bz2
	xfce4-sensors-plugin-1.0.0-underlink.patch
	xfce4-sensors-plugin-1.0.0-libnotify-0.7.patch)
md5sums=('74d83628246536d575f954c76724982b'
         'f0295e3a7c6beb267355f64fc0ed44d5'
         'b5e9013de01cd8f2b4a72c8f052d2c37')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # panel 4.8 patch from http://bugzilla.xfce.org/show_bug.cgi?id=7095
  patch -Np1 -i ${srcdir}/xfce4-sensors-plugin-1.0.0-underlink.patch

  patch -Np1 -i ${srcdir}/xfce4-sensors-plugin-1.0.0-libnotify-0.7.patch

  xdt-autogen
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--with-pathhddtemp=/usr/sbin/hddtemp \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
