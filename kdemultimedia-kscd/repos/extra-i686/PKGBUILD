# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-kscd
pkgver=17.08.2
pkgrel=1
pkgdesc="KDE CD player"
url='https://kde.org/applications/multimedia/kscd/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdemultimedia')
depends=('kdebase-runtime' 'libmusicbrainz3')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/$pkgver/src/kscd-$pkgver.tar.xz"{,.sig})
sha256sums=('1ed1017911ec2b3d7bcef0f789f404fa67889867b7c1563e74aef1072e6fe9dc'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../kscd-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
