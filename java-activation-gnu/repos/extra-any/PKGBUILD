# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=activation
pkgname=java-${_libname}-gnu
pkgver=1.1.1
pkgrel=3
pkgdesc="JavaBeans Activation Framework (JAF), framework for declaring what beans operate on what MIME type data"
arch=('any')
url="https://www.gnu.org/s/classpathx/jaf/jaf.html"
license=('LGPL')
depends=('java-runtime-headless')
makedepends=('apache-ant')
source=(https://ftp.gnu.org/gnu/classpathx/${_libname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('2875BCCCAE65860C6087CA8FE9D9750C4A8782B8') # Chris Burdess
sha512sums=('d323644df13a4572bf1c10e7175c1c4cfa3078483886bba7b308989c215a23e12939b054124dbaf0437a4c5fdcb551b4e2189c09afc078218963389e4b3c3b7d'
            'SKIP')

build() {
  cd "${srcdir}/${_libname}-${pkgver}"

  ant dist
}

package() {
  install -Dm644 ${srcdir}/${_libname}-${pkgver}/activation.jar \
                 ${pkgdir}/usr/share/java/${_libname}-${pkgver}.jar

  ln -s ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}.jar
}
