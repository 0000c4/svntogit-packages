# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rinutils
pkgver=0.1.4
pkgrel=1
pkgdesc="C11 / gnu11 utilities C library"
arch=(any)
url="https://www.shlomifish.org/open-source/projects/"
license=(MIT)
depends=()
makedepends=(cmake)
source=(https://github.com/shlomif/rinutils/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('8e201b3f736f3671d0da9e19e15dcce943bb7170bcb3c9b17474103520bec3fc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

