# $Id$ 
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-themes
pkgver=2.91.90
pkgrel=1
pkgdesc="Default themes for GNOME"
arch=(any)
license=('LGPL')
depends=('librsvg' 'gtk-engines' 'gtk3-engines')
makedepends=('intltool' 'icon-naming-utils')
url="http://www.gnome.org"
groups=('gnome')
replaces=('gtk-thinice-engine' 'clearlooks-gtk2')
options=(!makeflags)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('1e65f0d7eb0fa14320b532599ba20f5594654077a47ed423efc0e64863e62b31')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "$pkgdir/usr/share/themes"
  for _theme in *; do
    if [[ -f $_theme/gtk-3.0/gtkrc ]]; then
      mkdir $_theme/gtk-2.0
      ln -s ../gtk-3.0/gtkrc $_theme/gtk-2.0/gtkrc
    fi
  done
}
