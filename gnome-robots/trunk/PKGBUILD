# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-robots
pkgver=3.32.0
pkgrel=1
pkgdesc="Avoid the robots and make them crash into each other"
url="https://wiki.gnome.org/Apps/Robots"
arch=(x86_64)
license=(GPL)
depends=(gtk3 libcanberra librsvg libgnome-games-support)
makedepends=(meson gobject-introspection yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=5caaee84c26d02186489a65a77f717f4fccbf43c  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-robots.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
