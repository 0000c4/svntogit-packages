# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=valgrind
pkgver=3.3.1
pkgrel=2
pkgdesc="A tool to help find memory-management problems in programs"
arch=(i686 x86_64)
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.8' 'glibc<2.9')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0539e2fa4aadb2cd4ca4bba65b1fe8b5')
options=(!emptydirs)

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}

  # make sure our CFLAGS are respected
  sed -i -e 's:^CFLAGS="-Wno-long-long":CFLAGS="$CFLAGS -Wno-long-long":' configure.in

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit || return 1
  else
    ./configure --prefix=/usr --mandir=/usr/share/man || return 1
  fi
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
}
