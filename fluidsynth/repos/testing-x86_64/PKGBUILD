# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=1.1.6
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('i686' 'x86_64')
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'dbus-core' 'libpulse')
makedepends=('cmake' 'ladspa' 'doxygen')
optdepends=('pulseaudio: PulseAudio sound support')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'fluidsynth.conf'
        'fluidsynth.rc'
        'fluidsynth.systemd')
md5sums=('ae5aca6de824b4173667cbd3a310b263'
         '7d0ab0d75d4e87652f4cb7ffeac8a82b'
         'b296dbfb524c2164f552f68bd9abe2ec'
         'e0406748a154409907df62292be32e8a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -Denable-ladspa=ON \
          -DLIB_SUFFIX=""
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir/fluidsynth.conf" \
    "$pkgdir/etc/conf.d/fluidsynth"

  install -Dm755 "$srcdir/fluidsynth.rc" \
    "$pkgdir/etc/rc.d/fluidsynth"

  install -Dm644 "$srcdir/fluidsynth.systemd" \
    "$pkgdir/usr/lib/systemd/system/fluidsynth.service"
}

# vim:set ts=2 sw=2 et:
