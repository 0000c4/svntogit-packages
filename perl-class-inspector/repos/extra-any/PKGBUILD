# Maintainer:

pkgname=perl-class-inspector
pkgver=1.34
pkgrel=1
pkgdesc="Get information about a class and its structure"
arch=('any')
url="http://search.cpan.org/dist/Class-Inspector"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/P/PL/PLICEASE/Class-Inspector-${pkgver}.tar.gz")
sha512sums=('c9f3f3dbc10c45811d0dffca3f31b9579200cd3b5288bb878bf97f38c4c153bb665632e5238ecce55bd3c515200c8d9500d5b1e461a9ea47e5ad011783323a80')

build() {
  cd Class-Inspector-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Class-Inspector-${pkgver}
  make test
}

package() {
  cd Class-Inspector-${pkgver}
  make DESTDIR="$pkgdir" install
}
