# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxpython
pkgver=2.8.12.0
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python"
arch=('i686' 'x86_64')
license=('custom:wxWindows')
url="http://www.wxpython.org"
depends=('wxgtk' 'python2')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
md5sums=('402e0b81e06f596d849e221a7a76acc6')
sha1sums=('f9ce806dcb5517beb6e3d2ee0ecbf8e569b7f8e4')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --with-gnomeprint --disable-optimize --enable-mediactrl \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys
  cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
  python2 setup.py WXPORT=gtk2 UNICODE=1 build
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
  python2 setup.py WXPORT=gtk2 UNICODE=1 install --root="${pkgdir}"
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
