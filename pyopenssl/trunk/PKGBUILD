# $Id$
# Maintainer : Ionut Biru<ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=pyopenssl
pkgname=('pyopenssl' 'python2-pyopenssl')
pkgver=0.11
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/pyopenssl/"
license=('LGPL2.1')
makedepends=('python' 'python2' 'openssl')
source=(http://launchpad.net/${pkgname}/main/${pkgver}/+download/pyOpenSSL-${pkgver}.tar.gz)
md5sums=('9e76e57e00fa48745fcfe23944db1e6c')

build() {
    cd "${srcdir}"
    cp -a "pyOpenSSL-${pkgver}"{,-python2}

    #build python 3 module
    cd "pyOpenSSL-${pkgver}"
    python setup.py build

    #build python 3 module
    cd ../"pyOpenSSL-${pkgver}-python2"
    python2 setup.py build
}

package_pyopenssl(){
    pkgdesc="Python3 wrapper module around the OpenSSL library"
    depends=('python>=3.1' 'openssl')

    cd "${srcdir}/pyOpenSSL-${pkgver}"
    python setup.py install --root "${pkgdir}" --optimize=1
}

package_python2-pyopenssl(){
    pkgdesc="Python2 wrapper module around the OpenSSL library"
    depends=('python2>=2.7' 'openssl')

    cd "${srcdir}/pyOpenSSL-${pkgver}-python2"
    python2 setup.py install --root "${pkgdir}" --optimize=1
}
