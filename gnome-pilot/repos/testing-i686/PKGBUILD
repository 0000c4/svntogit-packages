# $Id$
# Jan de Groot <jgc@archlinux.org>

pkgname=gnome-pilot
pkgver=2.91.92
pkgrel=1
pkgdesc="Pilot Support for Gnome"
arch=(i686 x86_64)
license=('GPL')
depends=('pilot-link' 'evolution-data-server')
makedepends=('intltool' 'gob2' 'gnome-doc-utils' 'gnome-panel')
optdepends=('gnome-panel')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gnome-pilot.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('dc7a166c95580c2935b1cab822d9e6d63d5ef08bb19eacf4a84f6eda071f07c3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-pilot \
      --localstatedir=/var --disable-static \
      --without-hal
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
