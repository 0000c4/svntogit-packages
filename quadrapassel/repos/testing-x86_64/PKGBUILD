# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=quadrapassel
pkgver=3.22.0+86+g4d059ac
pkgrel=1
pkgdesc="Fit falling blocks together (Tetris-like game for GNOME)"
url="https://wiki.gnome.org/Apps/Quadrapassel"
arch=(x86_64)
license=(GPL)
depends=(clutter-gtk libcanberra librsvg libmanette)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib git vala)
groups=(gnome-extra)
_commit=4d059ac6e5edd7212cdf4f1b624d9bd14d1c1f2b  # master
source=("git+https://gitlab.gnome.org/GNOME/quadrapassel.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
