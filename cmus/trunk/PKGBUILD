# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=cmus
pkgver=2.4.1
pkgrel=1
pkgdesc="A very feature-rich ncurses-based music player"
arch=('i686' 'x86_64')
url="http://cmus.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('libao' 'libmpcdec' 'ffmpeg' 'flac' 'libmad' 'faad2' 'libmodplug' 
             'libvorbis' 'libpulse' 'wavpack' 'libmp4v2')
optdepends=('alsa-lib: for ALSA output plugin support'
            'libao: for AO output plugin support'
            'libpulse: for PulseAudio output plugin support'
            'ffmpeg: for ffmpeg input plugin support'
            'libmodplug: for modplug input plugin support'
            'faad2: for input AAC plugin support'
            'libmad: for mp3 input plugin support'
            'libmpcdec: for musepack input plugin support'
            'wavpack: for wavpack input plugin support'
            'libvorbis: for vorbis input plugin support'
            'flac: for flac input plugin support'
            'libmp4v2: for mp4 input plugin support')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-v$pkgver.tar.bz2")
md5sums=('7932bbada04bc9a273c332a323fc5704')
sha1sums=('918c637becdcbca3652b836b38b164eb852fa896')

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  sed -i 's|ffmpeg/avcodec.h|libavcodec/avcodec.h|' configure ffmpeg.c
  sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' ffmpeg.c
  sed -i 's|ffmpeg/avio.h|libavformat/avio.h|' ffmpeg.c
  ./configure prefix=/usr CONFIG_ARTS=n
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 644 contrib/_cmus "$pkgdir/usr/share/zsh/site-functions/_cmus"
}
