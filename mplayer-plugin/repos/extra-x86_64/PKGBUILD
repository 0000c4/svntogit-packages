# $Id$
# Mantainer: Alexander Baldeck <alexander@archlinux.org>
pkgname=mplayer-plugin
pkgver=3.50
pkgrel=4
pkgdesc="mplayerplug-in is a browser plugin that uses mplayer to play videos from websites."
arch=(i686 x86_64)
license=('GPL')
url="http://mplayerplug-in.sourceforge.net/"
depends=('gtk2>=2.12.10' 'mozilla-common>=1.1-2' 'mplayer' 'libxpm')
makedepends=('seamonkey>=1.1.9' 'pkgconfig')
backup=(etc/mplayerplug-in.conf)
install=mplayer-plugin.install
source=(http://downloads.sourceforge.net/mplayerplug-in/mplayerplug-in-${pkgver}.tar.gz
	mplayerplug-in_xulrunner-1.9.patch)
md5sums=('7a9306c39f6550090c16b9c1d87e3168'
         '83ec37f4009d862e259697e7fe395c7f')

build() {
  cd ${startdir}/src/mplayerplug-in
  patch -Np1 -i ${startdir}/src/mplayerplug-in_xulrunner-1.9.patch || return 1
  autoconf || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-faad --enable-gtk2 --enable-wmp \
      --enable-qt --enable-rm --enable-gmp || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
}
