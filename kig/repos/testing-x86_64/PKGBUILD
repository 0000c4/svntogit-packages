# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kig
pkgver=17.12.0
pkgrel=2
pkgdesc="Interactive Geometry"
url="https://kde.org/applications/education/kig/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(ktexteditor boost-libs python2 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost qt5-xmlpatterns)
optdepends=('qt5-xmlpatterns: support for GeoGebra files')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('196e3a4c2c41acb54f1ce86aaa82eb773aded763c74724346fe24da47b6cb0984816b9a44fdb1c761c990cb3560522ac701513f0d046e25544e3488f61a5c067'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  # Use the python2 executable
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i "$pkgdir"/usr/bin/pykig.py
}
