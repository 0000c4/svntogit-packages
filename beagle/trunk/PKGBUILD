# $Id$
# Maintainer: Daniel Isenmann <daniel @ archlinux.org>
# Contributor: Shadowhand <woody.gilk@gmail.com>
pkgname=beagle
pkgver=0.3.8
pkgrel=2
pkgdesc="Search tool that ransacks your personal information space"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/beagle/"
license=('custom')
depends=('gnome-sharp>=2.24.0' 'gmime>=2.2.18' 'evolution-sharp>=0.18.1' 'libexif' 'librsvg>=2.22.2' 'sqlite3' 'libxss' 'wv>=1.2.4' 'chmlib' 'gsf-sharp>=0.8.1' 'libsm' 'xdg-utils' 'ndesk-dbus-glib' 'mono>=2.0' 'taglib-sharp')
makedepends=('perlxml' 'zip' 'pkgconfig')
options=('!libtool')
backup=('etc/beagle/crawl-applications' 'etc/beagle/crawl-documentation'
        'etc/beagle/crawl-windows')
install=beagle.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.3/${pkgname}-${pkgver}.tar.bz2
	beagle-0.2.1-spew.patch)
md5sums=('fcafad26efa65a3bf06a019268ae6230'
         '74feeb60c70999db8895b3a9d260869a')

build() {
  export MONO_SHARED_DIR=${startdir}/src/.wabi
  mkdir -p ${MONO_SHARED_DIR}

  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/CRAWL_USER=beagleindex/CRAWL_USER=beaglidx/' tools/beagle-crawl-system.in || return 1
  sed -i -e 's/libchm.so.1/libchm.so.0/' Util/ChmFile.cs || return 1
  patch -Np1 -i "${srcdir}/beagle-0.2.1-spew.patch" || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-inotify \
	--disable-libbeagle \
	--disable-static \
	--disable-docs || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/var/cache/beagle/indexes" || return 1
  chown -R 86:86 "${pkgdir}/var/cache/beagle" || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/beagle"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/beagle/" || return 1
}
