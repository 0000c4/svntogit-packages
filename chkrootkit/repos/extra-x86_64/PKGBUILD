# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=chkrootkit
pkgver=0.49
pkgrel=2
pkgdesc="Locally checks for signs of a rootkit"
arch=('i686' 'x86_64')
url="http://www.chkrootkit.org"
depends=('sh' 'net-tools')
license=('BSD')
source=(ftp://ftp.pangeia.com.br/pub/seg/pac/${pkgname}.tar.gz fix-tools-path.patch)
md5sums=('304d840d52840689e0ab0af56d6d3a18'
         '6a2f3038114b8b14e1ad74e30fe44eee')
sha1sums=('cec1a3c482b95b20d3a946b07fffb23290abc4a6'
          'f192cda177ec1920ce3313ed983ac44ee571ca6c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/fix-tools-path.patch"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  for i in check_wtmpx chkdirs chklastlog chkproc chkrootkit chkrootkit.orig \
           chkutmp chkwtmp ifpromisc strings-static ; do
    install -D -m755 $i "${pkgdir}/usr/bin/$i"
  done
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
