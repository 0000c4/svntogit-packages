# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=itext
pkgver=5.2.1
pkgrel=1
pkgdesc="Java Library for creating and manipulating PDFs on the fly"
arch=('any')
url="http://itext.sourceforge.net"
license=('AGPL' 'custom')
depends=('java-runtime')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.zip
        LICENSE)
md5sums=('7296b8b240ee288ede7126055bb8f1d6'
         '3ac26f502df3162d6b612286827e0c35' )

package() {
  cd ${srcdir}

  install -d "${pkgdir}/usr/share/java/${pkgname}"

  for j in itextpdf itext-xtra; do
    install -m644 ${j}-${pkgver}.jar \
                  ${pkgdir}/usr/share/java/${pkgname}/${j}-${pkgver}.jar
    install -m644 ${j}-${pkgver}-sources.jar \
                  ${pkgdir}/usr/share/java/${pkgname}/${j}-sources-${pkgver}.jar
    install -m644 ${j}-${pkgver}-javadoc.jar \
                  ${pkgdir}/usr/share/java/${pkgname}/${j}-javadoc-${pkgver}.jar

    ln -s ${pkgname}/${j}-${pkgver}.jar ${pkgdir}/usr/share/java/${j}.jar
  done

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
