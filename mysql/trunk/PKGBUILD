# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=mysql
pkgname=('libmysqlclient' 'mysql-clients' 'mysql')
pkgver=5.5.8
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mysql.com/"
makedepends=('cmake' 'openssl' 'tcp_wrappers')
options=('!libtool')
source=("http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.5/${pkgbase}-${pkgver}.tar.gz"
        'mysqld'
        'my.cnf')
md5sums=('42e866302b61f5e213afd33e04677017'
         '2234207625baa29b2ff7d7b4f088abce'
         '69336edb8a16294b18eaec22fee7ea68')

build() {
  cd "${srcdir}"
  mkdir build
  cd build

  # CFLAGS/CXXFLAGS as suggested upstream
  CFLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
  CXXFLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \

  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DINSTALL_INFODIR=/usr/share/mysql/docs \
    -DINSTALL_MANDIR=/usr/share/man \
    -DINSTALL_SCRIPTDIR=/usr/bin \
    -DINSTALL_DOCREADMEDIR=/usr/share/mysql \
    -DINSTALL_SUPPORTFILESDIR=/usr/share/mysql \
    -DWITH_READLINE=OFF \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=ON \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DMYSQL_UNIX_ADDR=/var/run/mysqld/mysqld.sock \
    -DENABLED_LOCAL_INFILE=ON \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITH_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_FEDERATED_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1

  make
}

package_libmysqlclient(){
  pkgdesc="MySQL client libraries"
  depends=('openssl' 'gcc-libs' 'tcp_wrappers')
  
  cd "${srcdir}"/build
  for dir in include libmysql libmysqld libservices; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -d "${pkgdir}/usr/bin"
  install -m755 scripts/mysql_config "${pkgdir}/usr/bin/"
}

package_mysql-clients(){
  pkgdesc="MySQL client tools"
  depends=('libmysqlclient')
  
  cd "${srcdir}"/build/client
  make DESTDIR="${pkgdir}" install

  # provided by libmysqlclient
  rm -rf "${pkgdir}/usr/lib/"

  # provided by mysql
  rm -f "${pkgdir}"/usr/bin/{mysql_upgrade,mysqlbinlog,mysqltest}
}

package_mysql(){
  pkgdesc="A fast SQL database server"
  backup=('etc/mysql/my.cnf')
  install=mysql.install
  depends=('mysql-clients')
  optdepends=('perl-dbi' 'perl-dbd-mysql')

  cd "${srcdir}"/build
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/my.cnf ${pkgdir}/etc/mysql/my.cnf
  install -Dm755 ${srcdir}/mysqld ${pkgdir}/etc/rc.d/mysqld

  # provided by libmysqlclient
  rm -f ${pkgdir}/usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm -f ${pkgdir}/usr/lib/libmysqlclient*
  rm -f ${pkgdir}/usr/lib/libmysqld.a
  rm -f ${pkgdir}/usr/lib/libmysqlservices.a
  rm -rf ${pkgdir}/usr/include/
  
  # provided by mysql-clients
  rm -f ${pkgdir}/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}

  # not needed
  rm -rf ${pkgdir}/usr/{mysql-test,sql-bench}
  rm -f ${pkgdir}/usr/lib/mysql/plugin/*.a

  # These shouldn't be here
  rm -rf ${pkgdir}/usr/docs
  install -d ${pkgdir}/var/lib/mysql
  mv ${pkgdir}/usr/data/* ${pkgdir}/var/lib/mysql/
  chmod -R 700 ${pkgdir}/var/lib/mysql/
}
