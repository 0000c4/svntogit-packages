# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=mysql
pkgname=('libmysqlclient' 'mysql-clients' 'mysql')
pkgver=5.1.40
pkgrel=1
pkgdesc="A fast SQL database server"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.mysql.com/')
makedepends=('tcp_wrappers' 'zlib' 'perl' 'openssl' 'libtool' 'patch')
options=('!libtool')
source=("http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.1/mysql-$pkgver.tar.gz"
        'mysqld'
        'my.cnf'
        'mysqld.conf.d')
md5sums=('32e7373c16271606007374396e6742ad'
         'c093cc7eef5934f56f8bc775b559904b'
         '0ee035590ffc61d32de994f461fd2bd2'
         '4a9077fc95ec6db1d5420e0cdc74d31c')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --prefix=/usr \
    --libexecdir=/usr/sbin \
    --localstatedir=/var \
    --sysconfdir=/etc/mysql \
    --without-docs \
    --without-readline \
    --with-ssl \
    --with-libwrap \
    --with-charset=latin1 \
    --with-collation=latin1_general_ci \
    --with-extra-charsets=complex \
    --with-embedded-server \
    --enable-local-infile \
    --with-plugins=partition,ftexample,archive,blackhole,federated,heap,innobase,ndbcluster
}

package_libmysqlclient(){
  pkgdesc="MySQL client libraries"
  depends=('openssl' 'zlib' 'gcc-libs' 'tcp_wrappers')
  
  cd ${srcdir}/${pkgbase}-${pkgver}
  for dir in include strings mysys dbug storage regex extra vio sql; do
    pushd ${dir} || return 1
    make || return 1
    popd
  done
  for dir in libmysql libmysql_r libmysqld; do
    pushd ${dir} || return 1
    make link_sources
    make || return 1
    make DESTDIR=${pkgdir} install
    popd
  done
  cd include
  make DESTDIR=${pkgdir} install

  # Copy missing includes
  install -m644 *.h ${pkgdir}/usr/include/mysql

  cd ../scripts
  make mysql_config
  install -d ${pkgdir}/usr/bin
  install -m755 mysql_config ${pkgdir}/usr/bin || return 1
  
  # create library symlinks in /usr/lib
  ln -sf mysql/libmysqlclient.so.16 $pkgdir/usr/lib/libmysqlclient.so.16
  ln -sf libmysqlclient.so.16 $pkgdir/usr/lib/libmysqlclient.so
  ln -sf libmysqlclient.so.16 $pkgdir/usr/lib/libmysqlclient.so.1
  ln -sf mysql/libmysqlclient_r.so.16  $pkgdir/usr/lib/libmysqlclient_r.so.16
  ln -sf libmysqlclient_r.so.16 $pkgdir/usr/lib/libmysqlclient_r.so
  ln -sf libmysqlclient_r.so.16 $pkgdir/usr/lib/libmysqlclient_r.so.1
}

package_mysql-clients(){
  pkgdesc="MySQL client tools"
  depends=("libmysqlclient>=${pkgver}")
  
  cd ${srcdir}/${pkgbase}-${pkgver}
  pushd include || return
    make || return 1
  popd

  pushd libmysql
    make link_sources get_password.lo || return
  popd 
  
  for dir in strings regex mysys dbug extra; do
    pushd ${dir} || return 1
    make || return 1
    popd
  done

  cd client

  sed -i -e 's|\$(top_builddir)/libmysql/libmysqlclient.la|/usr/lib/mysql/libmysqlclient.so|g' Makefile
  make link_sources
  make || return 1
  make DESTDIR=${pkgdir} install

  # Removing libmysql stuff
  rm -rf $pkgdir/usr/lib/
}

package_mysql(){
  backup=('etc/my.cnf' 'etc/mysql/my.cnf' 'etc/conf.d/mysqld')
  depends=('mysql-clients')
  optdepends=('perl-dbi' 'perl-dbd-mysql')
  install=mysql.install

  cd ${srcdir}/${pkgbase}-${pkgver}
  pushd include || return
    make || return 1
  popd
    
  pushd libmysql
    make link_sources get_password.lo || return
  popd
  
  make || return 1
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/{mysql-test,sql-bench,lib,include}
  install -D -m644 ../my.cnf ${pkgdir}/etc/mysql/my.cnf
  install -D -m755 ../mysqld ${pkgdir}/etc/rc.d/mysqld
  install -D -m644 ../mysqld.conf.d ${pkgdir}/etc/conf.d/mysqld

  # Cleanup files provided by the other packages
  rm -f ${pkgdir}/usr/bin/{mysql,mysql_config,mysql_client_test_embedded,mysql_upgrade,mysqladmin,mysqlbinlog,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap,mysqltest,mysqltest_embedded}
  rm -rf ${pkgdir}/usr/{include,lib}

  # create directory for PID file
  install -d ${pkgdir}/var/run/mysqld
}

