# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=libksysguard
pkgver=5.5.5
pkgrel=1
pkgdesc='Libksysguard'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/libksysguard'
license=('LGPL')
depends=('libxres' 'plasma-framework' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdebase-workspace')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"
	libksysguard-glibc2.23.patch::"https://quickgit.kde.org/?p=libksysguard.git&a=commitdiff&h=b0578798&o=plain")
md5sums=('ff6c11140a83928c7b240ced2f06b3e8'
         '2d55eabfdfe6b2e3e9e77ae5f9c8cdd3')

prepare() {
  mkdir -p build

# Fix build with glibc 2.23
  cd $pkgname-$pkgver
  patch -p1 -i ../libksysguard-glibc2.23.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
