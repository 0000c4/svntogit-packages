# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-session
pkgver=2.91.91.3
pkgrel=3
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('upower' 'gtk3' 'gconf' 'startup-notification' 'hicolor-icon-theme'
         'libxtst' 'polkit-gnome' 'libgl' 'librsvg' 'gsettings-desktop-schemas'
         'consolekit' 'libsm' 'dconf')
makedepends=('intltool' 'mesa' 'xtrans')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f9f67211ac2606c0fa455aa2633a4ec81128e6c17bc24d232637f498978ee989')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-session \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
