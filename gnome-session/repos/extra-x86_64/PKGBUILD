# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-session
pkgver=2.26.1
pkgrel=1
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('policykit-gnome>=0.9.2' 'gnome-settings-daemon>=2.26.1' 'at-spi>=1.26.0' 'startup-notification>=0.10' 'dbus>=1.2.4.4permissive' 'libglade>=2.6.4' 'libxtst')
makedepends=('pkgconfig' 'intltool' 'xtrans')
options=(!emptydirs)
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
md5sums=('aa280939a3e07209081095e8e10fe619')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	      --with-at-spi-registryd-directory=/usr/lib/at-spi || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-session-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
