# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Christopher "nuopus" Cox <nuopus@gmail.com>

pkgname=laptop-mode-tools
pkgver=1.47
pkgrel=1
pkgdesc="Laptop mode is a kernel mode that allows you to extend the battery life of your laptop"
arch=('i686' 'x86_64')
url="http://www.samwel.tk/laptop_mode/"
license=('GPL')
depends=('bash' 'hdparm')
backup=(etc/laptop-mode/conf.d/ac97-powersave.conf
        etc/laptop-mode/conf.d/auto-hibernate.conf
        etc/laptop-mode/conf.d/battery-level-polling.conf
	etc/laptop-mode/conf.d/bluetooth.conf
        etc/laptop-mode/conf.d/configuration-file-control.conf
        etc/laptop-mode/conf.d/cpufreq.conf
        etc/laptop-mode/conf.d/dpms-standby.conf
        etc/laptop-mode/conf.d/ethernet.conf
	etc/laptop-mode/conf.d/hal-polling.conf
        etc/laptop-mode/conf.d/intel-hda-powersave.conf
	etc/laptop-mode/conf.d/intel-sata-powermgmt.conf
        etc/laptop-mode/conf.d/lcd-brightness.conf
	etc/laptop-mode/conf.d/sched-mc-power-savings.conf
        etc/laptop-mode/conf.d/start-stop-programs.conf
        etc/laptop-mode/conf.d/terminal-blanking.conf
        etc/laptop-mode/conf.d/usb-autosuspend.conf
	etc/laptop-mode/conf.d/video-out.conf
        etc/laptop-mode/conf.d/wireless-ipw-power.conf
        etc/laptop-mode/conf.d/wireless-iwl-power.conf
        etc/laptop-mode/laptop-mode.conf
        etc/laptop-mode/lm-profiler.conf)
source=(http://www.samwel.tk/laptop_mode/tools/downloads/${pkgname}_${pkgver}.tar.gz laptop-mode)
md5sums=('ec32f80d9bb427cb02a54d8c0a08f5e6' '241a0e07fc3b340b3381bcde8a74b71d')
sha1sums=('e38745b17b7259289190b0630c15c5e9e77ff891' 'e69239683c320ecc4c27060eb2ace768d06696bc')

#laptop-mode-tools has one of the worst install scripts ever... manual here
build() {
  cd ${srcdir}/${pkgname}_${pkgver}

#state dir
  install -d ${pkgdir}/var/run/laptop-mode-tools/

#binaries
  install -d ${pkgdir}/usr/sbin/
  install -m755 usr/sbin/* ${pkgdir}/usr/sbin/ || return 1

#config files
  install -d ${pkgdir}/etc/laptop-mode/conf.d
  install -m644 etc/laptop-mode/laptop-mode.conf ${pkgdir}/etc/laptop-mode/ || return 1
  install -m644 etc/laptop-mode/lm-profiler.conf ${pkgdir}/etc/laptop-mode/ || return 1
  install -m644 etc/laptop-mode/conf.d/* ${pkgdir}/etc/laptop-mode/conf.d/ || return 1

#modules
  install -d ${pkgdir}/usr/share/laptop-mode-tools/modules
  install -d ${pkgdir}/usr/share/laptop-mode-tools/module-helpers
  install -m755 usr/share/laptop-mode-tools/modules/* ${pkgdir}/usr/share/laptop-mode-tools/modules/ || return 1
  install -m755 usr/share/laptop-mode-tools/module-helpers/* ${pkgdir}/usr/share/laptop-mode-tools/module-helpers/ || return 1

#man pages
  install -d ${pkgdir}/usr/share/man/man8/
  install -m644 man/* ${pkgdir}/usr/share/man/man8/ || return 1

#acpi support
  install -d ${pkgdir}/etc/acpi/{actions,events}
  install -m755 etc/acpi/actions/* ${pkgdir}/etc/acpi/actions || return 1
  install -m644 etc/acpi/events/* ${pkgdir}/etc/acpi/events || return 1

#apm support - TODO still needed?
  install -d ${pkgdir}/etc/apm/event.d/
  install -m755 etc/apm/event.d/* ${pkgdir}/etc/apm/event.d || return 1

#pm-utils support
  install -d ${pkgdir}/etc/power/{event.d,scripts.d}
  install -d ${pkgdir}/etc/pm/sleep.d
  install -m755 etc/pm/sleep.d/* ${pkgdir}/etc/pm/sleep.d || return 1
  install -m755 etc/power/scripts.d/* ${pkgdir}/etc/power/scripts.d || return 1
  ln -fs /etc/power/scripts.d/laptop-mode ${pkgdir}/etc/power/event.d || return 1

#arch init script
  install -D -m755 ${srcdir}/laptop-mode ${pkgdir}/etc/rc.d/laptop-mode || return 1
}
