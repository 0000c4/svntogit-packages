# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kmldonkey
pkgver=2.0.2
_kdever=4.2.2
pkgrel=4
pkgdesc="An MLDonkey frontend for KDE"
arch=('i686' 'x86_64')
url='http://userbase.kde.org/KMLDonkey'
license=('GPL')
depends=('kdebase-workspace')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${_kdever}/extragear/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e9feb9f534176a2d90aaa4c119f8d65e')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
