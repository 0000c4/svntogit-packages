# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Anders Bostrom <anders.bostrom@home.se>
pkgname=thunderbird
pkgver=2.0.0.16
pkgrel=1
pkgdesc="Standalone Mail/News reader"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
provides=('mozilla-thunderbird')
conflicts=('mozilla-thunderbird')
replaces=('mozilla-thunderbird')
depends=('gtk2>=2.12.11' 'gcc-libs>=4.3.1' 'libidl2' 'mozilla-common' 'nss>=3.12' 'libxt' 'shared-mime-info' 'mime-types')
makedepends=('zip' 'pkgconfig' 'imagemagick' 'diffutils' 'patch' 'make' 'gcc' 'libgnomeui')
options=('!makeflags')
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}-source.tar.bz2
	mozconfig
        thunderbird.desktop
        thunderbird-1.5-lang.patch
	firefox-2.0-link-layout.patch)
md5sums=('591c9f80a2f6772c5acf8fb6128f40bd'
         '17e3015259da53f40bc445b27078f693'
         'ea4e3c3dee98e3891bef16409551eb6e'
         'bc6f10a06407faee6494acad546aabf9'
         'b933c00957ea793fe940f4d46a85e10e')

build() {
  cd ${startdir}/src/mozilla
  patch -Np1 -i ${startdir}/src/thunderbird-1.5-lang.patch || return 1
  patch -Np1 -i ${startdir}/src/firefox-2.0-link-layout.patch || return 1
  cp ${startdir}/src/mozconfig .mozconfig

  if [ "${CARCH}" = "x86_64" ]; then
    echo "ac_cv_visibility_pragma=no" >> .mozconfig
  fi

  export MOZ_PROJECT=mail
  unset CXXFLAGS
  unset CFLAGS

  make -f client.mk build || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  cd ${startdir}/pkg/usr/lib/thunderbird-${pkgver}
  export MOZ_DISABLE_GNOME=1
  export MOZTMP=`mktemp -d -p ${startdir}/src`
  LD_PRELOAD="" LD_LIBRARY_PATH="`pwd`" HOME="${MOZTMP}" ./thunderbird-bin -register || return 1
  rm -rf "${MOZTMP}"

  ln -sf thunderbird-${pkgver} ${pkgdir}/usr/lib/thunderbird || return 1
  ln -sf thunderbird-${pkgver} ${pkgdir}/usr/include/thunderbird || return 1
  ln -sf thunderbird-${pkgver} ${pkgdir}/usr/share/idl/thunderbird || return 1
  
  rm -rf ${startdir}/pkg/usr/bin/defaults

  install -m755 -d ${startdir}/pkg/usr/share/applications
  install -m755 -d ${startdir}/pkg/usr/share/pixmaps
  convert ${startdir}/src/mozilla/mail/app/default.xpm \
      ${startdir}/pkg/usr/share/pixmaps/thunderbird.png || return 1
  install -m644 ${startdir}/src/thunderbird.desktop \
      ${startdir}/pkg/usr/share/applications/ || return 1

  install -m644 ${startdir}/src/mozilla/mail/app/default.xpm \
      ${startdir}/pkg/usr/lib/thunderbird/icons/ || return 1

  rm -f ${pkgdir}/usr/lib/pkgconfig/thunderbird-ns{s,pr}.pc
  sed -e "s/thunderbird-${pkgver}/thunderbird/g" \
      -i ${pkgdir}/usr/lib/pkgconfig/*.pc || return 1
}
