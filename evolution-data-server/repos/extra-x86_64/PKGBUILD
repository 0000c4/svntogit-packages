# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.8.5
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=(i686 x86_64)
depends=(gnome-online-accounts nss krb5 libgweather libical db libgdata)
makedepends=(intltool gperf gobject-introspection vala python2 gnome-common)
options=('!libtool')
install=$pkgname.install
url="http://www.gnome.org"
license=(GPL)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
fix-google-2fa-1.patch
fix-google-2fa-2.patch
fix-google-2fa-3.patch
fix-google-2fa-4.patch)
sha256sums=('73c4ecfed65651cc72966b90081d7b72f9b42573585bc89797efea65ccfdbd2a'
            'c1de9fe146f0512cf6c0219341c8409a9e4d955c3eb6a85f7d07183972b9328e'
            '3e1c83dd00f01dfa0f9d67d2327de110b3e232a5f9a882ccce09a02392df7de6'
            '9da8cccf504f40b8ee9e068b465cdf0a1a858999dd88d8e8e96f1e4cdffbf1e2'
            '27022d00575ba1b9283d9420aa231c2770ff403b0b6c0442b6d679faefdbe00e')


prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../fix-google-2fa-1.patch
  patch -Np1 -i ../fix-google-2fa-2.patch
  patch -Np1 -i ../fix-google-2fa-3.patch
  patch -Np1 -i ../fix-google-2fa-4.patch
}

build() {
  cd "$pkgname-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-openldap=yes \
      --libexecdir=/usr/lib/evolution-data-server \
      --with-krb5=/usr --with-libdb=/usr \
      --enable-vala-bindings --disable-uoa PYTHON=python2
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
