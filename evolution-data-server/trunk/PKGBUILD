# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.28.1
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
depends=(gnome-online-accounts nss krb5 libgweather libical db libgdata libphonenumber)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost git cmake ninja)
license=(GPL)
_commit=a4f56fac872f1752305762b713040640e438e6fd  # tags/3.28.1^0
source=("git+https://git.gnome.org/browse/evolution-data-server#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_DATA_SERVER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  cd $pkgname

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CXXFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DENABLE_INTROSPECTION=ON \
    -DENABLE_VALA_BINDINGS=ON \
    -DENABLE_UOA=OFF \
    -DENABLE_GTK_DOC=ON \
    -DWITH_PHONENUMBER=ON
  ninja
}

check() {
  cd $pkgname
  # libedata-book tests fail because they try to
  # mkdir /usr/lib/evolution-data-server/camel-providers
  ninja test || :
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
