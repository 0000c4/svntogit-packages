# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=2.24.2
pkgrel=2
pkgdesc="Evolution Data Server provides a central location for addressbook and calendar in the GNOME Desktop"
arch=(i686 x86_64)
depends=('libsoup>=2.24.2.1' 'nss>=3.12.1' 'libgnome>=2.24.1' 'libglade>=2.6.3' 'gnome-keyring>=2.24.1' 'db4.1' 'heimdal>=1.2')
makedepends=('intltool' 'pkgconfig')
options=('!libtool')
url="http://www.gnome.org"
license=('GPL')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ee08e677d30fadd58e53e6e064d4ae6e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Look for correct DB, we want 4.1
  sed -e 's|DB_CFLAGS="-I$withval/include"|DB_CFLAGS="-I$withval/include/db4.1"|' \
      -e 's|DB_LIBS="-L$withval/lib -ldb"|DB_LIBS="-L$withval/lib -ldb-4.1"|' \
      -i configure || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var  --with-openldap=yes \
    --libexecdir=/usr/lib/evolution-data-server-1.2 \
    --with-krb5=/usr --enable-gnome-keyring \
    --with-libdb=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
