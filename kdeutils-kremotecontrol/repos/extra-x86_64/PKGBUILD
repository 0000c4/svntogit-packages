# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kremotecontrol
pkgver=17.08.3
pkgrel=1
pkgdesc='Configure your remote controls for use with applications'
url='https://kde.org/applications/utilities/kremotecontrol/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
source=("https://download.kde.org/stable/applications/$pkgver/src/kremotecontrol-$pkgver.tar.xz"{,.sig})
sha256sums=('e08c34dbac44794bd851f5336991cf932c7aff4827f2bb977268dfbbd7c143dc'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kremotecontrol-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
