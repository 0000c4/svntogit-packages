# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-ksecrets
pkgver=4.7.95
pkgrel=1
pkgdesc='Secrets Management Infrastructure for KDE'
url='http://techbase.kde.org/Projects/Utils/ksecretsservice'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/ksecrets-${pkgver}.tar.bz2")
sha1sums=('5c9936a62b0592efcdd9966da84572f8ba7bb71f')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ksecrets-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
