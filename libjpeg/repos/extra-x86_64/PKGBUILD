# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=libjpeg
pkgver=6b
pkgrel=6
pkgdesc="Library of JPEG support functions"
arch=('i686' 'x86_64')
url="http://www.ijg.org/"
license=('custom')
depends=('glibc')
makedepends=('libtool')
options=(!libtool)
source=(ftp://ftp.uu.net/graphics/jpeg/jpegsrc.v$pkgver.tar.gz)
md5sums=('dbd5f3b47ed13132f04c685d608a7547')

build() {
  cd $srcdir/jpeg-$pkgver
  cp /usr/share/libtool/config/config.{guess,sub} .
  sed -i "s#./libtool#libtool#" configure
  ./configure --prefix=/usr --enable-shared --enable-static
  make || return 1
  mkdir -p $pkgdir/usr/{bin,lib,include,share/man/man1}
  make prefix=$pkgdir/usr mandir=$pkgdir/usr/share/man/man1 install
  install -m644 jpegint.h $pkgdir/usr/include
  install -Dm644 README $pkgdir/usr/share/licenses/libjpeg/README
}
