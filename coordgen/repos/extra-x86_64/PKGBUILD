# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coordgen
pkgver=1.4.0
pkgrel=2
pkgdesc="Schrodinger-developed 2D Coordinate Generation"
arch=(x86_64)
url="https://github.com/schrodinger/coordgenlibs"
license=(BSD)
depends=(maeparser)
makedepends=(cmake boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/schrodinger/coordgenlibs/archive/v$pkgver.tar.gz"
        coordgen-fix-build.patch)
sha256sums=('2c739be72d04958c158e16c8402ad0dd2171859da6486096ba1b691c18d286e7'
            'b7d23ba17d4dbe6ebd1ac3f6c113d9a7519492cde0ab7534e477ac3fbc54860b')

prepare() {
  mkdir -p build

  cd coordgenlibs-$pkgver
  patch -p1 -i ../coordgen-fix-build.patch
}

build() {
  cd build
  cmake ../coordgenlibs-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOORDGEN_USE_MAEPARSER=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/coordgenlibs-$pkgver/templates.mae -t "$pkgdir"/usr/share/coordgen
  install -Dm644 "$srcdir"/coordgenlibs-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
