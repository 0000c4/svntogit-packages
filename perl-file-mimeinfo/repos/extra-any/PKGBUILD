# $Id$
# Maintainer: kevin <kevin@archlinux.org>

pkgname=perl-file-mimeinfo
pkgver=0.17
pkgrel=1
pkgdesc='Perl/CPAN File::MimeInfo module - Determine file type, includes mimeopen and mimetype'
arch=('any')
url="http://search.cpan.org/dist/File-MimeInfo/"
license=('GPL' 'PerlArtistic')
depends=('perl-file-basedir' 'perl-file-desktopentry' 'shared-mime-info')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MICHIELB/File-MimeInfo-${pkgver}.tar.gz)
md5sums=('979eb9516d300b2a2d14fffbe46a82d6')

build() {
  cd File-MimeInfo-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-MimeInfo-${pkgver}
  make test
}

package() {
  cd File-MimeInfo-${pkgver}
  make DESTDIR="${pkgdir}" install
}
