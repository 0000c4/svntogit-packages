#  $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-font-viewer
pkgver=3.28.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://git.gnome.org/browse/gnome-font-viewer"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gnome-desktop)
makedepends=(git meson)
groups=(gnome)
_commit=ff2a81f3c71f1f6a970b079e9df8fefed88d96cd  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/gnome-font-viewer#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
