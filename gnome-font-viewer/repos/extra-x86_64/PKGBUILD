#  $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-font-viewer
pkgver=3.23.91+2+g419afa1
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://git.gnome.org/browse/gnome-font-viewer"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 gnome-desktop)
makedepends=(intltool gnome-common git)
groups=(gnome)
_commit=419afa1a577a1717f40eff7394c791e382180bfe  # master
source=("git+https://git.gnome.org/browse/gnome-font-viewer#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
