# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klines
pkgver=16.12.2
pkgrel=1
pkgdesc="A simple but highly addictive, one player game"
url="http://kde.org/applications/games/klines/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-klines')
replaces=('kdegames-klines')
source=("http://download.kde.org/stable/applications/${pkgver}/src/klines-${pkgver}.tar.xz"{,.sig})
sha1sums=('81a122b3ffd7a5cbe41dd03e15e9e0942d2dd0cd'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
