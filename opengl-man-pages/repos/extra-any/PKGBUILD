# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=opengl-man-pages
_svndate=20110206
pkgver=0.${_svndate}
pkgrel=1
pkgdesc="OpenGL Man Pages"
arch=('any')
url="http://www.opengl.org/wiki/Getting_started/XML_Toolchain_and_Man_Pages"
license=('custom')
makedepends=('libxslt' 'docbook-xsl' 'w3c-mathml2' 'docbook-mathml')
options=('!makeflags')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}_${_svndate}.tar.xz LICENSE)
md5sums=('8c10a5f1054d453dbc1e01a5105c169e' 'cb856abe5968300057e650096ba25b2f')
sha1sums=('280d085b4dbdcc1946b541ef51a556fe3e12c1a8' 'b2cb9b184d85700f597dc3a5fa83572b6887bb46')

# source PKGBUILD && mksource
mksource() {
  mkdir ${pkgname}-source
  pushd ${pkgname}-source
  svn co --username anonymous --password anonymous https://cvs.khronos.org/svn/repos/ogl/trunk/ecosystem/public/sdk/docs/man/ man 
  find man -depth -type d -name .svn -exec rm -rf {} \;
  tar -cvJf ../${pkgname}_${_svndate}.tar.xz man/*
  popd
}

build() {
  cd "${srcdir}/man"
  export ROOT="${srcdir}/man"
  make
  for file in *.xml; do
    xsltproc --noout --nonet /usr/share/xml/docbook/xsl-stylesheets-1.76.1/manpages/docbook.xsl ${file}
  done
}

package() {
  cd "${srcdir}/man"
  install -d "${pkgdir}/usr/share/man/man3"
  install -m644 *.3G "${pkgdir}/usr/share/man/man3/"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
