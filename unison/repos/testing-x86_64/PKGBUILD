# $Id$
#Maintainer: Gaetan Bisson <bisson@archlinux.org>
#Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=unison
_pkgver=2.48.15v4
pkgver=${_pkgver%v*}
pkgrel=1
pkgdesc='File-synchronization tool'
#url='https://github.com/bcpierce00/unison/'
url='https://www.cis.upenn.edu/~bcpierce/unison/'
arch=('x86_64')
license=('GPL2')
makedepends=('ocaml' 'emacs')
source=("https://github.com/bcpierce00/unison/archive/v${_pkgver}.tar.gz"
        'large.patch'
        'desktop')
sha256sums=('f8c7e982634bbe1ed6510fe5b36b6c5c55c06caefddafdd9edc08812305fdeec'
            '2a971cf17e0a7303e99a793829e2b7b03533d17041e3b2ebba53555a5bd62284'
            'e9ef4e8b845453e21abafa8c9d153080375024d2465e3bf65f0e5a2ca1f5ea4a')

options=('!makeflags')

prepare() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	patch -p0 -i ../large.patch
}

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	export CFLAGS=
	make UISTYLE=text DEBUGGING=false THREADS=true
	mv src/unison src/unison-text
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	install -d "${pkgdir}"/usr/bin
	install -m755 src/unison-* "${pkgdir}"/usr/bin
	ln -s unison-text "${pkgdir}"/usr/bin/unison

	install -d "${pkgdir}"/usr/share/{pixmaps,applications}
	install -m644 icons/U.32x32x16m.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	install -m644 ../desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
