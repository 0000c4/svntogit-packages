# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=system-config-printer
pkgver=1.3.12
pkgrel=2
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('udev' 'pycups' 'python2-dbus' 'pysmbc' 'python2-pycurl' 'libusb-compat'
         'gnome-icon-theme' 'python2-notify')
makedepends=('intltool' 'xmlto' 'docbook-xsl' 'desktop-file-utils')
optdepends=('gnome-keyring: password management')
replaces=('system-config-printer-common' 'system-config-printer-gnome')
conflicts=('system-config-printer-common' 'system-config-printer-gnome')
source=(http://cyberelk.net/tim/data/${pkgname}/1.3/${pkgname}-${pkgver}.tar.xz{,.sig}
        'fix-typo-in-service.patch')
md5sums=('0775647e1c34ef25364de3c178a4ed36'
         '1aa2246047b424e2d23473749368b8c6'
         'd30d14aeb6d2bfeac0679d28704473ec')

build() {
  cd ${pkgname}-${pkgver}

  # Fixed upstream
  patch -p1 -i "${srcdir}"/fix-typo-in-service.patch

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py')
  sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" udev/udev-add-printer

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
