# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=system-config-printer
pkgname=('system-config-printer-common'
         'system-config-printer-gnome')
pkgver=1.3.8
pkgrel=2
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('udev' 'libcups' 'intltool' 'python2' 'xmlto' 'docbook-xsl' 'desktop-file-utils')
source=(http://cyberelk.net/tim/data/${pkgbase}/1.3/${pkgbase}-${pkgver}.tar.xz{,.sig})
md5sums=('ca04568dd7ff1ef7a381ad637d4e915b'
         '67c103760f9b4227bc1d99204d8378cd')

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py')
  sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" udev/udev-add-printer 

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules

  make
}

package_system-config-printer-common() {
  pkgdesc='Pygtk CUPS Configuration'
  depends=('udev' 'pycups' 'dbus-python' 'pysmbc')
  optdepends=('system-config-printer-gnome: for the GTK frontend'
	'kdeadmin-system-config-printer-kde: for the administration tool in KDE System Settings')
  replaces=('system-config-printer')
  provides=("system-config-printer=${pkgver}")
  conflicts=('system-config-printer')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  install -d "${pkgdir}"/usr/share/${pkgbase}
  install -m644 config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    "${pkgdir}"/usr/share/${pkgbase}/

  install -m755 pysmb.py \
    "${pkgdir}"/usr/share/${pkgbase}/

  install -d "${pkgdir}"/etc/dbus-1/system.d/
  install -m644 dbus/com.redhat.NewPrinterNotification.conf \
    dbus/com.redhat.PrinterDriversInstaller.conf \
    "${pkgdir}"/etc/dbus-1/system.d/
  install -d "${pkgdir}"/etc/cupshelpers/
  install -m644 xml/preferreddrivers.xml "${pkgdir}"/etc/cupshelpers/

  install -d "${pkgdir}"/lib/udev/rules.d
  install -m755 udev/{udev-add-printer,udev-configure-printer} \
    "${pkgdir}"/lib/udev/
  install -m644 udev/70-printers.rules \
    "${pkgdir}"/lib/udev/rules.d

  for file in build/lib/cupshelpers/*.py; do
    install -Dm644 $file \
      "${pkgdir}"/usr/lib/python2.7/site-packages/cupshelpers/$(basename $file)
  done
}

package_system-config-printer-gnome() {
  pkgdesc='A CUPS printer configuration tool and status applet - GTK frontend'
  depends=('system-config-printer-common' 'gnome-icon-theme' 'python-notify' 'python-pycurl')
  optdependence=('gnome-keyring: password management')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # files provided by system-config-printer-common
  cd "${pkgdir}"/usr/share/${pkgbase}
  rm config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    pysmb.py
  rm -r "${pkgdir}"/etc/dbus-1/
  rm -r "${pkgdir}"/etc/cupshelpers/
  rm -r "${pkgdir}"/etc/udev/
  rm -r "${pkgdir}"/usr/lib/
}
