# $Id: PKGBUILD,v 1.20 2008/12/29 15:30:49 BaSh Exp $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=system-config-printer
pkgname=('system-config-printer-common'
	'system-config-printer-gnome')
pkgver=1.1.12
pkgrel=3
pkgdesc="Pygtk CUPS Configuration"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('i686' 'x86_64')
license=('GPL')
makedepends=(# the makedeps
             'intltool' 'python>=2.6.2-5' 'xmlto' 'docbook-xsl' 'desktop-file-utils'
	    # the deps from system-config-printer-common
	    'pycups>=1.9.46-2' 'libxml2' 'dbus-python' 'pygobject'
	    # the deps from system-config-printer-gnome
	    'gnome-python') # 'python-notify' (community repo)
source=(http://cyberelk.net/tim/data/$pkgbase/1.1/$pkgbase-$pkgver.tar.bz2)
md5sums=('43be348b60651e79479b5c396d9f6003')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --disable-nls
  make || return 1
}

package_system-config-printer-common() {
  pkgdesc='Pygtk CUPS Configuration'
  depends=('pycups>=1.9.46-2' 'libxml2' 'dbus-python' 'pygobject')
  replaces=('system-config-printer')
  provides=('system-config-printer')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -d ${pkgdir}/usr/share/${pkgbase}
  install -m644 config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    XmlHelper.py \
    ${pkgdir}/usr/share/${pkgbase}/ || return 1

  install -m755 pysmb.py \
    ${pkgdir}/usr/share/${pkgbase}/ || return 1
  
  for file in build/lib/cupshelpers/*.py; do
    install -Dm644 $file \
      ${pkgdir}/usr/lib/python2.6/site-packages/cupshelpers/$(basename $file) || return 1
  done
}

package_system-config-printer-gnome() {
  pkgdesc='Pygtk CUPS Configuration - GNOME'
  depends=('system-config-printer-common' 'gnome-python') #'python-notify' 

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  cd ${pkgdir}/usr/share/${pkgbase}
  rm config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    XmlHelper.py \
    pysmb.py || return 1
  rm -r ${pkgdir}/usr/lib/python2.6/site-packages/cupshelpers/ || return 1
}
