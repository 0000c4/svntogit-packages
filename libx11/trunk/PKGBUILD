# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libx11
pkgver=1.4.2
pkgrel=2
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
options=('!libtool')
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        add-define-XK_SINHALA.patch
        xorg.sh)
sha1sums=('c93fc44dcc028319bee76f3e629ec43cfce42673'
          '4f5ccff733042a6ea0fe41082903c332336a173c'
          '4b756387426afb2a5bb7a155631ec42348abf5c1')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  patch -Np1 -i "${srcdir}/add-define-XK_SINHALA.patch"
  ./configure --prefix=/usr --disable-static --disable-xf86bigfont
  make
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/xorg.sh" "${pkgdir}/etc/profile.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
