# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor Jan de Groot <jgc@archlinux.org>
pkgname=libx11
pkgver=1.1.99.2
pkgrel=2
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb>=1.1.93' 'kbproto>=1.0.3')
makedepends=('xproto>=7.0.14' 'xextproto>=7.0.5' 'xf86bigfontproto>=1.1.2' 'xtrans>=1.2.3' 'kbproto>=1.0.3' 'inputproto>=1.5.0' 'xorg-util-macros>=1.2.1')
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
	012_ru_RU_UTF-8_XLC_LOCALE.diff
	libX11-1.1.99.2.hang.patch
	xorg.sh)
md5sums=('2450f861d39939e46f8a4ced1abc3504'
         'c7e38a68b932ed557bc71979eed2a8a1'
	 '570d5722c911a92d951010b192b7afa5'
         '942ff006e69e041f0307c1316e0dec5d')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  patch -Np1 -i "${srcdir}/012_ru_RU_UTF-8_XLC_LOCALE.diff" || return 1
  patch -Np1 -i "${srcdir}/libX11-1.1.99.2.hang.patch" || return 1

  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr \
              --with-xcb || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/xorg.sh" \
                "${pkgdir}/etc/profile.d/" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
