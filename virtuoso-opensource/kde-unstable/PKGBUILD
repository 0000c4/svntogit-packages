# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

pkgbase=virtuoso-opensource
pkgname=('virtuoso-server' 'virtuoso-drivers')
pkgver=6.0.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://virtuoso.openlinksw.com/wiki/main/Main/"
license=('GPL')
makedepends=('bison' 'flex' 'libxml2' 'zlib' 'openldap' 'readline' 'iodbc')
options=('!libtool')
source=("http://downloads.sourceforge.net/virtuoso/$pkgbase-$pkgver.tar.gz")
md5sums=('39b68d6c958ad36622ba4476e1ea5fd0')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  ./configure --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-rendezvous \
    --disable-hslookup \
    --disable-all-vads || return 1
  make || return 1
}

package_virtuoso-server(){
  pkgdesc="A scalable cross-platform server that combines SQL/RDF/XML Data Management with Web Application Server and Web Services Platform functionality"
  depends=('libxml2' 'zlib' 'openldap' 'readline')

  cd ${srcdir}/${pkgbase}-${pkgver}/binsrc/virtuoso
  install -D ${pkgdir}/usr/lib
  make DESTDIR=${pkgdir} install || return 1
}

package_virtuoso-drivers(){
  pkgdesc="Virtuoso ODBC driver"
  depends=('virtuoso-server' 'iodbc')

  cd ${srcdir}/${pkgbase}-${pkgver}/binsrc/driver
  make DESTDIR=${pkgdir} install || return 1
}
