# $Id$
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=scribus
pkgver=1.4.1
pkgrel=1
pkgdesc="A desktop publishing program"
arch=('i686' 'x86_64')
url="http://www.scribus.net"
license=('GPL')
install=scribus.install
depends=('libcups>=1.3.11' 'lcms>=1.18' 'qt' 'ghostscript>=8.70' \
         'libart-lgpl>=2.3.20' 'python2' 'libxml2>=2.7.3' 'cairo' \
         'desktop-file-utils' 'shared-mime-info')
makedepends=('cmake')
options=(!libtool)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz)
md5sums=('c79593dc61bc137d9e7968b37d070993')
sha1sums=('fb48bc492d79abc2275b1903692defe9b3931adf')

build() {
	cd "${srcdir}"

	sed -i -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
	       -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
               Scribus/scribus/plugins/scriptplugin/{samples,scripts}/*

	mkdir build
	cd build
	cmake ../Scribus \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install

	# fix icon path in .desktop file
	sed -i 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' \
	   "${srcdir}/Scribus/scribus.desktop"

	install -Dm644 "${srcdir}/Scribus/scribus.desktop" \
	   "${pkgdir}/usr/share/applications/scribus.desktop"
}
