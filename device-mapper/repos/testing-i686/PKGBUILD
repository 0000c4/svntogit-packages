# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=device-mapper
pkgver=1.02.31
_lvmver=2.02.45
pkgrel=1
pkgdesc="Device mapper userspace library and tools."
arch=('i686' 'x86_64')
url="http://sourceware.org/dm/"
license=('GPL')
groups=('base')
depends=('readline')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_lvmver}.tgz device-mapper.rules)
md5sums=('440daa01b8f2ec4fe97b1cc621108220' '065f32a36947e6183722ed9aef13dc0c')
sha1sums=('d2b167198e7a4efea743fefaa84c3a43715341eb' '003f5cf654503dcc9c1ba7c11c7d6e77bf27bb8d')

build() {
  cd ${srcdir}/LVM2.${_lvmver}
  ./configure --prefix=/usr || return 1
  make device-mapper || return 1
  make DESTDIR=${pkgdir} sbindir=${pkgdir}/sbin \
    libdir=${pkgdir}/lib install_device-mapper || return 1

  # install the static library as well
  install -D -m644 libdm/ioctl/libdevmapper.a ${pkgdir}/usr/lib/libdevmapper.a || return 1
  # install pkgconfig file
  install -D -m644 libdm/libdevmapper.pc ${pkgdir}/usr/lib/pkgconfig/libdevmapper.pc || return 1
  # install udev rule
  install -D -m644 ${srcdir}/device-mapper.rules ${pkgdir}/etc/udev/rules.d/device-mapper.rules || return 1
  # fix device-mapper link bug
  ln -sf /lib/libdevmapper.so ${pkgdir}/usr/lib/libdevmapper.so || return 1
}
