# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=device-mapper
pkgver=1.02.30
_lvmver=2.02.44
pkgrel=1
pkgdesc="Device mapper userspace library and tools."
arch=('i686' 'x86_64')
url="http://sourceware.org/dm/"
license=('GPL')
groups=('base')
depends=('readline')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_lvmver}.tgz device-mapper.rules)
md5sums=('4ed7b99903a6fc5165b7b0b8def42486' '065f32a36947e6183722ed9aef13dc0c')
sha1sums=('fc322098c769022bd5f508dd6ff2a321da75e50d' '003f5cf654503dcc9c1ba7c11c7d6e77bf27bb8d')

build() {
  cd ${srcdir}/LVM2.${_lvmver}
  ./configure --prefix=/usr || return 1
  make device-mapper || return 1
  make DESTDIR=${pkgdir} sbindir=${pkgdir}/sbin \
    libdir=${pkgdir}/lib install_device-mapper || return 1

  # install the static library as well
  install -D -m644 libdm/ioctl/libdevmapper.a ${pkgdir}/usr/lib/libdevmapper.a || return 1
  # install pkgconfig file
  install -D -m644 libdm/libdevmapper.pc ${pkgdir}/usr/lib/pkgconfig/libdevmapper.pc || return 1
  # install udev rule
  install -D -m644 ${srcdir}/device-mapper.rules ${pkgdir}/etc/udev/rules.d/device-mapper.rules || return 1
  # fix device-mapper link bug
  ln -sf /lib/libdevmapper.so ${pkgdir}/usr/lib/libdevmapper.so || return 1
}
