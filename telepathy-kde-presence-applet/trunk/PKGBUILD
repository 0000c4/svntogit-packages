# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-presence-applet
_pkgname=ktp-presence-applet
pkgver=0.5.0
pkgrel=1
pkgdesc="Plasma applet for managing your Telepathy account presence"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-contact-list')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
source=("http://download.kde.org/unstable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('f4736784443ea37dc2490acb5be3a2036ca179f5')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
