# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=3.30.0
pkgrel=1
pkgdesc="Clear hidden mines from a minefield"
url="https://wiki.gnome.org/Apps/Mines"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(gobject-introspection yelp-tools appstream-glib vala git meson)
groups=(gnome-extra)
_commit=15ac355d3e0d2639303a5944c1173a7199f8a824  # tags/3.30.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-mines.git#commit=$_commit"
        icon-fixes.diff)
sha256sums=('SKIP'
            '8f8667d9d6fe5ad9d789c1842653ae0b49b9eaa82b378d6ee7b7e2a8abffae57')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../icon-fixes.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
