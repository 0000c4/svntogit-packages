# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=1.0.0
pkgrel=1
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/libbluray.html'
license=('LGPL2.1')
depends=('fontconfig' 'glibc' 'libfreetype.so' 'libxml2')
makedepends=('apache-ant')
optdepends=('java-environment: BD-J library')
provides=('libbluray.so')
source=("ftp://ftp.videolan.org/pub/videolan/libbluray/${pkgver}/libbluray-${pkgver}.tar.bz2")
sha256sums=('f7e3add335c7bbef45824fcd2249a9bf293868598c13f8479352c44ec95374cc')

build() {
  cd libbluray-${pkgver}

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd libbluray-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
