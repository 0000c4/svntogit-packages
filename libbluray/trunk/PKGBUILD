# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=0.2.2
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback"
arch=('i686' 'x86_64')
url="http://www.videolan.org/developers/libbluray.html"
license=('LGPL2.1')
depends=('libxml2')
makedepends=('apache-ant' 'jdk7-openjdk')
optdepends=('java-environment: Blu-ray Disc Java support library')
options=(!libtool)
source=(ftp://ftp.videolan.org/pub/videolan/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('a27eaa929ef05c544beb966a24dfc5425546d6035bfb3e86dc60987b8a4761031ce27c3836a64f71cc76b5308c3cd75efb3bc1f238185819385477e64883888a')

build() {
  cd "$pkgname-$pkgver"

  . /etc/profile.d/jdk.sh
  ./configure --prefix=/usr \
    --enable-bdjava \
    --with-jdk=${JAVA_HOME} \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  #install bdj
  install -Dm644 src/.libs/libbluray.jar "$pkgdir/usr/share/java/libbluray.jar"
}

# vim:set ts=2 sw=2 et:
