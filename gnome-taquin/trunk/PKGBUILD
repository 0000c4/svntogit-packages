# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-taquin
pkgver=3.30.0
pkgrel=1
pkgdesc="Move tiles so that they reach their places"
url="https://wiki.gnome.org/Apps/Taquin"
arch=(x86_64)
license=(GPL3)
depends=(gtk3 librsvg libcanberra)
makedepends=(intltool vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=91b695ab4fb86c7548e446df5d4913c9880b2274  # tags/3.30.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-taquin.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
