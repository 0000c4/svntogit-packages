# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# _GNU_EFI_LIB_DIR="/usr/lib"

_actualname="gummiboot"
pkgname="${_actualname}-efi-i386"
pkgver="6"
pkgrel="1"
pkgdesc="Simple i386 UEFI Boot Manager for EFISTUB Kernels"
url="http://freedesktop.org/wiki/Software/gummiboot"
arch=('any')
license=('GPL2')
makedepends=('git' 'gnu-efi')
depends=('dosfstools' 'efibootmgr')
optdepends=('mactel-boot: For bless command in Apple Mac systems')
conflicts=("${_actualname}-efi-i386")
backup=('boot/efi/loader/loader.conf'
        'boot/efi/loader/entries/archlinux-core.conf'
        'boot/efi/loader/entries/archlinux-core-fallback.conf')
options=('!strip' 'docs')
install="${pkgname}.install"
source=("ftp://ftp.archlinux.org/other/${_actualname}/${_actualname}-${pkgver}.tar.gz"
        'loader.conf'
        'archlinux-core.conf'
        'archlinux-core-fallback.conf')
md5sums=('6160227e9078fb74ad75f88ec1524a76'
         '2ac026e44a52aa79fdd43ce03731eec1'
         'a78960ab591de72b658ba1b49f832365'
         '1cf1f96890a4904298cb561d5d6a40e5')

build() {
	
	if [[ "${CARCH}" != "i686" ]]; then
		echo "${pkgname} package can be built only in a i686 system. Exiting."
		exit 1
	fi
	
	cd "${srcdir}/${_actualname}-${pkgver}/"
	echo
	
	make clean || true
	echo
	
	make
	echo
	
}

package() {
	
	cd "${srcdir}/${_actualname}-${pkgver}/"
	
	## Install gummiboot i386 UEFI application
	install -d "${pkgdir}/boot/efi/EFI/arch/gummiboot/"
	install -D -m0644 "${srcdir}/${_actualname}-${pkgver}/gummiboot.efi" "${pkgdir}/boot/efi/EFI/arch/gummiboot/gummibootia32.efi"
	
	## Install gummiboot configuration files
	install -d "${pkgdir}/boot/efi/loader/entries/"
	install -D -m0644 "${srcdir}/loader.conf" "${pkgdir}/boot/efi/loader/loader.conf"
	install -D -m0644 "${srcdir}/archlinux-core.conf" "${pkgdir}/boot/efi/loader/entries/archlinux-core.conf"
	install -D -m0644 "${srcdir}/archlinux-core-fallback.conf" "${pkgdir}/boot/efi/loader/entries/archlinux-core-fallback.conf"
	
}
