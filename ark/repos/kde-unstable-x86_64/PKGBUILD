# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=20.03.90
pkgrel=2
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('p7zip: 7Z format support' 'unrar: RAR decompression support' 'unarchiver: RAR format support'
            'lzop: LZO format support' 'lrzip: LRZ format support')
groups=(kde-applications kdeutils)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         ark-revert-qt5.15-port.patch::"https://cgit.kde.org/ark.git/patch/?id=7a3a689b")
sha256sums=('1e5738d9c7da3e9a8d8f065fb33ff2093c180a64f50fe55ae44ea302a433f895'
            'SKIP'
            'b6d848177814ac04abfe5078197eadd80b745e99b4ea074a9eecbcb1502c2601')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../ark-revert-qt5.15-port.patch
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

