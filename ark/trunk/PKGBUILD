# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=15.12.0
pkgrel=2
pkgdesc='Archiving Tool'
arch=(i686 x86_64)
url='http://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kparts kpty libarchive hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=(p7zip zip unzip unrar)
conflicts=(kdeutils-ark)
replaces=(kdeutils-ark)
groups=(kde-applications kdeutils)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	bug357057.patch::"https://quickgit.kde.org/?p=ark.git&a=commitdiff&h=087e5a&o=plain")
md5sums=('2fe131a931d51603eff33b80d45a73b7'
         '4a3ae6fb4f3cce9a49f7b89812e547b3')

prepare() {
  mkdir -p build

# Fix opening rar files if unrar is installed but rar isn't http://bugs.kde.org/show_bug.cgi?id=357057
  cd $pkgname-$pkgver
  patch -p1 -i ../bug357057.patch
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

