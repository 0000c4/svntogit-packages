# $ID$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-tie-cycle
_dist=Tie-Cycle
pkgver=1.221
pkgrel=1
pkgdesc="Cycle through a list of values via a scalar"
arch=('any')
url='https://metacpan.org/release/${_dist}'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/B/BD/BDFOY/${_dist}-${pkgver}.tar.gz)
md5sums=('773b14e02cb5815d40160cf7255897e0')
sha512sums=('39ed261ef2e3a6a93cc63197966ccae6ddee2473c24241067223448badeb76509682c52043bb34299a27f2f3a35956d3022af2fb13bd331ed2cc132dbc703087')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
