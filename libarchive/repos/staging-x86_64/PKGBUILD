# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
pkgname=libarchive
pkgver=3.0.2
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat')
source=("http://libarchive.googlecode.com/files/libarchive-${pkgver}.tar.gz")
md5sums=('4df33cb107c9702c80473e0794ddf833')
sha256sums=('b27a10182f4fc3f69d01419a59ddb16fccf5df8c4b166008c04f41905e29fd74')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  # currently fails in a minor test involving atime updates (#60)
  make check || :
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
