# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
pkgname=libarchive
pkgver=2.8.5
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat')
source=("http://libarchive.googlecode.com/files/libarchive-${pkgver}.tar.gz"
        release-2.8-fixes.patch)
md5sums=('9caf51dcf6213e9c9f5a1c27448b9c90'
         '14dbbf2d31d68d8eedb93ec5531f3b0c')
sha256sums=('13993e0ffbd121ccda46ea226b1f8eac218de0fa8da7d8b1f998093d5c32a72d'
            'fcadc5ac3020a9fab5922d343baca28f067cc051e9b6f59e480349ba5b0a5060')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 < ../release-2.8-fixes.patch
  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
