# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=pcre
pkgver=8.13
pkgrel=2
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i686' 'x86_64')
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs')
options=('!libtool')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2
        r661.diff)
md5sums=('5e595edbcded141813fa1a10dbce05cb'
         '70e2725b46ff6f3f114ce5067fd03405')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
  # http://bugs.exim.org/show_bug.cgi?id=1136
  patch -Np2 -i "${srcdir}"/r661.diff
  sed -i '12140d' testdata/testoutput2
  
  [ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"
  ./configure --prefix=/usr --enable-utf8 --enable-unicode-properties
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # grep uses pcre, so we need the libs in /lib
  install -dm755 "${pkgdir}"/lib
  mv "${pkgdir}"/usr/lib/libpcre.so.* "${pkgdir}"/lib/
  ln -sf /lib/libpcre.so.0 "${pkgdir}"/usr/lib/libpcre.so

  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
