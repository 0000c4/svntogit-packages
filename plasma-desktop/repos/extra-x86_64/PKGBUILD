# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.5.2
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"
	tooltip-thumbnails.patch::"https://quickgit.kde.org/?p=plasma-desktop.git&a=commitdiff&h=ae00bb&o=plain")
depends=('powerdevil' 'polkit-kde-agent' 'libcanberra' 'libxkbfile' 'kmenuedit'
         'systemsettings' 'ksysguard' 'kpeople' 'baloo' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'xf86-input-evdev' 'xf86-input-synaptics' 'xorg-server-devel')
optdepends=('plasma-nm: Network manager applet')
conflicts=('kdebase-workspace' 'kcm-touchpad-frameworks')
replaces=('kcm-touchpad-frameworks')
install="${pkgname}.install"
groups=('plasma')
md5sums=('be58ab47cb66b25b0f00561c110d9ce6'
         '6227e9332a715c2cf4dcf92e0cc02eda')

prepare() {
  mkdir -p build

# Fix performance regression caused by taskbar tooltips http://bugs.kde.org/show_bug.cgi?id=356938
  cd $pkgname-$pkgver
  patch -p1 -i ../tooltip-thumbnails.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
