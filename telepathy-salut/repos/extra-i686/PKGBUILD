# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Daniel Balieiro <daniel@balieiro.com>
pkgname=telepathy-salut
pkgver=0.5.0
pkgrel=1
pkgdesc="A link-local XMPP connection manager for Telepathy"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL')
depends=('telepathy-glib' 'avahi' 'libsoup')
makedepends=('libxslt' 'python2' 'xmldiff')
install=telepathy-salut.install
source=(http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('0642bd33d5d5e8e50087ace1faa769cd')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr \
    	--libexecdir=/usr/lib/telepathy \
        --disable-plugins --disable-Werror \
        --with-tls=openssl
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
