# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=unicode-character-database
pkgver=10.0.0
pkgrel=1
pkgdesc="Unicode Character Database"
arch=(any)
license=(BSD)
url="http://www.unicode.org/"
source=("UCD-$pkgver.zip::http://www.unicode.org/Public/zipped/$pkgver/UCD.zip"
        "Unihan-$pkgver.zip::http://www.unicode.org/Public/zipped/$pkgver/Unihan.zip"
        LICENSE)
noextract=({UCD,Unihan}-$pkgver.zip)
sha256sums=('cb26d649f8bac8b12f69e2fbcd77d1759ecdcd7c8e8f1c4385a9c5a36cf14891'
            '01232063a8529636cf155ba7a1dbad329cb2e63acde83a3d607b5eafa8f933a5'
            'dfc6b3430adc9e474443645fe5c55b29ecfe166434c2d5232909f4865d81ca16')

package() {
  install -d "$pkgdir/usr/share/$pkgname"
  cd "$pkgdir/usr/share/$pkgname"

  for _f in UCD Unihan; do
    cp "$srcdir/$_f-$pkgver.zip" $_f.zip
    bsdtar -x --no-same-owner --no-same-permissions -f $_f.zip
  done

  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
