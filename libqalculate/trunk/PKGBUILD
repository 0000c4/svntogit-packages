# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libqalculate
pkgver=1.0.0
pkgrel=1
pkgdesc="Multi-purpose desktop calculator"
arch=('i686' 'x86_64')
url="http://qalculate.github.io/"
license=('GPL')
depends=('libxml2' 'cln' 'glib2')
makedepends=('perl-xml-parser' 'intltool')
optdepends=('gnuplot: for plotting support' 
            'wget: for retrieval of exchange rates (not needed if you use one of the GUI)')
options=('!makeflags')
source=(https://github.com/Qalculate/libqalculate/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d9cffe69eff1a8d13f8b3b69b4c7f93ce60b5f800d8352b3851b49056fc6bf1b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
