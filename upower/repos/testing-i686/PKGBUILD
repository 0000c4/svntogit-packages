# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.9.18
pkgrel=2
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd-tools' 'libsystemd' 'libusb' 'polkit' 'pm-utils' 'dbus-glib' 'libimobiledevice')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'systemd')
options=('!libtool')
source=($url/releases/$pkgname-$pkgver.tar.xz
        0001-Send-resume-signal-when-built-with-systemd-and-using.patch)
md5sums=('bfaaa1e7f8479fca5594406b3c36dff9'
         '188afea4919dc324b2efbae35d9d216a')

build() {
  cd "$pkgname-$pkgver"

  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure

  # https://bugs.freedesktop.org/show_bug.cgi?id=54371
  patch -Np1 -i ../0001-Send-resume-signal-when-built-with-systemd-and-using.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
