# $Id$
# Maintainer: tobias <tobias at archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-clipman-plugin
pkgver=1.1.3
pkgrel=2
pkgdesc="A clipboard plugin for the Xfce4 panel"
arch=(i686 x86_64)
license=('custom')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-clipman-plugin"
groups=('xfce4-goodies')
depends=('libxfcegui4' 'xfce4-panel' 'libunique' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/1.1/${pkgname}-${pkgver}.tar.bz2)
md5sums=('2ba70c6bd710e2a18cba5add66d297dc')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # fix for exo 0.5.6
  sed -i -e "s/exo-0.3/exo-1/g" configure
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-unique \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
