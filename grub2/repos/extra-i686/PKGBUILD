# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: dongiovanni <dongiovanni.archlinux.de>

pkgname=grub2
pkgver=1.98
pkgrel=2
pkgdesc="The GNU GRand Unified Bootloader"
url="http://www.gnu.org/software/grub/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sh' 'lzo2' 'freetype2')
optdepends=('ruby:usr/share/grub/genmk.rb script' 'libusb: /sbin/grub-emu')
makedepends=('bdf-unifont' 'ruby')
conflicts=('grub')
provides=('grub')
install=${pkgname}.install
source=(ftp://alpha.gnu.org/gnu/grub/grub-${pkgver}.tar.gz  
	'grubconfig.archlinux.patch' '05_archtheme')
md5sums=('c0bcf60e524739bb64e3a2d4e3732a59'
         'c71f5b20b35f3e1da5e9e3577e486af3'
         'e143ae2e453b7c3ced309667df30da55')

build() {
  cd $srcdir/grub-${pkgver}/util/grub.d

  # modify scripts to detect Arch Linux kernels
  patch -Np3 -i ${srcdir}/grubconfig.archlinux.patch || return 1

  # run autogen.sh to create configure files
  cd ../..
  ./autogen.sh

  # fix unifont.bdf location
  sed -i 's|/usr/src/unifont.bdf|/usr/share/fonts/misc/unifont.bdf|' configure || return 1

  CFLAGS= ./configure --prefix=/usr --enable-grub-mkfont --bindir=/bin \
		--sbindir=/sbin --mandir=/usr/share/man \
		--infodir=/usr/share/info --sysconfdir=/etc

  CFLAGS= make || return 1
}

package() {
  cd $srcdir/grub-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  # install script to set default colors
  install -Dm755 ${srcdir}/05_archtheme ${pkgdir}/etc/grub.d/05_archtheme

}
