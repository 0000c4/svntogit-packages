# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Keshav P R <skodabenz at rocketmail dot com>

# _grub2_rev=3072

_grub2_lua_ver=19
_grub2_gpxe_ver=12
_grub2_ntldr_ver=13
_grub2_915_ver=6

pkgname=('grub2-common' 'grub2-bios' 'grub2-efi-i386')
pkgbase="grub2"
pkgver='1.99~rc1'
pkgrel=1
url="http://www.gnu.org/software/grub/"
arch=('i686' 'x86_64')
license=('GPL3')
optdepends=()
makedepends=('bdf-unifont' 'python2' 'xz' 'autogen' 'texinfo' 'help2man' 'gettext' 'device-mapper')
options=(strip purge docs zipman !emptydirs)
groups=('grub2')
conflicts=()
provides=()

source=("ftp://alpha.gnu.org/gnu/grub/grub-${pkgver}.tar.gz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_lua_r${_grub2_lua_ver}.tar.xz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_ntldr-img_r${_grub2_ntldr_ver}.tar.xz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_915resolution_r${_grub2_915_ver}.tar.xz"
	'archlinux_grub2_mkconfig_fixes.patch'
	'grub.default'
	'grub.cfg'
	'20_memtest86+'
	'05_archtheme')

noextract=("grub2_extras_lua_r${_grub2_lua_ver}.tar.xz"
	"grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz"
	"grub2_extras_ntldr-img_r${_grub2_ntldr_ver}.tar.xz"
	"grub2_extras_915resolution_r${_grub2_915_ver}.tar.xz")


build() {
	# set architecture dependent variables
	if [ "${CARCH}" = "x86_64" ] ; then
		_EFIEMU="--enable-efiemu"
	else
		_EFIEMU="--disable-efiemu"
	fi
	_HOST="${CARCH}"

	build_grub2-common_and_bios
	build_grub2-efi
}

build_grub2-common_and_bios() {
	# copy the source for building the common/bios package
	cp -r ${srcdir}/grub-${pkgver} ${srcdir}/grub2_bios-${pkgver}
  
	## Apply Archlinux specific fixes to enable grub2-mkconfig detect Arch kernels and initramfs
	cd ${srcdir}/grub2_bios-${pkgver}
	patch -Np1 -i ${srcdir}/archlinux_grub2_mkconfig_fixes.patch

	# add the grub-extra sources
	export GRUB_CONTRIB=${srcdir}/grub2_bios-${pkgver}/grub2-extras/  
	install -d ${srcdir}/grub2_bios-${pkgver}/grub2-extras 
	bsdtar xf ${srcdir}/grub2_extras_lua_r${_grub2_lua_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
	bsdtar xf ${srcdir}/grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
	bsdtar xf ${srcdir}/grub2_extras_ntldr-img_r${_grub2_ntldr_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
	bsdtar xf ${srcdir}/grub2_extras_915resolution_r${_grub2_915_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
  
	## Need to use python2
	sed -i 's|python|python2|' ${srcdir}/grub2_bios-${pkgver}/autogen.sh

	# start the actual build process
	cd ${srcdir}/grub2_bios-${pkgver}
	./autogen.sh
  
	## fix unifont.bdf location so grub-mkfont can create *.pf2 files
	sed -i 's|/usr/share/fonts/unifont|/usr/share/fonts/misc|' ${srcdir}/grub2_bios-${pkgver}/configure
  
#	mkdir ${srcdir}/grub2_bios-${pkgver}/BUILD_BIOS
#	cd ${srcdir}/grub2_bios-${pkgver}/BUILD_BIOS
  
	CFLAGS="" ./configure \
		--with-platform=pc --enable-mm-debug \
		${_EFIEMU} --host=${CARCH}-unknown-linux-gnu \
		--enable-grub-mkfont --prefix=/usr \
		--bindir=/bin --sbindir=/sbin \
		--mandir=/usr/share/man --infodir=/usr/share/info \
		--sysconfdir=/etc
	CFLAGS="" make 
}

build_grub2-efi() {
	# copy the source for building the efi package
	cp -r ${srcdir}/grub-${pkgver} ${srcdir}/grub2_efi-${pkgver}

	# add the grub-extra sources
	export GRUB_CONTRIB=${srcdir}/grub2_efi-${pkgver}/grub2-extras/  
	install -d ${srcdir}/grub2_efi-${pkgver}/grub2-extras  
	bsdtar xf ${srcdir}/grub2_extras_lua_r${_grub2_lua_ver}.tar.xz \
		-C ${srcdir}/grub2_efi-${pkgver}/grub2-extras
	bsdtar xf ${srcdir}/grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz \
		-C ${srcdir}/grub2_efi-${pkgver}/grub2-extras
  
	## Need to use python2
	sed -i 's|python|python2|' ${srcdir}/grub2_efi-${pkgver}/autogen.sh
	
	# start the actual build process
	cd ${srcdir}/grub2_efi-${pkgver}
	./autogen.sh
	#install -d ${srcdir}/grub2_efi-${pkgver}/BUILD_EFI_i386
	#cd ${srcdir}/grub2_efi-${pkgver}/BUILD_EFI_i386

	CFLAGS="" ./configure \
		--with-platform=efi --target=i386 \
		--enable-mm-debug --disable-efiemu \
		--host=${CARCH}-unknown-linux-gnu \
		--prefix=/usr --bindir=/bin \
		--sbindir=/sbin --mandir=/usr/share/man \
		--infodir=/usr/share/info --sysconfdir=/etc
	CFLAGS="" make 
}

package_grub2-common() {
	pkgdesc="The GNU GRand Unified Bootloader version 2 - Files common for all platforms"
	install="grub2.install"
	depends=('xz' 'freetype2' 'device-mapper')
	conflicts=('grub2')
	backup=('boot/grub/grub.cfg' 'etc/default/grub' 'etc/grub.d/40_custom')
  
	cd ${srcdir}/grub2_bios-${pkgver}
	make DESTDIR=${pkgdir} install 

	## install grub2-extras ntldr-img's grubinst as /sbin/grubinst
	install -Dm755 ${srcdir}/grub2_bios-${pkgver}/grub-core/grubinst ${pkgdir}/sbin/grubinst
  
	install -Dm755 ${pkgdir}/sbin/grub-install ${pkgdir}/sbin/grub_bios-install
	install -Dm755 ${pkgdir}/sbin/grub-install ${pkgdir}/sbin/grub_efi_x86_64-install
	install -Dm755 ${pkgdir}/sbin/grub-install ${pkgdir}/sbin/grub_efi_i386-install
    
	sed -i "s|^\(target_cpu\)=.*|\1=i386|; \
		s|^\(platform\)=.*|\1=pc|" \
		${pkgdir}/sbin/grub_bios-install

	sed -i "s|^\(target_cpu\)=.*|\1=x86_64|; \
		s|^\(platform\)=.*|\1=efi|" \
		${pkgdir}/sbin/grub_efi_x86_64-install

	sed -i "s|^\(target_cpu\)=.*|\1=i386|; \
		s|^\(platform\)=.*|\1=efi|" \
		${pkgdir}/sbin/grub_efi_i386-install
  
	## install extra /etc/grub.d/ files
	install -Dm755 ${srcdir}/05_archtheme ${pkgdir}/etc/grub.d/05_archtheme
	install -Dm755 ${srcdir}/20_memtest86+ ${pkgdir}/etc/grub.d/20_memtest86+
    
	## install /etc/default/grub (used by grub-mkconfig)
	install -Dm644 ${srcdir}/grub.default ${pkgdir}/etc/default/grub

	## install grub.cfg (needed so it doesn't get removed on upgrading because it was previously here)
	install -Dm755 ${srcdir}/grub.cfg ${pkgdir}/boot/grub/grub.cfg

	# remove platform specific files
	rm -rf ${pkgdir}/usr/lib/grub/i386-pc/
}

package_grub2-bios() {
	pkgdesc="The GNU GRand Unified Bootloader version 2 - Built for PC BIOS"
	depends=(grub2-common=${pkgver})
	replaces=('grub2')
  
	cd ${srcdir}/grub2_bios-${pkgver}
	make DESTDIR=${pkgdir} install 

	## remove non platform-specific files
	rm -rf ${pkgdir}/{boot,bin,sbin,etc,usr/share}
	rm ${pkgdir}/usr/lib/grub/{grub-mkconfig_lib,update-grub_lib}
}

package_grub2-efi-i386() {

	pkgdesc="The GNU GRand Unified Bootloader version 2 - i386 UEFI version"
	depends=("grub2-common=${pkgver}" 'dosfstools')
	optdepends=('efibootmgr')
  
	cd ${srcdir}/grub2_efi-${pkgver}
	make DESTDIR=${pkgdir} install 

	## remove non platform-specific files
	rm -rf ${pkgdir}/{bin,sbin,etc,usr/share/info,usr/share/man,usr/lib/grub}
}
sha1sums=('866b6979974ad7209d236cb4e1819039f0bd1c6d'
          'f116fed72eb70da1d503cf3c225dce68d658dabd'
          'b04994c005910b4bf123f034b30109d5c8d6fd86'
          '963517b6f178d3cec3ff65f64cf6128cbde2f515'
          '644fe2bd6edaff03bc2e7a27bf1c0845a4568d6f'
          '2c22c7e900e4da9a555d26d455ed3fbf974d553d'
          '23cb743d0b864d415b033f71c79ebf182e909264'
          '88c15058f5079c470d8a5061e0de0f1e8fe1effd'
          '82a27eca5277218cf57c6c5767e0b17a72f62229'
          '22bd1a4c18d8b93130ca46348c2a9ea2f8b90c6b')
