# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.34.0
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=(i686 x86_64)
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('intltool')
optdepends=('python2: rsvg commandline utility')
options=('!libtool' '!emptydirs')
url="http://librsvg.sourceforge.net/"
install=librsvg.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.34/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('9d2111e086dcffd12466f48ab1ca088654011684c499a4cc4676da918290f3cd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' rsvg.in
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} \
      --with-croco --disable-static \
      --with-svgz --disable-gtk-theme
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
