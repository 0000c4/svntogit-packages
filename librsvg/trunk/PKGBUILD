# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.32.0
pkgrel=2
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk2>=2.22.0' 'libcroco>=0.6.2')
makedepends=('intltool' 'pkg-config' 'gtk-doc')
optdepends=('python2: rsvg commandline utility')
options=('!libtool')
url="http://librsvg.sourceforge.net/"
install=librsvg.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e0f6f936dab583de317cc0c36a48f80bdb9c93775225ee84140c7e5e3f841068')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' rsvg.in
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} \
      --with-croco --disable-static \
      --with-svgz --enable-gtk-theme
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
