# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=3.9.99
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('dconf' 'gtk3' 'hicolor-icon-theme')
makedepends=('gtk-doc' 'intltool' 'gobject-introspection' 'itstool' 'docbook-xsl' 'desktop-file-utils')
options=('!libtool' '!emptydirs')
groups=('gnome')
install=gucharmap.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('7a9425f07f8e4053e0382e627d65ca428674f8c63f2c721877ae8c62c25ccb00')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
