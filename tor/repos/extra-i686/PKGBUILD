# $Id$
# Maintainer: 
# Contributor: simo <simo@archlinux.org>

pkgname=tor
pkgver=0.2.1.20
pkgrel=1
pkgdesc="Anonymizing overlay network"
arch=('i686' 'x86_64')
url="http://www.torproject.org/"
license=('BSD')
depends=('openssl' 'libevent' 'tsocks' 'bash')
backup=('etc/tor/torrc' 'etc/tor/torrc-dist' 'etc/tor/tor-tsocks.conf')
install=tor.install
source=(http://www.torproject.org/dist/tor-${pkgver}.tar.gz
       tor.conf
       tor.rc)
md5sums=('0d62ee2332fdd95de43debac7435df19' '56c75d4e8a66f34167d31e38c43793dd'\
         '4e39d56f462fc9f59e91715ac1b994c0')
sha1sums=('bfc6c7e9ccee23abc4e97ca4ba98aa3ad7784262' '091385e9604d2ec519c1092ca875885c04c62a7c'\
         '978588d0078465b6383422772d187e92963eb41f')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var || return 1
    make || return 1

    install -d "$pkgdir/var/lib" || return 1
    install -o43 -g43 -d -m0700 "$pkgdir/var/lib/tor" || return 1

    make DESTDIR="$pkgdir" install || return 1
    mv "$pkgdir/etc/tor/torrc.sample" "$pkgdir/etc/tor/torrc-dist" || return 1

    install -D -m644 "$srcdir/tor.conf" "$pkgdir/etc/tor/torrc" || return 1
    install -D -m755 "$srcdir/tor.rc" "$pkgdir/etc/rc.d/tor" || return 1
    
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/tor/LICENSE" || return 1
}
