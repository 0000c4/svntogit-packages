# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=5.0
pkgrel=1
pkgdesc='A C/C++ development platform for KDE'
arch=(i686 x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
depends=(grantlee-qt5 hicolor-icon-theme kcmutils threadweaver kitemmodels ktexteditor knotifyconfig knewstuff libkomparediff2 qt5-webkit)
makedepends=(extra-cmake-modules boost subversion python)
optdepends=('subversion: Subversion plugin')
source=("http://download.kde.org/stable/kdevelop/${pkgver}.0/src/$pkgname-$pkgver.tar.xz")
sha1sums=('9cefe1c2b18187c1dde5252de9ac2648f3118bae')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
