# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=baobab
pkgver=3.22.0+3+g423a1a1
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
url="https://wiki.gnome.org/Apps/Baobab"
arch=(i686 x86_64)
license=(GPL2)
depends=(dconf gtk3 gsettings-desktop-schemas librsvg)
makedepends=(intltool itstool vala gobject-introspection yelp-tools appstream-glib git)
groups=(gnome)
_commit=423a1a17a03737595e8e311a9e3ad7b40bc999c7  # master
source=("git://git.gnome.org/baobab#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install

  # https://bugs.archlinux.org/task/31861
  sed -i '/MimeType/d' "$pkgdir/usr/share/applications/org.gnome.baobab.desktop"
}

# vim:set ts=2 sw=2 et:
