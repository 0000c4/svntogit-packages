# $Id$
# Contributor: Jose Javier  <jojapa@terra.es>
pkgname=tftp-hpa
pkgver=5.1
pkgrel=0
pkgdesc="Official tftp server"
arch=('i686' 'x86_64')
url="http://www.kernel.org/pub/software/network/tftp/"
license=('BSD')
depends=('readline>=6.0')
conflicts=('netkit-tftp')
backup=('etc/conf.d/tftpd')
source=(http://www.kernel.org/pub/software/network/tftp/$pkgname-$pkgver.tar.gz
        tftpd.rc tftpd.conf LICENSE)
md5sums=('d086b1bd6e5ed6375ad407e273afccdf'
         'a974d02526f19222146c8e3d3b598871'
         '414a79dc891bced056d99a9bb790fc9e'
         '6ce21e27b6fdc1a1adf85c81e42aeecf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --without-tcpwrappers
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALLROOT="${pkgdir}" install
  install -d "${pkgdir}/etc/rc.d"
  install -d "${pkgdir}/var/tftpboot"
  install -m755 "${srcdir}/tftpd.rc" "${pkgdir}/etc/rc.d/tftpd"
  install -D -m644 "${srcdir}/tftpd.conf" "${pkgdir}/etc/conf.d/tftpd"
  rm "${pkgdir}/usr/share/man/man8/tftpd.8"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
