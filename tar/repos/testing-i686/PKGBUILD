# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=tar
pkgver=1.24
pkgrel=1
pkgdesc="Utility used to store, backup, and transport files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/tar/tar.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
options=('!emptydirs')
install=tar.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.bz2
        tar.1
        tar-1.22-fortifysourcessigabrt.patch
        tar-1.24-directory-change-issue.patch)
md5sums=('ba905d8b1dcf06254e8025cc4468d8e5'
         'e0538778516a284e3558c454b2576c2f'
         'ab85070f3b950789900adfbaac3b28da'
         'f7598695d12918ab23ce7cb52ccffc5a')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  
  # http://www.mail-archive.com/bug-tar@gnu.org/msg02601.html
  patch -Np1 -i $srcdir/tar-1.22-fortifysourcessigabrt.patch
  
  # http://git.savannah.gnu.org/cgit/tar.git/commit/?id=acb77ac5
  patch -Np1 -i $srcdir/tar-1.24-directory-change-issue.patch

  ./configure --prefix=/usr --libexecdir=/usr/lib/tar --bindir=/bin
  make
  make check
}
  
package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/tar.1 ${pkgdir}/usr/share/man/man1/tar.1
}
