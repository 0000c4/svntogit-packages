# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=(glibmm glibmm-docs)
pkgver=2.50.0
pkgrel=1
pkgdesc="C++ bindings for Glib"
url="http://www.gtkmm.org/"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 libsigc++)
makedepends=(git mm-common perl-xml-parser clang)
_commit=cf798ba21055d05044302561353d3abd0c9615a1
source=("git://git.gnome.org/glibmm#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase
  ./configure --prefix=/usr --enable-maintainer-mode
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -j1
}

package_glibmm() {
  cd $pkgbase
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  depends=()

  cd $pkgbase/docs
  make DESTDIR="$pkgdir" install
}
