# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfloppy
pkgver=16.12.3
pkgrel=1
pkgdesc='Floppy Formatter'
url='https://kde.org/applications/utilities/kfloppy/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin)
conflicts=(kdeutils-kfloppy)
replaces=(kdeutils-kfloppy)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f74f3cc1f88182debe868ce2b390183a56b2daaed4a3575ff96d8a86e5cdaf1e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
