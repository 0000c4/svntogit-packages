# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zlib
pkgver=1.2.3.7
pkgrel=2
pkgdesc='compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('i686' 'x86_64')
license=('custom')
url="http://www.zlib.net/"
groups=('base')
depends=('glibc')
# source from zlib-devel
source=("ftp://ftp.archlinux.org/other/zlib/zlib-${pkgver}.tar.gz"
        'gzflush.patch')
md5sums=('323e45f54f321116a34950e8fbf5b6e7'
         '7e2f3d8815f1e9398e46a2a0e47399fb')

build() {
	cd ${srcdir}/zlib-$pkgver
	# add missing line which causes a segfault when using gzflush on read
	patch -p0 -i ${srcdir}/gzflush.patch || return 1
	export CFLAGS="${CFLAGS/-O2/-O3} -DUNALIGNED_OK"
	./configure --prefix=/usr --shared
	make || return 1

	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir} || return 1
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
