# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=libraw
pkgver=0.18.8
pkgrel=1
pkgdesc="A library for reading RAW files obtained from digital photo cameras (CRW/CR2, NEF, RAF, DNG, and others)"
arch=('x86_64')
url="https://www.libraw.org/"
license=(CDDL LGPL)
depends=(lcms2 jasper)
source=($pkgname-$pkgver.tar.gz::"https://github.com/LibRaw/LibRaw/archive/$pkgver.tar.gz")
sha256sums=('aa5e276c71fbc796f4f5d0f9c292ece578d2ae7e5744f7ea10aa11b2ada83f3d')

prepare() {
  cd LibRaw-$pkgver
  ./mkdist.sh
}

build() {
  cd LibRaw-$pkgver
  ./configure --prefix=/usr \
    --disable-examples
  make
}

package() {
  cd LibRaw-$pkgver
  make DESTDIR="$pkgdir" install
}
