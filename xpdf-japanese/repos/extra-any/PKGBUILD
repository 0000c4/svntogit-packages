# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=xpdf-japanese
pkgver=20040727
pkgrel=5
pkgdesc='Adds support for Japanese fonts to xpdf'
arch=('any')
url='http://www.foolabs.com/xpdf/'
license=('GPL2' 'custom')
depends=('xpdf')
install=$pkgname.install
source=("ftp://ftp.foolabs.com/pub/xpdf/$pkgname.tar.gz" 'LICENSE')
sha1sums=('93182eaf9daf39dd09e054f4556bbdaf4c260bd2'
          'd2f9ed09e1239fc1fb5308758d9bbfcc0ceb893f')

build() {
  cd "$srcdir/$pkgname"
  install -d -m755 "$pkgdir"/usr/share/xpdf/CMap
  install -m644 -t "$pkgdir"/usr/share/xpdf *.{cidToUnicode,unicodeMap}
  install -m644 -t "$pkgdir"/usr/share/xpdf/CMap CMap/*

  # relocate language specific files
  sed -i 's|/usr/local/share/xpdf/japanese|/usr/share/xpdf|' add-to-xpdfrc

  # X-Fonts are no longer supported by xpdf
  sed -i 's|^displayCIDFontX.*$||' add-to-xpdfrc

  cat >> add-to-xpdfrc << END_OF_RC_TWEAK
displayCIDFontTT	Adobe-Japan1	/usr/share/fonts/TTF/Vera.ttf
displayCIDFontTT	GothicBBB-Medium-H	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	GothicBBB-Medium-Identity-H	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	HeiseiKakuGo-W5	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	HeiseiKakuGo-W9	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	ShinGo-Bold	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	FutoGoB101-Bold	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	GothicBBB-Meidum	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	MS-Gothic	/usr/share/fonts/TTF/VeraSe.ttf
displayCIDFontTT	Kochi-Gothic	/usr/share/fonts/TTF/VeraSe.ttf

END_OF_RC_TWEAK

  install -Dm644 add-to-xpdfrc "$pkgdir/etc/xpdf/$pkgname"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
