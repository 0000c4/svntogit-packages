# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-thumbnailers
pkgver=4.8.2
pkgrel=1
pkgdesc="Thumbnailers for various graphics file formats"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkexiv2' 'libkdcraw')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('cd6e99a6c0db6bda765c765356ba9bbd601fba0d')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
