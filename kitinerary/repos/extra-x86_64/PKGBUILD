# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=18.08.2
pkgrel=4
pkgdesc="Data model and extraction system for travel reservation information"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(qt5-declarative kmime kpkpass kcalcore kcontacts poppler)
makedepends=(extra-cmake-modules zxing-cpp)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kitinerary-poppler-0.71.patch::"https://cgit.kde.org/kitinerary.git/patch/?id=22175dc4")
sha256sums=('f88b3a8e52ebafc6dcefa410212d911280053da53deb4155ea3670588e7d2691'
            'SKIP'
            '4cf0a78cadcddf2a850e0c2c9cefd525d68b63199b9d8c3e2fb26c19a963a274')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kitinerary-poppler-0.71.patch # Fix build with poppler 0.71
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
