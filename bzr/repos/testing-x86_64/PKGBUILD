# $Id$
# Maintainer :
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=bzr
pkgver=2.3.0
pkgrel=1
pkgdesc="A decentralized revision control system (bazaar)"
arch=('i686' 'x86_64')
url="http://www.bazaar-vcs.org"
license=('GPL')
depends=('python2')
optdepends=('python-paramiko: for sftp support')
source=(http://launchpad.net/bzr/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('2d8bc55d43209189a209361178d9d372')
sha1sums=('42a5b270e75a57883ead3cee9e623e087f16afd8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|man/man1|share/man/man1|' setup.py
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"

  # bash-completion
  install -D -m644 contrib/bash/bzr "${pkgdir}/etc/bash_completion.d/bzr"
}
