# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=bzr
pkgver=2.1.2
pkgrel=2
pkgdesc="A decentralized revision control system (bazaar)"
arch=('i686' 'x86_64')
url="http://www.bazaar-vcs.org"
license=('GPL')
depends=('python2')
source=(http://launchpad.net/bzr/2.1/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz
        unittest-compat.patch
        python2.7-compat.patch)
md5sums=('834d18558b7f3e0f36e3933f8a177506'
         '34f84b51d8d2c3240f367f2e5d310f5a'
         '0f0d8e0d31032cde59565b6c74a37e41')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's|man/man1|share/man/man1|' setup.py
  # https://bugs.launchpad.net/bzr/+bug/612096
  patch -p0 -i ${srcdir}/python2.7-compat.patch
  # https://bugs.launchpad.net/bzr/+bug/582113
  patch -p0 -i ${srcdir}/unittest-compat.patch
  python2 setup.py install --prefix=/usr --root=${pkgdir}

  # bash-completion
  install -D -m644 contrib/bash/bzr ${pkgdir}/etc/bash_completion.d/bzr
  install -D -m644 contrib/bash/bzr.simple \
		   ${pkgdir}/etc/bash_completion.d/bzr.simple
}

