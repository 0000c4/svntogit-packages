# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=mythplugins
pkgname=('mythplugins-mytharchive'
         'mythplugins-mythbrowser'
         'mythplugins-mythflix'
         'mythplugins-mythgallery'
         'mythplugins-mythgame'
         'mythplugins-mythmovies'
         'mythplugins-mythmusic'
         'mythplugins-mythnews'
         'mythplugins-mythvideo'
         'mythplugins-mythweather'
         'mythplugins-mythweb'
         'mythplugins-mythzoneminder')
pkgver=0.22
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.mythtv.org"
license=('GPL')
groups=('mythplugins')
makedepends=("mythtv>=${pkgver}" 'mplayer' 'mysql-python' 'fftw' 'dvdauthor' 
             'libexif' 'pil' 'libcdaudio' 'taglib' 'php-apache' 'perl-libwww' 
             'perl-xml-simple' 'perl-dbd-mysql')
source=(ftp://ftp.osuosl.org/pub/mythtv/${pkgbase}-${pkgver}.tar.bz2
        mtd
        mythweb.conf.apache)
md5sums=('09c8fa1058399a0c5db169a71561e985'
         '476c12ba074794ad7f4ae092bdf949d6'
         'dd3c2198b160d2e602390d4721b91232')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-all

  qmake mythplugins.pro || return 1
  make || return 1
}

package_mythplugins-mytharchive() {
  pkgdesc="MythTV plugin that lets you create DVDs from or archive your recorded shows"
  depends=("mythtv>=${pkgver}" 'libxmu' 'mysql-python' 'pil' 'dvdauthor')
  replaces=('mytharchive')
  cd "${srcdir}/${pkgbase}-${pkgver}/mytharchive"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythbrowser() {
  pkgdesc="Mini web browser for MythTV"
  depends=("mythtv>=${pkgver}" 'wget')
  replaces=('mythbrowser')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythbrowser"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythflix() {
  pkgdesc="Netflix access plugin for MythTV"
  depends=("mythtv>=${pkgver}")
  replaces=('mythflix')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythflix"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythgallery() {
  pkgdesc="Image gallery plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'libexif' 'libtiff')
  replaces=('mythgallery')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythgallery"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythgame() {
  pkgdesc="Game emulator module for MythTV"
  depends=("mythtv>=${pkgver}" 'zlib')
  replaces=('mythgame')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythgame"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythmovies() {
  pkgdesc="Local movie listings via MythTV"
  depends=("mythtv>=${pkgver}")
  replaces=('mythmovies')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythmovies"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythmusic() {
  pkgdesc="Music playing plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'cdparanoia' 'faad2' 'fftw' 'flac' 'lame'
           'libcdaudio' 'libid3tag' 'libmad' 'libvorbis' 'sdl' 'taglib')
  replaces=('mythmusic')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythmusic"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythnews() {
  pkgdesc="News checking plugin for MythTV"
  depends=("mythtv>=${pkgver}")
  replaces=('mythnews')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythnews"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythvideo() {
  pkgdesc="Video playback and browsing plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'mplayer' 'perl-libwww' 'perl-xml-simple')
  replaces=('mythvideo')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythvideo"
  make INSTALL_ROOT="${pkgdir}" install || return 1
  install -D -m755 ${srcdir}/mtd "${pkgdir}/etc/rc.d/mtd" || return 1
}

package_mythplugins-mythweather() {
  pkgdesc="Weather checking plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'wget')
  replaces=('mythweather')
  cd "${srcdir}/${pkgbase}-${pkgver}/mythweather"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}

package_mythplugins-mythweb() {
  pkgdesc="Web interface for MythTV's backend"
  depends=("mythtv>=${pkgver}" 'php-apache' 'perl-dbd-mysql')
  replaces=('mythweb')
  install=mythplugins-mythweb.install
  mkdir -p ${pkgdir}/srv/http/mythweb/{image_cache,php_sessions}
  cp -r ${srcdir}/mythplugins-$pkgver/mythweb/* ${pkgdir}/srv/http/mythweb
#  cp ${srcdir}/mythplugins-$pkgver/mythweb/data/.htaccess ${pkgdir}/srv/http/mythweb
  rm ${pkgdir}/srv/http/mythweb/README
  install -m644 ${srcdir}/mythweb.conf.apache ${pkgdir}/srv/http/mythweb/mythweb.conf.apache
  chown -R http:http ${pkgdir}/srv/http/mythweb
  chmod g+rw ${pkgdir}/srv/http/mythweb/{image_cache,php_sessions,data}
}

package_mythplugins-mythzoneminder() {
  pkgdesc="Allows for viewing of CCTV cameras through zoneminder"
  depends=("mythtv>=${pkgver}")
  replaces=('mythzoneminder')
  install=mythplugins-mythzoneminder.install
  cd "${srcdir}/${pkgbase}-${pkgver}/mythzoneminder"
  make INSTALL_ROOT="${pkgdir}" install || return 1
}
