# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gcc3
pkgver=3.3.6
pkgrel=4
pkgdesc="The GNU Compiler Collection"
arch=(i686 x86_64)
url="http://gcc.gnu.org"
license=('GPL') # with exception, per fsf.org
depends=('libstdc++5' 'gcc-libs' 'texinfo')
options=('!libtool' '!makeflags')
install=gcc.install
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-{core,g++}-${pkgver}.tar.bz2
	gcc-localeversion.patch
	gcc-3.3-pure64.patch)
md5sums=('18c52e6fb8966b7700665dca289d077f'
	 '6b3d00b8d079805be1b895f7f6ce47a0'
	 'edfe85cad727b75549fa5f8e5ac77a41'
	 'eb834abd7620a5f11492ee2c243b8346')
 
build() {
  cd "${srcdir}/gcc-${pkgver}"
  patch -Np0 -i "${srcdir}/gcc-localeversion.patch" || return 1

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 -i "${srcdir}/gcc-3.3-pure64.patch" || return 1
  fi

  # Don't run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  # Don't install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  export CFLAGS="${CFLAGS/-mtune=generic/}"
  export CXXFLAGS="${CXXFLAGS/-mtune=generic/}"

  mkdir ../gcc-build
  cd ../gcc-build
  ../gcc-${pkgver}/configure --prefix=/usr --enable-shared \
      --enable-languages=c,c++ --enable-threads=posix \
      --enable-__cxa_atexit  --disable-multilib --libdir=/usr/lib \
      --enable-clocale=gnu --program-suffix=-3.3 --mandir=/usr/share/man \
      --infodir=/usr/share/info

  make bootstrap || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -f ${pkgdir}/usr/lib/lib{stdc++,supc++,gcc_s}.*
  rm -f ${pkgdir}/usr/share/locale/*/LC_MESSAGES/libstdc++.mo
  rm -rf "${pkgdir}/usr/share/man/man7"
  mv "${pkgdir}/usr/share/man/man1/cpp.1" "${pkgdir}/usr/share/man/man1/cpp-3.3.1" || return 1
  mv "${pkgdir}/usr/share/man/man1/gcov.1" "${pkgdir}/usr/share/man/man1/gcov-3.3.1" || return 1

  for inf in cpp cppinternals gcc gccint; do
    mv ${pkgdir}/usr/share/info/${inf}.info ${pkgdir}/usr/share/info/${inf}-3.3.info
    gzip -9 ${pkgdir}/usr/share/info/${inf}-3.3.info
  done
}
