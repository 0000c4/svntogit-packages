# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcharselect
pkgver=17.03.90
pkgrel=1
pkgdesc='Character Selector'
url="https://kde.org/applications/utilities/kcharselect/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kbookmarks')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
groups=('kde-applications' 'kdeutils')
conflicts=('kdeutils-kcharselect')
replaces=('kdeutils-kcharselect')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kcharselect-${pkgver}.tar.xz"{,.sig})
sha256sums=('c963de8abb38bae3d2ef8e2a50ec1686a6057a1eb55caf9e9748ddb4d445ba97'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
