# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcharselect
pkgver=17.04.3
pkgrel=1
pkgdesc='Character Selector'
url="https://kde.org/applications/utilities/kcharselect/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kbookmarks')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
groups=('kde-applications' 'kdeutils')
conflicts=('kdeutils-kcharselect')
replaces=('kdeutils-kcharselect')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kcharselect-${pkgver}.tar.xz"{,.sig})
sha256sums=('5d1bc04abffe2c28e86317d18963f710e0d26469424aec2bc61fdc589ba4c3b3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
