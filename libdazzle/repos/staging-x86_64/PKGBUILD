# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdazzle
pkgver=3.28.0
pkgrel=1
pkgdesc="A library to delight your users with fancy features"
url="https://git.gnome.org/browse/libdazzle"
arch=(x86_64)
license=(LGPL)
depends=(gtk3)
makedepends=(git meson vala gobject-introspection gtk-doc)
checkdepends=(xorg-server-xvfb)
_commit=00c71f0b6815c10fec8568f2ece6ffd7255a7af7  # tags/3.28.0^0
source=("git+https://gitlab.gnome.org/GNOME/libdazzle.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build -Denable_gtk_doc=true
  ninja -C build
}

check() {
  cd build
  xvfb-run meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
