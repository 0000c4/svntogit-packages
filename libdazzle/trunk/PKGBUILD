# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdazzle
pkgver=3.26.3
pkgrel=1
pkgdesc="A library to delight your users with fancy features"
url="https://git.gnome.org/browse/libdazzle"
arch=(x86_64)
license=(LGPL)
depends=(gtk3)
makedepends=(git meson vala gobject-introspection gtk-doc)
checkdepends=(xorg-server-xvfb)
_commit=35033dabaabdda2cf98686d8585f35c2ff75520c  # tags/3.26.3^0
source=("git+https://gitlab.gnome.org/GNOME/libdazzle.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -Denable_gtk_doc=true
  ninja -C build
}

check() {
  cd build
  xvfb-run meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
