# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=2.27.90
pkgrel=1
pkgdesc="The Control Center for GNOME"
arch=(i686 x86_64)
depends=('metacity>=2.27.0' 'libxss' 'desktop-file-utils' 'gnome-settings-daemon>=2.27.91' 'libgnomekbd>=2.27.4' 'libcanberra>=0.10' 'sound-theme-freedesktop' 'libunique>=1.0.8' 'gnome-menus>=2.27.5' 'evolution-data-server>=2.27.91' 'librsvg>=2.26.0')
makedepends=('gnome-doc-utils>=0.17.3' 'pkgconfig' 'intltool' 'libgnomeui')
url="http://www.gnome.org"
groups=('gnome')
replaces=('control-center')
conflicts=('control-center')
provides=('control-center')
install=gnome-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e0c1d474f9f780782801974c05c7076a29e2c556e4775defce26334b77711656')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-aboutme \
      --disable-scrollkeeper --disable-update-mimedb || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-control-center-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  # --disable-update-mimedb?
  rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
