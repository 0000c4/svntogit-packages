# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=2.27.92
pkgrel=1
pkgdesc="Archive manipulator for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('desktop-file-utils' 'gconf>=2.27.0')
makedepends=('intltool' 'gnome-doc-utils>=0.17.5' 'pkgconfig' 'nautilus>=2.27.92')
optdepends=('nautilus: nautilus shell extension')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('ccd73fbae830ef96ebfa13ff2850519eaa069be11d717e1df235cf8e581b01d8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/file-roller \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain file-roller ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
