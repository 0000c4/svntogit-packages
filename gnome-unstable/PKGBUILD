# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=2.27.6
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf>=2.27.0' 'pygtksourceview2>=2.7.0' 'enchant>=1.4.2' 'desktop-file-utils' 'iso-codes>=3.10.1' 'gtksourceview2>=2.7.3' 'libsm')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.17.5' 'intltool')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('189e68d03e3bfc7518d2dcd6a20882388ea21b1200f3bc78388991d420558570')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gedit ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
