# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-disk-utility
pkgver=0.5
pkgrel=1
pkgdesc="GNOME libraries and applications for dealing with storage devices"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org"
depends=('dbus-glib>=0.80' 'gnome-keyring>=2.27.90' 'gtk2>=2.17.11' 'libunique>=1.1.2' 'devicekit-disks>=006' 'libnotify>=0.4.5' 'hicolor-icon-theme')
makedepends=('gnome-doc-utils' 'gtk-doc' 'intltool')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-disk-utility.install
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.bz2
        bug593381.patch)
md5sums=('1764bfcaf3383d2a22c1d73d80be4f66'
         '0f232c09f8371c9d89940021061f0d91')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/bug593381.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-disk-utility \
      --disable-scrollkeeper --disable-nautilus || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
