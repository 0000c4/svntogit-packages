# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=2.28.0
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf>=2.27.0' 'vte>=0.21.7' 'startup-notification>=0.10')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.17.5' 'intltool') 
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-terminal.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c3f1ce6248e47cac5f3c5c0a79a079d5c7756ba1c4867eb43e20e929087b4a5a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-terminal ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
