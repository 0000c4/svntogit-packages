# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime
pkgver=2.4.9
pkgrel=1
pkgdesc="Core mime parsing library"
arch=(i686 x86_64)
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2>=2.21.6' 'zlib')
makedepends=('gtk-sharp-2>=2.12.8' 'pkgconfig')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.4/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('74ace088c6892da131fcc57f1ed4b5b802ac8f279aea1fed264b7f026af3b255')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # These are gmime alternatives for the same shareutils tools
  mv ${pkgdir}/usr/bin/uuencode ${pkgdir}/usr/bin/guuencode || return 1
  mv ${pkgdir}/usr/bin/uudecode ${pkgdir}/usr/bin/guudecode || return 1
}
