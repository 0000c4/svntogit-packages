# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-session
pkgver=2.27.5
pkgrel=1
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('devicekit-power>=010' 'gtk2>=2.17.9' 'gconf>=2.26.2-3' 'startup-notification>=0.10' 'hicolor-icon-theme' 'libxtst' 'polkit-gnome>=0.94')
makedepends=('pkgconfig' 'intltool' 'xtrans')
options=(!emptydirs)
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2
        polkit1.patch)
sha256sums=('9089a1959aae16eb7a9116d5d50e83b798495724e358e92ae2600c6a79575284'
            '257da057c3579abcc7b22dc7079980f8004a5c6e6221c87694c40fc3b35d4747')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/polkit1.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --with-at-spi-registryd-directory=/usr/lib/at-spi \
      --enable-polkit --enable-splash || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-session-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
