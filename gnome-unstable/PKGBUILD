# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=2.27.92
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=(i686 x86_64)
license=('LGPL')
depends=('gconf>=2.27.0' 'libsoup-gnome>=2.27.92')
makedepends=('pkgconfig' 'intltool' 'gtk-doc')
options=('!libtool' '!emptydirs')
conflicts=('gnome-applets<2.22.0')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2
        01_gettext_not_xml.patch)
sha256sums=('e35c974de22dbba5019eeb54b4177872f53fed4124106f7b57a6973e1b73e2ee'
            'd01b9656fa89af6c6ec2424c9550d989e51755d352b48018489d8a6ea9b719ca')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/01_gettext_not_xml.patch" || return 1
  libtoolize --force || return 1
  gtkdocize || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-locations-compression || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgweather ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
