# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk2
pkgver=2.17.11
pkgrel=1
pkgdesc="The GTK+ Toolkit (v2)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk2.install
depends=('atk>=1.27.90' 'pango>=1.25.5' 'libxcursor' 'libxinerama' 'libxrandr>=1.3.0' 'libxi>=1.2.1' 'libcups>=1.4.0' 'libxcomposite' 'libxdamage' 'heimdal>=1.2.1' 'gnutls>=2.8.3' 'shared-mime-info')
makedepends=('pkgconfig' 'gtk-doc')
replaces=('gtkprint-cups' 'gail')
conflicts=('gtkprint-cups' 'gail')
provides=('gail=1.22.3')
options=('!libtool' '!docs')
backup=(etc/gtk-2.0/gtkrc)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.17/gtk+-${pkgver}.tar.bz2
        libjpeg-7.patch
        gtk2-fix-install.patch)
sha256sums=('dbbb13462ec385ba6803f08b5c6d15342b74282828b2fe699e75723aa0e89c70'
            '977ba13f3e2778cb82aadfd5e27fc4a8b715346e30e1cd1e6e76bec30079bf99'
            '08cb92a62ce3318efa3a0cf0fcb25cc6dadfec3699d715476570946d062c28fa')

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}/libjpeg-7.patch" || return 1
  patch -Np1 -i "${srcdir}/gtk2-fix-install.patch" || return 1

  autoreconf || return 1
  CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-xinput=yes \
      --without-libjasper \
      --with-included-loaders=png || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc" || return 1
}
