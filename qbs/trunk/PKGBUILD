# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Donald Carr <sirspudd@gmail.com>
# Contributor: Jake Petroules <jake.petroules@qt.io>

pkgname=qbs
pkgver=1.11.0
pkgrel=2
pkgdesc='Cross platform build tool'
arch=(x86_64)
url='https://wiki.qt.io/Qbs'
license=(LGPL)
depends=(qt5-script)
source=(http://download.qt.io/official_releases/qbs/${pkgver}/qbs-src-${pkgver}.tar.gz)
sha256sums=('6ce30c468e688f6843468324a34a7191409b471c3ff6f3e834ef8bfa7b3467cd')

build() {
  cd $pkgname-src-$pkgver
  qmake QBS_INSTALL_PREFIX=/usr \
    CONFIG+=qbs_enable_project_file_updates \
    QBS_LIBEXEC_INSTALL_DIR=/usr/lib/qbs \
    qbs.pro
  make
}

package() {
  cd $pkgname-src-$pkgver
  INSTALL_ROOT="$pkgdir" make install

  # For some reason they don't install all the headers so we'll have to do that
  # by hand.
  cp src/lib/corelib/tools/*.h "$pkgdir"/usr/include/qbs/tools/
}
