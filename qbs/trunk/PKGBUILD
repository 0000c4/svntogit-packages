# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Donald Carr <sirspudd@gmail.com>
# Contributor: Jake Petroules <jake.petroules@qt.io>
pkgname=qbs
pkgver=1.8.1
pkgrel=2
pkgdesc='Cross platform build tool'
arch=('x86_64' 'i686')
url='https://code.qt.io/qt-labs/qbs.git'
license=('LGPL')
depends=('qt5-base' 'qt5-script')
makedepends=('gcc')
source=($pkgname-$pkgver.tar.gz::"https://github.com/qt-labs/qbs/archive/v$pkgver.tar.gz"
        qbs-missing-header.patch::"https://github.com/qt-labs/qbs/commit/4143de9a.patch")
sha512sums=('6e8bff90cb8c412fd8645bf174e0e32ba8bab1bb0007b18bc4e543f9e80003745c1973b2fb753ed320b956d061685bd1c6d3ea618431e8d4ccbee21599a16a29'
            '218292bdcd05141c6546e6b3cf757288ca05624cd4605629389aea928bc7525874dd3fd8a64fa7aaf2eefd6916f48f238b23dd167b7a6e47c1d0af1f37824917')

prepare() {
  cd $pkgname-$pkgver
# Fix missing header
  patch -p1 -i ../qbs-missing-header.patch
}

build() {
  cd qbs-$pkgver
  qmake QBS_INSTALL_PREFIX=/usr QBS_LIBEXEC_INSTALL_DIR=/usr/lib/qbs QBS_RELATIVE_LIBEXEC_PATH=../lib/qbs CONFIG+=qbs_enable_project_file_updates qbs.pro
  make
}

package() {
  cd qbs-$pkgver
  INSTALL_ROOT="$pkgdir" make install
}
