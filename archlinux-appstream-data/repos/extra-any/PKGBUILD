# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20180318
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('193198cd8cfdb4ae515eda48c2255cb24547d5790d2c51c70d7add598a967d75'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'd7ec83d9dd26d28895a4a32df19015ce7f0cab95e6f24d45e3f92d23a2416697'
            'acf941b86fbc9458e717be9bbd6df771d39ff026cf867896446597d26227cab1'
            '227238bce83e0ca549e934e1a3e51ad94f3110c644b011a26257cb42167fcbd8'
            '8f616bc95d4d058e2a3a5b0f50199f05bd5b3c7d59415e2e9e98312930faa664'
            'fceecf8db2841d50499ba8881336b9a4be65db590fcbf1c074e5ae075236a1ee'
            'ffc5d76c8cf2d134efc3410786a27e2b64884887a072cd35a9ba4c9d05bff272')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
