# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20171017
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-64x64.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz $_repo-icons-{64x64,128x128}.tar.gz)
done
sha256sums=('1433a8a1a0f3bb7a2fb9bed816fb611e25fc0aa175439f07c646dcae512add0e'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '9b066cd170252d91eccd8072019dbeae583d6d9f0bfac87ca1afee91595ba31c'
            'cc93f064a835a4fa0640d55cae5185351742de82f9887c28b0eec5f70f2ae757'
            '641fd8f7a024082cc61f2f2bfd518c91a4327b76b34f3dbbd3ba39654fd1a666'
            '60aee3c13a3247239807a243fdf1cdc751eb21ca78a371b827a49f8409ae3483'
            '105711f0cca9534a57e852af7028ac28e1f9ec6d69c0307e7b9c46439a5781ea'
            '340b159077bbfdeef9f0ba8531c2bfa1acc326fad8b418279f4d7078898e80a9')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
