# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20170719
pkgrel=2
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}.xml.gz
        https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}-icons-64x64.tar.gz
        https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
sha256sums=('da6f7e30fa64478d2c25891065b50590c3304558a70112a1be4a5d34de6b6376'
            'aa13956066b1a92704fadc2e29d6ec41933268413d0318d7cf4c90745e693e36'
            '34d860a76294a0971d247e776bfd897a2041d7bd4f7a7459d1f1006c8aa7180f'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '5be911b8e87e301a2363303aa182ca69f6e9059f0775cd1941905f40efab11cb'
            '28b4e0fa50e141b441dad986a6b7385bf4a2330c39df4c41298f2006af5c9869'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '4970bfa1803211b7b6040c597a1ec5385b4fc85f3522828debb4206c19b238e5'
            '74351560c78933a74c2162f615734633c8285f6bb477109ae5bf306e5e5a5c2d')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
