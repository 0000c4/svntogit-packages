# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20170711
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}.xml.gz
 https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-64x64.tar.gz
 https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
md5sums=('cedb6c57a3f6e5eadfa5c36442f6ea24'
         '3373a17226323e4b9b4a1e73aa4f3f0a'
         '8be57611fef339537cbfd6cec3a8f27f'
         '31f6566d35ccd604be46ed5b1f813cdf'
         '6758c141c3fbc172bfc519d1df555893'
         '1a1270352c835938dbb411a96eadbd3c'
         '31f6566d35ccd604be46ed5b1f813cdf'
         'ecaf582b585f8a0b4eec479d3a4521b2'
         'a74649657f2816f351e8f06bdd400bf3')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
