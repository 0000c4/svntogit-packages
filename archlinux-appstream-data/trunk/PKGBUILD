# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20170719
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}.xml.gz
        https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}-icons-64x64.tar.gz
        https://sources.archlinux.org/other/packages/$pkgname/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
sha256sums=('6141dfee0cc6137f287bf3fcf81dd2a147c2386c452ee0db0369186eef7e2123'
            '7497b8488d9f2e3fb1c77ddbaee1b62365301a2dcb5c7c25df22c4b165df34b9'
            '6fa425376c01d51d495bdfb70c9e37bc08a0212db7acf83f6adcc716a1cc0fa2'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '74806ea0c6027f20687e951064b8df00049d5d56a705a9069b095bc853dcb910'
            '9f3bb45d30b65744c1c2699f948cf4c46bb8cf1759d0b320bb975c41717cda4b'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '5aa069f3a401747357d1480bb0d865b17020cd451846572ea4d826d5c400a28c'
            'daa56376c713198e7c3210f51c0bf0c32fcf78ccd5607152c1ea4b526e3342cc')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
