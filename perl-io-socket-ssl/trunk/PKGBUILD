# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Francois Charette <francois.archlinux.org>

pkgname=perl-io-socket-ssl
_cpanname=IO-Socket-SSL
pkgver=1.53
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('perl-net-ssleay' 'perl>=5.10.0')
options=(!emptydirs)
source=(http://www.cpan.org/authors/id/S/SU/SULLR/${_cpanname}-${pkgver}.tar.gz)
md5sums=('a99c266408ba64a184dc8a6e090ab8bf')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}

check() {
  cd ${srcdir}/${_cpanname}-${pkgver}
  make test
}
