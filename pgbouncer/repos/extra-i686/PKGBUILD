# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pgbouncer
_dlid=3393
pkgver=1.5.4
pkgrel=3
pkgdesc="A lightweight connection pooler for PostgreSQL"
arch=('i686' 'x86_64')
url="http://pgfoundry.org/projects/pgbouncer"
license=('BSD')
depends=('libevent>=2.0' 'postgresql')
makedepends=('asciidoc' 'xmlto')
backup=('etc/pgbouncer/pgbouncer.ini')
install=$pkgname.install
source=("http://pgfoundry.org/frs/download.php/${_dlid}/${pkgname}-${pkgver}.tar.gz"
        pgbouncer.ini
        pgbouncer.service
        pgbouncer.tmpfiles.conf)
md5sums=('9ffaf2e6232e18e676651429813732df'
         '810813b9ef8891be382be9ffdd425edc'
         '9036ba469f700d52fc49ed406da089a2'
         '472659f4c5de2b59a420f5ca42327ec1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 ../pgbouncer.ini "$pkgdir/etc/pgbouncer/pgbouncer.ini"
  install -D -m644 ../pgbouncer.service "$pkgdir/usr/lib/systemd/system/pgbouncer.service"
  install -D -m644 ../pgbouncer.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/pgbouncer.conf"
  mkdir -p "$pkgdir/var/log/pgbouncer"
}

# vim:set ts=2 sw=2 et:
