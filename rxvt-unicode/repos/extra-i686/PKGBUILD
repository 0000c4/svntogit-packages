# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

pkgname=rxvt-unicode
pkgver=9.10
pkgrel=1
pkgdesc="An unicode enabled rxvt-clone terminal emulator (urxvt)"
arch=(i686 x86_64)
depends=('gcc-libs' 'libxft' 'gdk-pixbuf2')
makedepends=('ncurses' 'perl>=5.12.2' 'pkgconfig')
optdepends=('perl: lots of utilities' 'gtk2-perl: to use the urxvt-tabbed')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=("GPL")
source=(http://dist.schmorp.de/rxvt-unicode/${pkgname}-${pkgver}.tar.bz2 \
        ${pkgname}.desktop ${pkgname}.png popup-menu-hang.diff)
md5sums=('a23aa40b31e843878b6f9c44768de430'
         '5bfefa1b41c2b81ca18f2ef847330543'
         '84328cada91751df07324d95f8be4d1b'
         'aead33ba3b08eeb251fb0c1427a4a024')
sha1sums=('54c896a4885b8984b0dfa652269c3c34c4d75f2d'
	  '79b7cacb7d6ce4a34f8a31a2e33475a8f7bf69cb'
	  '9ff58d7b1d60838c51c9045bc0be342fac456790'
          'ec4013f4af3a45da928b75f40fbf6389070e7fdd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../popup-menu-hang.diff
  ./configure --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-256-color \
    --enable-font-styles \
    --enable-xim \
    --enable-keepscrolling \
    --enable-selectionscrolling \
    --enable-smart-resize \
    --enable-pixbuf \
    --enable-transparency \
    --enable-utmp \
    --enable-wtmp \
    --enable-lastlog \
    --disable-frills
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}/usr/share/terminfo"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  make DESTDIR="${pkgdir}" install
 # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  install -Dm 755 doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed"
 # install freedesktop menu and icon ( icon from cvs checkout )
  install -Dm644 ../${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 ../${pkgname}.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
