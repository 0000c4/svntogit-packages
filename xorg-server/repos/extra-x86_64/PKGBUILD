# $Id$
# Maintainer: Alexander Baldeck <kth5@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-server
pkgver=1.5.3
pkgrel=5
pkgdesc="X.Org X servers"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('libx11>=1.2' 'libxkbui>=1.0.2' 'libgl' 'libxfont>=1.4.0' 'openssl>=0.9.8k' 'libxxf86misc>=1.0.1' 'libpciaccess>=0.10.5' 'libxv>=1.0.4' 'libxaw>=1.0.5' 'pixman>=0.14.0' 'hal>=0.5.11' 'xcursor-themes' 'xkeyboard-config>=1.5' 'xorg-server-utils' 'xorg-utils' 'xorg-fonts-misc' 'xbitmaps' 'libdrm>=2.3.1' 'diffutils' 'xf86-input-evdev')
makedepends=('glproto>=1.4.9' 'xf86driproto>=2.0.4' 'mesa>=7.2' 'randrproto>=1.2.2' 'renderproto>=0.9.3' 'scrnsaverproto>=1.1.0' 'fontsproto>=2.0.2' 'videoproto>=2.2.2' 'compositeproto>=0.4' 'trapproto>=3.4.3' 'resourceproto>=1.0.2' 'xineramaproto>=1.1.2' 'evieext>=1.0.2' 'libxkbfile>=1.0.5' 'libxmu>=1.0.4' 'libxpm>=3.5.7' 'libxxf86dga>=1.0.2' 'inputproto>=1.5.0' 'xcmiscproto>=1.1.2' 'bigreqsproto' 'libdrm=2.3.1' 'xtrans')
options=('!libtool')
provides=('x-server')
groups=('xorg')
install=xorg-server.install
source=(${url}/releases/individual/xserver/${pkgname}-${pkgver}.tar.bz2
	xserver-1.5-branch-4970d.patch
	xorg-redhat-die-ugly-pattern-die-die-die.patch
	xserver-1.5.0-force-SwitchCoreKeyboard-for-evdev.patch
	xserver-1.5.3-AEI-on-by-default.patch
	xsync-fix-idletime.patch
	libxfont-1.4.0-buildfix.patch
	disable-loadable-fontrenderer.patch
	remove-NOFONTSERVERACCESS.patch
	remove-xprintism.patch)
md5sums=('308971036e25250e7fe3cccfd5a120f8'
         '61f9bb524501247dce647b218ad14347'
         '1a336eb22e27cbf443ec5a2ecddfa93c'
         '7b03ba9b75d795e4f7fdfaa550d97159'
         '63d1d61d217005db7c23c4a98fc3e7eb'
         '620a3f8554328edf2b0760d5a45ddff3'
         '9e201c4e408c9eab6c0bf2e4c25c6992'
         '1c62e0f713f5df16db86c9db77d13b7c'
         '51136fda7acc0c891752257d4236434c'
         '8fde54747a1f2f81dd3535437a25e789')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Upstream commits from server-1.5 branch
  patch -Np1 -i "${srcdir}/xserver-1.5-branch-4970d.patch" || return 1

  # Get rid of the ugly pattern
  patch -Np3 -i "${srcdir}/xorg-redhat-die-ugly-pattern-die-die-die.patch" || return 1

  # Patch from Fedora to fix FS#12248
  patch -Np1 -i "${srcdir}/xserver-1.5.0-force-SwitchCoreKeyboard-for-evdev.patch" || return 1

  # Upstream patch to fix evdev issues with >=2.1.0
  patch -Np1 -i "${srcdir}/xserver-1.5.3-AEI-on-by-default.patch" || return 1

  # Fix idle time calculation, backport from server-1.6 branch
  patch -Np1 -i "${srcdir}/xsync-fix-idletime.patch" || return 1

  # Buildfixes for new libxfont
  patch -Np1 -i "${srcdir}/remove-NOFONTSERVERACCESS.patch" || return 1
  patch -Np1 -i "${srcdir}/remove-xprintism.patch" || return 1
  patch -Np1 -i "${srcdir}/disable-loadable-fontrenderer.patch" || return 1
  patch -Np1 -i "${srcdir}/libxfont-1.4.0-buildfix.patch" || return 1

  # Fix dbus config path
  sed -i -e 's/\$(sysconfdir)/\/etc/' config/Makefile.*  || return 1

  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1

  ./configure --prefix=/usr \
              --enable-ipv6 \
              --enable-dri \
              --disable-dmx \
              --enable-xvfb \
              --enable-xnest \
              --enable-composite \
              --enable-xcsecurity \
	      --disable-appgroup \
              --enable-xorg \
              --enable-xtrap \
              --enable-xevie \
	      --enable-xephyr \
              --enable-glx-tls \
              --enable-xorgcfg \
	      --enable-kdrive \
              --enable-install-setuid \
              --enable-config-hal \
	      --enable-config-dbus \
	      --enable-record \
	      --disable-xfbdev \
	      --disable-xfake \
	      --disable-xsdl \
	      --disable-kdrive-vesa \
	      --disable-dri2 \
              --disable-xprint \
              --disable-static \
              --sysconfdir=/etc/X11 \
              --localstatedir=/var \
              --with-default-font-path=/usr/share/fonts/misc,/usr/share/fonts/100dpi:unscaled,/usr/share/fonts/75dpi:unscaled,/usr/share/fonts/TTF,/usr/share/fonts/Type1 \
              --with-xkb-path=/usr/share/X11/xkb \
              --with-xkb-output=/var/lib/xkb \
              --with-dri-driver-path=/usr/lib/xorg/modules/dri || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -rf "${pkgdir}/var/log" || return 1

  install -m755 -d "${pkgdir}/etc/X11" || return 1
  install -m755 -d "${pkgdir}/var/lib/xkb" || return 1

  # No longer needed, but as nvidia-utils mangles these files,
  # keep them like this so they can be restored
  mv "${pkgdir}/usr/lib/xorg/modules/libwfb.so" \
     "${pkgdir}/usr/lib/xorg/modules/libwfb.so.1.4" || return 1

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg" || return 1

  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libdri.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libdri.xorg" || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
