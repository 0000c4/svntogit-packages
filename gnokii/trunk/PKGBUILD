# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.26
pkgrel=2
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb' 'libxpm' 'bluez>=4.1' 'libical')
makedepends=('gtk2>=2.12.11' 'libmysqlclient>=5.0.60' 'postgresql-libs>=8.3.3')
optdepends=('gtk2: xgnokii GUI'
            'libmysqlclient: smsd mysql backend'
	    'postgresql-libs: smsd postgresql backend')
backup=(etc/gnokiirc)
options=('!libtool' '!makeflags')
source=(http://www.gnokii.org/download/gnokii/gnokii-${pkgver}.tar.bz2 gnokii.patch)
md5sums=('60f817d8ce748fea0ec9a05b3537e08d'
         '4d764727686ee34bc73489c8b1321c82')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/gnokii.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --enable-security || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/etc"
  install -m644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc" || return 1
}
