# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20120119
pkgrel=1
pkgdesc='Outline and bitmap font editor'
arch=('i686' 'x86_64')
url='http://fontforge.sourceforge.net/'
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2' 'libspiro')
options=('!libtool' '!makeflags')
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('2c363af075e77bf35d21ee1939e3d6ab39a9f80c')

# git clone git://fontforge.git.sourceforge.net/gitroot/fontforge/fontforge; cd fontforge; git archive --prefix=${pkgname}-${pkgver}/ master | xz > ../${pkgname}-${pkgver}.tar.xz

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/python /python2 /g' Makefile.dynamic.in
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-type3 \
		--enable-devicetables \
		--enable-double \
		--with-regular-link \
		--with-python=python2 \
		--enable-pyextension \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
