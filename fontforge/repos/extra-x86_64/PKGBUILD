# $Id$
# Maintainer:
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20100501
pkgrel=3
pkgdesc='An outline and bitmap font editor'
arch=('i686' 'x86_64')
url='http://fontforge.sourceforge.net'
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_full-${pkgver}.tar.bz2"
	'spline-fix.patch')
sha1sums=('483425116c6af6ad90e37f9ada0c28e34f251ec3'
          '05d7603aa8873c1f417d69e95d647116b51061b6')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Fixes regression affecting lilypond, see FS#20991 (from SVN)
	patch -p1 < ../spline-fix.patch

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-type3 \
		--enable-double \
		--enable-devicetables \
		--with-regular-link \
		--with-python=python \
		--enable-pyextension
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -j1 DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
