# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksudoku
pkgver=17.04.0
pkgrel=1
pkgdesc="A logic-based symbol placement puzzle"
url="https://kde.org/applications/games/ksudoku/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'glu')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/${pkgver}/src/ksudoku-${pkgver}.tar.xz"{,.sig})
sha256sums=('7a63951ab81ccca46cd3fabc3ce911711fafd3aee5312e168bdaac7f5bd22110'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ksudoku-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
