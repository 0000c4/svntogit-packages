# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=at-spi2-core
pkgver=1.91.93
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus-core' 'glib2' 'libxtst')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
groups=('gnome')
source=(http://download.gnome.org/sources/${pkgname}/1.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e06fa4e17ebafb203126051000d2274f1ccaa7e04bff248f4524ac2411b29582')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  sed -i -e '/--pkg dbus-glib-1/s/-glib//' atspi/Makefile.am
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
