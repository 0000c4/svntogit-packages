# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=at-spi2-core
pkgver=1.91.92
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus-core' 'dbus-glib' 'libxtst')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
groups=('gnome')
source=(http://download.gnome.org/sources/${pkgname}/1.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('17ef8a4a54aa0499a983d4dc59d5f592840136ac9eb7d80154c287635becd959')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
