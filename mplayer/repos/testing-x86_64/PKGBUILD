# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer
pkgver=30286
pkgrel=1
pkgdesc="A movie player for linux"
arch=('i686' 'x86_64')
depends=('libxxf86dga' 'libxv' 'libmad' 'giflib' 'cdparanoia' 'libxinerama'
         'sdl' 'lame' 'libtheora' 'xvidcore' 'zlib' 'libmng' 'libxss'
         'libgl' 'smbclient' 'aalib' 'jack-audio-connection-kit' 'libcaca'
         'x264>=20100107' 'faac' 'lirc-utils' 'ttf-dejavu' 'libxvmc' 'libjpeg>=7'
	 'enca' 'libvdpau>=0.3' 'opencore-amr' 'a52dec' 'libdca')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.11')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2 liba52_gcc_bug.patch
	mplayer.desktop mplayer.png fix_building.patch )
md5sums=('bbfbe6651c09324756322f7ff7b23dbe'
         'ac53c73dd6f69d91ab0ea0591df4a653'
         'f91b1be03ed301cc15142f49455c1b88'
         'd00874ccc644b7f43d6ef1c942fcef28'
         '294c9c6af43c0453da7dd8de1a188f8a')

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  # Needed to compile using gcc 4.4.0
#  patch -p0 < ${srcdir}/liba52_gcc_bug.patch || return 1 

  cd ${srcdir}/${pkgname}
  #bad revision
  patch -p0 < ${srcdir}/fix_building.patch || return 1

  ./configure --prefix=/usr \
  	--enable-runtime-cpudetection \
	--disable-gui \
	--disable-arts \
	--disable-liblzo \
	--disable-speex \
      	--disable-openal \
	--disable-fribidi \
	--disable-libdv \
	--disable-musepack \
	--disable-esd \
	--disable-mga \
	--enable-xvmc \
	--language=all \
      	--confdir=/etc/mplayer \
	--extra-cflags="-fno-strict-aliasing" || return 1

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make || return 1
  make -j1 DESTDIR=${pkgdir} install || return 1
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/ || return 1
  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf || return 1
  rm -rf ${pkgdir}/usr/share/mplayer/font
  #desktop file  FS#14770
  install -Dm644 ${srcdir}/mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop || return 1
  install -Dm644 ${srcdir}/mplayer.png ${pkgdir}/usr/share/pixmaps/mplayer.png || return 1
}
