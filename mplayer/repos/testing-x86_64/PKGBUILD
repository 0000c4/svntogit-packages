# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer
pkgver=29188
pkgrel=2
pkgdesc="A movie player for linux"
arch=('i686' 'x86_64')
depends=('libxxf86dga' 'libxv' 'libmad' 'giflib' 'cdparanoia'
         'sdl' 'lame' 'libtheora' 'xvidcore' 'zlib' 'libmng' 'libxss'
         'libgl' 'smbclient' 'aalib' 'jack-audio-connection-kit' 'libcaca'
         'x264>=20090416' 'faac' 'lirc-utils' 'ttf-dejavu' 'libxvmc')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'live-media' 'libdca' 'mesa')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(ftp://ftp.archlinux.org/other/mplayer/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d9bb0b5eb1d8822a899c1e84d17b5995')

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS

  cd ${srcdir}/${pkgname}

  ./configure --prefix=/usr --disable-gui --disable-arts --enable-x11 \
      --enable-runtime-cpudetection --confdir=/etc/mplayer --disable-nas \
      --enable-gl --enable-tv-v4l1 --enable-tv-v4l2 --enable-largefiles \
      --disable-liblzo --disable-speex --disable-openal \
      --disable-fribidi --disable-libdv --disable-musepack \
      --language=all --disable-esd --disable-mga \
      --disable-libamr_nb --enable-lirc --enable-radio --enable-radio-capture \
      --extra-cflags="-I/usr/lib/live-media" --disable-nemesi --enable-freetype \
      --enable-xvmc || return 1

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make || return 1
  make -j1 DESTDIR=${pkgdir} install || return 1
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/ || return 1
  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf || return 1
  rm -rf ${pkgdir}/usr/share/mplayer/font
}
