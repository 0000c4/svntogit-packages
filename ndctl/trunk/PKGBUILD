# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=71
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'keyutils' 'kmod' 'systemd-libs' 'util-linux-libs')
makedepends=('asciidoctor' 'bash-completion' 'systemd' 'xmlto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz")
sha512sums=('3c5e22aee1bd1411a4c838051a00e1c2f26e69b3bdf5ac9182e82c431f6df97551e8b5e8784cce9673f0c5cb9f47ccade4506f4d6c0774977a25a768196cccfd')

prepare() {
  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' $pkgname-$pkgver/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
