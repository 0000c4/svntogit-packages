# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=70
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'keyutils' 'kmod' 'systemd-libs' 'util-linux-libs')
makedepends=('asciidoctor' 'bash-completion' 'systemd' 'xmlto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz")
sha512sums=('3ad44efd56f0ef156716f4db5b6c18241e8c5cf52f3b3a3455012f3006d21dd3e2c63a83aa7f7ac8113d61fa8419b80d6182195dff5d176272af63a2d85cb487')

prepare() {
  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' $pkgname-$pkgver/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
