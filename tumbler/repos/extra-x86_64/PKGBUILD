# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

pkgname=tumbler
pkgver=0.2.1
pkgrel=2
pkgdesc="D-Bus service for applications to request thumbnails"
arch=('x86_64')
url="http://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('gdk-pixbuf2')
makedepends=('intltool' 'python' 'ffmpegthumbnailer' 'poppler-glib' 'libgsf'
             'libopenraw' 'freetype2' 'xfce4-dev-tools')
optdepends=('ffmpegthumbnailer: for video thumbnails'
            'poppler-glib: for PDF thumbnails'
            'libgsf: for ODF thumbnails'
            'libopenraw: for RAW thumbnails'
            'freetype2: for font thumbnails')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        use-pkg-config-to-find-FreeType.patch
        accept-network-paths-in-ffmpeg-thumbnailer-plugin.patch)
sha256sums=('204b677be6c350ca2c84789ada668435de73de8460660cc6230b1604eaa8f5db'
            'e585caff41845817636d5d3c006ac63882ffc439e1f9bc4fe9361e76fa9a6c9f'
            'd9fca39e2490eebcdd782cf3d885857ca17e030dac61d5e8f2065f840441272b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../use-pkg-config-to-find-FreeType.patch
  patch -Np1 -i ../accept-network-paths-in-ffmpeg-thumbnailer-plugin.patch
  mkdir m4
  xdt-autogen
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --disable-static \
    --disable-debug \
    --disable-gstreamer-thumbnailer
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
