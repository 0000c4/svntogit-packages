# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

pkgname=tumbler
pkgver=0.1.23
pkgrel=1
pkgdesc="D-Bus service for applications to request thumbnails"
url="http://git.xfce.org/xfce/tumbler/"
license=('GPL2' 'LGPL')
arch=('i686' 'x86_64')
groups=('xfce4')
depends=('poppler-glib' 'dbus-glib' 'gdk-pixbuf2')
makedepends=('intltool' 'ffmpegthumbnailer' 'gstreamer0.10')
optdepends=('ffmpegthumbnailer:	for video thumbnails'
            'gstreamer0.10: for video thumbnails')
options=('!libtool')
source=(http://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c6cf6821f5d884cbf2c26f45f2fecc74')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
