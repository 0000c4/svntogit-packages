# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.13.0
pkgrel=2
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(intel-dri libxvmc libpciaccess libdrm xcb-util libxfixes)
makedepends=('xorg-server-devel' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'xcb-util')
conflicts=('xorg-server<1.9.0' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        git-d2bc2212.patch
        disable-pageflip.patch
        8xx-enable-shadow.patch)
md5sums=('de2f8a5836d90c71f3175dcd46d03ec0'
         '90ce91a6b32257def4b0ce973c212ae7'
         '0ac1841fb62de8ef01eb871e41f7a933'
         '2e5eb0035b06c21f15c19b68142b75c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/git-d2bc2212.patch"
  patch -Np1 -i "${srcdir}/disable-pageflip.patch"
  patch -Np0 -i "${srcdir}/8xx-enable-shadow.patch"
  ./configure --prefix=/usr --enable-dri
  make
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
