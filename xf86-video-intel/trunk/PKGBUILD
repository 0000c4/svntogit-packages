# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-intel
pkgver=2.7.0
pkgrel=3
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri>=7.4.1' 'libpciaccess>=0.10.6' 'libdrm>=2.4.9' 'libxvmc')
makedepends=('pkgconfig' 'xorg-server>=1.6.1' 'xf86driproto>=2.0.4' 'glproto>=1.4.9' 'mesa>=7.4.1' 'xineramaproto')
conflicts=('xorg-server<1.6.0' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 LICENSE
	2.7-branch.patch)
md5sums=('5832172ac69b9a066a202e1578a5d3c8'
	 'b924480931774dec14515e270db4924d'
	 '8c941e35cb56426900cade66b3c0f055')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/2.7-branch.patch" || return 1
  ./configure --prefix=/usr --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
