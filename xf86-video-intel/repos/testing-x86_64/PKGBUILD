# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-intel
pkgver=2.7.99.901
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri>=7.4.2' 'libpciaccess>=0.10.6' 'libdrm>=2.4.11-2' 'libxvmc')
makedepends=('pkgconfig' 'xorg-server>=1.6.1' 'xf86driproto>=2.0.4' 'glproto>=1.4.9' 'mesa>=7.4.2' 'xineramaproto')
conflicts=('xorg-server<1.6.1' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        fix-batch-init.patch
        LICENSE)
md5sums=('bd2052cb77094aaea76bf9d6e1ce8ce6'
         'b757cac117bcffba7a501e602a6c46a3'
         'b924480931774dec14515e270db4924d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-batch-init.patch" || return 1
  ./configure --prefix=/usr --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
