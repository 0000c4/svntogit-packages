# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=sof-firmware
pkgver=1.4.1
pkgrel=1
pkgdesc="Sound Open Firmware"
arch=(any)
url="https://www.sofproject.org/"
license=(custom:BSD custom:ISC)
options=(!strip)
source=(ftp://ftp.alsa-project.org/pub/misc/sof/$pkgname-$pkgver.tar.bz2)
sha256sums=('8ea4701f2ce25c0653fb4099c77ce810aa6554d763449eb052f1cde04d3a0bfc')

prepare() {
  cd usr/lib/firmware/intel

  mv sof/signed/debug/* sof/debug
  local f; for f in sof/signed/*-signed-intel.ri; do
    f=${f##*/}
    mv sof/signed/$f sof/${f/-signed-intel/}
  done
  rmdir sof/signed/debug sof/signed
  rm -r sof/gcc

  mv sof/LICENCE "$srcdir/LICENSE"
  rm sof-tplg/LICENCE
}

package() {
  cp -r usr "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
