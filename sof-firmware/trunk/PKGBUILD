# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=sof-firmware
pkgver=1.4.2
pkgrel=1
pkgdesc="Sound Open Firmware"
arch=(any)
url="https://www.sofproject.org/"
license=(custom:BSD custom:ISC)
options=(!strip)
source=(https://www.alsa-project.org/files/pub/misc/sof/$pkgname-$pkgver.tar.bz2)
sha256sums=('c5e91059375a4a45a7de5b156a8573349a20e130bf6e675adf8554f9800070da')

prepare() {
  cd usr/lib/firmware/intel

  mv sof/signed/debug/* sof/debug
  local f; for f in sof/signed/*-signed-intel.ri; do
    f=${f##*/}
    mv sof/signed/$f sof/${f/-signed-intel/}
  done
  rmdir sof/signed/debug sof/signed
  rm -r sof/gcc

  mv sof/LICENCE "$srcdir/LICENSE"
  rm sof-tplg/LICENCE
}

package() {
  cp -r usr "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
