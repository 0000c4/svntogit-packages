# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=tracker
pkgname=(tracker libtracker-sparql)
pkgver=1.10.1
pkgrel=1
pkgdesc="Desktop-neutral user information store, search tool and indexer"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(i686 x86_64)
license=(GPL)
makedepends=(libgee libsecret upower libexif exempi poppler-glib libgsf icu enca networkmanager
             gtk3 gobject-introspection intltool giflib gst-plugins-base-libs totem-plparser taglib
             libvorbis flac vala libgxps libnautilus-extension libmediaart libxslt libiptcdata
             libosinfo libcue libstemmer git gtk-doc libgrss)
options=('!emptydirs')
_commit=d90dfb2e185bb8d5a03c03f92fdbcf1fbae4768a  # tags/1.10.1^0
source=("git://git.gnome.org/tracker#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/tracker \
    --disable-unit-tests \
    --enable-libflac \
    --enable-libvorbis \
    --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_tracker() {
  depends=("libtracker-sparql=$pkgver-$pkgrel" libgee libsecret
           upower libexif exempi poppler-glib libgsf enca libiptcdata libcue libosinfo
           networkmanager gtk3 libmediaart libgxps taglib flac libvorbis totem-plparser
           gst-plugins-base-libs giflib libgrss gvfs)
  optdepends=("nautilus: edit files' tracker tags")
  groups=('gnome')

  cd $pkgbase
  make DESTDIR="$pkgdir" install

  make DESTDIR="$pkgdir" -C src/libtracker-sparql uninstall
  make DESTDIR="$pkgdir" -C src/libtracker-sparql-backend uninstall
  make DESTDIR="$pkgdir" -C src/libtracker-data uninstall-libtracker_dataLTLIBRARIES
  make DESTDIR="$pkgdir" -C src/libtracker-common uninstall-libtracker_commonLTLIBRARIES
}

package_libtracker-sparql() {
  pkgdesc="$pkgdesc (SPARQL library)"
  depends=(sqlite icu glib2 libffi pcre util-linux libstemmer)

  cd $pkgbase
  make DESTDIR="$pkgdir" -C src/libtracker-common install-libtracker_commonLTLIBRARIES
  make DESTDIR="$pkgdir" -C src/libtracker-data install-libtracker_dataLTLIBRARIES
  make DESTDIR="$pkgdir" -C src/libtracker-sparql install
  make DESTDIR="$pkgdir" -C src/libtracker-sparql-backend install
}
