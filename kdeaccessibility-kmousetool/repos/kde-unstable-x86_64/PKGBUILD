# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmousetool
pkgver=16.11.90
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
url='http://kde.org/applications/utilities/kmousetool/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kmousetool-${pkgver}.tar.xz"{,.sig})
sha1sums=('79d06e9e52dbd8045ca0e92cdbf3a22276802262'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kmousetool-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
