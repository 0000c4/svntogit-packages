# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalarm
pkgver=20.03.90
pkgrel=1
pkgdesc="Personal alarm scheduler"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime perl phonon-qt5)
makedepends=(extra-cmake-modules kdoctools boost kdepim-apps-libs mailcommon)
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d1154095b878a0836139b31a0ec73a86a19fc6189a769cd1b06b0f87577aa12a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
