# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=composer
pkgver=1.9.2
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php')
makedepends=('git' 'unzip')
checkdepends=('mercurial')
replaces=('php-composer')
conflicts=('php-composer')
provides=('php-composer')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
sha256sums=('SKIP'
            '58753998712ae435915a452d701ae28a9389653bbf36b3b6adf24e03d90a9467')

prepare() {
	cd "${srcdir}/${pkgname}"

	php "${srcdir}/${pkgname}-${pkgver}.phar" -n install --no-dev --prefer-dist
}

build() {
	cd "${srcdir}/${pkgname}"

	php -d phar.readonly=Off bin/compile
}

check() {
	cd "${srcdir}/${pkgname}"

	echo "Skipping tests due to incompability with PHP 7.4" 1>&2
	return

	git config --global user.email "${USER}@${HOST}"
	git config --global user.name "${USER}"

	# Install the dev dependencies to run the unit tests
	bin/composer -n install --prefer-dist

	vendor/bin/phpunit
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 composer.phar "${pkgdir}/usr/bin/composer"
}
