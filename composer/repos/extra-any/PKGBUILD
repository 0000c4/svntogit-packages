# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=composer
pkgver=1.10.6
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php')
makedepends=('git' 'unzip')
checkdepends=('mercurial')
replaces=('php-composer')
conflicts=('php-composer')
provides=('php-composer')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
sha512sums=('SKIP'
            'f5daa14bc155792b9e04424998585547fb902997ebc1c658d5786cb777517e051991a5bd0f6a4371da06e872c991d7d1255ef31d6ee6a477d644226de896948a')

prepare() {
	cd "${srcdir}/${pkgname}"

	php "${srcdir}/${pkgname}-${pkgver}.phar" -n install --no-dev --prefer-dist
}

build() {
	cd "${srcdir}/${pkgname}"

	php -d phar.readonly=Off bin/compile
}

check() {
	cd "${srcdir}/${pkgname}"

	git config --global user.email "${USER}@${HOST}"
	git config --global user.name "${USER}"

	# Install the dev dependencies to run the unit tests
	bin/composer -n install --prefer-dist

	vendor/bin/simple-phpunit
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 composer.phar "${pkgdir}/usr/bin/composer"
}
