# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=liferea
pkgver=1.4.16b
pkgrel=1
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gtk2>=2.10.11' 'gconf>=2.18.0.1-4' 'xulrunner>=1.8.1.11' 'libnotify>=0.4.4' 'libnetworkmanager' 'libxslt>=1.1.20' 'libglade' 'sqlite3')
makedepends=('perlxml' 'pkgconfig' 'diffutils')
options=('!libtool' 'force')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz)
md5sums=('b7d8ef4b5d054a7c57e53789d7259edb')
sha1sums=('cf6264716c7c9a2eebfe776c4113bf2a1bf74f32')

build() {
  [ -z "${MOZ_PLUGIN_PATH}" ] && . /etc/profile.d/mozilla-common.sh

  cd ${srcdir}/liferea-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-gtkhtml2 || return 1
  make LDFLAGS+="-R/usr/lib/xulrunner" || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
  
  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
