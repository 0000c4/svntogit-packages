# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=liferea
pkgver=1.4.16b
pkgrel=2
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf>=2.22.0' 'xulrunner>=1.9' 'libnotify>=0.4.4' 'libnetworkmanager' 'libxslt>=1.1.20' 'libglade>=2.6.2')
makedepends=('perlxml' 'pkgconfig' 'diffutils')
options=('!libtool' 'force' '!emptydirs')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz
	liferea-1.4.16b-xulrunner-1.9.patch)
md5sums=('b7d8ef4b5d054a7c57e53789d7259edb'
	 '8acce1acf4d2840b4ff2be918be62772')
sha1sums=('cf6264716c7c9a2eebfe776c4113bf2a1bf74f32'
	  '6adc9d8e4fdf0597631affeafbd21b06606595d5')

build() {
  cd ${srcdir}/liferea-${pkgver}
  patch -Np1 -i ${startdir}/src/liferea-1.4.16b-xulrunner-1.9.patch || return 1
  autoconf || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --enable-xulrunner --disable-gtkhtml2 \
	      --disable-webkit --disable-gecko || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
  
  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
