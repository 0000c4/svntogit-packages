# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=liferea
pkgver=1.10.1.a
pkgrel=1
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('webkitgtk' 'libnotify' 'json-glib' 'libpeas' 'python2-gobject' 
         'gnome-icon-theme' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('gnome-keyring: for keyring plugin support'
            'libgnome-keyring: for keyring plugin support'
	    'gst-plugins-base: for media player plugin support'
	    'gst-plugins-good: for media player plugin support'
	    'gst-plugins-bad: for media player plugin support')
options=('!libtool' '!emptydirs')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver%*.*}a.tar.bz2)
sha1sums=('c0c8508adb6ccfda46453b6a213c15ab78ad5d2c')

build() {
  cd liferea-${pkgver%*.*}a
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd liferea-${pkgver%*.*}a
  make DESTDIR="${pkgdir}" install
}
