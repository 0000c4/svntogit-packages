# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=liferea
pkgver=1.6.2
pkgrel=1
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf>=2.26.0' 'libwebkit>=1.1.10' 'libnotify>=0.4.5' 'libxslt>=1.1.24' 'libglade>=2.6.4' 'sqlite3>=3.6.15')
makedepends=('pkgconfig' 'intltool' 'lua')
optdepends=('lua: for lua scripting support')
options=('!libtool' '!emptydirs')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz)
md5sums=('dc1de9a080be73eecf53a8f551d7ad52')
sha1sums=('df3f48a2ebef780a9428c790d8fa6db885c14fe4')

build() {
  cd "${srcdir}/liferea-${pkgver}"
  libtoolize --force --copy || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-install || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas || return 1
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
