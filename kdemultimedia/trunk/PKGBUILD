# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdemultimedia
pkgname=('kdemultimedia-dragonplayer'
         'kdemultimedia-juk'
         'kdemultimedia-kioslave'
         'kdemultimedia-kmix'
         'kdemultimedia-kscd'
         'kdemultimedia-libkcddb'
         'kdemultimedia-libkcompactdisc'
         'kdemultimedia-mplayerthumbs'
         'kdemultimedia-strigi-analyzer')
pkgver=4.2.98
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdelibs' 'kdelibs-experimental' 'kdebase-runtime' 'tunepimp' 'cdparanoia' 'mplayer')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('f54daa25d2d99a15aa633a8068df6ffc')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xine=OFF \
		-DWITH_PulseAudio=OFF
	make
}

package_kdemultimedia-dragonplayer() {
	pkgdesc='Video Player'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdemultimedia.install'
	cd $srcdir/build/dragonplayer
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/dragonplayer
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-juk() {
	pkgdesc='Music Player'
	depends=('kdelibs' 'kdebase-runtime' 'tunepimp')
	install='kdemultimedia.install'
	cd $srcdir/build/juk
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/juk
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kioslave() {
	pkgdesc='Audio CD Browser'
	depends=('kdemultimedia-libkcompactdisc' 'kdemultimedia-libkcddb' 'cdparanoia' 'flac')
	cd $srcdir/build/kioslave
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kioslave
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kmix() {
	pkgdesc='Sound Mixer'
	depends=('kdelibs-experimental' 'kdebase-runtime')
	install='kdemultimedia.install'
	cd $srcdir/build/kmix
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmix
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kscd() {
	pkgdesc='CD Player'
	depends=('kdelibs' 'kdebase-runtime' 'musicbrainz')
	install='kdemultimedia.install'
	cd $srcdir/build/kscd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kscd
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-libkcddb() {
	pkgdesc='Configure the CDDB Retrieval'
	groups=()
	depends=('kdelibs' 'musicbrainz')
	cd $srcdir/build/libkcddb
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-libkcompactdisc() {
	pkgdesc='A CD drive interface for the KDE Project'
	groups=()
	depends=('kdelibs')
	cd $srcdir/build/libkcompactdisc
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-mplayerthumbs() {
	pkgdesc='Video Files (MPlayerThumbs)'
	depends=('kdelibs' 'kdebase-runtime' 'mplayer')
	replaces=('mplayerthumbs')
	conflicts=('mplayerthumbs')
	cd $srcdir/build/mplayerthumbs
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-strigi-analyzer() {
	pkgdesc='Strigi-Analyzer for KDE Multimedia'
	depends=('kdelibs')
	groups=(${groups[@]} 'strigi-analyzer')
	cd $srcdir/build/strigi-analyzer
	make DESTDIR=$pkgdir install
}
