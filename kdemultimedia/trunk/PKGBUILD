# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdemultimedia
pkgver=4.1.1
pkgrel=1
pkgdesc="KDE Multimedia Programs"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'kdebase-runtime' 'xine-lib' 'tunepimp' 'cdparanoia')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('efbf12cf33aafb13848fda258d001322')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
