# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdemultimedia
pkgname=('kdemultimedia-dragonplayer'
         'kdemultimedia-juk'
         'kdemultimedia-kioslave'
         'kdemultimedia-kmix'
         'kdemultimedia-kscd'
         'kdemultimedia-libkcddb'
         'kdemultimedia-libkcompactdisc'
         'kdemultimedia-mplayerthumbs'
         'kdemultimedia-strigi-analyzer')
pkgver=4.2.90
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdelibs' 'kdelibs-experimental' 'kdebase-runtime' 'tunepimp' 'cdparanoia')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('3a00b7590971b8fcadf465485fe59743')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_PulseAudio=OFF
	make
}

package_kdemultimedia-dragonplayer() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime' 'kdemultimedia-strigi-analyzer')
	install='kdemultimedia.install'
	cd $srcdir/build/dragonplayer
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/dragonplayer
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-juk() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime' 'tunepimp' 'kdemultimedia-strigi-analyzer')
	install='kdemultimedia.install'
	cd $srcdir/build/juk
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/juk
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kioslave() {
	pkgdesc=''
	depends=('kdebase-runtime' 'kdemultimedia-libkcompactdisc' 'kdemultimedia-libkcddb' 'cdparanoia')
	install='kdemultimedia.install'
	cd $srcdir/build/kioslave
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kioslave
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kmix() {
	pkgdesc=''
	depends=('kdelibs-experimental' 'kdebase-runtime')
	install='kdemultimedia.install'
	cd $srcdir/build/kmix
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmix
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kscd() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime' 'musicbrainz')
	install='kdemultimedia.install'
	cd $srcdir/build/kscd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kscd
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-libkcddb() {
	pkgdesc=''
	groups=()
	depends=('kdelibs' 'musicbrainz')
	install='kdemultimedia.install'
	cd $srcdir/build/libkcddb
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-libkcompactdisc() {
	pkgdesc=''
	groups=()
	depends=('kdelibs')
	install='kdemultimedia.install'
	cd $srcdir/build/libkcompactdisc
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-mplayerthumbs() {
	pkgdesc=''
	depends=('kdelibs' 'kdebase-runtime' 'kdemultimedia-strigi-analyzer')
	install='kdemultimedia.install'
	cd $srcdir/build/mplayerthumbs
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-strigi-analyzer() {
	pkgdesc=''
	depends=('kdelibs')
	install='kdemultimedia.install'
	cd $srcdir/build/strigi-analyzer
	make DESTDIR=$pkgdir install
}

