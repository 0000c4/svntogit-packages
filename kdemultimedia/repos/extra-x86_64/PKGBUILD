# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdemultimedia
pkgname=('kdemultimedia-dragonplayer'
         'kdemultimedia-ffmpegthumbs'
         'kdemultimedia-juk'
         'kdemultimedia-kioslave'
         'kdemultimedia-kmix'
         'kdemultimedia-kscd'
         'kdemultimedia-mplayerthumbs')
pkgver=4.8.4
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
makedepends=('cmake' 'automoc4' 'kdelibs' 'ffmpeg' 'mplayer' 'libpulse'
             'libmusicbrainz3' 'taglib' 'libcanberra')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz"
        'mplayerthumbs.config'
        'ffmpeg0.11.patch')
sha1sums=('dac721e82308a6347d578361e25da7c466a26ba7'
          'ba016fa2563c14ffcba852c62506b66bfc6ee683'
          '48331f7e2dab760c20708b81fe6c4d47c83a63e7')

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}/ffmpegthumbs
    patch -p1 -i ${srcdir}/ffmpeg0.11.patch

    cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_TunePimp=OFF
	make
}

package_kdemultimedia-dragonplayer() {
	pkgdesc='Video Player'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/multimedia/dragonplayer/"
	install='kdemultimedia-dragonplayer.install'
	cd $srcdir/build/dragonplayer
	make DESTDIR=$pkgdir install
    cd $srcdir/build/dragonplayer/doc
    make DESTDIR=$pkgdir install
}

package_kdemultimedia-ffmpegthumbs() {
	pkgdesc='Video Files (ffmpegthumbs)'
	depends=('kdebase-runtime' 'ffmpeg')
	install='kdemultimedia.install'
	cd $srcdir/build/ffmpegthumbs
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-juk() {
	pkgdesc='Music Player'
    depends=('kdebase-runtime' 'taglib')
	url="http://kde.org/applications/multimedia/juk"
	install='kdemultimedia.install'
	cd $srcdir/build/juk
	make DESTDIR=$pkgdir install
    cd $srcdir/build/juk/doc
    make DESTDIR=$pkgdir install
}

package_kdemultimedia-kioslave() {
	pkgdesc='Audio CD Browser'
	depends=('kdelibs' 'libmusicbrainz3' 'cdparanoia')
	conflicts=('kdemultimedia-libkcddb' 'kdemultimedia-libkcompactdisc')
	replaces=('kdemultimedia-libkcddb' 'kdemultimedia-libkcompactdisc')
	for i in kioslave libkcddb libkcompactdisc; do
		cd $srcdir/build/${i}
		make DESTDIR=$pkgdir install
	done
}

package_kdemultimedia-kmix() {
	pkgdesc='Sound Mixer'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/multimedia/kmix"
	install='kdemultimedia.install'
	cd $srcdir/build/kmix
	make DESTDIR=$pkgdir install
    cd $srcdir/build/kmix/doc
    make DESTDIR=$pkgdir install
}

package_kdemultimedia-kscd() {
	pkgdesc='CD Player'
	depends=('kdebase-runtime' 'libmusicbrainz3')
	url="http://kde.org/applications/multimedia/kscd"
	install='kdemultimedia.install'
	cd $srcdir/build/kscd
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-mplayerthumbs() {
	pkgdesc='Video Files (MPlayerThumbs)'
	depends=('kdebase-runtime' 'mplayer')
	replaces=('mplayerthumbs')
	conflicts=('mplayerthumbs')
	cd $srcdir/build/mplayerthumbs
	make DESTDIR=$pkgdir install
	install -D -m644 $srcdir/mplayerthumbs.config $pkgdir/usr/share/config/mplayerthumbs
}
