# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.9.0
pkgrel=1
pkgdesc="A service which logs the users's activities and events and makes relevant information available to other applications"
arch=('i686' 'x86_64')
url="http://zeitgeist-project.com/"
license=('GPL')
depends=('dbus-python' 'python2-gobject2' 'pyxdg' 'xapian-core')
makedepends=('intltool' 'raptor' 'python2-rdflib')
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('fc2706611f435a027cd520f0da0982d7'
         '96e74a5050bf0daa961dd56b0f73fd5d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/python -/$PYTHON -/' configure
  PYTHON=/usr/bin/python2 LIBS+="-lgmodule-2.0" ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/zeitgeist --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" zeitgeistdocdir=/usr/share/doc/zeitgeist install
  install -D -m644 data/zeitgeist-daemon.bash_completion "$pkgdir/usr/share/bash-completion/completions/zeitgeist-daemon"
}
