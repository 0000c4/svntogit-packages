# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=1.0
pkgrel=1
pkgdesc="Service logging user activities and events"
arch=(i686 x86_64)
url="https://launchpad.net/zeitgeist/"
license=(GPL2 LGPL2.1)
depends=(json-glib telepathy-glib gtk3 xapian-core sqlite)
makedepends=(gobject-introspection vala raptor python2-rdflib)
provides=(zeitgeist-datahub)
conflicts=(zeitgeist-datahub)
replaces=(zeitgeist-datahub)
source=(https://launchpad.net/zeitgeist/${pkgver:0:3}/${pkgver}/+download/zeitgeist-${pkgver}.tar.xz{,.asc})
sha256sums=('5ff508508f7a7b46acc9fbf1cf73fecb1aec214e18d9d22325aae8c2c2e8ddc8'
            'SKIP')
validpgpkeys=('E4884AEEDE4CC02043C3D8045DECDBA89270E723') #Rico Tzschichholz <ricotz@ubuntu.com>

prepare() {
  cd $pkgname-$pkgver

  sed -i 's/python -/$PYTHON -/' configure.ac
  sed -i '1s/python$/python2/' data/ontology2code

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --enable-fts
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  # Log test fails in chroot, as we have no X and no proper dbus
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
