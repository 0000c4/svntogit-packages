# $ID$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-lingua-translit
_dist=Lingua-Translit
pkgver=0.26
pkgrel=1
pkgdesc="Transliteration of text between writing systems"
arch=('any')
url='https://metacpan.org/release/${_dist}'
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/A/AL/ALINKE/${_dist}-${pkgver}.tar.gz)
md5sums=('fa13fd95053cafd590fef213477196d7')
sha512sums=('33db0a60d8f60328f958bfa9e313d985b54c79a4556e9a5dba695ac4afea7d1148a2b6225dbcc626b56214ba01e04ee53418b712e3d20738e84baa2287874f48')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
