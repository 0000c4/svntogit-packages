# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-juk
pkgver=16.12.0
pkgrel=1
pkgdesc='A jukebox, tagger and music collection manager'
url='http://kde.org/applications/multimedia/juk/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdemultimedia')
depends=('kdebase-runtime' 'taglib')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/juk-${pkgver}.tar.xz"{,.sig})
sha1sums=('aeb41c2bb5ea16537dde776e7fa7e5461018b074'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../juk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TunePimp=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
