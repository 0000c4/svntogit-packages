# $Id$
# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.32.1.1
pkgrel=1
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libunique>=1.1.6' 'gconf>=2.32.0' 'vte>=0.26.2' 'gdl>=2.30.1' 'autogen>=5.9.9' 'devhelp>=2.32.0' 'gtksourceview2>=2.10.5' 'glade>=3.6.7' 'libgda>=4.2.1' 'subversion>=1.6.9' 'gnome-icon-theme>=2.31.0' 'hicolor-icon-theme' 'vala>=0.10.1' 'graphviz>=2.6.23' 'perl-locale-gettext')
makedepends=('gnome-doc-utils>=0.20.2' 'intltool')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('f2b67609890a45d907ab5b42fd5963ba7cb41193d4a0dd2543584eca2ec5db58')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package(){
  cd "${srcdir}/anjuta-${pkgver}"

  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/share/anjuta/project/{pygtk,python}/src/main.py

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain anjuta ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
