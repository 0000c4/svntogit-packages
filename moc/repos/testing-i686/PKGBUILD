# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=moc
pkgver=20110528
pkgrel=1
pkgdesc="An ncurses console audio player with support for the mp3, ogg, and wave formats"
arch=('i686' 'x86_64')
url="http://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libsamplerate' 'libtool')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug')
optdepends=('speex: for using the speex plugin' 
	    'ffmpeg: for using the ffmpeg plugin'
	    'taglib: for using the musepack plugin' 
	    'libmpcdec: for using the musepack plugin' 
            'wavpack: for using the wavpack plugin' 
	    'libmodplug: for using the modplug plugin')
options=('!libtool')
source=(ftp://ftp.archlinux.org/other/moc/${pkgname}-${pkgver}.tar.xz \
        gcc-undefined-symbols.diff)
md5sums=('e9394640fecb725ad8146575a3091134'
         'efacb8559e9145e15b0c25f8fa2a9d79')
sha1sums=('d09c27602a60e3e9fde476e13d7c1a8b21489505'
          'a811a4ac7e049914aab528d3f06a6be6634c2720')

build() {
  cd "${srcdir}/${pkgname}"
  patch -p0 -i ../gcc-undefined-symbols.diff
  sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' decoder_plugins/ffmpeg/ffmpeg.c
  sed -i 's|avcodec_decode_audio|avcodec_decode_audio2|' decoder_plugins/ffmpeg/ffmpeg.c
# Disabling aac to use the external ffmpeg to play them (FS#13164)
  ./autogen.sh
  ./configure --prefix=/usr --without-rcc --without-aac \
    --with-oss  --with-alsa  --with-jack --with-mp3 \
    --with-musepack --with-vorbis --with-flac --with-wavpack \
    --with-sndfile --with-modplug --with-ffmpeg --with-speex \
    --with-samplerate --with-curl
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
