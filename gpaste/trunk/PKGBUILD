# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gpaste
pkgver=3.34.1
pkgrel=1
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD)
arch=(x86_64)
depends=(gtk3)
makedepends=(vala appstream-glib gobject-introspection gnome-shell
             gnome-control-center git meson)
optdepends=("wgetpaste: Upload clipboard contents")
_commit=f03463cbaf6a9666a76fb05f04d6d9baad13b9eb  # tags/v3.34.1^0
source=("git+https://github.com/keruspe/gpaste#commit=$_commit"
        build.diff)
sha256sums=('SKIP'
            '236d50bac1a3e86f19b39a74b6b7c14c1545d0396200d7dad993beed663484ef')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../build.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}
