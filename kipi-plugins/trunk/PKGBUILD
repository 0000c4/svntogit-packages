# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kipi-plugins
pkgver=1.8.0
pkgrel=1
pkgdesc="libkipi plugins for digikam and kde apps"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.kipi-plugins.org"
makedepends=('pkgconfig' 'cmake' 'automoc4' 'libgpod' 'opencv' 'qjson')
depends=('kdegraphics-libs' 'kdepimlibs')
source=("http://downloads.sourceforge.net/kipi/$pkgname-$pkgver.tar.bz2")
optdepends=('imagemagick: for picture manipulation'
            'qca: for shwup webservice support'
            'libgpod: send images to ipod'
            'opencv: red eye removal plugin'
            'hugin: for expoblending plugin'
            'qjson: for debianscreenshots plugin')
install=${pkgname}.install

build() {
  cd $srcdir
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=$pkgdir install
}
md5sums=('1e1ad2a3f8328e94ed7c7f92d51a1334')
