# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.5.4.0
pkgrel=1
pkgdesc="plugin to switch keyboard layouts for the Xfce4 panel"
arch=(i686 x86_64)
license=('custom')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-xkb-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier>=5.0' 'librsvg')
makedepends=('intltool')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/0.5/${pkgname}-${pkgver}.tar.bz2)
md5sums=('3ea1efe366367af660e6e1d24a240b5f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
