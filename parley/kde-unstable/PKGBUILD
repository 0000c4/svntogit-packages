# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=parley
pkgver=20.03.80
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="https://kde.org/applications/education/parley/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(kross knewstuff libkeduvocdocument qt5-webengine kcmutils)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         parley-qt5.15.patch::"https://cgit.kde.org/parley.git/patch/?id=14313733")
sha256sums=('84ee5eedad55d7524062fe291437e6a61e1db52568c67053f0482973f515e534'
            'SKIP'
            '431f4af2e1a057caad10daa13338f4f37468dddd7c114fbaf5045c9725cb8724')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../parley-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
