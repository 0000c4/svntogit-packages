# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=parley
pkgver=16.12.2
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="http://kde.org/applications/education/parley/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kross' 'knewstuff' 'libkeduvocdocument' 'khtml' 'qt5-multimedia' 'kcmutils')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-parley')
conflicts=('kdeedu-parley')
source=("http://download.kde.org/stable/applications/${pkgver}/src/parley-${pkgver}.tar.xz"{,.sig})
sha1sums=('d184d6aef0f2170d24dcad2c13b7303fc6165355'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../parley-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
