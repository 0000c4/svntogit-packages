# $Id$
# Maintainer: andyrtr <andyrtr funnychar archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.1.6
pkgrel=1
pkgdesc="new modern file manager for Xfce"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify' 'udev'
         'gtk2' 'exo>=0.5.4' 'libxfce4util>=4.7.3' 'libpng') # 'fam' - obsolete?
makedepends=('intltool' 'gtk-doc' 'xfce4-panel>=4.7.4')
optdepends=( # 'gamin:   a fam replacement to make thunar monitor changes to files on the fly'
            'xfce4-panel: for trash applet'
	    'tumbler: for thumbnail previews')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/1.1/Thunar-${pkgver}.tar.bz2)
md5sums=('a96fdbc0b3d422f864d50374b5d6be41')

build() {
  cd ${srcdir}/Thunar-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-gio-unix \
	--enable-dbus \
	--enable-startup-notification \
	--enable-gudev \
	--enable-notifications \
	--enable-exif \
	--enable-pcre \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/Thunar-${pkgver}
  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop
}
