# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.2.3
pkgrel=5
pkgdesc="Modern file manager for Xfce"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify' 'udev'
         'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'polkit-gnome: for mounting internal partitions (needs root password)'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/1.2/Thunar-$pkgver.tar.bz2
        fix-segfault-on-rename.patch
        fix-sorting-of-large-numbers-5356.patch
        fix-launching-multiple-files-7456.patch
        fix-crash-8168.patch)
md5sums=('0a2c36cdd3cb3a2ab52cbf6055415b85'
         '07e568a84f3a24e32d9d37803a250099'
         '926dbefb639c429c8acf064922b4fbe0'
         '7ef67fa9faa414ba03053dc69f4cdebc'
         '54a88687621bcbd5ab7d951a952d3558')

build() {
  cd "$srcdir/Thunar-$pkgver"
  # add fixes from git tree
  patch -Np1 -i ../fix-sorting-of-large-numbers-5356.patch
  patch -Np1 -i ../fix-launching-multiple-files-7456.patch
  patch -Np1 -i ../fix-crash-8168.patch
  patch -Np1 -i ../fix-segfault-on-rename.patch
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-dbus \
    --enable-startup-notification \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Thunar-$pkgver"

  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    "$pkgdir/usr/share/applications/Thunar-folder-handler.desktop"
}

# vim:set ts=2 sw=2 et:
