# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=17.12.0
pkgrel=3
pkgdesc='UML modeller'
arch=(x86_64)
url='https://kde.org/applications/development/umbrello'
license=(GPL)
depends=(hicolor-icon-theme ktexteditor kdelibs4support qt5-webkit)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        umbrello-fix-launch.patch)
sha256sums=('8979d3bec788f7ba02934469afb7bb5c33480f6ae18554e57dc684795fdfd9c4'
            'SKIP'
            '5fc9b318ad6e3614e121e8ee683e7fcd785e51f9c4befd64138326a8aa6193fd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../umbrello-fix-launch.patch # Fix launching from application menu https://bugs.kde.org/show_bug.cgi?id=388549
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

