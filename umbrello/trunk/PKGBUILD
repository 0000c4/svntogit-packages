# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=17.08.3
pkgrel=2
pkgdesc='UML modeller'
arch=(x86_64)
url='https://kde.org/applications/development/umbrello'
license=(GPL)
depends=(hicolor-icon-theme ktexteditor qt5-webkit)
makedepends=(extra-cmake-modules kdoctools python kdelibs4support kdesignerplugin)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        umbrello-data-dir.patch::"https://cgit.kde.org/umbrello.git/patch/?id=cbd0a043027d2a7bc3fc00cb054b891ee19b97bd")
sha256sums=('e0c8845e03c251c9e8b33f9d3e09d312c5e8f6b8200f84f5d3610970a74d24bc'
            'SKIP'
            '88ef917cc660563754fbc9bf5019db579daa6470a13d8850a3d7787b9c98b3ad')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../umbrello-data-dir.patch # Fix data dir in dot generator
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

