# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=19.11.90
pkgrel=1
pkgdesc='UML modeller'
arch=(x86_64)
url='https://kde.org/applications/development/umbrello'
license=(GPL)
depends=(kdelibs4support kdevelop qt5-webkit)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin kdevelop-pg-qt llvm)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('cdb1b53cdb7fc330988227a4e9ef556791709e0cc05225734cedaaf72923f69c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

