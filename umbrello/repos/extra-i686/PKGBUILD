# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=16.12.3
pkgrel=1
pkgdesc='UML modeller'
arch=(i686 x86_64)
url='https://kde.org/applications/development/umbrello'
license=(GPL)
depends=(hicolor-icon-theme ktexteditor qt5-webkit)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdesdk-umbrello)
replaces=(kdesdk-umbrello)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('94813479532c69336d642ff215059172d2054aa90a0a7050d73b5c01ab8392a8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

