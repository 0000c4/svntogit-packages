# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=atk
pkgver=2.21.90+3+g72c7544
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2')
makedepends=('gobject-introspection' 'git' 'gtk-doc')
url='https://developer.gnome.org/atk/'
_commit=72c7544a5dd95f798ab655df230ffa8101247f7e
source=("git://git.gnome.org/atk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/ATK_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
