# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-net-server
pkgver=2.002
pkgrel=1
pkgdesc='Extensible, general Perl server engine'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Net-Server
source=("http://search.cpan.org/CPAN/authors/id/R/RH/RHANDOM/Net-Server-$pkgver.tar.gz")
md5sums=(7e6fed08f0d9a39202e919ad1360bda9)
sha512sums=(fea00d4e7a7029682759ce7da5e41dd8a433fad75a7c33279b6ffa6ee44e63326b65b2f2d80942424fc86c1a42762a9abef60740530d915c8d3e8ef3761a6562)
_dir="$srcdir/Net-Server-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
