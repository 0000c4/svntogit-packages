# $Id$
# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-net-server
pkgver=2.008
pkgrel=1
pkgdesc='Extensible, general Perl server engine'
arch=('any')
url="https://metacpan.org/release/Net-Server"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/R/RH/RHANDOM/Net-Server-$pkgver.tar.gz"
        'Use-File-Temp-instead-of-POSIX-tmpnam.patch')
md5sums=('85d488bc800f582839dd924ffa3f0d26'
         'e2ee4ba0d489f1e6fb13bfa3724fe2c9')

prepare() {
  cd Net-Server-$pkgver
  # https://github.com/rhandom/perl-net-server/issues/9
  patch -Np1 -i ../Use-File-Temp-instead-of-POSIX-tmpnam.patch
}

build() {
  cd Net-Server-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-Server-$pkgver
  make test
}

package() {
  cd Net-Server-$pkgver
  make install DESTDIR="$pkgdir"
}
