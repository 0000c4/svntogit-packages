# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-common-internals
_pkgname=ktp-common-internals
pkgver=0.5.3
pkgrel=1
pkgdesc="Common components for KDE-Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('kdelibs' 'telepathy-qt' 'telepathy-mission-control')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('b5201215a068a46457d07c6c01365cc5df15c6c8')

build() {
  mkdir build
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
