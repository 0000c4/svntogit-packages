# $Id: PKGBUILD 249445 2015-10-16 23:55:30Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=nototools
_commit=0c3cc04
pkgver=git20160719
pkgrel=2
pkgdesc="Noto fonts support tools and scripts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(Apache)
depends=(python2)
makedepends=(python2-setuptools)
source=("https://github.com/googlei18n/nototools/archive/$_commit.zip" data_path.patch
        https://github.com/googlei18n/nototools/commit/0484c9e4.patch)
sha256sums=('1d699a9e0b7ddf734331d97e83b6f3c8e6c0d6a268089fb0b41af64cf74fe068'
            '8ef0f97a049a28d5b9dd8afa8b89ead06ae2cb81ebbf17da5e6f053c51b9b28c'
            'a00d1401f5e0ba6258bc87298c991a4238ad7c3eb3fc07ead418928779d58370')

prepare() {
  cd nototools-*
  find -name '*.py' | xargs sed -e 's|/usr/bin/python|/usr/bin/python2|g' -e 's|/usr/bin/env python|/usr/bin/env python2|' -i

# Fix data files location
  patch -p0 -i ../data_path.patch
# Remove broken unichr import
  patch -p1 -R -i ../0484c9e4.patch
}

package() {
  cd nototools-*
  python2 setup.py install --root="$pkgdir" --optimize=1

  mkdir -p "$pkgdir"/usr/share/nototools
  cp -r third_party "$pkgdir"/usr/share/nototools
}
