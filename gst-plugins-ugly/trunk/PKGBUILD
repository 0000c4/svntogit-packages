# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-ugly
pkgver=1.10.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(LGPL)
depends=(gst-plugins-base-libs libdvdread lame libmpeg2 a52dec libmad libsidplay
         libcdio libx264 opencore-amr mpg123)
makedepends=(python gtk-doc x264 autoconf-archive git)
options=(!emptydirs)
_commit=8bc5142a88be4b502f1f18440b0292208583afa1  # tags/1.10.1^0
source=("git://anongit.freedesktop.org/gstreamer/gst-plugins-ugly#commit=$_commit"
        "gst-common::git://anongit.freedesktop.org/gstreamer/common")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer Ugly Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --enable-experimental --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
