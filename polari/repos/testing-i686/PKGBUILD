# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=polari
pkgver=3.22.0
pkgrel=1
pkgdesc="An IRC Client for GNOME"
url="https://wiki.gnome.org/Apps/Polari"
arch=(i686 x86_64)
license=(GPL)
depends=(gjs gtk3 telepathy-glib telepathy-idle telepathy-mission-control telepathy-logger)
makedepends=(intltool gobject-introspection appdata-tools yelp-tools autoconf-archive appstream-glib git)
groups=(gnome-extra)
_commit=29f367be24b9c33ca8d030a7d8f769cde6372831  # tags/3.22.0^0
source=("git://git.gnome.org/polari#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
