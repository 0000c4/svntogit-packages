# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=syslog-ng
pkgver=2.1.3
_shortver="${pkgver%.*}"
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=(i686 x86_64)
license=('GPL')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glibc')
makedepends=('flex' 'pkgconfig' 'glib2' 'eventlog' 'tcp_wrappers')
optdepends=('logrotate')
provides=('logger')
backup=('etc/syslog-ng.conf' 'etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$_shortver/src/$pkgname-$pkgver.tar.gz
        syslog-ng.conf syslog-ng.logrotate syslog-ng.rc)
md5sums=('e502745de194570182ad9d69c9860d7d'
         'ee95af3a4f969f3759e477289305c8c6'
         '9714c7479119922b55a7992677ebecb5'
         '9bd98250cfa49e13fa3fa731b6040549')

build() {
    cd $srcdir/$pkgname-$pkgver

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var/lib/syslog-ng \
                --enable-tcp-wrapper \
                --enable-mixed-linking \
                --disable-spoof-source
    make || return 1
    make DESTDIR=$pkgdir install || return 1

    mkdir -p $pkgdir/var/lib/syslog-ng
    install -D -m644 $srcdir/syslog-ng.conf $pkgdir/etc/syslog-ng.conf
    install -D -m644 $srcdir/syslog-ng.logrotate $pkgdir/etc/logrotate.d/syslog-ng
    install -D -m755 $srcdir/syslog-ng.rc $pkgdir/etc/rc.d/syslog-ng
}
