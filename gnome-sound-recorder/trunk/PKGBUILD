# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sound-recorder
pkgver=3.28.1
pkgrel=2
pkgdesc="A utility to make simple audio recording from your GNOME desktop"
url="https://wiki.gnome.org/Apps/SoundRecorder"
arch=(any)
license=(GPL2)
depends=(gtk3 gjs gst-plugins-good gst-plugins-base)
makedepends=(intltool gobject-introspection git gnome-common)
groups=('gnome-extra')
_commit=721080149bb6e58fcae6f277fc8c37ddd30e1c49  # tags/3.28.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-sound-recorder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix for js60
  git cherry-pick -n 455ba5f9e327b6d9495b1fd7dc5dd0a6bd95ae99

  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
