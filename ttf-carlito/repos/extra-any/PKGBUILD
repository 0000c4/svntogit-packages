# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Kewl <xrjy@nygb.rh.bet(rot13)>
# Contributor: Justin Settle <jus10 at partlycloudy dot org>

pkgname=ttf-carlito
pkgver=20130920
pkgrel=3
pkgdesc='Googles Carlito font'
arch=('any')
license=('custom:OFL')
url='https://code.google.com/p/chromium/issues/detail?id=280557'
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
provides=('google-crosextra-carlito-fonts')
source=("https://gsdview.appspot.com/chromeos-localmirror/distfiles/crosextrafonts-${pkgname#ttf-}-${pkgver}.tar.gz"
        30-0-google-crosextra-carlito-fontconfig.conf
        62-google-crosextra-carlito-fontconfig.conf)
sha256sums=('4bd12b6cbc321c1cf16da76e2c585c925ce956a08067ae6f6c64eff6ccfdaf5a'
            'e13cc8ac68125ade2a99a2672dd1e53956dca93e0aaa3df833cbbf4fd15dc791'
            '31a74cd8044c8c8644f1555c06f406559f2e14334dd671a608145431a4299bd3')

package() {
  cd "crosextrafonts-${pkgname#ttf-}-${pkgver}"

  install -d "${pkgdir}/usr/share/fonts/${pkgname}/"
  install -m644 *.ttf "${pkgdir}/usr/share/fonts/${pkgname}/"

  install -Dm644 ../30-0-google-crosextra-carlito-fontconfig.conf "$pkgdir/etc/fonts/conf.avail/30-0-google-crosextra-carlito-fontconfig.conf"
  install -Dm644 ../62-google-crosextra-carlito-fontconfig.conf "$pkgdir/etc/fonts/conf.avail/62-google-crosextra-carlito-fontconfig.conf"

  mkdir "$pkgdir/etc/fonts/conf.d"
  ln -srt "$pkgdir/etc/fonts/conf.d" "$pkgdir"/etc/fonts/conf.avail/*

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
