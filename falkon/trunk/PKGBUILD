# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=3.0.1
pkgrel=5
pkgdesc="Cross-platform QtWebEngine browser"
arch=(x86_64)
url="https://www.falkon.org/"
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg)
makedepends=(extra-cmake-modules qt5-tools kwallet libgnome-keyring pyside2 shiboken2)
optdepends=('kwallet: kf5 kwallet integration' 'libgnome-keyring: gnome keyring integration'
            'pyside2: Python plugins')
replaces=(qupzilla)
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        falkon-pyside2.patch::"https://cgit.kde.org/falkon.git/patch/?id=4c83ba3d"
        falkon-webinspector.patch::"https://cgit.kde.org/falkon.git/patch/?id=48ca42d6")
sha256sums=('7b6ed54986543dd5462602943fb824df12f15d2061996310c4c4b891433f1a83'
            'SKIP'
            '454750ebdcf4a5f1ae322c3322ccc725b830d2441e9f4a86507e35fc79568206'
            '5f499c1ae86d441450902dd83a394cd9a72f766e4c1e32a1bbc5ba44b6d0ebb8')
validpgpkeys=(3CB7B8416EF6778B409FDC43EBC3FC294452C6D8) # David Rosca <nowrep@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../falkon-pyside2.patch # Fix build with Pyside2 5.11.0
  patch -p1 -i ../falkon-webinspector.patch # Fix WebInspector with Qt 5.11
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
