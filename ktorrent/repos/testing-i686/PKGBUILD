# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=5.0
pkgrel=1
pkgdesc="A powerful BitTorrent client for KDE"
arch=('i686' 'x86_64')
url='http://ktorrent.pwsp.net/'
license=('GPL2')
depends=('libktorrent' 'kdelibs4support' 'kcmutils' 'knotifyconfig' 'kross')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'boost' 'plasma-workspace' 'kdnssd' 'taglib')
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin')
install=${pkgname}.install
source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha1sums=('06c9e9a644704b72c20842ab19f794f659be9f0a')

prepare() {
  mkdir -p build
}

build() {
	cd build
	cmake ../$pkgname-$pkgver \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
          -DKDE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
