# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jochen Immend√∂rfer <jochen dot immendoerfer at gmail dot com>

# NOTE: mixartloader/miXart8.elf is a blob lacking full RELRO and PIE
# https://mailman.alsa-project.org/pipermail/alsa-devel/2020-January/161887.html

pkgname=alsa-firmware
pkgver=1.2.4
pkgrel=1
pkgdesc="Firmware binaries for loader programs in alsa-tools and hotplug firmware loader"
arch=('any')
url="https://alsa-project.org/"
license=('BSD' 'GPL2' 'LGPL2.1' 'custom')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alsa-project/${pkgname}/archive/v${pkgver}.tar.gz")
options=(!strip)
sha512sums=('f85c003f2d5c73e1623fd3902d8297101dbd2e698bf0c0441a136fbbeffa7fe6a65ea9b7c4a12291a5d8d838f801b1e3df5fb422be52935de811858532a81295')
b2sums=('3a03372f9cd97f99d9e44803eb82bce1164882eda0af91505ae0c05acfd9e61e30c243ab36960fe4b07fa9e469cf4ea1d0f401e75269ffcf24784b1975018e60')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-buildfw \
              --with-hotplug-dir=/usr/lib/firmware
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # remove files which conflict with linux-firmware
  rm -rf "${pkgdir}"/usr/lib/firmware/{ct{efx,speq}.bin,ess,korg,sb16,yamaha}
  # remove broken symlinks (broken upstream)
  rm -rf "${pkgdir}"/usr/lib/firmware/turtlebeach
  install -vDm 644 aica/license.txt \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/aica/"
  install -vDm 644 ca0132/creative.txt \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/ca0132/"
}
