# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tomboy
pkgver=0.15.6
pkgrel=1
pkgdesc="Desktop note-taking application for Linux and Unix"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org/projects/tomboy/"
depends=('gtkspell' 'gmime>=2.4.7' 'ndesk-dbus-glib>=0.4.1' 'gnome-sharp>=2.24.1' 'mono-addins>=0.4' 'libsm' 'gnome-desktop-sharp>=2.26.0' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'gnome-panel>=2.27.91' 'gnome-doc-utils>=0.17.4')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=tomboy.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.15/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c619b6f4cee4a2da63c14487d1c4aaf090a05fecf73df99e87c7c86a0243f24e')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
 
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain tomboy ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
