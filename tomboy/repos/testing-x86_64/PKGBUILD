# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tomboy
pkgver=1.8.0
pkgrel=1
pkgdesc="Desktop note-taking application for Linux and Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/tomboy/"
depends=('gtk2' 'gtkspell' 'gconf' 'gconf-sharp' 'dbus-sharp' 'dbus-sharp-glib' 'mono-addins' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig'  'gnome-doc-utils')
options=('!libtool' '!emptydirs' '!makeflags')
groups=('gnome-extra')
install=tomboy.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.8/${pkgname}-${pkgver}.tar.xz)
sha256sums=('4f3808a8c0ef94440a2ca005e41b095a417bfd22dc081e2e55e5de69b4e4df70')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-update-mimedb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain tomboy ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
