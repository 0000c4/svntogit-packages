# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime
pkgver=2.4.21
pkgrel=1
pkgdesc="Core mime parsing library"
arch=(i686 x86_64)
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2>=2.24.1' 'zlib>=1.2.5')
makedepends=('gtk-sharp-2>=2.12.10' 'pkgconfig')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.4/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c183bfbd20c4da5e93c24775ff7d6caf836646b43936e15f4f4810eda5181ab5')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
  make DESTDIR="${pkgdir}" install

  # These are gmime alternatives for the same shareutils tools
  mv "${pkgdir}/usr/bin/uuencode" "${pkgdir}/usr/bin/guuencode"
  mv "${pkgdir}/usr/bin/uudecode" "${pkgdir}/usr/bin/guudecode"
}
