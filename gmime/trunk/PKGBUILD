# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime
pkgver=2.6.5
pkgrel=1
pkgdesc="Core mime parsing library"
arch=('i686' 'x86_64')
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2' 'gpgme' 'zlib')
makedepends=('gtk-sharp-2')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('fe4db888ccdcdab0c3366cb5a61413b8bf4dc4b96b146f39e35380d9276334e0')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="$srcdir/weird"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
