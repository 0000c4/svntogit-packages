# $Id$
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=ettercap
pkgname=('ettercap' 'ettercap-gtk')
pkgver=NG_0.7.4.1
_origver=${pkgver/_/-}
pkgrel=2
arch=('i686' 'x86_64')
url="http://ettercap.sourceforge.net/"
license=('GPL')
depends=('ncurses' 'openssl' 'libpcap' 'zlib' 'pcre' 'libtool' 'libnet'
         'pkgconfig' 'gtk2')
source=("http://drizztbsd.github.com/${pkgbase}/dist/${pkgbase}-${_origver}.tar.gz"
        'ettercap.desktop')
md5sums=('c6ae5f86361a85a8e193051f594a74fa'
         '2fb9c1fa838f33aab427865ae4fc93ac')

build() {
  cd "${srcdir}"
  
  cp -r ${pkgbase}-${_origver} ${pkgbase}-${_origver}-gtk

  cd ${pkgbase}-${_origver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --datarootdir=/usr/share \
              --enable-plugins \
              --enable-https \
              --disable-gtk
  make

  cd ../${pkgbase}-${_origver}-gtk
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --datarootdir=/usr/share \
              --enable-plugins \
              --enable-https \
              --enable-gtk
  make
}

package_ettercap() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - console"
  depends=('ncurses' 'openssl' 'libpcap' 'zlib' 'pcre' 'libtool' 'libnet')
  backup=('etc/etter.conf')
  options=('!libtool')

  cd "${srcdir}"/${pkgbase}-${_origver}
  make DESTDIR="${pkgdir}" install
}

package_ettercap-gtk() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - GTK"
  depends=('ettercap' 'gtk2')
  optdepends=('gksu: to run ettercap from the menu')
  install=ettercap-gtk.install

  cd "${srcdir}"/${pkgbase}-${_origver}-gtk
  install -Dm755 src/ettercap "${pkgdir}"/usr/bin/ettercap-gtk
  install -Dm644 "${srcdir}"/ettercap.desktop \
    "${pkgdir}"/usr/share/applications/ettercap.desktop
  install -Dm644 share/ettercap.png \
    "${pkgdir}"/usr/share/ettercap/ettercap.png
  install -Dm644 desktop/ettercap.xpm \
    "${pkgdir}"/usr/share/pixmaps/ettercap.xpm
}
