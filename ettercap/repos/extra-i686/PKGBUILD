# $Id$
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ettercap
pkgver=NG_0.7.3
_origver=NG-0.7.3
pkgrel=16
pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - console"
arch=('i686' 'x86_64')
url="http://ettercap.sourceforge.net/"
license=('GPL')
depends=('ncurses' 'openssl' 'libpcap' 'zlib' 'pcre' 'libtool' 'libnet')
makedepends=('libnet' 'pkgconfig')
options=(!libtool)
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_origver}.tar.gz"
        01_pointers_and_ints_dont_mix.diff)
md5sums=('28fb15cd024162c55249888fe1b97820'
         'ad235cd90aaa3e168575d06efac4d862')

build() {
  cd ${srcdir}/${pkgname}-${_origver}
  unset LDFLAGS

  libtoolize --force --copy
  aclocal
  autoconf

  patch -Np1 -i ${srcdir}/01_pointers_and_ints_dont_mix.diff

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --datarootdir=/usr/share \
              --enable-plugins \
              --enable-https \
              --disable-gtk
  sed -i 's/LTDL_SHLIB_EXT/\".so\"/' src/ec_plugins.c
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_origver}

  make DESTDIR=${pkgdir} install
}
