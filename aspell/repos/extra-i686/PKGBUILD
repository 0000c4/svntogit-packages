# $Id$
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aspell
pkgver=0.60.6
_pkgmajorver=0.60
pkgrel=5
pkgdesc="A spell checker designed to eventually replace Ispell"
url="http://aspell.net/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gcc-libs' 'ncurses>=5.6-7')
optdepends=('perl: to import old dictionaries')
options=('!libtool')
install=aspell.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('bc80f0198773d5c05086522be67334eb')
sha1sums=('335bcb560e00f59d89ec9e4c4114c325fb0e65f4')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install
	ln -s ${pkgname}-${_pkgmajorver} \
		"${pkgdir}/usr/lib/${pkgname}"
}
