# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=evolution-groupwise
pkgver=3.4.0
pkgrel=1
pkgdesc="GroupWise connector for evolution"
arch=(i686 x86_64)
url="http://projects.gnome.org/evolution/"
license=(LGPL)
depends=('evolution-data-server' 'gtkhtml4')
makedepends=(intltool evolution)
optdepends=('evolution: evolution plugin')
options=(!libtool)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('b5e882811d10411249accb4ce1131d2e2d336d357b68a8f5dcfe7576b320c717')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

