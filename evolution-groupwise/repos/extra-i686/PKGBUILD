# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=evolution-groupwise
pkgver=3.4.2
pkgrel=1
pkgdesc="GroupWise connector for evolution"
arch=(i686 x86_64)
url="http://projects.gnome.org/evolution/"
license=(LGPL)
depends=('evolution-data-server' 'gtkhtml4')
makedepends=(intltool evolution)
optdepends=('evolution: evolution plugin')
options=(!libtool)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('e26e08eb065df21118c92f29be4cfa2ce2faa52c950210e15a3efbdc9cace2aa')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

