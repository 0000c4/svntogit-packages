# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=ekiga
pkgver=3.3.2
pkgrel=6
pkgdesc="VOIP/Videoconferencing app with full SIP and H.323 support (GnomeMeeting expanded and renamed)"
url="http://www.ekiga.org"
license=(GPL)
arch=(i686 x86_64)
depends=('opal' 'boost-libs' 'libxv' 'libnotify' 'hicolor-icon-theme' 'avahi' 'gtk2' 'gconf')
makedepends=('intltool' 'gnome-doc-utils' 'evolution-data-server' 'boost')
optdepends=('evolution-data-server: Evolution integration')
options=(!emptydirs)
groups=('gnome-extra')
install=ekiga.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        gcc47.patch)
sha256sums=('d4dd47a47696c53e6f3839eef6e0e896dd3d07babb2e68ff8fe994d67ba8807a'
            '9e3000016e1174be5d6cc60c89abeaeb61a98eb163a38abf11f4af4f2d6363ec')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/gcc47.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-install \
      --disable-scrollkeeper --enable-dbus
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain ekiga "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
