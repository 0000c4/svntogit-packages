# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=bc
pkgver=1.07
pkgrel=2
pkgdesc="An arbitrary precision calculator language"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/bc/"
license=('GPL')
depends=('readline')
makedepends=('ed')
replaces=('bc-readline')
conflicts=('bc-readline')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig} bc-fix-hang.patch)
sha256sums=('55cf1fc33a728d7c3d386cc7b0cb556eb5bacf8e0cb5a3fcca7f109fc61205ad'
            'SKIP'
            '64719ac2d2a1622c241f5647973122d3ca742a28649875cd29ee957bf383dc38')
validpgpkeys=('00E426232F384BF6D32D8B1881C24FF12FB7B14B') # Phil Nelson <philnelson@acm.org>

prepare() {
  cd $pkgname-$pkgver
  # fix hang when building kernel
  patch -p1 -i ../bc-fix-hang.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info --with-readline
  make -j1
}

check () {
  cd "${pkgname}-${pkgver}"
  echo "quit" | ./bc/bc -l Test/checklib.b
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
