# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt5-webkit-ng
_qtver=5.212.0
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64 i686)
url='https://github.com/annulen/webkit/wiki'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API (maintained fork)'
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen)
makedepends=(cmake ruby gperf python2 qt5-doc qt5-tools)
optdepends=('gst-plugins-good: Webm codec support')
conflicts=(qt5-webkit)
provides=(qt5-webkit)
source=("https://github.com/annulen/webkit/archive/qtwebkit-$pkgver-alpha.tar.gz"
        qt5-webkit-ng-gcc7.patch qt5-webkit-ng-functional.patch
        qt5-webkit-ng-no-expansion-to-defined.patch qt5-webkit-ng-i686-linking.patch)
sha256sums=('bf8637ad44efd781893c60a932256f435f538bea63333a7f438b897500044d98'
            '58ecbd8708f5afe4e974fdcd6c1f32fea61ccc55dc9bb711768f2d0da6963fb8'
            'c85344edcc23930c0190b2f8ec3847bf8bdbbf7f1d4fa877c502e354e5f4233e'
            '8f46bca6d2cb99701cfd82302da0a5c19ce09c73c5ed625a27b77e0d0146c0b4'
            '2b494dcd22db8d50b3b4ddbc73639f089041681575fbc435e600788e7e2a6525')

prepare() {
  mkdir -p build

  cd webkit-qtwebkit-$pkgver-alpha
# Fix build with GCC 7
  patch -p1 -i ../qt5-webkit-ng-gcc7.patch
  patch -p1 -i ../qt5-webkit-ng-functional.patch
  patch -p1 -i ../qt5-webkit-ng-no-expansion-to-defined.patch
# Fix i686 linking
  patch -p1 -i ../qt5-webkit-ng-i686-linking.patch
}

build() {
  cd build  

  cmake ../webkit-qtwebkit-$pkgver-alpha \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
