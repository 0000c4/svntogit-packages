# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmouth
pkgver=17.12.2
pkgrel=1
arch=(x86_64)
pkgdesc='Speech Synthesizer Frontend'
url='https://kde.org/applications/utilities/kmouth/'
license=(GPL FDL)
groups=(kde-applications kdeaccessibility)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdeaccessibility-kmouth)
replaces=(kdeaccessibility-kmouth)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('40bc016c986fa072915c383c880b48e371607396b2e0ac894f9ff73a3ecf6d8a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
