# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.3.86
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='http://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh')
makedepends=('txt2tags')
source=("http://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz")
md5sums=('0384b7baa7267075bc098c04b84ddd6a')

build() {
	cd ${srcdir}/grml-etc-core/doc
	make
}

package() {
	cd ${srcdir}/grml-etc-core
	install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
	install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
	install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

	install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
	ln -sf grmlzshrc.5.gz ${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz
}
