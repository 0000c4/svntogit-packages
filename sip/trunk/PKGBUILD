# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=sip
pkgname=('sip' 'python-sip')
pkgver=4.12
pkgrel=3
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
makedepends=('python' 'gcc-libs')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/${pkgbase}-${pkgver}.tar.gz")
md5sums=('8a0998fbaf34bdab2c15af3d4fa3bc0e')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  python configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  
  cd ${srcdir}/${pkgbase}-${pkgver}
  install -Dm755 sipgen/sip ${pkgdir}/usr/bin/sip
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_python-sip() {
  pkgdesc="Python 3 bindings for sip"
  depends=('sip' 'python')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Provided by sip package
  rm ${pkgdir}/usr/bin/sip
}
