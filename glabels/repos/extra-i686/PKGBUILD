# $Id$
# Maintainer: Damir Perisa <damir@archlinux.org>

pkgname=glabels
pkgver=2.2.6
pkgrel=1
pkgdesc="Creating labels and business cards the very easy way"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://glabels.sourceforge.net/"
depends=('libgnomeui' 'libsm' 'desktop-file-utils' 'shared-mime-info')
makedepends=('pkgconfig' 'perlxml' 'gnome-doc-utils>=0.11.2' 'intltool')
options=('!libtool')
install=glabels.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('594fffa4116a88772752c2582aa3605d')
sha1sums=('f51ee679072cd5e0e5bfe1e0a7d62265a78003d6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
              --disable-update-mimedb --disable-update-desktopdb  || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
