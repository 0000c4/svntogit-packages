# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-logs
pkgver=3.28.4
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Logs"
license=(GPL)
depends=(systemd gtk3 gsettings-desktop-schemas)
makedepends=(appstream-glib git meson itstool)
groups=(gnome)
_commit=438c13c2896c600192625fdef7735c566ce51fe0  # tags/gnome-logs-3.28.4^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-logs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-logs-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D tests=true
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
