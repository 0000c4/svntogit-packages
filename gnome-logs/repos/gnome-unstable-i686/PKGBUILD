# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-logs
pkgver=3.24.0+2+g88dd918
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Logs"
license=(GPL)
groups=(gnome-extra)
depends=(systemd gtk3 gsettings-desktop-schemas)
makedepends=(intltool itstool gnome-common appstream-glib git)
_commit=88dd918dd2be72e4cb18d7056f7aae0c266bfb5c  # gnome-3-24
source=("git+https://git.gnome.org/browse/gnome-logs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-logs-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f 3.24.0 5ca64e34adb364ced0b8e07d46b8ed9825454403
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
