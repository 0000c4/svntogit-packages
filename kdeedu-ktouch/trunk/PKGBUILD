# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-ktouch
pkgver=16.08.3
pkgrel=1
pkgdesc="Touch Typing Tutor"
url="http://kde.org/applications/education/ktouch/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdebase-runtime' 'kqtquickcharts' 'libxkbfile')
makedepends=('cmake' 'automoc4' 'mesa')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ktouch-${pkgver}.tar.xz")
sha1sums=('4f58eca9a4692e0f990e53e40d53d541c4e015dd')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ktouch-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
