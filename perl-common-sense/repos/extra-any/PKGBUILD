# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-common-sense
pkgver=3.5
pkgrel=1
pkgdesc='save a tree AND a kitten, use common::sense!'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=http://search.cpan.org/dist/common-sense
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/common-sense-$pkgver.tar.gz")
md5sums=(af2182e3bd91f7300d2caa5edd988bfb)
sha512sums=(b9d36dfaa9d076a0c0c1b1f9b3800e7c8e803272777ebbba6360a97f26b98202c2ea72096bcbaa2413fc17b66ba53019bc8e75ff3f2934f99d3b5f733b7bd7f0)
_dir="$srcdir/common-sense-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
