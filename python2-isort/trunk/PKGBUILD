# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python2-isort
pkgver=4.3.10
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python2-futures' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/timothycrosley/isort/archive/$pkgver.tar.gz")
sha512sums=('6f1150b236b11f235f2a9e6054b341483b4df0582af1a3d92ff5d1f0358c95c0a986fe0ab80301f2bec7cb10814604d5198781dbe2de5f6096ad8da65b16ed9a')

build() {
  cd isort-$pkgver
  python2 setup.py build
}

package() {
  cd isort-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/isort{,2}
}
