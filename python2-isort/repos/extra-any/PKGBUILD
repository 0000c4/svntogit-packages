# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python2-isort
pkgver=4.3.12
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python2-futures' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/timothycrosley/isort/archive/$pkgver.tar.gz")
sha512sums=('66568a821a703cceb31129d032554017e9762a82cb3194b983a9114f1e7ca2941cc1d9ebcb3f684636fdec5023f3e7c86d55833d71c7051ff13eba1302c81935')

build() {
  cd isort-$pkgver
  python2 setup.py build
}

package() {
  cd isort-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/isort{,2}
}
